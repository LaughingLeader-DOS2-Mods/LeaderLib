Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLLIB_Array_Length(_ID, _Length)
//DB_LLLIB_Array_Data(_ID, _Index, _Value)
//DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value)
//DB_LLLIB_Array_Iterator(_ID, _CurrentIndex)
//DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length)
//DB_LLLIB_Array_Temp_PopValue(_ID, _Value)
//DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value)
KBSECTION
//REGION ARRAY_LENGTH
QRY
LeaderLib_Array_QRY_RebuildArrayLength((STRING)_ID, (STRING)_ArrayType)
THEN
LLLIB_Array_RebuildArrayLength(_ID, _ArrayType);

PROC
LLLIB_Array_RebuildArrayLength((STRING)_ID, (STRING)_ArrayType)
AND
DB_LLLIB_Array_Length(_ID, _Length)
THEN
NOT DB_LLLIB_Array_Length(_ID, _Length);

PROC
LLLIB_Array_RebuildArrayLength((STRING)_ID, (STRING)_ArrayType)
THEN
DB_LLLIB_Array_Length(_ID, 0);

PROC
LLLIB_Array_RebuildArrayLength((STRING)_ID, "Array")
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
AND
DB_LLLIB_Array_Length(_ID, _Total)
AND
IntegerSum(_Total, 1, _NewTotal)
THEN
NOT DB_LLLIB_Array_Length(_ID, _Total);
DB_LLLIB_Array_Length(_ID, _NewTotal);

PROC
LLLIB_Array_RebuildArrayLength((STRING)_ID, "Dictionary")
AND
DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value)
AND
DB_LLLIB_Array_Length(_ID, _Total)
AND
IntegerSum(_Total, 1, _NewTotal)
THEN
NOT DB_LLLIB_Array_Length(_ID, _Total);
DB_LLLIB_Array_Length(_ID, _NewTotal);

PROC
LLLIB_Array_IncreaseArrayLength((STRING)_ID)
AND
DB_LLLIB_Array_Length(_ID, _Length)
AND
IntegerSum(_Length, 1, _NextLength)
THEN
NOT DB_LLLIB_Array_Length(_ID, _Length);
DB_LLLIB_Array_Length(_ID, _NextLength);
DB_LLLIB_Array_Iterator(_ID, _NextLength);

PROC
LLLIB_Array_DecreaseArrayLength((STRING)_ID)
AND
DB_LLLIB_Array_Length(_ID, _Length)
AND
IntegerSubtract(_Length, 1, _NextLength)
THEN
NOT DB_LLLIB_Array_Length(_ID, _Length);
NOT DB_LLLIB_Array_Iterator(_ID, _Length);
DB_LLLIB_Array_Length(_ID, _NextLength);
//END_REGION

//REGION ARRAY_NEW
PROC
LLLIB_Array_NewArray((STRING)_ID)
AND
NOT DB_LLLIB_Array_Length(_ID, _)
THEN
DB_LLLIB_Array_Length(_ID, 0);
DB_LLLIB_Array_Iterator(_ID, 0);
//END_REGION

//REGION ARRAY_ADD
PROC
LLLIB_Array_AddToArray((STRING)_ID, (STRING)_Value)
AND
NOT DB_LLLIB_Array_Length(_ID, _)
THEN
LLLIB_Array_NewArray(_ID);

PROC
LLLIB_Array_AddToArray((STRING)_ID, (STRING)_Value)
AND
DB_LLLIB_Array_Length(_ID, _NextIndex)
THEN
DB_LLLIB_Array_Data(_ID, _NextIndex, _Value);
LLLIB_Array_IncreaseArrayLength(_ID);

PROC
LLLIB_Array_AddToArrayAtIndex((STRING)_ID, (STRING)_Value, (INTEGER)_Index)
AND
NOT DB_LLLIB_Array_Data(_ID, _, _Value)
THEN
DB_LLLIB_Array_Data(_ID, _Index, _Value);
LLLIB_Array_IncreaseArrayLength(_ID);

PROC
LLLIB_Array_AddToArrayAtIndex((STRING)_ID, (STRING)_Value, (INTEGER)_Index)
AND
NOT DB_LLLIB_Array_Data(_ID, _, _Value)
AND
NOT DB_LLLIB_Array_Data(_ID, _Index, _)
THEN
LLLIB_Array_ShiftArrayDown(_ID, _Index);
DB_LLLIB_Array_Data(_ID, _Index, _Value);
LLLIB_Array_IncreaseArrayLength(_ID);

PROC
LLLIB_Array_ShiftArrayDown((STRING)_ID, (INTEGER)_AtIndex)
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
AND
_Index >= _AtIndex
AND
IntegerSum(_Index, 1, _NextIndex)
THEN
NOT DB_LLLIB_Array_Data(_ID, _Index, _Value);
DB_LLLIB_Array_Data(_ID, _NextIndex, _Value);

PROC
LLLIB_Array_ShiftArrayUp((STRING)_ID, (INTEGER)_StartIndex)
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
AND
_Index > _StartIndex
AND
IntegerSubtract(_Index, 1, _NextIndex)
THEN
NOT DB_LLLIB_Array_Data(_ID, _Index, _Value);
DB_LLLIB_Array_Data(_ID, _NextIndex, _Value);
//END_REGION

//REGION ARRAY_REMOVE
PROC
LLLIB_Array_RemoveFromArray((STRING)_ID, (STRING)_Value)
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
THEN
NOT DB_LLLIB_Array_Data(_ID, _Index, _Value);
LLLIB_Array_DecreaseArrayLength(_ID);
LLLIB_Array_ShiftArrayUp(_ID, _Index);

PROC
LLLIB_Array_RemoveFromArrayAtIndex((STRING)_ID, (INTEGER)_Index)
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
THEN
LLLIB_Array_RemoveFromArray(_ID, _Value);
//END_REGION

//REGION ARRAY_FIRST_ENTRY

QRY
LeaderLib_Array_QRY_GetFirstEntry((STRING)_ID)
THEN
LLLIB_Array_GetFirstEntry(_ID);

PROC
LLLIB_Array_GetFirstEntry((STRING)_ID)
AND
NOT DB_LLLIB_Array_Iterator(_ID, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:GetFirstEntry] [ERROR] No iterator found for array [",_ID,"]!");

PROC
LLLIB_Array_GetFirstEntry((STRING)_ID)
AND
NOT DB_LLLIB_Array_Data(_ID, _, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:GetFirstEntry] [ERROR] No entries found for array [",_ID,"]!");

PROC
LLLIB_Array_GetFirstEntry((STRING)_ID)
AND
DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value)
THEN
NOT DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value);

PROC
LLLIB_Array_GetFirstEntry((STRING)_ID)
AND
DB_LLLIB_Array_Iterator(_ID, _Index)
AND
NOT DB_LLLIB_Array_Temp_FirstEntry(_ID, _, _)
//AND
//IntegertoString(_Index, _IndexStr)
//AND
//LeaderLog_QRY_Log("DEBUG", "[LLLIB:DatabaseExtensions:GetFirstEntry] Checking index [",_IndexStr,"] in array [",_ID,"]")
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:GetFirstEntry] First entry for array [",_ID,"] set to [",_IndexStr,"](",_Value,").");
DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value);
//END_REGION

//REGION ARRAY_POP
QRY
LeaderLib_Array_QRY_Pop((STRING)_ID)
THEN
LLLIB_Array_Pop(_ID);

/* Stack-like behavior. Removed the top entry at index 0, shifts all other entries up one, and sets DB_LLLIB_Array_Temp_PopValue(_ID, _Value)
*/
PROC
LLLIB_Array_Pop((STRING)_ID)
THEN
LLLIB_Array_Internal_ClearPreviousPop(_ID);
LLLIB_Array_GetFirstEntry(_ID);
LLLIB_Array_Internal_Pop(_ID);

PROC
LLLIB_Array_Internal_ClearPreviousPop((STRING)_ID)
AND
DB_LLLIB_Array_Temp_PopValue(_ID, _Value)
THEN
NOT DB_LLLIB_Array_Temp_PopValue(_ID, _Value);

PROC
LLLIB_Array_Internal_Pop((STRING)_ID)
AND
DB_LLLIB_Array_Length(_ID, _Length)
AND
_Length <= 0
AND
IntegertoString(_Length, _LengthStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:Pop] [ERROR] Error popping array [",_ID,"]: length is [",_LengthStr,"]!");

PROC
LLLIB_Array_Internal_Pop((STRING)_ID)
AND
NOT DB_LLLIB_Array_Length(_ID, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:Pop] [ERROR] Error popping array [",_ID,"]: length is null!");

PROC
LLLIB_Array_Internal_Pop((STRING)_ID)
AND
NOT DB_LLLIB_Array_Temp_FirstEntry(_ID, _, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:Pop] [ERROR] No first entry found for array [",_ID,"]!");

PROC
LLLIB_Array_Internal_Pop((STRING)_ID)
AND
DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value)
THEN
DB_LLLIB_Array_Temp_PopValue(_ID, _Value);
NOT DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value);
//LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:Pop] Popped [",_Value,"] from array [",_ID,"].");
LLLIB_Array_RemoveFromArray(_ID, _Value);
//END_REGION

//REGION ARRAY_CLEAR
QRY
LeaderLib_Array_QRY_ClearArrayEntries((STRING)_ID)
THEN
LLLIB_Array_ClearArrayEntries(_ID);

PROC
LLLIB_Array_ClearArrayEntries((STRING)_ID)
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
THEN
NOT DB_LLLIB_Array_Data(_ID, _Index, _Value);

PROC
LLLIB_Array_ClearArrayLength((STRING)_ID)
AND
DB_LLLIB_Array_Length(_ID, _Length)
THEN
NOT DB_LLLIB_Array_Length(_ID, _Length);

PROC
LLLIB_Array_ClearArrayIterators((STRING)_ID)
AND
DB_LLLIB_Array_Iterator(_ID, _CurrentIndex)
THEN
NOT DB_LLLIB_Array_Iterator(_ID, _CurrentIndex);

PROC
LLLIB_Array_ClearArrayIterators((STRING)_ID)
AND
DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length)
THEN
NOT DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length);

PROC
LLLIB_Array_ClearArray((STRING)_ID)
THEN
LLLIB_Array_ClearArrayEntries(_ID);
LLLIB_Array_ClearArrayIterators(_ID);
LLLIB_Array_ClearArrayLength(_ID);

PROC
LLLIB_Array_ClearArray((STRING)_ID)
AND
DB_LLLIB_Array_Temp_PopValue(_ID, _Value)
THEN
NOT DB_LLLIB_Array_Temp_PopValue(_ID, _Value);

PROC
LLLIB_Array_ClearArray((STRING)_ID)
AND
DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value)
THEN
NOT DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value);
//END_REGION

//REGION ARRAY_ITERATOR
QRY
LeaderLib_Array_QRY_BuildArrayIterator((STRING)_ID)
THEN
LLLIB_Array_BuildArrayIterator(_ID);

/*
//Clear
PROC
LLLIB_Array_BuildArrayIterator((STRING)_ID)
AND
DB_LLLIB_Array_Iterator(_ID, _CurrentIndex)
THEN
NOT DB_LLLIB_Array_Iterator(_ID, _CurrentIndex);
*/

//Clear2
PROC
LLLIB_Array_BuildArrayIterator((STRING)_ID)
AND
DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length)
THEN
NOT DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length);

PROC
LLLIB_Array_BuildArrayIterator((STRING)_ID)
AND
DB_LLLIB_Array_Length(_ID, _Length)
AND
NOT DB_LLLIB_Array_Iterator(_ID, _Length)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:BuildArrayIterator] Building array iterator for [",_ID,"].");
LLLIB_Array_Internal_StartArrayIteratorBuilder(_ID);

PROC
LLLIB_Array_Internal_StartArrayIteratorBuilder((STRING)_ID)
AND
DB_LLLIB_Array_Length(_ID, _Length)
THEN
DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, 0, _Length);
LLLIB_Array_Internal_OnBuildArrayIterator(_ID);

PROC
LLLIB_Array_Internal_OnBuildArrayIterator((STRING)_ID)
AND
DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length)
AND
_CurrentIndex <= _Length
AND
NOT DB_LLLIB_Array_Iterator(_ID, _CurrentIndex)
THEN
DB_LLLIB_Array_Iterator(_ID, _CurrentIndex);

PROC
LLLIB_Array_Internal_OnBuildArrayIterator((STRING)_ID)
AND
DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length)
AND
_CurrentIndex <= _Length
AND
IntegerSum(_CurrentIndex, 1, _NextIndex)
THEN
NOT DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length);
DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _NextIndex, _Length);
LLLIB_Array_Internal_OnBuildArrayIterator(_ID);

/*
IF
DB_LLLIB_Array_Iterator(_ID, _Index)
AND
IntegertoString(_Index, _IndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:ArrayIterator] Added [",_IndexStr,"] to array iterator [",_ID,"][",_SessionID,"].");
*/
//END_REGION

//REGION ARRAY_QUERIES
QRY
LeaderLib_Array_QRY_DataExists((STRING)_ID)
AND
DB_LLLIB_Array_Data(_ID, _, _)
THEN
DB_NOOP(1);

QRY
LeaderLib_Array_QRY_EmptyLength((STRING)_ID)
AND
DB_LLLIB_Array_Length(_ID, _Length)
AND
_Length <= 0
THEN
DB_NOOP(1);

QRY
LeaderLib_Array_QRY_EmptyLength((STRING)_ID)
AND
NOT DB_LLLIB_Array_Length(_ID, _)
THEN
DB_NOOP(1);

QRY
LeaderLib_Array_QRY_DictionaryDataExists((STRING)_ID)
AND
DB_LLLIB_Dictionary_Data(_ID, _, _, _)
THEN
DB_NOOP(1);
//END_REGION

//REGION DICTIONARY_ADD
PROC
LLLIB_Array_AddToDictionary((STRING)_ID, (STRING)_Key, (STRING)_Value)
AND
NOT DB_LLLIB_Array_Length(_ID, _)
THEN
LLLIB_Array_NewArray(_ID);

PROC
LLLIB_Array_AddToDictionary((STRING)_ID, (STRING)_Key, (STRING)_Value)
AND
DB_LLLIB_Array_Length(_ID, _NextIndex)
THEN
DB_LLLIB_Dictionary_Data(_ID, _NextIndex, _Key, _Value);
LLLIB_Array_IncreaseArrayLength(_ID);

PROC
LLLIB_Array_AddToDictionaryAtIndex((STRING)_ID, (STRING)_Key, (STRING)_Value, (INTEGER)_Index)
AND
NOT DB_LLLIB_Dictionary_Data(_ID, _Index, _, _)
THEN
DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value);
LLLIB_Array_IncreaseArrayLength(_ID);
//LLLIB_Array_RebuildArrayLength(_ID, "Dictionary");

QRY
LeaderLib_Array_QRY_DictionaryEntryMatch((STRING)_ID, (STRING)_Key1, (STRING)_Value1, (STRING)_Key2, (STRING)_Value2)
AND
_Value1 == _Value2
AND
_Key1 == _Key2
THEN
DB_NOOP(1);

PROC
LLLIB_Array_AddToDictionaryAtIndex((STRING)_ID, (STRING)_Key, (STRING)_Value, (INTEGER)_Index)
AND
DB_LLLIB_Dictionary_Data(_ID, _Index, _OtherKey, _OtherValue)
AND
NOT LeaderLib_Array_QRY_DictionaryEntryMatch(_ID, _Key, _Value, _OtherKey, _OtherValue)
THEN
LLLIB_Array_ShiftDictionaryDown(_ID, _Index);
DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value);
LLLIB_Array_IncreaseArrayLength(_ID);
//LLLIB_Array_RebuildArrayLength(_ID, "Dictionary");

PROC
LLLIB_Array_ShiftDictionaryDown((STRING)_ID, (INTEGER)_AtIndex)
AND
DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value)
AND
_Index >= _AtIndex
AND
IntegerSum(_Index, 1, _NextIndex)
THEN
NOT DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value);
DB_LLLIB_Dictionary_Data(_ID, _NextIndex, _Key, _Value);

PROC
LLLIB_Array_ShiftDictionaryUp((STRING)_ID, (INTEGER)_StartIndex)
AND
DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value)
AND
_Index > _StartIndex
AND
IntegerSubtract(_Index, 1, _NextIndex)
THEN
NOT DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value);
DB_LLLIB_Dictionary_Data(_ID, _NextIndex, _Key, _Value);
//END_REGION

//REGION DICTIONARY_CLEAR
QRY
LeaderLib_Array_QRY_ClearDictionaryEntries((STRING)_ID)
THEN
LLLIB_Array_ClearDictionaryEntries(_ID);

PROC
LLLIB_Array_ClearDictionaryEntries((STRING)_ID)
AND
DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value)
THEN
NOT DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value);

PROC
LLLIB_Array_ClearDictionaryLength((STRING)_ID)
AND
DB_LLLIB_Array_Length(_ID, _Length)
THEN
NOT DB_LLLIB_Array_Length(_ID, _Length);

PROC
LLLIB_Array_ClearDictionaryIterators((STRING)_ID)
AND
DB_LLLIB_Array_Iterator(_ID, _CurrentIndex)
THEN
NOT DB_LLLIB_Array_Iterator(_ID, _CurrentIndex);

PROC
LLLIB_Array_ClearDictionaryIterators((STRING)_ID)
AND
DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length)
THEN
NOT DB_LLLIB_Array_Temp_ArrayIteratorBuilder(_ID, _CurrentIndex, _Length);

PROC
LLLIB_Array_ClearDictionary((STRING)_ID)
THEN
LLLIB_Array_ClearDictionaryEntries(_ID);
LLLIB_Array_ClearDictionaryIterators(_ID);
LLLIB_Array_ClearDictionaryLength(_ID);

PROC
LLLIB_Array_ClearDictionary((STRING)_ID)
AND
DB_LLLIB_Array_Temp_PopValue(_ID, _Value)
THEN
NOT DB_LLLIB_Array_Temp_PopValue(_ID, _Value);

PROC
LLLIB_Array_ClearDictionary((STRING)_ID)
AND
DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value)
THEN
NOT DB_LLLIB_Array_Temp_FirstEntry(_ID, _Index, _Value);
//END_REGION

//REGION DEBUG
/*
PROC
LLLIB_Array_AddToArray((STRING)_ID, (STRING)_Value)
AND
DB_LLLIB_Array_Length(_ID, _Length)
AND
IntegerSubtract(_Length, 1, _AddedIndex)
AND
IntegertoString(_AddedIndex, _AddedIndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:AddToArray] Added '",_Value,"' to array [",_ID,"] at index [",_AddedIndexStr,"].");
*/

/*
PROC
LLLIB_Array_AddToDictionaryAtIndex((STRING)_ID, (STRING)_Key, (STRING)_Value, (INTEGER)_Index)
AND
DB_LLLIB_Dictionary_Data(_ID, _Index, _Key, _Value)
AND
IntegertoString(_Index, _IndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:AddToArray] Added [",_Key,"] = '",_Value,"' to dictionary [",_ID,"] at index [",_IndexStr,"].");

PROC
LLLIB_Array_AddToDictionary((STRING)_ID, (STRING)_Key, (STRING)_Value)
AND
DB_LLLIB_Array_Length(_ID, _Length)
AND
IntegerSubtract(_Length, 1, _AddedIndex)
AND
IntegertoString(_AddedIndex, _AddedIndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:AddToArray] Added '",_Value,"' to array [",_ID,"] at index [",_AddedIndexStr,"].");
*/

/*
PROC
LLLIB_Array_ShiftArrayDown((STRING)_ID, (INTEGER)_AtIndex)
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
AND
_Index > _AtIndex
AND
IntegertoString(_Index, _IndexStr)
AND
IntegertoString(_AtIndex, _AtIndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:ShiftArrayDown(",_AtIndexStr,")] Shifted '",_Value,"' in array [",_ID,"] down, to index [",_IndexStr,"].");

PROC
LLLIB_Array_ShiftArrayUp((STRING)_ID, (INTEGER)_StartIndex)
AND
DB_LLLIB_Array_Data(_ID, _Index, _Value)
AND
_Index >= _StartIndex
AND
IntegertoString(_Index, _IndexStr)
AND
IntegertoString(_StartIndex, _StartIndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:DatabaseExtensions:ShiftArrayUp(",_StartIndexStr,")] Shifted '",_Value,"' in array [",_ID,"] up, to index [",_IndexStr,"].");
*/
//END_REGION

//REGION UPDATER
PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "DatabaseExtensions", "DB_LLLIB_Array_Length", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "DatabaseExtensions", "DB_LLLIB_Array_Data", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "DatabaseExtensions", "DB_LLLIB_Dictionary_Data", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "DatabaseExtensions", "DB_LLLIB_Array_Iterator", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "DatabaseExtensions", "DB_LLLIB_Array_Temp_ArrayIteratorBuilder", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "DatabaseExtensions", "DB_LLLIB_Array_Temp_PopValue", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "DatabaseExtensions", "DB_LLLIB_Array_Temp_FirstEntry", 3);
LeaderLog_Log("TRACE", "[LLLIB:DatabaseExtensions] Databases registered.");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
