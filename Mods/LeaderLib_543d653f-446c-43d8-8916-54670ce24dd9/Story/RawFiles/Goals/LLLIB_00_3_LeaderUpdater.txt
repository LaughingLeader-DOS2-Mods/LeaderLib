Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_Mods_Registered(_ModName, _Author, _Version)
//DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
//DB_LeaderUpdater_RegisteredDatabase(_ModName, _Author, _ReferenceName, _DatabaseName, _Arity)
//DB_LeaderUpdater_RegisteredGoal(_ModName, _Author, _GoalTitle)
//DB_LeaderUpdater_LastSavedSettings(_ModName, _Author, _SettingsID, _Version)
KBSECTION
//REGION REGISTERING
PROC
LeaderUpdater_Register_Mod((STRING)_ModName, (STRING)_Author, (STRING)_Version)
AND
NOT DB_Mods_Registered(_ModName, _Author, _Version)
THEN
LeaderUpdater_Clear_RegisteredModVersions(_ModName, _Author);
DB_Mods_Registered(_ModName, _Author, _Version);
LeaderLog_Log("DEBUG", "[LeaderUpdater:RegisterMod] Registered mod [",_ModName,"] by author [",_Author,"] with version [",_Version,"].");

PROC
LeaderUpdater_Register_ActiveGoal((STRING)_ModName, (STRING)_Author, (STRING)_GoalTitle)
AND
NOT DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
THEN
DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle);

PROC
LeaderUpdater_Register_Database((STRING)_ModName, (STRING)_Author, (STRING)_ReferenceName, (STRING)_DatabaseName, (INTEGER)_Arity)
AND
NOT DB_LeaderUpdater_RegisteredDatabase(_ModName, _Author, _, _DatabaseName, _Arity)
THEN
DB_LeaderUpdater_RegisteredDatabase(_ModName, _Author, _ReferenceName, _DatabaseName, _Arity);

PROC
LeaderUpdater_Register_Goal((STRING)_ModName, (STRING)_Author, (STRING)_GoalTitle)
THEN
DB_LeaderUpdater_RegisteredGoal(_ModName, _Author, _GoalTitle);
//END_REGION

//REGION VERSION_DATA
PROC
LeaderUpdater_SaveSettingsVersionData((STRING)_ModName, (STRING)_Author, (STRING)_SettingsID)
AND
DB_LeaderUpdater_LastSavedSettings(_ModName, _Author, _SettingsID, _PastVersion)
THEN
NOT DB_LeaderUpdater_LastSavedSettings(_ModName, _Author, _SettingsID, _PastVersion);

/*Saves the current version for a particular SettingsID. Useful so newer settings, introduced in the current version, don't "update".*/
PROC
LeaderUpdater_SaveSettingsVersionData((STRING)_ModName, (STRING)_Author, (STRING)_SettingsID)
AND
DB_Mods_Registered(_ModName, _Author, _Version)
THEN
DB_LeaderUpdater_LastSavedSettings(_ModName, _Author, _SettingsID, _Version);

QRY
LeaderUpdater_QRY_SettingsNeedsUpdate((STRING)_ModName, (STRING)_Author, (STRING)_SettingsID)
AND
DB_LeaderUpdater_LastSavedSettings(_ModName, _Author, _SettingsID, _PastVersion)
AND
DB_Mods_Registered(_ModName, _Author, _Version)
AND
_PastVersion != _Version
THEN
DB_NOOP(1);

QRY
LeaderUpdater_QRY_SettingsNeedsUpdate((STRING)_ModName, (STRING)_Author, (STRING)_SettingsID)
AND
NOT DB_LeaderUpdater_LastSavedSettings(_ModName, _Author, _SettingsID, _)
THEN
DB_NOOP(1);
//END_REGION

//REGION QUERIES
QRY
LeaderUpdater_QRY_ModIsRegistered((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_Registered(_ModName, _Author, _Version)
THEN
DB_NOOP(1);

QRY
LeaderUpdater_QRY_ModUpdateNeeded((STRING)_ModName, (STRING)_Author, (STRING)_Version)
AND
NOT DB_Mods_Registered(_ModName, _Author, _Version)
THEN
DB_NOOP(1);

QRY
LeaderUpdater_QRY_ModIsActive((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_Registered(_ModName, _Author, _Version)
AND
DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
AND
SysIsActive(_GoalTitle)
THEN
DB_NOOP(1);

QRY
LeaderUpdater_QRY_ModRegisteredActiveGoal((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
THEN
DB_NOOP(1);

QRY
LeaderUpdater_QRY_ModVersionIsInWrongColumn((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_Registered(_ModName, _Author, _Version)
AND
NOT LLLIB_StringExt_QRY_IsVersionString(_Version)
THEN
DB_NOOP(1);

PROC
LeaderUpdater_CheckIfModIsActive((STRING)_ModName, (STRING)_Author, (STRING)_ActiveEvent, (STRING)_NotActiveEvent)
AND
LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
THEN
LeaderLib_SendEvent(_ActiveEvent);

PROC
LeaderUpdater_CheckIfModIsActive((STRING)_ModName, (STRING)_Author, (STRING)_ActiveEvent, (STRING)_NotActiveEvent)
AND
NOT LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
THEN
LeaderLib_SendEvent(_NotActiveEvent);
//END_REGION

//REGION RESET
PROC
LeaderUpdater_Clear_RegisteredModVersions((STRING)_ModName, (STRING)_Author)
AND
LeaderUpdater_QRY_ModIsRegistered(_ModName, _Author)
THEN
LeaderLog_Log("DEBUG", "[LeaderUpdater:RegisterMod] Removing registered versions of mod [",_ModName,"] by author [",_Author,"].");

PROC
LeaderUpdater_Clear_RegisteredModVersions((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_Registered(_ModName, _Author, _Version)
THEN
NOT DB_Mods_Registered(_ModName, _Author, _Version);

PROC
LeaderUpdater_Clear_ActiveGoals((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
THEN
NOT DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle);

PROC
LeaderUpdater_Clear_AllDatabases((STRING)_ModName, (STRING)_Author)
AND
DB_LeaderUpdater_RegisteredDatabase(_ModName, _Author, _GoalTitle, _DatabaseName, _Arity)
THEN
SysClear(_DatabaseName, _Arity);

PROC
LeaderUpdater_Reset_AllGoals((STRING)_ModName, (STRING)_Author)
AND
DB_LeaderUpdater_RegisteredGoal(_ModName, _Author, _GoalTitle)
AND
SysIsCompleted(_GoalTitle)
THEN
SysSetGoalSleeping(_GoalTitle);

PROC
LeaderUpdater_Clear_AllRegisteredData((STRING)_ModName, (STRING)_Author)
THEN
LeaderUpdater_Clear_AllDatabases(_ModName, _Author);
LeaderUpdater_Clear_ActiveGoals(_ModName, _Author);
LeaderUpdater_Clear_RegisteredModVersions(_ModName, _Author);

PROC
LeaderUpdater_Clear_AllRegisteredData((STRING)_ModName, (STRING)_Author)
AND
DB_LeaderUpdater_RegisteredDatabase(_ModName, _Author, _ReferenceName, _DatabaseName, _Arity)
THEN
NOT DB_LeaderUpdater_RegisteredDatabase(_ModName, _Author, _ReferenceName, _DatabaseName, _Arity);

PROC
LeaderUpdater_Clear_AllRegisteredData((STRING)_ModName, (STRING)_Author)
AND
DB_LeaderUpdater_RegisteredGoal(_ModName, _Author, _GoalTitle)
THEN
NOT DB_LeaderUpdater_RegisteredGoal(_ModName, _Author, _GoalTitle);
//END_REGION

//REGION UPDATER
IF
StoryEvent(_,"LeaderLib_Commands_UpdateSettings")
THEN
//LeaderLog_Log("SYSTEM", "[LLLIB:LeaderUpdater] Databases updated.");
DB_NOOP(1);

PROC
LLLIB_System_RegisterDatabases()
THEN
/* Probably don't want to actually clear these.
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderUpdater", "DB_Mods_Registered", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderUpdater", "DB_Mods_ActiveGoal", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderUpdater", "DB_LeaderUpdater_RegisteredDatabase", 5);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderUpdater", "DB_LeaderUpdater_RegisteredGoal", 3);
*/
LeaderLog_Log("SYSTEM", "[LLLIB:LeaderUpdater] Databases registered.");
//END_REGION

//REGION UNINSTALLING
PROC
LeaderUpdater_UnregisterMod((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_Registered(_ModName, _Author, _Version)
THEN
NOT DB_Mods_Registered(_ModName, _Author, _Version);

PROC
LeaderUpdater_UninstallMod((STRING)_ModName, (STRING)_Author)
THEN
//Currently unimplemented
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
