Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object)
//DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _Template)
//DB_LLLIB_Treasure_RegisteredTraderTreasure(_TreasureID, _TraderID)
//DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel)

//DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID)
//DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID)

//DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType)
//DB_LLLIB_Treasure_TreasureGenerationType(_TreasureID, _GenerationType)
//DB_LLLIB_Treasure_RegisteredGenerationTypes(_GenerationType)
//DB_LLLIB_Treasure_ItemGenerationFlag(_TreasureID, _ItemEntry, _Flag, _FlagType)
//DB_LLLIB_Treasure_GenerationSpeed(_TreasureID, _TickRate)

//DB_LLLIB_Treasure_ItemLevel(_TreasureID, _ItemEntry, _MinLevel, _MaxLevel, _UsePartyLevel)
//DB_LLLIB_Treasure_ItemDeltaMods(_TreasureID, _ItemEntry, _Deltamod, _Chance)
//DB_LLLIB_Treasure_ItemRunes(_TreasureID, _ItemEntry, _Rune, _Chance)
//DB_LLLIB_Treasure_ItemMaxAmount(_TreasureID, _ItemEntry, _MaxAmount)

//DB_LLLIB_Treasure_OnlyOnce(_TreasureID, _ItemEntryOrTreasureTable, _OnCompletionEvent)
//DB_LLLIB_Treasure_GeneratedEvent(_TreasureID, _ItemEntry, _OnGeneratedEvent)
//DB_LLLIB_Treasure_ItemDoneGenerating(_Object, _TreasureID, _ItemEntry)

//DB_LLLIB_Treasure_TreasureTableLimit(_TreasureID, _TreasureTable, _Limit)
KBSECTION
PROC
LeaderLib_Treasure_JustRegistered((STRING)_TreasureID, (STRING)_RegisteredVal, (STRING)_Type)
THEN
DB_NOOP(1);

PROC
LeaderLib_Treasure_JustRegisteredObject((STRING)_TreasureID, (GUIDSTRING)_RegisteredVal, (STRING)_Type)
AND
String(_RegisteredVal, _Str)
THEN
LeaderLib_Treasure_JustRegistered(_TreasureID, _Str, _Type);

//REGION TREASURE_REGISTERING
PROC
LeaderLib_Treasure_Register_TreasureToObject((STRING)_TreasureID, (GUIDSTRING)_Object)
AND
NOT DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object)
THEN
DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object);
LeaderLib_Treasure_JustRegisteredObject(_TreasureID, _Object, "Object");

PROC
LeaderLib_Treasure_Register_TreasureToTemplate((STRING)_TreasureID, (STRING)_ObjectTemplate)
AND
NOT DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _ObjectTemplate)
THEN
DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _ObjectTemplate);
LeaderLib_Treasure_JustRegistered(_TreasureID, _ObjectTemplate, "ObjectTemplate");

PROC
LeaderLib_Treasure_Register_TreasureToTrader((STRING)_TreasureID, (STRING)_TraderID)
AND
NOT DB_LLLIB_Treasure_RegisteredTraderTreasure(_TreasureID, _TraderID)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:Register_TreasureToTrader] [",_TreasureID,"] registered to [",_TraderID,"].");
DB_LLLIB_Treasure_RegisteredTraderTreasure(_TreasureID, _TraderID);
LeaderLib_Treasure_JustRegistered(_TreasureID, _TraderID, "TraderTreasure");

PROC
LeaderLib_Treasure_Register_TreasureTable((STRING)_TreasureID, (STRING)_TreasureTable)
THEN
LeaderLib_Treasure_Register_TreasureTable(_TreasureID, _TreasureTable, "",1,-1,-1);

PROC
LeaderLib_Treasure_Register_TreasureTable((STRING)_TreasureID, (STRING)_TreasureTable, (STRING)_RequirementID)
THEN
LeaderLib_Treasure_Register_TreasureTable(_TreasureID, _TreasureTable, _RequirementID,1,-1,-1);

PROC
LeaderLib_Treasure_Register_TreasureTable((STRING)_TreasureID, (STRING)_TreasureTable, (STRING)_RequirementID, (INTEGER)_UsePartyLevel)
THEN
// 1, 1 for the level range here, just in case _UsePartyLevel is 0
LeaderLib_Treasure_Register_TreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, 1, 1);

PROC
LeaderLib_Treasure_Register_TreasureTable((STRING)_TreasureID, (STRING)_TreasureTable, (STRING)_RequirementID, (INTEGER)_UsePartyLevel, (INTEGER)_Level)
THEN
LeaderLib_Treasure_Register_TreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _Level, _Level);

PROC
LeaderLib_Treasure_Register_TreasureTable((STRING)_TreasureID, (STRING)_TreasureTable, (STRING)_RequirementID, (INTEGER)_UsePartyLevel, (INTEGER)_MinLevel, (INTEGER)_MaxLevel)
AND
NOT DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel)
THEN
DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel);
LeaderLib_Treasure_JustRegistered(_TreasureID, _TreasureTable, "TreasureTable");

PROC
LeaderLib_Treasure_AddTreasureItemTemplate((STRING)_TreasureID, (STRING)_ItemEntry)
THEN
LeaderLib_Treasure_AddTreasureItemTemplate(_TreasureID, _ItemEntry, 1, "");

PROC
LeaderLib_Treasure_AddTreasureItemTemplate((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount)
THEN
LeaderLib_Treasure_AddTreasureItemTemplate(_TreasureID, _ItemEntry, _Amount, "");

PROC
LeaderLib_Treasure_AddTreasureItemTemplate((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID)
AND
NOT DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID)
THEN
DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID);
LeaderLib_Treasure_JustRegistered(_TreasureID, _ItemEntry, "ItemTemplate");

PROC
LeaderLib_Treasure_AddTreasureItemStat((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount)
THEN
LeaderLib_Treasure_AddTreasureItemStat(_TreasureID, _ItemEntry, _Amount, "");

PROC
LeaderLib_Treasure_AddTreasureItemStat((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID)
AND
NOT DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID)
THEN
DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID);
LeaderLib_Treasure_JustRegistered(_TreasureID, _ItemEntry, "ItemStat");

//END_REGION

//REGION CONFIGURATION
/* Generation types:
MANUAL
TRADE_GENERATION_START
TRADE_GENERATION_END
TRADE_OPENED
DIALOG_STARTED
DIALOG_ENDED
FLAG
OPENED
LEVEL_LOADED
PARTY_LEVELED_UP
*/
PROC
LeaderLib_Treasure_Configure_GenerationType((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_GenerationType)
AND
NOT DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType)
THEN
DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType);

PROC
LeaderLib_Treasure_Configure_DefaultGenerationType((STRING)_TreasureID, (STRING)_GenerationType)
AND
NOT DB_LLLIB_Treasure_TreasureGenerationType(_TreasureID, _GenerationType)
THEN
DB_LLLIB_Treasure_TreasureGenerationType(_TreasureID, _GenerationType);

PROC
LeaderLib_Treasure_Configure_GenerationFlag((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_Flag, (STRING)_FlagType)
AND
NOT DB_LLLIB_Treasure_ItemGenerationFlag(_TreasureID, _ItemEntry, _Flag, _FlagType)
THEN
LeaderLib_Treasure_Configure_GenerationType(_TreasureID, _ItemEntry, "FLAG");
DB_LLLIB_Treasure_ItemGenerationFlag(_TreasureID, _ItemEntry, _Flag, _FlagType);

PROC
LeaderLib_Treasure_Configure_GenerationSpeed((STRING)_TreasureID, (INTEGER)_TickRate)
AND
DB_LLLIB_Treasure_GenerationSpeed(_TreasureID, _TickRate)
THEN
NOT DB_LLLIB_Treasure_GenerationSpeed(_TreasureID, _TickRate);

PROC
LeaderLib_Treasure_Configure_GenerationSpeed((STRING)_TreasureID, (INTEGER)_TickRate)
THEN
DB_LLLIB_Treasure_GenerationSpeed(_TreasureID, _TickRate);

PROC
LeaderLib_Treasure_Configure_ItemLevel((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_UsePartyLevel)
THEN
LeaderLib_Treasure_Configure_ItemLevel(_TreasureID, _ItemEntry, _UsePartyLevel, 1, 1);

PROC
LeaderLib_Treasure_Configure_ItemLevel((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_UsePartyLevel, (INTEGER)_Level)
THEN
LeaderLib_Treasure_Configure_ItemLevel(_TreasureID, _ItemEntry, _UsePartyLevel, _Level, _Level);

PROC
LeaderLib_Treasure_Configure_ItemLevel((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_UsePartyLevel, (INTEGER)_MinLevel, (INTEGER)_MaxLevel)
AND
DB_LLLIB_Treasure_ItemLevel(_TreasureID, _ItemEntry, _PrevVal, _PrevMin, _PrevMax)
THEN
NOT DB_LLLIB_Treasure_ItemLevel(_TreasureID, _ItemEntry, _PrevVal, _PrevMin, _PrevMax);
DB_LLLIB_Treasure_ItemLevel(_TreasureID, _ItemEntry, _UsePartyLevel, _MinLevel, _MaxLevel);
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem] Previous settings for ",_TreasureID,":",_ItemEntry," were overwritten.");

PROC
LeaderLib_Treasure_Configure_ItemLevel((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_UsePartyLevel, (INTEGER)_MinLevel, (INTEGER)_MaxLevel)
AND
NOT DB_LLLIB_Treasure_ItemLevel(_TreasureID, _ItemEntry,_,_,_)
THEN
DB_LLLIB_Treasure_ItemLevel(_TreasureID, _ItemEntry, _UsePartyLevel, _MinLevel, _MaxLevel);

PROC
LeaderLib_Treasure_Configure_AddDeltaMod((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_Deltamod, (INTEGER)_Chance)
THEN
DB_LLLIB_Treasure_ItemDeltaMods(_TreasureID, _ItemEntry, _Deltamod, _Chance);

PROC
LeaderLib_Treasure_Configure_AddRune((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_Rune, (INTEGER)_Chance)
THEN
DB_LLLIB_Treasure_ItemRunes(_TreasureID, _ItemEntry, _Rune, _Chance);

PROC
LeaderLib_Treasure_Configure_AddMaxAmount((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_MaxAmount)
THEN
DB_LLLIB_Treasure_ItemMaxAmount(_TreasureID, _ItemEntry, _MaxAmount);

PROC
LeaderLib_Treasure_AddGenerationEvent((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_OnGeneratedEvent)
THEN
DB_LLLIB_Treasure_GeneratedEvent(_TreasureID, _ItemEntry, _OnGeneratedEvent);

PROC
LeaderLib_Treasure_Configure_GenerateOnce((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_OnCompletionEvent)
THEN
DB_LLLIB_Treasure_OnlyOnce(_TreasureID, _ItemEntry, _OnCompletionEvent);

//Default the treasure as a whole to keep generating, provided the max item limit isn't reached
PROC
LeaderLib_Treasure_Configure_GenerateTreasureEndlessly((STRING)_TreasureID)
AND
NOT DB_LLLIB_Treasure_KeepGeneratingTreasure(_TreasureID)
THEN
DB_LLLIB_Treasure_KeepGeneratingTreasure(_TreasureID);

PROC
LeaderLib_Treasure_Configure_GenerateItemEndlessly((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Active)
AND
DB_LLLIB_Treasure_KeepGeneratingItem(_TreasureID, _ItemEntry, _OtherVal)
THEN
NOT DB_LLLIB_Treasure_KeepGeneratingItem(_TreasureID, _ItemEntry, _OtherVal);

//Keep generating the item on each generation event, provided the max limit isn't reached on the object
PROC
LeaderLib_Treasure_Configure_GenerateEndlessly((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Active)
THEN
DB_LLLIB_Treasure_KeepGeneratingItem(_TreasureID, _ItemEntry, _Active);

PROC
LeaderLib_Treasure_Configure_TreasureTable_Limit((STRING)_TreasureID, (STRING)_TreasureTable, (INTEGER)_Limit)
AND
DB_LLLIB_Treasure_TreasureTableLimit(_TreasureID, _TreasureTable, _OtherLimit)
THEN
NOT DB_LLLIB_Treasure_TreasureTableLimit(_TreasureID, _TreasureTable, _OtherLimit);

PROC
LeaderLib_Treasure_Configure_TreasureTable_Limit((STRING)_TreasureID, (STRING)_TreasureTable, (INTEGER)_Limit)
THEN
DB_LLLIB_Treasure_TreasureTableLimit(_TreasureID, _TreasureTable, _Limit);
//END_REGION

//REGION INTERNAL_GENERATION_TYPES
PROC
LeaderLib_Treasure_Configure_DefaultGenerationType((STRING)_TreasureID, (STRING)_GenerationType)
AND
NOT DB_LLLIB_Treasure_RegisteredGenerationTypes(_GenerationType)
THEN
DB_LLLIB_Treasure_RegisteredGenerationTypes(_GenerationType);

PROC
LeaderLib_Treasure_Configure_GenerationType((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_GenerationType)
AND
NOT DB_LLLIB_Treasure_RegisteredGenerationTypes(_GenerationType)
THEN
DB_LLLIB_Treasure_RegisteredGenerationTypes(_GenerationType);

PROC
LLLIB_Treasure_RebuildRegisteredGenerationTypes()
THEN
SysClear("DB_LLLIB_Treasure_RegisteredGenerationTypes", 1);

PROC
LLLIB_Treasure_RebuildRegisteredGenerationTypes()
AND
DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType)
AND
NOT DB_LLLIB_Treasure_RegisteredGenerationTypes(_GenerationType)
THEN
DB_LLLIB_Treasure_RegisteredGenerationTypes(_GenerationType);
//END_REGION

//REGION CLEARING_TREASURE
PROC
LeaderLib_Treasure_Clear_AllEntriesForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object)
THEN
NOT DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object);

PROC
LeaderLib_Treasure_Clear_AllEntriesForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _Template)
THEN
NOT DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _Template);

PROC
LeaderLib_Treasure_Clear_AllEntriesForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_RegisteredTraderTreasure(_TreasureID, _TraderID)
THEN
NOT DB_LLLIB_Treasure_RegisteredTraderTreasure(_TreasureID, _TraderID);

PROC
LeaderLib_Treasure_Clear_AllEntriesForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel)
THEN
NOT DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel);

PROC
LeaderLib_Treasure_Clear_AllEntriesForTreasure((STRING)_TreasureID)
THEN
LeaderLib_Treasure_Clear_AllItemsForTreasure(_TreasureID);
LLLIB_Treasure_RebuildRegisteredGenerationTypes();

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID)
THEN
NOT DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID);

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID)
THEN
NOT DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID);

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_ItemLevel(_TreasureID, _ItemEntry, _MinLevel, _MaxLevel, _UsePartyLevel)
THEN
NOT DB_LLLIB_Treasure_ItemLevel(_TreasureID, _ItemEntry, _MinLevel, _MaxLevel, _UsePartyLevel);

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType)
THEN
NOT DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType);

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_ItemGenerationFlag(_TreasureID, _ItemEntry, _Flag, _FlagType)
THEN
NOT DB_LLLIB_Treasure_ItemGenerationFlag(_TreasureID, _ItemEntry, _Flag, _FlagType);

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_ItemDeltaMods(_TreasureID, _ItemEntry, _Deltamod, _Chance)
THEN
NOT DB_LLLIB_Treasure_ItemDeltaMods(_TreasureID, _ItemEntry, _Deltamod, _Chance);

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_ItemRunes(_TreasureID, _ItemEntry, _Rune, _Chance)
THEN
NOT DB_LLLIB_Treasure_ItemRunes(_TreasureID, _ItemEntry, _Rune, _Chance);

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_OnlyOnce(_TreasureID, _ItemEntryOrTreasureTable, _OnCompletionEvent)
THEN
NOT DB_LLLIB_Treasure_OnlyOnce(_TreasureID, _ItemEntryOrTreasureTable, _OnCompletionEvent);

PROC
LeaderLib_Treasure_Clear_AllItemsForTreasure((STRING)_TreasureID)
AND
DB_LLLIB_Treasure_GeneratedEvent(_TreasureID, _ItemEntry, _OnGeneratedEvent)
THEN
NOT DB_LLLIB_Treasure_GeneratedEvent(_TreasureID, _ItemEntry, _OnGeneratedEvent);
//END_REGION

//REGION TRADER_REGISTERING
PROC
LeaderLib_Trader_Register_Trader((STRING)_TraderID, (STRING)_Template)
AND
NOT DB_LLLIB_Traders_RegisteredTrader(_TraderID,_,_)
THEN
DB_LLLIB_Traders_RegisteredTrader(_TraderID, _Template, 0);

PROC
LeaderLib_Trader_Register_Trader((STRING)_TraderID, (STRING)_Template, (INTEGER)_PlaySpawn)
AND
DB_LLLIB_Traders_RegisteredTrader(_TraderID,_,_)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Trader][ERROR] Error adding trader with ID '", _TraderID, "': Trader already exists!");

PROC
LeaderLib_Trader_Register_Trader((STRING)_TraderID, (STRING)_Template, (INTEGER)_PlaySpawn)
AND
NOT DB_LLLIB_Traders_RegisteredTrader(_TraderID,_,_)
THEN
DB_LLLIB_Traders_RegisteredTrader(_TraderID, _Template, _PlaySpawn);

PROC
LeaderLib_Trader_Register_GlobalTrader((STRING)_TraderID, (CHARACTERGUID)_Trader)
THEN
DB_LLLIB_Traders_RegisteredGlobalTrader(_TraderID, _Trader);

PROC
LeaderLib_Trader_Register_Level((STRING)_TraderID, (STRING)_Level)
AND
NOT DB_LLLIB_Traders_RegisteredLevel(_TraderID,_Level,_)
THEN
LeaderLib_Trader_Register_Level(_TraderID, _Level, "");

PROC
LeaderLib_Trader_Register_Level((STRING)_TraderID, (STRING)_Level, (STRING)_Requirement)
AND
DB_LLLIB_Traders_RegisteredLevel(_TraderID, _Level, _LastRequirement)
AND
_LastRequirement != _Requirement
THEN
NOT DB_LLLIB_Traders_RegisteredLevel(_TraderID, _Level, _LastRequirement);
DB_LLLIB_Traders_RegisteredLevel(_TraderID, _Level, _Requirement);

PROC
LeaderLib_Trader_Register_Level((STRING)_TraderID, (STRING)_Level, (STRING)_Requirement)
AND
NOT DB_LLLIB_Traders_RegisteredLevel(_TraderID,_Level,_)
THEN
DB_LLLIB_Traders_RegisteredLevel(_TraderID, _Level, _Requirement);

PROC
LeaderLib_Trader_Register_StartingGold((STRING)_TraderID, (STRING)_Level, (INTEGER)_Amount)
THEN
LeaderLib_Trader_Register_StartingGold(_TraderID, _Level, _Amount, "");

PROC
LeaderLib_Trader_Register_StartingGold((STRING)_TraderID, (STRING)_Level, (INTEGER)_Amount, (STRING)_Requirement)
THEN
DB_LLLIB_Traders_StartingGold(_TraderID, _Level, _Amount, _Requirement);

PROC
LeaderLib_Trader_Register_Dialog((STRING)_TraderID, (STRING)_Dialog)
THEN
LeaderLib_Trader_Register_Dialog(_TraderID, _Dialog, "", "");

PROC
LeaderLib_Trader_Register_Dialog((STRING)_TraderID, (STRING)_Dialog, (STRING)_Level)
THEN
LeaderLib_Trader_Register_Dialog(_TraderID, _Dialog, _Level, "");

PROC
LeaderLib_Trader_Register_Dialog((STRING)_TraderID, (STRING)_Dialog, (STRING)_Level, (STRING)_RequirementID)
AND
DB_LLLIB_Traders_Dialog(_TraderID, _Dialog, _Level, _PastRequirementID)
AND
_PastRequirementID != _RequirementID
THEN
NOT DB_LLLIB_Traders_Dialog(_TraderID, _Dialog, _Level, _PastRequirementID);
DB_LLLIB_Traders_Dialog(_TraderID, _Dialog, _Level, _RequirementID);
LeaderLog_Log("DEBUG", "[LLLIB:TraderSystem:Register_Dialog] Requirement for [",_TraderID,"][",_Level,"][",_Dialog,"] changed from [",_PastRequirementID,"] to [",_RequirementID,"].");

PROC
LeaderLib_Trader_Register_Dialog((STRING)_TraderID, (STRING)_Dialog, (STRING)_Level, (STRING)_RequirementID)
AND
NOT DB_LLLIB_Traders_Dialog(_TraderID, _Dialog, _Level, _)
THEN
DB_LLLIB_Traders_Dialog(_TraderID, _Dialog, _Level, _RequirementID);

PROC
LeaderLib_Trader_Register_Position((STRING)_TraderID, (STRING)_Level, (REAL)_x, (REAL)_y, (REAL)_z)
AND
NOT DB_LLLIB_Traders_LevelPosition(_TraderID, _Level, _,_,_)
THEN
LeaderLib_Trader_Register_Level(_TraderID, _Level);
DB_LLLIB_Traders_LevelPosition(_TraderID, _Level, _x, _y, _z);

PROC
LeaderLib_Trader_Register_PositionTrigger((STRING)_TraderID, (STRING)_Level, (TRIGGERGUID)_Trigger)
AND
NOT DB_LLLIB_Traders_LevelPositionTrigger(_TraderID, _Level, _)
THEN
LeaderLib_Trader_Register_Level(_TraderID, _Level);
DB_LLLIB_Traders_LevelPositionTrigger(_TraderID, _Level, _Trigger);

PROC
LeaderLib_Trader_Register_PositionObject((STRING)_TraderID, (STRING)_Level, (GUIDSTRING)_Object)
AND
NOT DB_LLLIB_Traders_LevelPositionObject(_TraderID, _Level, _)
THEN
LeaderLib_Trader_Register_Level(_TraderID, _Level);
DB_LLLIB_Traders_LevelPositionObject(_TraderID, _Level, _Object);

PROC
LeaderLib_Trader_Register_CreationEvent((STRING)_TraderID, (STRING)_EventName)
THEN
DB_LLLIB_Traders_CreationEvents(_TraderID, _EventName);

PROC
LeaderLib_Trader_Register_Seat((STRING)_TraderID, (STRING)_Level, (ITEMGUID)_Seat)
THEN
LeaderLib_Trader_Register_Seat(_TraderID, _Level, _Seat, "");

PROC
LeaderLib_Trader_Register_Seat((STRING)_TraderID, (STRING)_Level, (ITEMGUID)_Seat, (STRING)_RequirementID)
THEN
DB_LLLIB_Traders_Seat(_TraderID, _Level, _Seat, _RequirementID);
//END_REGION

//REGION TRADER_CLEARING
PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
LeaderLib_Traders_QRY_IsRegistered(_TraderID)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Traders] Trader [",_TraderID,"] removed from all associated databases.");

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_RegisteredTrader(_TraderID, _Template, _PlaySpawn)
THEN
NOT DB_LLLIB_Traders_RegisteredTrader(_TraderID, _Template, _PlaySpawn);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_StartingGold(_TraderID, _Level, _Amount, _RequirementID)
THEN
NOT DB_LLLIB_Traders_StartingGold(_TraderID, _Level, _Amount, _RequirementID);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_RegisteredGlobalTrader(_TraderID, _Trader)
THEN
NOT DB_LLLIB_Traders_RegisteredGlobalTrader(_TraderID, _Trader);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_RegisteredLevel(_TraderID, _Level, _RequirementID)
THEN
NOT DB_LLLIB_Traders_RegisteredLevel(_TraderID, _Level, _RequirementID);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_Dialog(_TraderID, _Level, _Dialog, _RequirementID)
THEN
NOT DB_LLLIB_Traders_Dialog(_TraderID, _Level, _Dialog, _RequirementID);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_LevelPosition(_TraderID, _Level, _x, _y, _z)
THEN
NOT DB_LLLIB_Traders_LevelPosition(_TraderID, _Level, _x, _y, _z);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_LevelPositionObject(_TraderID, _Level, _Object)
THEN
NOT DB_LLLIB_Traders_LevelPositionObject(_TraderID, _Level, _Object);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_LevelPositionTrigger(_TraderID, _Level, _Trigger)
THEN
NOT DB_LLLIB_Traders_LevelPositionTrigger(_TraderID, _Level, _Trigger);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_CreationEvents(_TraderID, _EventName)
THEN
NOT DB_LLLIB_Traders_CreationEvents(_TraderID, _EventName);

PROC
LeaderLib_Teasure_Clear_Trader((STRING)_TraderID)
AND
DB_LLLIB_Traders_Seat(_TraderID, _Level, _Seat, _RequirementID)
THEN
NOT DB_LLLIB_Traders_Seat(_TraderID, _Level, _Seat, _RequirementID);
//END_REGION

//REGION UPDATER
PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_RegisteredObject", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_RegisteredObjectTemplate", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_RegisteredTraderTreasure", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_RegisteredTreasureTable", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_RegisteredTreasureTable", 6);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_TreasureItemTemplates", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_TreasureItemStats", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_ItemLevel", 5);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_ItemGenerationType", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_TreasureGenerationType", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_RegisteredGenerationTypes", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_ItemGenerationFlag", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_ItemDeltaMods", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_ItemRunes", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_ItemMaxAmount", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_OnlyOnce", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_GeneratedEvent", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_ItemDoneGenerating", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Treasure_Registering", "DB_LLLIB_Treasure_GenerationSpeed", 2);

LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_RegisteredTrader", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_RegisteredGlobalTrader", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_RegisteredLevel", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_StartingGold", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_Dialog", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_LevelPosition", 5);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_LevelPositionTrigger", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_LevelPositionObject", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_CreationEvents", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Traders_Registering", "DB_LLLIB_Traders_Seat", 4);
LeaderLog_Log("TRACE", "[LLLIB:Treasure:Registering] Databases registered.");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
