Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLLIB_Dictionary_Data("LLLIB_System_EventStack", _Index, _StartEvent, _CompleteEvent)
//DB_LLLIB_EventFlow_CurrentEvent(_StackName, _Index)
//DB_LLLIB_EventFlow_EventStack(_StackName, _OnFlowCompleteEvent, _WaitTime)
//DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent)
//DB_LLLIB_EventFlow_WaitTimer(_StackName, _TimerName)
//DB_LLLIB_EventFlow_CustomWaitTime(_StackName, _OnCompleteEvent, _WaitTime)
//DB_LLLIB_EventFlow_DefaultStackInitialized(_Val, _IsCharacterCreationLevel)
KBSECTION
IF
RegionStarted(_Level)
AND
NOT DB_LLLIB_EventFlow_DefaultStackInitialized(_,_)
AND
IsCharacterCreationLevel(_Level, _IsCharacterCreationLevel)
THEN
DB_LLLIB_EventFlow_DefaultStackInitialized(1, _IsCharacterCreationLevel);
LLLIB_EventFlow_Init_LeaderLibStack();

//Re-init after a character creation level
IF
RegionEnded(_Level)
AND
DB_LLLIB_EventFlow_DefaultStackInitialized(_Val, _IsCharacterCreationLevel)
AND
_IsCharacterCreationLevel > 0
THEN
NOT DB_LLLIB_EventFlow_DefaultStackInitialized(_Val, _IsCharacterCreationLevel);

//REGION DEFAULT_FLOW
PROC
LLLIB_EventFlow_Init_LeaderLibStack()
AND
LLLIB_EventFlow_QRY_CurrentlyRunning("LLLIB_DefaultEventFlow")
THEN
LLLIB_EventFlow_ForceStop("LLLIB_DefaultEventFlow");

PROC
LLLIB_EventFlow_Init_LeaderLibStack()
THEN
LLLIB_Updater_SaveSettingsVersionData("LLLIB_Settings_EventFlowDefaultStack");
LLLIB_EventFlow_Clear_StackData("LLLIB_DefaultEventFlow");
LLLIB_EventFlow_AddStack("LLLIB_DefaultEventFlow", "LeaderLib_Events_OnDefaultEventFlowComplete", 100);

PROC
LLLIB_EventFlow_Init_LeaderLibStack()
AND
DB_LLLIB_EventFlow_DefaultStackInitialized(_Val, 0)
THEN
LLLIB_EventFlow_SetCustomWaitTime("LLLIB_DefaultEventFlow", "LeaderLib_Events_OnTraderSpawnComplete", 250);
LLLIB_Array_AddToDictionary("LLLIB_DefaultEventFlow", "LeaderLib_Commands_SpawnTraders", "LeaderLib_Events_OnTraderSpawnComplete");
LLLIB_Array_AddToDictionary("LLLIB_DefaultEventFlow", "LeaderLib_Commands_StartTreasureGeneration", "LeaderLib_Events_OnTreasureGenerationComplete");
LLLIB_Array_AddToDictionary("LLLIB_DefaultEventFlow", "LeaderLib_Commands_CheckTraders", "LeaderLib_Events_OnTraderCheckingComplete");

PROC
LLLIB_EventFlow_Init_LeaderLibStack()
AND
LLLIB_ModMenu_QRY_SortNeeded()
THEN
LLLIB_Array_AddToDictionary("LLLIB_DefaultEventFlow", "LeaderLib_Commands_SortMenus", "LeaderLib_Events_OnMenuSortComplete");
//END_REGION

//REGION DEFAULT_START
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
NOT LLLIB_EventFlow_QRY_CurrentlyRunning("LLLIB_DefaultEventFlow")
THEN
//Before starting the flow, let other mods do whatever they need to do for a few frames.
TimerLaunch("LLLIB_Timers_StartDefaultFlow", 50);

IF
TimerFinished("LLLIB_Timers_StartDefaultFlow")
THEN
LeaderLib_SendEvent("LeaderLib_Commands_StartDefaultEventFlow");

IF
StoryEvent(_, "LeaderLib_Commands_StartDefaultEventFlow")
AND
NOT LLLIB_EventFlow_QRY_CurrentlyRunning("LLLIB_DefaultEventFlow")
THEN
GlobalSetFlag("LeaderLib_DefaultEventFlowRunning");
LLLIB_EventFlow_Start("LLLIB_DefaultEventFlow");

IF
StoryEvent(_, "LeaderLib_Events_OnDefaultEventFlowComplete")
THEN
GlobalClearFlag("LeaderLib_DefaultEventFlowRunning");
LeaderLog_Log("DEBUG", "[LLLIB:EventFlow] LeaderLib event flow complete.");
//END_REGION

//REGION ADD_AND_START
PROC
LLLIB_EventFlow_AddStack((STRING)_StackName, (STRING)_OnFlowCompleteEvent, (INTEGER)_TimeBetweenEvents)
AND
NOT DB_LLLIB_EventFlow_EventStack(_StackName, _, _)
THEN
DB_LLLIB_EventFlow_EventStack(_StackName, _OnFlowCompleteEvent, _TimeBetweenEvents);
LLLIB_Array_NewArray(_StackName);

PROC
LLLIB_EventFlow_Start((STRING)_StackName)
AND
NOT LLLIB_EventFlow_QRY_CurrentlyRunning(_StackName)
THEN
LLLIB_EventFlow_SetCurrentEvent(_StackName, 0);

PROC
LLLIB_EventFlow_SetCustomWaitTime((STRING)_StackName, (STRING)_CompleteEvent, (INTEGER)_WaitTime)
AND
DB_LLLIB_EventFlow_CustomWaitTime(_StackName, _CompleteEvent, _LastWaitTime)
AND
_WaitTime != _LastWaitTime
THEN
NOT DB_LLLIB_EventFlow_CustomWaitTime(_StackName, _CompleteEvent, _LastWaitTime);

PROC
LLLIB_EventFlow_SetCustomWaitTime((STRING)_StackName, (STRING)_CompleteEvent, (INTEGER)_WaitTime)
THEN
DB_LLLIB_EventFlow_CustomWaitTime(_StackName, _CompleteEvent, _WaitTime);
//END_REGION

//REGION QUERIES
QRY
LLLIB_EventFlow_QRY_CurrentlyRunning((STRING)_StackName)
AND
DB_LLLIB_EventFlow_CurrentEvent(_StackName, _Index)
THEN
DB_NOOP(1);
//END_REGION

//REGION SET_CURRENT_EVENT
PROC
LLLIB_EventFlow_SetCurrentEvent((STRING)_StackName, (INTEGER)_Index)
AND
DB_LLLIB_EventFlow_CurrentEvent(_StackName, _CurrentIndex)
AND
_CurrentIndex != _Index
THEN
NOT DB_LLLIB_EventFlow_CurrentEvent(_StackName, _CurrentIndex);
DB_LLLIB_EventFlow_CurrentEvent(_StackName, _Index);

PROC
LLLIB_EventFlow_SetCurrentEvent((STRING)_StackName, (INTEGER)_Index)
THEN
DB_LLLIB_EventFlow_CurrentEvent(_StackName, _Index);

PROC
LLLIB_EventFlow_SetCurrentEvent((STRING)_StackName, (INTEGER)_Index)
AND
DB_LLLIB_EventFlow_CurrentEvent(_StackName, _Index)
AND
DB_LLLIB_Dictionary_Data(_StackName, _Index, _StartEvent, _OnCompleteEvent)
THEN
LLLIB_EventFlow_LaunchTimeoutTimer(_StackName, _OnCompleteEvent);
LeaderLog_Log("DEBUG", "[LLLIB:EventFlow:SetCurrentEvent] Event flow [",_StackName,"] sent out StartEvent [",_StartEvent,"].");
LeaderLib_SendEvent(_StartEvent);

PROC
LLLIB_EventFlow_SetCurrentEvent((STRING)_StackName, (INTEGER)_Index)
AND
DB_LLLIB_Array_Length(_StackName, _Length)
AND
_Index >= _Length
THEN
LLLIB_EventFlow_OnEventFlowComplete(_StackName);

PROC
LLLIB_EventFlow_NextEvent((STRING)_StackName)
AND
DB_LLLIB_EventFlow_CurrentEvent(_StackName, _Index)
AND
IntegerSum(_Index, 1, _NextIndex)
THEN
LLLIB_EventFlow_SetCurrentEvent(_StackName, _NextIndex);
//END_REGION

//REGION TIMEOUT
PROC
LLLIB_EventFlow_LaunchTimeoutTimer((STRING)_StackName, (STRING)_OnCompleteEvent)
AND
LeaderLog_QRY_Log("COMBINE", "LLLIB_Timers_EventFlow_", _StackName, "_", _OnCompleteEvent)
AND
DB_LeaderLog_Temp_CombinedString(_TimerName)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_TimerName);
DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent);
TimerLaunch(_TimerName, 60000);

IF
TimerFinished(_TimerName)
AND
DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:EventFlow:Timeout] Event flow [",_StackName,"] timed out (completion event [",_OnCompleteEvent,"] never fired). Starting next event in the stack.");
NOT DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent);
LLLIB_EventFlow_NextEvent(_StackName);

PROC
LLLIB_EventFlow_StopTimeoutTimer((STRING)_StackName, (STRING)_OnCompleteEvent)
AND
DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent)
THEN
NOT DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent);
TimerCancel(_TimerName);

PROC
LLLIB_EventFlow_CancelAllTimeoutTimers((STRING)_StackName)
AND
DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent)
THEN
NOT DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent);
TimerCancel(_TimerName);
//END_REGION

//REGION NEXT
IF
StoryEvent(_, _OnCompleteEvent)
AND
DB_LLLIB_Dictionary_Data(_StackName, _Index, _StartEvent, _OnCompleteEvent)
AND
LLLIB_EventFlow_QRY_CurrentlyRunning(_StackName)
AND
DB_LLLIB_EventFlow_EventStack(_StackName, _OnFlowCompleteEvent, _WaitTime)
AND
NOT DB_LLLIB_EventFlow_CustomWaitTime(_StackName, _OnCompleteEvent, _)
AND
NOT DB_LLLIB_EventFlow_WaitTimer(_StackName, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:EventFlow:OnEventComplete] Event flow [",_StackName,"] received OnCompleteEvent [",_OnCompleteEvent,"]. Starting wait timer for next event.");
LLLIB_EventFlow_StopTimeoutTimer(_StackName, _OnCompleteEvent);
LLLIB_EventFlow_StartWaitTimer(_StackName, _WaitTime);

IF
StoryEvent(_, _OnCompleteEvent)
AND
DB_LLLIB_Dictionary_Data(_StackName, _Index, _StartEvent, _OnCompleteEvent)
AND
LLLIB_EventFlow_QRY_CurrentlyRunning(_StackName)
AND
DB_LLLIB_EventFlow_EventStack(_StackName, _OnFlowCompleteEvent, _WaitTime)
AND
DB_LLLIB_EventFlow_CustomWaitTime(_StackName, _OnCompleteEvent, _CustomWaitTime)
AND
NOT DB_LLLIB_EventFlow_WaitTimer(_StackName, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:EventFlow:OnEventComplete] Event flow [",_StackName,"] received OnCompleteEvent [",_OnCompleteEvent,"]. Starting wait timer for next event, and using a custom wait time.");
LLLIB_EventFlow_StopTimeoutTimer(_StackName, _OnCompleteEvent);
LLLIB_EventFlow_StartWaitTimer(_StackName, _CustomWaitTime);

PROC
LLLIB_EventFlow_StartWaitTimer((STRING)_StackName, (INTEGER)_WaitTime)
AND
StringConcatenate("LLLIB_Timers_EventFlow_WaitTimer_", _StackName, _TimerName)
THEN
DB_LLLIB_EventFlow_WaitTimer(_StackName, _TimerName);
TimerLaunch(_TimerName, _WaitTime);

IF
TimerFinished(_TimerName)
AND
DB_LLLIB_EventFlow_WaitTimer(_StackName, _TimerName)
THEN
NOT DB_LLLIB_EventFlow_WaitTimer(_StackName, _TimerName);
LeaderLog_Log("DEBUG", "[LLLIB:EventFlow:WaitTimer] Event flow [",_StackName,"] wait timer finished. Sending next event.");
LLLIB_EventFlow_NextEvent(_StackName);

PROC
LLLIB_EventFlow_CancelWaitTimer((STRING)_StackName)
AND
DB_LLLIB_EventFlow_WaitTimer(_StackName, _TimerName)
THEN
TimerCancel(_TimerName);
NOT DB_LLLIB_EventFlow_WaitTimer(_StackName, _TimerName);
//END_REGION

//REGION CLEAR
PROC
LLLIB_EventFlow_Clear_StackData((STRING)_StackName)
AND
DB_LLLIB_EventFlow_EventStack(_StackName, _OnFlowCompleteEvent, _WaitTime)
THEN
LLLIB_Array_ClearDictionary(_StackName);
NOT DB_LLLIB_EventFlow_EventStack(_StackName, _OnFlowCompleteEvent, _WaitTime);

PROC
LLLIB_EventFlow_Clear_StackData((STRING)_StackName)
THEN
LLLIB_EventFlow_Clear_SessionData(_StackName);

PROC
LLLIB_EventFlow_Clear_SessionData((STRING)_StackName)
AND
DB_LLLIB_EventFlow_CurrentEvent(_StackName, _Index)
THEN
NOT DB_LLLIB_EventFlow_CurrentEvent(_StackName, _Index);

PROC
LLLIB_EventFlow_Clear_SessionData((STRING)_StackName)
AND
DB_LLLIB_EventFlow_WaitTimer(_StackName, _TimerName)
THEN
TimerCancel(_TimerName);
NOT DB_LLLIB_EventFlow_WaitTimer(_StackName, _TimerName);

PROC
LLLIB_EventFlow_Clear_SessionData((STRING)_StackName)
AND
DB_LLLIB_EventFlow_EventTimeout(_StackName, _TimerName, _OnCompleteEvent)
THEN
LLLIB_EventFlow_StopTimeoutTimer(_StackName, _OnCompleteEvent);
//END_REGION

//REGION COMPLETE
PROC
LLLIB_EventFlow_OnEventFlowComplete((STRING)_StackName)
AND
DB_LLLIB_EventFlow_EventStack(_StackName, _OnFlowCompleteEvent, _WaitTime)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:EventFlow:OnEventFlowComplete] Event flow [",_StackName,"] completed. Clearing dictionary and sending completion event [",_OnFlowCompleteEvent,"].");
LeaderLib_SendEvent(_OnFlowCompleteEvent);
LLLIB_EventFlow_Clear_SessionData(_StackName);
//END_REGION

//REGION FORCE_STOP
PROC
LLLIB_EventFlow_ForceStop((STRING)_StackName)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:EventFlow:ForceStop] Forcefully stopping event flow [",_StackName,"]. Sending stop events for all entries in the stack.");
LLLIB_EventFlow_CancelAllTimeoutTimers(_StackName);
LLLIB_EventFlow_CancelWaitTimer(_StackName);

PROC
LLLIB_EventFlow_ForceStop((STRING)_StackName)
AND
DB_LLLIB_Dictionary_Data("LLLIB_System_EventStack", _Index, _StartEvent, _CompleteEvent)
THEN
DB_LLLIB_EventFlow_StopEvent(_StartEvent);

PROC
LLLIB_EventFlow_ForceStop((STRING)_StackName)
THEN
LLLIB_EventFlow_Clear_SessionData(_StackName);
//END_REGION

//REGION UPDATER
IF
StoryEvent(_,"LLLIB_Events_OnUpdateSettings")
AND
LLLIB_Updater_QRY_SettingsNeedsUpdate("LLLIB_Settings_EventFlowDefaultStack")
THEN
LLLIB_EventFlow_Init_LeaderLibStack();

PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "EventFlow", "DB_LLLIB_EventFlow_CurrentEvent", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "EventFlow", "DB_LLLIB_EventFlow_EventStack", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "EventFlow", "DB_LLLIB_EventFlow_EventTimeout", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "EventFlow", "DB_LLLIB_EventFlow_WaitTimer", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "EventFlow", "DB_LLLIB_EventFlow_CustomWaitTime", 3);
LeaderLog_Log("SYSTEM", "[LLLIB:EventFlow] Databases registered.");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
