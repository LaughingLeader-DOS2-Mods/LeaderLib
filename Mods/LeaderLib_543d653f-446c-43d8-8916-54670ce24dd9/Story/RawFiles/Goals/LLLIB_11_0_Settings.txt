Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_Settings_InitSettings();

//DB_LLLIB_Settings_ItemTemplates(_Name, _ItemTemplate)
//DB_LLLIB_Settings_DialogVariables(_VariableName, _VariableValue)
//DB_LLLIB_Settings_QueueRates(_ReferenceName, _TickRate, _TimeoutRate)
//DB_LLLIB_Settings_Temp_RegisteredModsList(_Str)
//DB_LLLIB_Settings_Temp_ActiveModText(_ModName, _Author, _Str)
KBSECTION
//REGION SETTINGS
PROC
LLLIB_Settings_InitSettings()
THEN
LLLIB_Updater_SaveSettingsVersionData("LLLIB_Settings_GeneralSettings");
LLLIB_Settings_InitEquipmentSlots();
LLLIB_Settings_InitStats();

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_Settings_QueueRates", 3);
DB_LLLIB_Settings_QueueRates("TraderSpawn", 1000, 2000);
DB_LLLIB_Settings_QueueRates("TraderCheck", 100, 500);
DB_LLLIB_Settings_QueueRates("Treasure", 100, 60000);
DB_LLLIB_Settings_QueueRates("ItemGeneration", 0, 2000);

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_Settings_ItemTemplates", 2);
DB_LLLIB_Settings_ItemTemplates("Book_ModMenu", "LLLIB_BOOK_ModMenu_646a1194-3383-47fa-a379-ebefa3d2b108");

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_Settings_DialogVariables", 2);
DB_LLLIB_Settings_DialogVariables("PlayerName", "LLLIB_PlayerName_6c5b9559-53e3-47f7-8881-309a930e4be3");
DB_LLLIB_Settings_DialogVariables("SpeakerName", "LLLIB_SpeakerName_9fc648d4-7984-44e7-afa4-b8a07f21185b");
DB_LLLIB_Settings_DialogVariables("ModMenu_PageInfo", "LLLIB_ModMenu_PageInfo_2b744313-9239-412c-80a8-f3b5e8b56156");
DB_LLLIB_Settings_DialogVariables("ModList", "LLLIB_Settings_ModList_50c55fbe-ad1e-490e-92d1-8836ca42ca12");

PROC
LLLIB_Settings_InitEquipmentSlots()
AND
NOT DB_LLLIB_EquipmentSlots(_)
THEN
DB_LLLIB_EquipmentSlots("Weapon");
DB_LLLIB_EquipmentSlots("Shield");
DB_LLLIB_EquipmentSlots("Helmet");
DB_LLLIB_EquipmentSlots("Breast");
DB_LLLIB_EquipmentSlots("Leggings");
DB_LLLIB_EquipmentSlots("Ring");
DB_LLLIB_EquipmentSlots("Belt");
DB_LLLIB_EquipmentSlots("Boots");
DB_LLLIB_EquipmentSlots("Gloves");
DB_LLLIB_EquipmentSlots("Amulet");
DB_LLLIB_EquipmentSlots("Ring2");

PROC
LLLIB_Settings_InitEquipmentSlots()
AND
LLLIB_Array_QRY_EmptyLength("LeaderLib_EquipmentSlots")
AND
DB_LLLIB_EquipmentSlots(_Slot)
THEN
LLLIB_Array_AddToArray("LeaderLib_EquipmentSlots", _Slot);

PROC
LLLIB_Settings_InitStats()
AND
NOT DB_LLLIB_Attributes(_)
THEN
DB_LLLIB_Attributes("Strength");
DB_LLLIB_Attributes("Finesse");
DB_LLLIB_Attributes("Intelligence");
DB_LLLIB_Attributes("Constitution");
DB_LLLIB_Attributes("Memory");
DB_LLLIB_Attributes("Wits");


PROC
LLLIB_Settings_InitStats()
AND
NOT DB_LLLIB_Abilities(_)
THEN
DB_LLLIB_Abilities("WarriorLore");
DB_LLLIB_Abilities("RangerLore");
DB_LLLIB_Abilities("RogueLore");
DB_LLLIB_Abilities("SingleHanded");
DB_LLLIB_Abilities("TwoHanded");
DB_LLLIB_Abilities("Reflection");
DB_LLLIB_Abilities("Ranged");
DB_LLLIB_Abilities("Shield");
DB_LLLIB_Abilities("Reflexes");
DB_LLLIB_Abilities("PhysicalArmorMastery");
DB_LLLIB_Abilities("Sourcery");
DB_LLLIB_Abilities("Telekinesis");
DB_LLLIB_Abilities("FireSpecialist");
DB_LLLIB_Abilities("WaterSpecialist");
DB_LLLIB_Abilities("AirSpecialist");
DB_LLLIB_Abilities("EarthSpecialist");
DB_LLLIB_Abilities("Necromancy");
DB_LLLIB_Abilities("Summoning");
DB_LLLIB_Abilities("Polymorph");
DB_LLLIB_Abilities("Repair");
DB_LLLIB_Abilities("Sneaking");
DB_LLLIB_Abilities("Pickpocket");
DB_LLLIB_Abilities("Thievery");
DB_LLLIB_Abilities("Loremaster");
DB_LLLIB_Abilities("Crafting");
DB_LLLIB_Abilities("Barter");
DB_LLLIB_Abilities("Charm");
DB_LLLIB_Abilities("Intimidate");
DB_LLLIB_Abilities("Reason");
DB_LLLIB_Abilities("Persuasion");
DB_LLLIB_Abilities("Leadership");
DB_LLLIB_Abilities("Luck");
DB_LLLIB_Abilities("DualWielding");
DB_LLLIB_Abilities("Wand");
DB_LLLIB_Abilities("MagicArmorMastery");
DB_LLLIB_Abilities("VitalityMastery");
DB_LLLIB_Abilities("Perseverance");
DB_LLLIB_Abilities("Runecrafting");
DB_LLLIB_Abilities("Brewmaster");
//END_REGION

//REGION UPDATER
IF
StoryEvent(_,"LLLIB_Events_OnUpdateSettings")
AND
LLLIB_Updater_QRY_SettingsNeedsUpdate("LLLIB_Settings_GeneralSettings")
THEN
LLLIB_Settings_InitSettings();
LeaderLog_Log("SYSTEM", "[LLLIB:Settings] Settings updated.");

PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Settings", "DB_LLLIB_Settings_QueueRates", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Settings", "DB_LLLIB_Settings_ItemTemplates", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Settings", "DB_LLLIB_Settings_DialogVariables", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Settings", "DB_LLLIB_Settings_Temp_RegisteredModsList", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Settings", "DB_LLLIB_Settings_Temp_ActiveModText", 3);
LeaderLog_Log("TRACE", "[LLLIB:Settings] Databases registered.");
//END_REGION

//REGION MOD_DATA
IF
ObjectFlagSet("LLLIB_Settings_ListMods", _Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_Settings_ListMods")
THEN
LLLIB_Settings_BuildModList();

PROC
LLLIB_Settings_BuildModList()
AND
DB_LLLIB_Settings_Temp_ActiveModText(_ModName, _Author, _Str)
THEN
NOT DB_LLLIB_Settings_Temp_ActiveModText(_ModName, _Author, _Str);

PROC
LLLIB_Settings_BuildModList()
AND
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_)
THEN
DB_LLLIB_Settings_Temp_RegisteredModsList("");

QRY
LLLIB_Settings_QRY_SetActiveText((STRING)_ModName, (STRING)_Author)
AND
LeaderUpdater_QRY_ModRegisteredActiveGoal(_ModName, _Author)
AND
LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
THEN
DB_LLLIB_Settings_Temp_ActiveModText(_ModName, _Author, " <b>(Active)</b>");

QRY
LLLIB_Settings_QRY_SetActiveText((STRING)_ModName, (STRING)_Author)
AND
LeaderUpdater_QRY_ModRegisteredActiveGoal(_ModName, _Author)
AND
NOT LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
THEN
DB_LLLIB_Settings_Temp_ActiveModText(_ModName, _Author, " <b>(Deactivated)</b>");

QRY
LLLIB_Settings_QRY_SetActiveText((STRING)_ModName, (STRING)_Author)
AND
NOT LeaderUpdater_QRY_ModRegisteredActiveGoal(_ModName, _Author)
THEN
DB_LLLIB_Settings_Temp_ActiveModText(_ModName, _Author, " <b>(No Active Goal Registered)</b>");

PROC
LLLIB_Settings_BuildModList()
AND
DB_Mods_Registered(_ModName, _Author, _Version)
AND
DB_LLLIB_Settings_Temp_RegisteredModsList(_Str)
AND
StringConcatenate("<b>", _ModName, _NameStr)
AND
LLLIB_Settings_QRY_SetActiveText(_ModName, _Author)
AND
DB_LLLIB_Settings_Temp_ActiveModText(_ModName, _Author, _ActiveStr)
AND
LeaderLog_QRY_Log("COMBINE", _Str, _NameStr, "</b> (", _Version, ") by ",_Author, _ActiveStr, "<br>")
AND
DB_LeaderLog_Temp_CombinedString(_CombinedStr)
THEN
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_Str);
DB_LLLIB_Settings_Temp_RegisteredModsList(_CombinedStr);
NOT DB_LeaderLog_Temp_CombinedString(_CombinedStr);
NOT DB_LLLIB_Settings_Temp_ActiveModText(_ModName, _Author, _ActiveStr);

PROC
LLLIB_Settings_BuildModList()
AND
DB_LLLIB_Settings_DialogVariables("ModList", _DialogVar)
AND
DB_LLLIB_Settings_Temp_RegisteredModsList(_Str)
THEN
DialogSetVariableString("LLLIB_SettingsMenu", _DialogVar, _Str);
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_Str);
//END_REGION

//REGION SETTINGS_MENU
IF
DialogStarted("LLLIB_SettingsMenu", _Instance)
THEN
LLLIB_Settings_BuildModList();
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLLIB_11__Start"
