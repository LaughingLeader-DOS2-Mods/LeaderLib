Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_EffectFlags_InitSettings();

//DB_LeaderLib_Effects_EffectFlags(_ID, _Group, _Flag, _Effect, _Loop, _BoneName)
//DB_LeaderLib_Effects_StopGroupFlags(_ID, _Group, _StopFlag)
KBSECTION
//REGION SETTINGS
PROC
LLLIB_EffectFlags_InitSettings()
THEN
LeaderUpdater_SaveSettingsVersionData("LLLIB_Settings_Effects");
LeaderLib_Effects_Register_StopGroupFlag("LeaderLib.Default", "CameraShake", "LeaderLib_Effects_StopCameraShake");
LeaderLib_Effects_Register_EffectFlag("LeaderLib.Default", "CameraShake", "LeaderLib_Effects_CameraShake_Loop_Normal", "RS3_FX_GP_ScriptedEvent_CameraShake_Loop_00-50", 1, "");
//END_REGION

//REGION UPDATING
IF
StoryEvent(_,"LeaderLib_Events_Internal_OnUpdateSettings")
AND
LeaderUpdater_QRY_SettingsNeedsUpdate("LLLIB_Settings_Effects")
THEN
LLLIB_EffectFlags_InitSettings();
//END_REGION

//REGION REGISTERING
PROC
LeaderLib_Effects_Register_EffectFlag((STRING)_ID, (STRING)_Group, (STRING)_Flag, (STRING)_Effect, (INTEGER)_Loop, (STRING)_BoneName)
AND
DB_LeaderLib_Effects_EffectFlags(_OtherID, _OtherGroup, _Flag, _OtherEffect, _OtherLoop, _OtherBoneName)
THEN
NOT DB_LeaderLib_Effects_EffectFlags(_OtherID, _OtherGroup, _Flag, _OtherEffect, _OtherLoop, _OtherBoneName);

PROC
LeaderLib_Effects_Register_EffectFlag((STRING)_ID, (STRING)_Group, (STRING)_Flag, (STRING)_Effect, (INTEGER)_Loop, (STRING)_BoneName)
THEN
DB_LeaderLib_Effects_EffectFlags(_ID, _Group, _Flag, _Effect, _Loop, _BoneName);

PROC
LeaderLib_Effects_Register_StopGroupFlag((STRING)_ID, (STRING)_Group, (STRING)_Flag)
THEN
DB_LeaderLib_Effects_StopGroupFlags(_ID, _Group, _Flag);
//END_REGION

//REGION EFFECT_PROCS
PROC
LLLIB_Effects_Internal_ParseEffect((GUIDSTRING)_Target, (STRING)_Effect, (INTEGER)_Loop, (STRING)_BoneName)
AND
_Loop > 0
AND
DB_CurrentLevel(_Region)
AND
GetUUID(_Target, _UUID)
AND
LeaderLog_QRY_Log("COMBINE", _UUID, "_", _Effect, "_", _BoneName)
AND
DB_LeaderLog_Temp_CombinedString(_ID)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_ID);
LeaderLog_Log("DEBUG", "[LLLIB:EffectFlags:ParseEffect] Playing loop effect [",_Effect,"] on target [",_UUID,"].");
PROC_LoopEffect(_Effect, _Target, _ID, _Region, _BoneName);

PROC
LLLIB_Effects_Internal_ParseEffect((GUIDSTRING)_Target, (STRING)_Effect, (INTEGER)_Loop, (STRING)_BoneName)
AND
_Loop <= 0
THEN
LeaderLog_Log("DEBUG", "[LLLIB:EffectFlags:ParseEffect] Playing effect [",_Effect,"] on target.");
PlayEffect(_Target, _Effect, _BoneName);

PROC
LeaderLib_Effects_StopEffect((GUIDSTRING)_Target, (STRING)_Effect)
AND
DB_LoopEffect(_Target, _fxHandle, _ID, _Region, _Effect, _BoneName)
THEN
PROC_StopLoopEffect(_Target, _ID);

PROC
LeaderLib_Effects_StopEffect((GUIDSTRING)_Target, (STRING)_Effect, (STRING)_BoneName)
AND
DB_LoopEffect(_Target, _fxHandle, _ID, _Region, _Effect, _BoneName)
THEN
PROC_StopLoopEffect(_Target, _ID);

PROC
LeaderLib_Effects_StopGroupEffects((GUIDSTRING)_Target, (STRING)_Group)
AND
DB_LoopEffect(_Target, _fxHandle, _ID, _Region, _Effect, _BoneName)
THEN
PROC_StopLoopEffect(_Target, _ID);
//END_REGION

//REGION EFFECT_FLAGS
IF
ObjectFlagSet(_Flag, _Target, _)
AND
DB_LeaderLib_Effects_EffectFlags(_ID, _Group, _Flag, _Effect, _Loop, _BoneName)
THEN
LLLIB_Effects_Internal_ParseEffect(_Target, _Effect, _Loop, _BoneName);

IF
ObjectFlagCleared(_Flag, _Target, _)
AND
DB_LeaderLib_Effects_EffectFlags(_ID, _Group, _Flag, _Effect, _Loop, _BoneName)
THEN
LeaderLib_Effects_StopEffect(_Target, _Effect, _BoneName);

IF
ObjectFlagSet(_StopFlag, _Target, _)
AND
DB_LeaderLib_Effects_StopGroupFlags(_ID, _Group, _StopFlag)
THEN
ObjectClearFlag(_Target, _StopFlag);
LeaderLib_Effects_StopGroupEffects(_Target, _Group);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLLIB_11__Start"
