Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_LeaderTrader_InitSettings();
LLLIB_LeaderTrader_RegisterTraderSettings();
//DB_LLLIB_LeaderTrader(_LeaderTrader)
//DB_LLLIB_LeaderTrader_MetPlayer(_Player)

//Settings
//DB_LLLIB_LeaderTrader_Dialog(_ReferenceName, _Dialog)
//DB_LLLIB_LeaderTrader_Appearance(_Level, _Template)
//DB_LLLIB_LeaderTrader_IntroDialogRequirements(_Level, _Dialog, _Flag, _RequirementID)
//Dynamic
//DB_LLLIB_LeaderTrader_CurrentAppearance(_Template)
/*Non-Global Workaround*/
//DB_LLLIB_LeaderTrader_SetFlags(_Flag, _Val)
//DB_LLLIB_LeaderTrader_SetTags(_Tag, _Val)
KBSECTION
//REGION INIT
PROC
LLLIB_LeaderTrader_InitSettings()
THEN
LeaderUpdater_SaveSettingsVersionData("LLLIB_Settings_LeaderTrader");

LeaderLib_Helper_ClearExistingDatabase("DB_LLLIB_LeaderTrader_Dialog", 2);
DB_LLLIB_LeaderTrader_Dialog("Default", "LLLIB_LeaderTrader_Default");
DB_LLLIB_LeaderTrader_Dialog("Tutorial_Intro", "LLLIB_LeaderTrader_Tutorial_Intro");
DB_LLLIB_LeaderTrader_Dialog("FortJoy_Intro", "LLLIB_LeaderTrader_FortJoy_Intro");
DB_LLLIB_LeaderTrader_Dialog("LV_Intro", "LLLIB_LeaderTrader_LadyVengeance_Intro");
DB_LLLIB_LeaderTrader_Dialog("Arx_Intro", "LLLIB_LeaderTrader_Arx_Intro");

LeaderLib_Helper_ClearExistingDatabase("DB_LLLIB_LeaderTrader_Appearance", 2);
//Naked version
DB_LLLIB_LeaderTrader_Appearance("TUT_Tutorial_A", "LLLIB_LeaderTrader_Default_e4cbf1f4-4eea-457c-93a3-ded05ae3605e");
DB_LLLIB_LeaderTrader_Appearance("LeaderLib_TestLevel", "LLLIB_LeaderTrader_Default_e4cbf1f4-4eea-457c-93a3-ded05ae3605e");
//For other acts
DB_LLLIB_LeaderTrader_Appearance("Default", "LLLIB_LeaderTrader_Casual_da17fca2-fe16-46d9-80b3-08971a8c8d49");

LeaderLib_Helper_ClearExistingDatabase("DB_LLLIB_LeaderTrader_IntroDialogRequirements", 4);
DB_LLLIB_LeaderTrader_IntroDialogRequirements("TUT_Tutorial_A", "LLLIB_LeaderTrader_Tutorial_Intro", "LLLIB_LeaderTrader_TUT_IntroDone", "LLLIB_LeaderTrader_TUT_IntroPending");
DB_LLLIB_LeaderTrader_IntroDialogRequirements("FJ_FortJoy_Main", "LLLIB_LeaderTrader_FortJoy_Intro", "LLLIB_LeaderTrader_FTJ_IntroDone", "LLLIB_LeaderTrader_FTJ_IntroPending");
DB_LLLIB_LeaderTrader_IntroDialogRequirements("LV_HoE_Main", "LLLIB_LeaderTrader_LadyVengeance_Intro", "LLLIB_LeaderTrader_LadyVengeance_IntroDone", "LLLIB_LeaderTrader_LadyVengeance_IntroPending");
DB_LLLIB_LeaderTrader_IntroDialogRequirements("RC_Main", "LLLIB_LeaderTrader_LadyVengeance_Intro", "LLLIB_LeaderTrader_LadyVengeance_IntroDone", "LLLIB_LeaderTrader_LadyVengeance_IntroPending");
DB_LLLIB_LeaderTrader_IntroDialogRequirements("CoS_Main", "LLLIB_LeaderTrader_LadyVengeance_Intro", "LLLIB_LeaderTrader_LadyVengeance_IntroDone", "LLLIB_LeaderTrader_LadyVengeance_IntroPending");
DB_LLLIB_LeaderTrader_IntroDialogRequirements("Arx_Main", "LLLIB_LeaderTrader_Arx_Intro", "LLLIB_LeaderTrader_Arx_IntroDone", "LLLIB_LeaderTrader_Arx_IntroPending");
//END_REGION

//REGION REGISTER
/*Dialog and Dialog Flags*/
PROC
LLLIB_LeaderTrader_RegisterDialogSettings()
AND
DB_LLLIB_LeaderTrader_IntroDialogRequirements(_Level, _Dialog, _Flag, _RequirementID)
THEN
LeaderLib_Trader_Register_Dialog("LLLIB_LeaderTrader", _Dialog, _Level, _RequirementID);
LeaderLib_Requirements_Add_FlagRequirement(_RequirementID, _Flag, "Global", 0);

PROC
LLLIB_LeaderTrader_RegisterTraderSettings()
AND
NOT DB_LLLIB_LeaderTrader_Registered(_)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:LeaderTrader:RegisterTraderSettings] Registering LeaderTrader treasure/trader settings.");
DB_LLLIB_LeaderTrader_Registered(1);

DB_LLLIB_LeaderTrader_CurrentAppearance("LLLIB_LeaderTrader_Default_e4cbf1f4-4eea-457c-93a3-ded05ae3605e");

LeaderLib_Trader_Register_Dialog("LLLIB_LeaderTrader", "LLLIB_LeaderTrader_Default"); // Default
LLLIB_LeaderTrader_RegisterDialogSettings();
LeaderLib_Trader_Register_Dialog("LLLIB_LeaderTrader", "LLLIB_LeaderTrader_Tutorial_Intro", "LeaderLib_TestLevel", "LLLIB_LeaderTrader_TUT_IntroPending"); // Debug

LeaderLib_Trader_Register_CreationEvent("LLLIB_LeaderTrader", "LeaderLib_Events_OnLeaderTradeSpawned");

//Backup positions, in case the position objects were destroyed
LeaderLib_Trader_Register_Position("LLLIB_LeaderTrader", "TUT_Tutorial_A", 31.19, 4.0, -252.77);
LeaderLib_Trader_Register_Position("LLLIB_LeaderTrader", "FJ_FortJoy_Main", 220.01, -16.79, 362.97);
LeaderLib_Trader_Register_Position("LLLIB_LeaderTrader", "LV_HoE_Main", 341.49, 7.81, 581.80);
LeaderLib_Trader_Register_Position("LLLIB_LeaderTrader", "RC_Main", 746.05, 8.11, -37.73);
LeaderLib_Trader_Register_Position("LLLIB_LeaderTrader", "CoS_Main", -19.44, 7.46, 674.49);
LeaderLib_Trader_Register_Position("LLLIB_LeaderTrader", "Arx_Main", 426.14, 40.12, 24.94);

LeaderLib_Trader_Register_PositionObject("LLLIB_LeaderTrader", "FJ_FortJoy_Main", CHARACTERGUID_S_FTJ_StartingAreaChild_39cbb883-7e80-445c-b5f8-0e42d2283d73);

LeaderLib_Requirements_Add_FlagRequirement("LLLIB_LeaderTrader_WindegoHasNotCastSpell", "TUT_LowerDeck_WindegoHasCastSpell", "Global", 0);
LeaderLib_Requirements_Add_FlagRequirement("LLLIB_LeaderTrader_PartyFoundSeekerCamp", "FTJ_SW_BeenToShelter", "Object", 1);
LeaderLib_Requirements_Add_RegionRequirement("LLLIB_RegionIsFortJoy", "FJ_FortJoy_Main");
LeaderLib_Requirements_Add_RegionRequirement("LLLIB_RegionIsDebugLevel", "LeaderLib_TestLevel");

LeaderLib_Trader_Register_StartingGold("LLLIB_LeaderTrader", "LeaderLib_TestLevel", 20000);
LeaderLib_Trader_Register_StartingGold("LLLIB_LeaderTrader", "TUT_Tutorial_A", 50);
LeaderLib_Trader_Register_StartingGold("LLLIB_LeaderTrader", "FJ_FortJoy_Main", 100);
/*
LeaderLib_Trader_Register_StartingGold("LLLIB_LeaderTrader", "FJ_FortJoy_Main", 4000, "LLLIB_LeaderTrader_PartyFoundSeekerCamp");
LeaderLib_Trader_Register_StartingGold("LLLIB_LeaderTrader", "LV_HoE_Main", 4500);
LeaderLib_Trader_Register_StartingGold("LLLIB_LeaderTrader", "RC_Main", 5000);
LeaderLib_Trader_Register_StartingGold("LLLIB_LeaderTrader", "CoS_Main", 10000);
LeaderLib_Trader_Register_StartingGold("LLLIB_LeaderTrader", "Arx_Main", 13000);
*/

LeaderLib_Treasure_Register_TreasureToTrader("LLLIB_LeaderTrader_Misc", "LLLIB_LeaderTrader");
LeaderLib_Treasure_Register_TreasureToTrader("LLLIB_LeaderTrader_ModItems", "LLLIB_LeaderTrader");
LeaderLib_Treasure_Register_TreasureToTrader("LLLIB_LeaderTrader_ModBooks", "LLLIB_LeaderTrader");
LeaderLib_Treasure_Register_TreasureToTrader("LeaderLib.LeaderTrader.Skillbooks", "LLLIB_LeaderTrader");

LeaderLib_Treasure_Register_ItemTemplate("LeaderLib.LeaderTrader.Skillbooks", "BOOK_Skill_LLLIB_ControlSummon_832058fd-fd40-47d0-bea3-0e108761d4e4", 2);

//LeaderLib_Treasure_Configure_DefaultGenerationType("LLLIB_LeaderTrader_ModItems", "DIALOG_STARTED");
//LeaderLib_Treasure_Configure_DefaultGenerationType("LLLIB_LeaderTrader_ModBooks", "DIALOG_STARTED");

LeaderLib_Treasure_Register_ItemTemplate("LLLIB_LeaderTrader_ModBooks", "LLLIB_BOOK_ModMenu_646a1194-3383-47fa-a379-ebefa3d2b108", 1);
LeaderLib_Treasure_Configure_AddMaxAmount("LLLIB_LeaderTrader_ModBooks", "LLLIB_BOOK_ModMenu_646a1194-3383-47fa-a379-ebefa3d2b108", 4);
//LeaderLib_Treasure_Configure_GenerationType("LLLIB_LeaderTrader_ModBooks", "LLLIB_BOOK_ModMenu_646a1194-3383-47fa-a379-ebefa3d2b108", "DIALOG_STARTED");
//LeaderLib_Treasure_Register_TreasureTable("LLLIB_LeaderTrader_Misc", "ST_WeaponNormal", "LLLIB_RegionIsFortJoy", 1);
//LeaderLib_Treasure_Register_TreasureTable("LLLIB_LeaderTrader_Misc", "TEST_Generation", "LLLIB_RegionIsDebugLevel", 1);
//LeaderLib_Treasure_Register_ItemTemplate("LLLIB_LeaderTrader_ModBooks", "CON_Potion_Fire_Res_A_e78b642c-0216-4df4-b691-a41ff4747b6a", 8);
//LeaderLib_Treasure_Register_ItemStat("LLLIB_LeaderTrader_ModBooks", "WPN_Cheat_Sword_1H_RuneSlot2", 1);

LLLIB_LeaderTrader_RegisterDeckSeat("LeaderLib_TestLevel", ITEMGUID_LLLIB_DebugLevel_TraderChair_8468a5fd-00a4-42c2-aa15-1b1a25e46738);
LLLIB_LeaderTrader_RegisterDeckSeat("TUT_Tutorial_A", ITEMGUID_S_TUT_KitchenBench_e628d79a-8f7b-4681-a55f-57f0b42a29db, "LLLIB_LeaderTrader_WindegoHasNotCastSpell");
LLLIB_LeaderTrader_RegisterDeckSeat("LV_HoE_Main", ITEMGUID_FUR_Humans_Citz_Stool_C_005_a2a11ad5-645b-4719-bfd5-be04f32fbb2a);
LLLIB_LeaderTrader_RegisterDeckSeat("RC_Main", ITEMGUID_FUR_Humans_Citz_Stool_C_005_a2a11ad5-645b-4719-bfd5-be04f32fbb2a);
LLLIB_LeaderTrader_RegisterDeckSeat("CoS_Main", ITEMGUID_FUR_Humans_Citz_Stool_C_005_a2a11ad5-645b-4719-bfd5-be04f32fbb2a);
LLLIB_LeaderTrader_RegisterDeckSeat("Arx_Main", ITEMGUID_FUR_Humans_Citz_Stool_C_005_a2a11ad5-645b-4719-bfd5-be04f32fbb2a);

PROC
LLLIB_LeaderTrader_RegisterDeckSeat((STRING)_LevelName, (ITEMGUID)_Seat)
THEN
LLLIB_LeaderTrader_RegisterDeckSeat(_LevelName, _Seat, "");

PROC
LLLIB_LeaderTrader_RegisterDeckSeat((STRING)_LevelName, (ITEMGUID)_Seat, (STRING)_RequirementID)
AND
NOT DB_LLLIB_Traders_Seat("LLLIB_LeaderTrader", _LevelName, _, _)
THEN
LeaderLib_Trader_Register_Level("LLLIB_LeaderTrader", _LevelName, _RequirementID);
LeaderLib_Trader_Register_PositionObject("LLLIB_LeaderTrader", _LevelName, (GUIDSTRING)_Seat);
LeaderLib_Trader_Register_Seat("LLLIB_LeaderTrader", _LevelName, (ITEMGUID)_Seat);

IF
GameStarted(_,_)
THEN
LLLIB_LeaderTrader_RegisterTrader();
LLLIB_LeaderTrader_UpdateSettings();

PROC
LLLIB_LeaderTrader_RegisterTrader()
AND
NOT DB_LLLIB_LeaderTrader(_)
AND
ObjectExists(CHARACTERGUID_S_LLLIB_LeaderTrader_61ae5acc-1537-4970-82bb-d408a3334574, 1)
THEN
DB_LLLIB_LeaderTrader((CHARACTERGUID)CHARACTERGUID_S_LLLIB_LeaderTrader_61ae5acc-1537-4970-82bb-d408a3334574);
LeaderLib_Trader_Register_GlobalTrader("LLLIB_LeaderTrader", CHARACTERGUID_S_LLLIB_LeaderTrader_61ae5acc-1537-4970-82bb-d408a3334574);
LeaderLog_Log("DEBUG", "[LeaderLib:LeaderTrader] Registered the global leader trader to the trader database.");

//If the global version doesn't exist for some reason, use the template
PROC
LLLIB_LeaderTrader_RegisterTrader()
AND
NOT DB_LLLIB_LeaderTrader(_)
AND
ObjectExists(CHARACTERGUID_S_LLLIB_LeaderTrader_61ae5acc-1537-4970-82bb-d408a3334574, 0)
THEN
LeaderLib_Trader_Register_Trader("LLLIB_LeaderTrader", "LLLIB_LeaderTrader_Default_e4cbf1f4-4eea-457c-93a3-ded05ae3605e", 0);
LeaderLog_Log("DEBUG", "[LeaderLib:LeaderTrader] [ERROR] Global trader version doesn't exist! Using the template version.");
GlobalSetFlag("LLLIB_LeaderTraderIsNotGlobal");
//END_REGION

//REGION SETTINGS_UPDATE
//For updating treasure/trader settings on existing saves
PROC
LLLIB_LeaderTrader_UpdateSettings()
THEN
LLLIB_LeaderTrader_Internal_RegisterNewTreasure();

PROC
LLLIB_LeaderTrader_Internal_RegisterNewTreasure()
AND
NOT DB_LLLIB_Treasure_RegisteredTraderTreasure("LeaderLib.LeaderTrader.Skillbooks", "LLLIB_LeaderTrader")
THEN
LeaderLib_Treasure_Register_TreasureToTrader("LeaderLib.LeaderTrader.Skillbooks", "LLLIB_LeaderTrader");
LeaderLib_Treasure_Register_ItemTemplate("LeaderLib.LeaderTrader.Skillbooks", "BOOK_Skill_LLLIB_ControlSummon_832058fd-fd40-47d0-bea3-0e108761d4e4", 2);
//END_REGION

//REGION NON_GLOBAL_WORKAROUND
IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_LeaderTrader, _)
AND
GlobalGetFlag("LLLIB_LeaderTraderIsNotGlobal", 1)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
DB_LLLIB_LeaderTrader_SetFlags(_Flag, 1);

IF
ObjectFlagCleared(_Flag, (CHARACTERGUID)_LeaderTrader, _)
AND
GlobalGetFlag("LLLIB_LeaderTraderIsNotGlobal", 1)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_LLLIB_LeaderTrader_SetFlags(_Flag, 1)
THEN
NOT DB_LLLIB_LeaderTrader_SetFlags(_Flag, 1);
DB_LLLIB_LeaderTrader_SetFlags(_Flag, 0);

IF
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
GlobalGetFlag("LLLIB_LeaderTraderIsNotGlobal", 1)
AND
DB_LLLIB_LeaderTrader_SetFlags(_Flag, 1)
THEN
ObjectSetFlag(_LeaderTrader, _Flag);

IF
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
GlobalGetFlag("LLLIB_LeaderTraderIsNotGlobal", 1)
AND
DB_LLLIB_LeaderTrader_SetFlags(_Flag, 0)
THEN
ObjectClearFlag(_LeaderTrader, _Flag);

IF
ObjectWasTagged((CHARACTERGUID)_LeaderTrader, _Tag)
AND
GlobalGetFlag("LLLIB_LeaderTraderIsNotGlobal", 1)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
DB_LLLIB_LeaderTrader_SetTags(_Tag, 1);

IF
ObjectLostTag((CHARACTERGUID)_LeaderTrader, _Tag)
AND
GlobalGetFlag("LLLIB_LeaderTraderIsNotGlobal", 1)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_LLLIB_LeaderTrader_SetTags(_Tag, 1)
THEN
NOT DB_LLLIB_LeaderTrader_SetTags(_Tag, 1);
DB_LLLIB_LeaderTrader_SetTags(_Tag, 0);

IF
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
GlobalGetFlag("LLLIB_LeaderTraderIsNotGlobal", 1)
AND
DB_LLLIB_LeaderTrader_SetTags(_Tag, 1)
THEN
SetTag(_LeaderTrader, _Tag);

IF
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
GlobalGetFlag("LLLIB_LeaderTraderIsNotGlobal", 1)
AND
DB_LLLIB_LeaderTrader_SetTags(_Tag, 0)
THEN
ClearTag(_LeaderTrader, _Tag);
//END_REGION

//REGION DEFAULT_DIALOG_GREETING_CHANGE
IF
DialogStarted(_Dialog, _Instance)
AND
DB_LLLIB_LeaderTrader_Dialog("Default", _Dialog)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
DB_LLLIB_LeaderTrader_Temp_ActiveDialog(_Player, _Instance);
LLLIB_LeaderTrader_AdjustGreeting(_Instance, _Player);

PROC
LLLIB_LeaderTrader_AdjustGreeting((INTEGER)_Instance, (CHARACTERGUID)_Player)
AND
DB_LLLIB_LeaderTrader_MetPlayer(_Player)
AND
DB_LeaderLib_Dialog_Vars("LeaderTrader_Greeting", _Var)
THEN
DialogSetVariableStringForInstance(_Instance, _Var, "Welcome back! Looking to trade?");

PROC
LLLIB_LeaderTrader_AdjustGreeting((INTEGER)_Instance, (CHARACTERGUID)_Player)
AND
NOT DB_LLLIB_LeaderTrader_MetPlayer(_Player)
AND
DB_LeaderLib_Dialog_Vars("LeaderTrader_Greeting", _Var)
THEN
DialogSetVariableStringForInstance(_Instance, _Var, "Hello! Looking to trade?");

IF
ObjectFlagSet("StartTrade", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLLIB_LeaderTrader_Temp_ActiveDialog(_Player, _Instance)
AND
DB_LeaderLib_Dialog_Vars("LeaderTrader_Greeting", _Var)
THEN
DialogSetVariableStringForInstance(_Instance, _Var, "Anything else?");

IF
RequestTrade(_Player, _LeaderTrader)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_LLLIB_LeaderTrader_Temp_ActiveDialog(_Player, _Instance)
AND
DB_LeaderLib_Dialog_Vars("LeaderTrader_Greeting", _Var)
THEN
DialogSetVariableStringForInstance(_Instance, _Var, "Anything else?");

IF
DialogEnded(_Dialog, _Instance)
AND
DB_LLLIB_LeaderTrader_Temp_ActiveDialog(_Player, _Instance)
THEN
NOT DB_LLLIB_LeaderTrader_Temp_ActiveDialog(_Player, _Instance);
//END_REGION

//REGION AVOID_COMBAT
IF
CombatStarted(_CombatID)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
CharacterIsInCombat(_LeaderTrader, 1)
AND
CombatGetIDForCharacter(_LeaderTrader, _CombatID)
AND
ObjectGetFlag(_LeaderTrader, "LLLIB_LeaderTrader_Hiding", 0)
THEN
DB_LLLIB_LeaderTrader_AvoidingCombat(_CombatID);
DisplayText(_LeaderTrader, "I'm retired! Leave me be.");
CharacterUseSkill(_LeaderTrader, "Shout_ChameleonSkin", _LeaderTrader, 0, 1, 1);
ObjectSetFlag(_LeaderTrader, "LLLIB_LeaderTrader_Hiding", 0);

IF
CharacterStatusApplied(_LeaderTrader, "INVISIBLE", _)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
ObjectGetFlag(_LeaderTrader, "LLLIB_LeaderTrader_Hiding", 1)
THEN
SetOnStage(_LeaderTrader, 0);

IF
CombatEnded(_CombatID)
AND
DB_LLLIB_LeaderTrader_AvoidingCombat(_CombatID)
THEN
TimerLaunch("LLLIB_Timers_LeaderTrader_CombatEnded", 3000);
NOT DB_LLLIB_LeaderTrader_AvoidingCombat(_CombatID);

IF
TimerFinished("LLLIB_Timers_LeaderTrader_CombatEnded")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
SetOnStage(_LeaderTrader, 1);
ObjectClearFlag(_LeaderTrader, "LLLIB_LeaderTrader_Hiding");
ObjectSetFlag(_LeaderTrader, "LLLIB_LeaderTrader_WasHiding", 0);
ApplyStatus(_LeaderTrader, "INVISIBLE", 6.0, 1);

IF
CharacterStatusRemoved(_LeaderTrader, "INVISIBLE", _)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
ObjectGetFlag(_LeaderTrader, "LLLIB_LeaderTrader_WasHiding", 1)
THEN
DisplayText(_LeaderTrader, "Hrmph...");
ObjectClearFlag(_LeaderTrader, "LLLIB_LeaderTrader_WasHiding");

IF
ObjectFlagCleared("LLLIB_LeaderTrader_WasHiding", (CHARACTERGUID)_LeaderTrader, _)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_LLLIB_Traders_Spawned(_LeaderTrader, _TraderID, _Level)
AND
DB_LLLIB_Traders_Seat(_TraderID, _Level, _Seat, _RequirementID)
THEN
//Reset the chair logic
ClearVarObject(_LeaderTrader, "Seat");
SetVarObject(_LeaderTrader, "Seat", _Seat);
//END_REGION

//REGION ATTACKED
IF
AttackedByObject((CHARACTERGUID)_LeaderTrader, (CHARACTERGUID)_AttackerOwner, _Attacker, _DamageType, _DamageSource)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
CharacterIsPlayer(_AttackerOwner, 1)
AND
NOT DB_LLLIB_LeaderTrader_IsRespondingToAttack(_)
THEN
DB_LLLIB_LeaderTrader_IsRespondingToAttack(1);
Proc_StartDialog(1, "LLLIB_LeaderTrader_AttackResponse", _LeaderTrader);

IF
AutomatedDialogEnded("LLLIB_LeaderTrader_AttackResponse", _Instance)
AND
DB_LLLIB_LeaderTrader_IsRespondingToAttack(_Val)
THEN
NOT DB_LLLIB_LeaderTrader_IsRespondingToAttack(_Val);
//END_REGION

//REGION SPAWN_EVENT
IF
StoryEvent((CHARACTERGUID)_Trader, "LeaderLib_Events_OnLeaderTradeSpawned")
AND
DB_CurrentLevel(_Level)
THEN
LLLIB_LeaderTrader_OnSpawned(_Trader, _Level);

IF
StoryEvent((CHARACTERGUID)_Trader, "LeaderLib_Events_OnLeaderTradeSpawned")
AND
DB_CurrentLevel("TUT_Tutorial_A")
THEN
LeaderLib_Helper_UnequipWeapons(_Trader);

IF
StoryEvent((CHARACTERGUID)_Trader, "LeaderLib_Events_OnLeaderTradeSpawned")
AND
DB_CurrentLevel("FJ_FortJoy_Main")
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:LeaderTrader] Walking to position in FortJoy.");
CharacterMoveToPosition(_Trader, 220.01, -16.79, 362.97, 0, "LLLIB_Events_LeaderTraderFinishedMoving");
TimerLaunch("LLLIB_LeaderTrader_MoveFailsafeTimer", 10000);

IF
StoryEvent((CHARACTERGUID)_LeaderTrader, "LLLIB_Events_LeaderTraderFinishedMoving")
THEN
TimerCancel("LLLIB_LeaderTrader_MoveFailsafeTimer");

IF
TimerFinished("LLLIB_LeaderTrader_MoveFailsafeTimer")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
GetPosition(_LeaderTrader, _x,_y,_z)
THEN
//Hopefully cancel walking
CharacterMoveToPosition(_LeaderTrader, _x,_y,_z, 0, "");

IF
DB_LLLIB_Traders_Spawned(_LeaderTrader, "LLLIB_LeaderTrader", _Level)
THEN
LLLIB_LeaderTrader_Set(_LeaderTrader);

PROC
LLLIB_LeaderTrader_Set((CHARACTERGUID)_Character)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
_Character != _LeaderTrader
AND
ObjectExists(_LeaderTrader, 0)
THEN
NOT DB_LLLIB_LeaderTrader(_LeaderTrader);

PROC
LLLIB_LeaderTrader_Set((CHARACTERGUID)_Character)
AND
NOT DB_LLLIB_LeaderTrader(_)
THEN
DB_LLLIB_LeaderTrader(_Character);
//END_REGION

//REGION APPEARANCE_CHANGING
PROC
LLLIB_LeaderTrader_SetAppearance((CHARACTERGUID)_LeaderTrader, (STRING)_NextTemplate)
AND
DB_LLLIB_LeaderTrader_CurrentAppearance(_Template)
AND
_NextTemplate != _Template
THEN
NOT DB_LLLIB_LeaderTrader_CurrentAppearance(_Template);

PROC
LLLIB_LeaderTrader_SetAppearance((CHARACTERGUID)_LeaderTrader, (STRING)_Template)
AND
NOT DB_LLLIB_LeaderTrader_CurrentAppearance(_Template)
THEN
CharacterTransform(_LeaderTrader, _Template, 1, 1, 1, 1, 1, 1, 0);
DB_LLLIB_LeaderTrader_CurrentAppearance(_Template);

IF
StoryEvent((CHARACTERGUID)_LeaderTrader, "LeaderLib_Events_GenerationComplete")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:LeaderTrader] Treasure generation complete.");

PROC
LLLIB_LeaderTrader_OnSpawned((CHARACTERGUID)_LeaderTrader, (STRING)_Level)
AND
DB_LLLIB_LeaderTrader_Appearance(_Level, _Template)
THEN
LLLIB_LeaderTrader_SetAppearance(_LeaderTrader, _Template);

PROC
LLLIB_LeaderTrader_OnSpawned((CHARACTERGUID)_LeaderTrader, (STRING)_Level)
AND
NOT DB_LLLIB_LeaderTrader_Appearance(_Level, _)
AND
DB_LLLIB_LeaderTrader_Appearance("Default", _Template)
THEN
LLLIB_LeaderTrader_SetAppearance(_LeaderTrader, _Template);

PROC
LLLIB_LeaderTrader_OnSpawned((CHARACTERGUID)_LeaderTrader, "TUT_Tutorial_A")
THEN
LeaderLib_Treasure_CreateItemByStat_Cleanup(_LeaderTrader);
LeaderLib_Treasure_CreateItemByStat_AddToQueue(_LeaderTrader, "ARM_Purge_UpperBody", 1, 0, 1);
LeaderLib_Treasure_CreateItemByStat_AddToQueue(_LeaderTrader, "ARM_Purge_LowerBody", 1, 0, 1);
LeaderLib_Treasure_CreateItemByStat_Start(_LeaderTrader, 100);
DB_TUT_LowerDeck_WindegoSpareCharacters(_LeaderTrader); // To avoid exploding

PROC
LLLIB_LeaderTrader_OnSpawned((CHARACTERGUID)_LeaderTrader, "LeaderLib_TestLevel")
THEN
LeaderLib_Treasure_CreateItemByStat_Cleanup(_LeaderTrader);
//CharacterTransform(_LeaderTrader, "LLLIB_LeaderTrader_Act1_da17fca2-fe16-46d9-80b3-08971a8c8d49", 1, 1, 1, 1, 1, 1, 0);
LeaderLib_Treasure_CreateItemByStat_AddToQueue(_LeaderTrader, "ARM_Purge_UpperBody", 1, 0, 1);
LeaderLib_Treasure_CreateItemByStat_AddToQueue(_LeaderTrader, "ARM_Purge_LowerBody", 1, 0, 1);
LeaderLib_Treasure_CreateItemByStat_Start(_LeaderTrader, 100);

PROC
LLLIB_LeaderTrader_OnSpawned((CHARACTERGUID)_LeaderTrader, (STRING)_Level)
THEN
ApplyStatus(_LeaderTrader, "LLLIB_PACIFIST", -1.0, 1);
LeaderLib_Helper_RefreshEquipment(_LeaderTrader);

IF
RegionEnded(_Level)
AND
DB_LLLIB_LeaderTrader_CurrentAppearance(_Template)
THEN
NOT DB_LLLIB_LeaderTrader_CurrentAppearance(_Template);
//END_REGION

//REGION TUTORIAL
IF
GlobalFlagSet("TUT_LowerDeck_WindegoHasCastSpell")
AND
DB_LLLIB_Traders_Spawned(_LeaderTrader, "LLLIB_LeaderTrader", _Level)
THEN
DisplayText(_LeaderTrader, "Hrmph. Best to avoid trouble...");
CharacterUseSkill(_LeaderTrader, "Shout_ChameleonSkin", _LeaderTrader, 0, 1, 1);
TimerLaunch("LLLIB_Timeers_LeaderTrader_TUTDisappear", 2000);

IF
TimerFinished("LLLIB_Timeers_LeaderTrader_TUTDisappear")
AND
DB_LLLIB_Traders_Spawned(_LeaderTrader, "LLLIB_LeaderTrader", "TUT_Tutorial_A")
THEN
NOT DB_LLLIB_Traders_Spawned(_LeaderTrader, "LLLIB_LeaderTrader", "TUT_Tutorial_A");

IF
TimerFinished("LLLIB_Timeers_LeaderTrader_TUTDisappear")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
SetOnStage(_LeaderTrader, 0);

IF
RegionEnded("TUT_Tutorial_A")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_TUT_LowerDeck_WindegoSpareCharacters(_LeaderTrader)
THEN
NOT DB_TUT_LowerDeck_WindegoSpareCharacters(_LeaderTrader);
//END_REGION

//REGION KNOWING
IF
DialogEnded(_Dialog, _Instance)
AND
DB_LLLIB_LeaderTrader_Dialog(_ReferenceName, _Dialog)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
NOT DB_LLLIB_LeaderTrader_MetPlayer(_Player)
THEN
DB_LLLIB_LeaderTrader_MetPlayer(_Player);

IF
ObjectFlagSet(_Flag, _Speaker, _Instance)
AND
DB_LLLIB_LeaderTrader_IntroDialogRequirements(_Level, _Dialog, _Flag, _RequirementID)
AND
ObjectGetFlag(_Speaker, "LLLIB_LeaderTrader_PlayerIntroducedSelf", 1)
THEN
ObjectSetFlag(_Speaker, "LLLIB_LeaderTrader_KnowsPlayer");
//END_REGION

//REGION DIALOG_TRADE_TOGGLING
//Disable trading until the player and trader have talked
IF
DB_Dialogs(_LeaderTrader, _Dialog)
AND
DB_LLLIB_LeaderTrader((CHARACTERGUID)_LeaderTrader)
AND
DB_LLLIB_LeaderTrader_IntroDialogRequirements(_Level, _Dialog, _Flag, _RequirementID)
THEN
CharacterSetCanTrade(_LeaderTrader, 0);

IF
DB_Dialogs(_LeaderTrader, "LLLIB_LeaderTrader_Default")
AND
DB_LLLIB_LeaderTrader((CHARACTERGUID)_LeaderTrader)
THEN
CharacterSetCanTrade(_LeaderTrader, 1);
//END_REGION

//REGION DIALOG_NAME
IF
StoryEvent((CHARACTERGUID)_LeaderTrader, "LeaderLib_Events_OnLeaderTradeSpawned")
AND
GlobalGetFlag("LeaderLib_DiscoveredLeaderTraderBackground", 1)
THEN
CharacterSetCustomName(_LeaderTrader, "Lothar, Retired Commander");
LLLIB_Dialog_SetNameOverride(_LeaderTrader, "Lothar, Retired Commander");

IF
GlobalFlagSet("LeaderLib_DiscoveredLeaderTraderBackground")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
CharacterSetCustomName(_LeaderTrader, "Lothar, Retired Commander");
LLLIB_Dialog_SetNameOverride(_LeaderTrader, "Lothar, Retired Commander");

IF
StoryEvent((CHARACTERGUID)_LeaderTrader, "LeaderLib_Events_OnLeaderTradeSpawned")
AND
NOT GlobalGetFlag("LeaderLib_DiscoveredLeaderTraderBackground", 1)
THEN
LLLIB_Dialog_SetNameOverride(_LeaderTrader, "Lothar");
//END_REGION

//REGION FLAG_COMMANDS
IF
ObjectFlagSet("LLLIB_LeaderTrader_GiveIntroItems", (CHARACTERGUID)_Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLLIB_LeaderTrader_GiveIntroItems");
LLLIB_LeaderTrader_GiveIntroItems(_Speaker);

PROC
LLLIB_LeaderTrader_GiveIntroItems((CHARACTERGUID)_Speaker)
AND
DB_LLLIB_Settings_ItemTemplates("Book_ModMenu", _ModMenuBookTemplate)
AND
ItemTemplateIsInUserInventory(_Speaker, _ModMenuBookTemplate, 0, 0)
THEN
ItemTemplateAddTo(_ModMenuBookTemplate, _Speaker, 1);

PROC
LLLIB_LeaderTrader_GiveIntroItems((CHARACTERGUID)_Speaker)
AND
DB_LLLIB_Settings_ItemTemplates("Book_ModMenu", _ModMenuBookTemplate)
AND
DB_IsPlayer(_Player)
AND
_Player != _Speaker
AND
NOT LeaderLib_Helper_QRY_UserMatch(_Speaker, _Player)
AND
ItemTemplateIsInUserInventory(_Player, _ModMenuBookTemplate, 0, 0)
AND
CharacterIsControlled(_Player, 1)
THEN
ItemTemplateAddTo(_ModMenuBookTemplate, _Player, 1);
//END_REGION

//REGION FIXES
IF
ObjectFlagSet("LLLIB_LeaderTrader_GaveIntroItems", (CHARACTERGUID)_Character, _)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
_Character != _LeaderTrader
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:LeaderTrader] Hey! 'LLLIB_LeaderTrader_GaveIntroItems' was set on the wrong character! Wake up! ;)");
ObjectClearFlag(_Character, "LLLIB_LeaderTrader_GaveIntroItems");
ObjectSetFlag(_LeaderTrader, "LLLIB_LeaderTrader_GaveIntroItems");

//Don't display "New Items in Stock" if an intro dialog is set.
IF
StoryEvent((CHARACTERGUID)_LeaderTrader, "LeaderLib_Events_NewItemsInStock")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
NOT DB_LLLIB_LeaderTrader_Temp_HasNewItems(_)
THEN
DB_LLLIB_LeaderTrader_Temp_HasNewItems(1);

IF
DB_Dialogs(_LeaderTrader, _Dialog)
AND
DB_LLLIB_LeaderTrader((CHARACTERGUID)_LeaderTrader)
AND
NOT DB_LLLIB_LeaderTrader_IntroDialogRequirements(_, _Dialog, _, _)
AND
DB_LLLIB_LeaderTrader_Temp_HasNewItems(1)
AND
HasActiveStatus(_LeaderTrader, "LLLIB_NEW_ITEMS", 0)
THEN
ApplyStatus(_LeaderTrader, "LLLIB_NEW_ITEMS", 180.0, 1);
LLLIB_LeaderTrader_Internal_ClearHasNewItems();

PROC
LLLIB_LeaderTrader_Internal_ClearHasNewItems()
AND
DB_LLLIB_LeaderTrader_Temp_HasNewItems(_Val)
THEN
NOT DB_LLLIB_LeaderTrader_Temp_HasNewItems(_Val);
//END_REGION

//REGION UPDATER
IF
StoryEvent(_,"LeaderLib_Events_Internal_OnUpdateSettings")
AND
LeaderUpdater_QRY_SettingsNeedsUpdate("LLLIB_Settings_LeaderTrader")
THEN
LLLIB_LeaderTrader_InitSettings();
LeaderLog_Log("SYSTEM", "[LeaderLib:LeaderTrader] Settings updated.");

PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader_MetPlayer", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader_Dialog", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader_Appearance", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader_CurrentAppearance", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader_IntroDialogRequirements", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader_Registered", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader_SetFlags", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "LeaderTrader", "DB_LLLIB_LeaderTrader_SetTags", 2);
LeaderLog_Log("TRACE", "[LeaderLib:LeaderTrader] Databases registered.");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLLIB_11__Start"
