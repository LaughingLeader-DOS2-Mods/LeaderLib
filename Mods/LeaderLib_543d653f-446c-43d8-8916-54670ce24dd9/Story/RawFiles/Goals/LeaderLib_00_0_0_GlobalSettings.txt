Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//REGION INIT_SETTING
PROC
LeaderLib_GlobalSettings_Internal_Init()
THEN
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_AutoAddModMenuBooksDisabled");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_AutosaveOnCombatStart");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_AutosavingEnabled");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_DialogRedirectionEnabled");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_DialogRedirection_DisableUserRestriction");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_DialogRedirection_HighestPersuasionEnabled");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_DisableAutosavingInCombat");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_FriendlyFireEnabled");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLog_CombatLog_Disabled");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLog_Debug_Enabled");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLog_Status_Disabled");
LeaderLib_Internal_GlobalSettings_Register_Flag("LeaderLib_DebugModeEnabled", 1);

LeaderLib_GlobalSettings_Register_GlobalInteger(7e737d2f-31d2-4751-963f-be6ccc59cd0c, "AutosaveInterval", 15);
LeaderLib_GlobalSettings_TransferVariables();

PROC
LeaderLib_Internal_GlobalSettings_Register_Flag((STRING)_Flag)
THEN
LeaderLib_GlobalSettings_Register_GlobalFlag(7e737d2f-31d2-4751-963f-be6ccc59cd0c, _Flag);

PROC
LeaderLib_Internal_GlobalSettings_Register_Flag((STRING)_Flag, (INTEGER)_SaveWhenFalse)
THEN
LeaderLib_GlobalSettings_Register_GlobalFlag(7e737d2f-31d2-4751-963f-be6ccc59cd0c, _Flag, _SaveWhenFalse);
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
THEN
LeaderLib_GlobalSettings_Internal_Init();
//END_REGION

//REGION VARIABLE_TRANSFER
QRY
LeaderLib_GlobalSettings_QRY_ModHasVariables((GUIDSTRING)_UUID)
AND
DB_LeaderLib_GlobalSettings_GlobalFlags(_UUID, _Flag, _SaveWhenFalse)
THEN
DB_NOOP(1);

QRY
LeaderLib_GlobalSettings_QRY_ModHasVariables((GUIDSTRING)_UUID)
AND
DB_LeaderLib_GlobalSettings_GlobalIntegers(_UUID, _VarName, _DefaultValue)
THEN
DB_NOOP(1);

//Transfers all saved variables to Lua
PROC
LeaderLib_GlobalSettings_TransferVariables()
THEN
LeaderLib_GlobalSettings_Internal_TransferFlags();
LeaderLib_GlobalSettings_Internal_TransferIntegers();

IF
TimerFinished("Timers_LeaderLib_GlobalSettings_TransferVariables")
THEN
LeaderLib_GlobalSettings_TransferVariables();

//Sends all flags to lua for export, if the extender is present
PROC
LeaderLib_GlobalSettings_Internal_TransferFlags()
THEN
DB_NOOP(1);
// [BEGIN_NO_OSITOOLS]
LeaderLog_Log("DEBUG", "[LeaderLib_00_1_1_System:LeaderLib_GlobalSettings_Internal_TransferFlags] (*WARNING*) The extender is not active. Skipping.");
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LeaderLib_GlobalSettings_Internal_TransferFlags()
AND
DB_LeaderLib_GlobalSettings_GlobalFlags(_UUID, _Flag, _SaveWhenFalse)
AND
DB_LeaderLib_Mods_Registered(_UUID, _ModID, _DisplayName, _Author, _Version, _Major, _Minor, _Revision, _Build)
AND
IntegertoString(_SaveWhenFalse, _IntStr)
AND
String(_UUID, _UUIDStr)
THEN
NRD_LuaCall("LeaderLib_Ext_GlobalSettings_StoreGlobalFlag", _UUIDStr, _DisplayName, _Author, _Flag, _IntStr);
*/

//Sends all vars to lua for export, if the extender is present
PROC
LeaderLib_GlobalSettings_Internal_TransferIntegers()
THEN
DB_NOOP(1);
// [BEGIN_NO_OSITOOLS]
LeaderLog_Log("DEBUG", "[LeaderLib_00_1_1_System:LeaderLib_GlobalSettings_Internal_TransferIntegers] (*WARNING*) The extender is not active. Skipping.");
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LeaderLib_GlobalSettings_Internal_TransferIntegers()
AND
DB_LeaderLib_GlobalSettings_GlobalIntegers(_UUID, _VarName, _DefaultValue)
AND
DB_LeaderLib_Mods_Registered(_UUID, _ModID, _DisplayName, _Author, _Version, _Major, _Minor, _Revision, _Build)
AND
IntegertoString(_DefaultValue, _IntStr)
AND
String(_UUID, _UUIDStr)
THEN
NRD_LuaCall("LeaderLib_Ext_GlobalSettings_StoreGlobalInteger", _UUIDStr, _DisplayName, _Author, _VarName, _IntStr);
*/
//END_REGION

//REGION VERSION_UPDATING_NEW
/* [OSITOOLS_ONLY]
PROC
LeaderLib_GlobalSettings_TransferVariables()
AND
DB_LeaderLib_Mods_Registered(_UUID, _ModID, _DisplayName, _Author, _Version, _Major, _Minor, _Revision, _Build)
AND
LeaderLib_GlobalSettings_QRY_ModHasVariables(_UUID)
AND
IntegertoString(_Version, _VersionStr)
THEN
NRD_LuaCall("LeaderLib_Ext_GlobalSettings_StoreModVersion", (STRING)_UUID, _DisplayName, _Author, _VersionStr);

PROC
LeaderLib_Mods_OnVersionChanged((GUIDSTRING)_UUID, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
LeaderLib_GlobalSettings_QRY_ModHasVariables(_UUID)
AND
DB_LeaderLib_Mods_Registered(_UUID, _ModID, _DisplayName, _Author, _Version, _Major, _Minor, _Revision, _Build)
AND
IntegertoString(_Version, _VersionStr)
THEN
NRD_LuaCall("LeaderLib_Ext_GlobalSettings_StoreModVersion", (STRING)_UUID, _DisplayName, _Author, _VersionStr);
*/
//END_REGION

//REGION GLOBAL_SETTING_INT_REGISTER
PROC
LeaderLib_GlobalSettings_Register_GlobalInteger((GUIDSTRING)_UUID, (STRING)_Name)
THEN
LeaderLib_GlobalSettings_Register_GlobalInteger(_UUID, _Name, 0);

PROC
LeaderLib_GlobalSettings_Register_GlobalInteger((GUIDSTRING)_UUID, (STRING)_VarName, (INTEGER)_DefaultValue)
AND
DB_LeaderLib_GlobalSettings_GlobalIntegers(_UUID, _VarName, _LastDefaultValue)
THEN
NOT DB_LeaderLib_GlobalSettings_GlobalIntegers(_UUID, _VarName, _LastDefaultValue);

PROC
LeaderLib_GlobalSettings_Register_GlobalInteger((GUIDSTRING)_UUID, (STRING)_VarName, (INTEGER)_DefaultValue)
THEN
DB_LeaderLib_GlobalSettings_GlobalIntegers(_UUID, _VarName, _DefaultValue);

//Called by LeaderLib_GlobalSettings.lua
PROC
LeaderLib_GlobalSettings_Internal_GetIntegerVariable((GUIDSTRING)_UUID, (STRING)_VarName)
AND
DB_LeaderLib_GlobalSettings_GlobalIntegers(_UUID, _VarName, _DefaultValue)
THEN
LeaderLib_GlobalSettings_GetIntegerVariable(_UUID, _VarName);

//Called by other mods
PROC
LeaderLib_GlobalSettings_SaveIntegerVariable((GUIDSTRING)_UUID, (STRING)_VarName, (INTEGER)_Value)
THEN
DB_NOOP(1);
/* [OSITOOLS_ONLY]
SetGlobalPriceModifier(_Value);
*/

//END_REGION

//REGION INTEGERS_MOD_PROCS
/* 
Called by LeaderLib when it goes to fetch the value of an integer variable.
Mods should listen for this with their UUID / VarName set and "reply" to the proc via:
(Dependency)
LeaderLib_GlobalSettings_SaveIntegerVariable(_UUID, _VarName, _Value);
(No dependency):
DB_LeaderLib_ModApi_GlobalSettings_SaveGlobalIntegerVariable((GUIDSTRING)_UUID, (STRING)_VarName, (INTEGER)_Value);
*/
PROC
LeaderLib_GlobalSettings_GetIntegerVariable((GUIDSTRING)_UUID, (STRING)_VarName)
THEN
DB_NOOP(1);

/*
Called by LeaderLib when a global variable is loaded.
Listen for this in mods to transfer the variable value to your scripts.
*/
PROC
LeaderLib_GlobalSettings_SetIntegerVariable((GUIDSTRING)_UUID, (STRING)_VarName, (INTEGER)_Value)
THEN
DB_NOOP(1);
//END_REGION

//REGION GLOBAL_SETTING_FLAG_REGISTER
PROC
LeaderLib_GlobalSettings_Register_GlobalFlag((GUIDSTRING)_UUID, (STRING)_Flag)
THEN
LeaderLib_GlobalSettings_Register_GlobalFlag(_UUID, _Flag, 0);

PROC
LeaderLib_GlobalSettings_Register_GlobalFlag((GUIDSTRING)_UUID, (STRING)_Flag, (INTEGER)_SaveWhenFalse)
AND
DB_LeaderLib_GlobalSettings_GlobalFlags(_UUID, _Flag, _LastVal)
THEN
NOT DB_LeaderLib_GlobalSettings_GlobalFlags(_UUID, _Flag, _LastVal);

PROC
LeaderLib_GlobalSettings_Register_GlobalFlag((GUIDSTRING)_UUID, (STRING)_Flag, (INTEGER)_SaveWhenFalse)
THEN
DB_LeaderLib_GlobalSettings_GlobalFlags(_UUID, _Flag, _SaveWhenFalse);
//END_REGION

//REGION SIGNAL_OTHER_MODS_REGISTRATION
IF
RegionStarted(_Region)
AND
NOT LeaderLib_Helper_QRY_IsCharacterCreationOrLobby(_Region)
THEN
LeaderLib_GlobalSettings_InitModRegistration();

//Signals other mods to register their settings
PROC
LeaderLib_GlobalSettings_InitModRegistration()
THEN
DB_NOOP(1);
//END_REGION

//REGION GLOBAL_SETTINGS_SAVE_LOAD
PROC
LeaderLib_SaveGlobalSettings()
AND
LeaderLib_QRY_ExtenderIsActive()
THEN
LeaderLib_GlobalSettings_TransferVariables();

/* [OSITOOLS_ONLY]
PROC
LeaderLib_SaveGlobalSettings()
THEN
LeaderLog_Log("DEBUG", "[LeaderLib_00_1_1_System:LeaderLib_SaveGlobalSettings] Saving global settings.");
NRD_LuaCall("LeaderLib_Ext_SaveGlobalSettings");
*/

PROC
LeaderLib_LoadGlobalSettings()
THEN
DB_NOOP(1);
// [BEGIN_NO_OSITOOLS]
LeaderLog_Log("DEBUG", "[LeaderLib_00_1_1_System:LeaderLib_LoadGlobalSettings] (*WARNING*) The extender is not active. Skipping.");
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LeaderLib_LoadGlobalSettings()
THEN
LeaderLog_Log("DEBUG", "[LeaderLib_00_1_1_System:LeaderLib_LoadGlobalSettings] Loading global settings.");
NRD_LuaCall("LeaderLib_Ext_LoadGlobalSettings");
*/

IF
GameStarted(_Region,_IsEditor)
AND
IsGameLevel(_Region, _IsGameLevel)
AND
IntegerMax(_IsEditor, _IsGameLevel, 1)
THEN
LeaderLib_LoadGlobalSettings();

IF
ObjectFlagSet("LeaderLib_Commands_LoadGlobalSettings", (CHARACTERGUID)_Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LeaderLib_Commands_LoadGlobalSettings", 0);
LeaderLib_LoadGlobalSettings();

IF
ObjectFlagSet("LeaderLib_Commands_LoadGlobalSettings", (CHARACTERGUID)_Speaker, _)
AND
CharacterIsControlled(_Speaker, 1)
AND
LeaderLib_QRY_ExtenderIsActive()
THEN
ShowNotification(_Speaker, "LeaderLib_Notifications_LoadGlobalSettings_Success");
//ShowNotification(_Speaker, "LeaderLib_Notifications_LoadGlobalSettings_Failed");

IF
ObjectFlagSet("LeaderLib_Commands_SaveGlobalSettings", (CHARACTERGUID)_Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LeaderLib_Commands_SaveGlobalSettings", 0);
//LeaderLib_SaveGlobalSettings();
TimerCancel("Timers_LeaderLib_SaveGlobalSettings");
TimerLaunch("Timers_LeaderLib_SaveGlobalSettings", 250);

IF
ObjectFlagSet("LeaderLib_Commands_SaveGlobalSettings", (CHARACTERGUID)_Speaker, _)
AND
CharacterIsControlled(_Speaker, 1)
AND
LeaderLib_QRY_ExtenderIsActive()
THEN
ShowNotification(_Speaker, "LeaderLib_Notifications_SaveGlobalSettings_Success");
//ShowNotification(_Speaker, "LeaderLib_Notifications_SaveGlobalSettings_Failed");
/*
IF
//StoryEvent(_,"LeaderLib_Initialized")
RegionStarted(_Region)
AND
IsGameLevel(_Region, 1)
THEN
LeaderLib_LoadGlobalSettings();
*/

IF
GlobalFlagSet("LeaderLib_Commands_SaveGlobalSettings")
THEN
GlobalClearFlag("LeaderLib_Commands_SaveGlobalSettings");
TimerCancel("Timers_LeaderLib_SaveGlobalSettings");
TimerLaunch("Timers_LeaderLib_SaveGlobalSettings", 250);

IF
StoryEvent(_, "LeaderLib_Commands_SaveGlobalSettings")
THEN
TimerCancel("Timers_LeaderLib_SaveGlobalSettings");
TimerLaunch("Timers_LeaderLib_SaveGlobalSettings", 250);

//Saving is delayed in case multiple mods are calling for it
IF
TimerFinished("Timers_LeaderLib_SaveGlobalSettings")
THEN
LeaderLib_SaveGlobalSettings();
//END_REGION

//REGION DEBUG
IF
TextEventSet("leaderlib_saveglobalsettings")
THEN
LeaderLib_SaveGlobalSettings();

IF
TextEventSet("leaderlib_loadglobalsettings")
THEN
LeaderLib_LoadGlobalSettings();

/* [OSITOOLS_ONLY]
IF
TextEventSet("leaderlib_savesettingstest")
THEN
NRD_LuaReset(1);
NRD_LuaCall("LeaderLib_Ext_GlobalSettings_Initialize");
LeaderLib_GlobalSettings_Register_GlobalInteger(7e737d2f-31d2-4751-963f-be6ccc59cd0c, "AutosaveInterval", 15);
LeaderLib_SaveGlobalSettings();
*/

/* [OSITOOLS_ONLY]
IF
TextEventSet("leaderlib_loadsettingstest")
THEN
NRD_LuaReset(1);
NRD_LuaCall("LeaderLib_Ext_GlobalSettings_Initialize");
LeaderLib_LoadGlobalSettings();
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__AAA_Z_LaughingLeader_LeaderLib_Top"