Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION QUERIES
QRY
LeaderLib_Auras_QRY_CanPulse((INTEGER)_HitCharacters, (INTEGER)_HitItems, (INTEGER)_HitSelf, (INTEGER)_TrackTargets)
AND
LeaderLib_Math_QRY_IsEqualToAny(1, _HitCharacters, _HitItems, _HitSelf, _TrackTargets)
THEN
DB_NOOP(1);
//END_REGION

//REGION LISTENER_EVENTS
PROC
LeaderLib_Auras_OnAuraApplied((GUIDSTRING)_Source, (STRING)_AuraID)
THEN
DB_NOOP(1);

PROC
LeaderLib_Auras_OnTargetFound((GUIDSTRING)_Source, (GUIDSTRING)_Target, (STRING)_AuraID)
THEN
DB_NOOP(1);

PROC
LeaderLib_Auras_OnTargetLost((GUIDSTRING)_Source, (GUIDSTRING)_Target, (STRING)_AuraID)
THEN
DB_NOOP(1);
//END_REGION

//REGION PULSING
PROC
LeaderLib_Auras_Internal_Pulse((GUIDSTRING)_Source, (STRING)_AuraID, (REAL)_Radius, (INTEGER)_HitCharacters, (INTEGER)_HitItems, (INTEGER)_HitSelf)
AND
GetUUID(_Source, _UUID)
AND
StringConcatenate("Iterators_AuraPulse_", _AuraID, _EventID)
AND
StringConcatenate("Timers_AuraPulse_", _AuraID, _TimerID)
AND
LeaderLib_StringExt_QRY_SafeCombine(_EventID, "Iterators_AuraPulse_", _AuraID, _UUID)
AND
DB_LeaderLib_StringExt_CombineResult(_EventID, _Event)
AND
LeaderLib_StringExt_QRY_SafeCombine(_TimerID, "Timers_AuraPulse_", _AuraID, _UUID)
AND
DB_LeaderLib_StringExt_CombineResult(_TimerID, _TimerEvent)
THEN
NOT DB_LeaderLib_StringExt_CombineResult(_EventID, _Event);
NOT DB_LeaderLib_StringExt_CombineResult(_TimerID, _TimerEvent);
DB_LeaderLib_Auras_Temp_PulsingEvent(_Source, _AuraID, _Event, _Radius, _TimerEvent);
LeaderLib_Timers_StartObjectTimer(_Source, 25, _TimerEvent, _TimerEvent);
LeaderLib_Auras_Internal_PulseNow(_Source, _AuraID, _Event, _Radius, _HitCharacters, _HitItems, _HitSelf);

PROC
LeaderLib_Auras_Internal_PulseNow((GUIDSTRING)_Source, (STRING)_AuraID, (STRING)_Event, (REAL)_Radius, (INTEGER)_HitCharacters, (INTEGER)_HitItems, (INTEGER)_HitSelf)
AND
NOT SysIsActive("LeaderLib_19_TS_AuraGenericEventListener")
THEN
SysActivateGoal("LeaderLib_19_TS_AuraGenericEventListener");

PROC
LeaderLib_Auras_Internal_PulseNow((GUIDSTRING)_Source, (STRING)_AuraID, (STRING)_Event, (REAL)_Radius, (INTEGER)_HitCharacters, (INTEGER)_HitItems, 1)
THEN
LeaderLib_Auras_Internal_OnPulseHit(_Source, _Source, _AuraID);

PROC
LeaderLib_Auras_Internal_PulseNow((GUIDSTRING)_Source, (STRING)_AuraID, (STRING)_Event, (REAL)_Radius, 1, (INTEGER)_HitItems, (INTEGER)_HitSelf)
THEN
CharacterLaunchIteratorAroundObject(_Source, _Radius, _Event);

PROC
LeaderLib_Auras_Internal_PulseNow((GUIDSTRING)_Source, (STRING)_AuraID, (STRING)_Event, (REAL)_Radius, (INTEGER)_HitCharacters, 1, (INTEGER)_HitSelf)
THEN
ItemLaunchIterator(_Event);

PROC
LeaderLib_Auras_Internal_OnPulseComplete((GUIDSTRING)_Source, (STRING)_AuraID)
AND
SysCount("DB_LeaderLib_Auras_Temp_PulsingEvent", 5, 0)
THEN
SysCompleteGoal("LeaderLib_19_TS_AuraGenericEventListener");
//END_REGION

//REGION PULSE_HIT
IF
CharacterCharacterEvent(_Source, _Target, "LeaderLib_Auras_OnPulseHit")
AND
DB_LeaderLib_Auras_Temp_Pulsing((GUIDSTRING)_Source, _AuraID)
THEN
LeaderLib_Auras_Internal_OnPulseHit((GUIDSTRING)_Source, (GUIDSTRING)_Target, _AuraID);

IF
CharacterItemEvent(_Source, _Target, "LeaderLib_Auras_OnPulseHit")
AND
DB_LeaderLib_Auras_Temp_Pulsing((GUIDSTRING)_Source, _AuraID)
THEN
LeaderLib_Auras_Internal_OnPulseHit((GUIDSTRING)_Source, (GUIDSTRING)_Target, _AuraID);

PROC
LeaderLib_Auras_Internal_OnPulseHit((GUIDSTRING)_Source, (GUIDSTRING)_Target, (STRING)_AuraID)
AND
_Source != _Target
THEN
DB_LeaderLib_Auras_Temp_NewTarget(_Source, _Target, _AuraID);
LeaderLib_Auras_OnTargetFound(_Source, _Target, _AuraID);
//END_REGION

//REGION PULSE_TARGETS
PROC
LeaderLib_Auras_Internal_ClearPulseTargets((GUIDSTRING)_Source, (STRING)_AuraID)
AND
DB_LeaderLib_Auras_Temp_NewTarget(_Source, _Target, _AuraID)
THEN
NOT DB_LeaderLib_Auras_Temp_NewTarget(_Source, _Target, _AuraID);
DB_LeaderLib_Auras_Temp_LastTarget(_Source, _Target, _AuraID);

PROC
LeaderLib_Auras_Internal_ComparePulseTargets((GUIDSTRING)_Source, (STRING)_AuraID)
AND
DB_LeaderLib_Auras_Temp_LastTarget(_Source, _Target, _AuraID)
AND
NOT DB_LeaderLib_Auras_Temp_NewTarget(_Source, _Target, _AuraID)
THEN
NOT DB_LeaderLib_Auras_Temp_LastTarget(_Source, _Target, _AuraID);
LeaderLib_Auras_OnTargetLost(_Source, _Target, _AuraID);

PROC
LeaderLib_Auras_Internal_CheckPulseTargets((GUIDSTRING)_Source, (STRING)_AuraID)
THEN
LeaderLib_Auras_Internal_ComparePulseTargets(_Source, _AuraID);
LeaderLib_Auras_Internal_ClearPulseTargets(_Source, _AuraID);
//END_REGION

//REGION PULSE_COMPLETE
PROC
LeaderLib_Auras_Internal_OnPulseComplete((GUIDSTRING)_Source, (STRING)_AuraID)
AND
DB_LeaderLib_Auras_Temp_Pulsing(_Source, _AuraID)
THEN
NOT DB_LeaderLib_Auras_Temp_Pulsing(_Source, _AuraID);
LeaderLib_Auras_OnPulseComplete(_Source, _AuraID);
//END_REGION

//REGION TEMPDATA_CLEAR
PROC
LeaderLib_Auras_Internal_ClearData((GUIDSTRING)_Source, (STRING)_AuraID)
AND
DB_LeaderLib_Auras_Temp_LastTarget(_Source, _Target, _AuraID)
THEN
NOT DB_LeaderLib_Auras_Temp_LastTarget(_Source, _Target, _AuraID);
LeaderLib_Auras_OnTargetLost(_Source, _Target, _AuraID);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
