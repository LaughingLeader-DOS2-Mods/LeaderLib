Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION TEMPDATA_CLEAR
PROC
LeaderLib_Auras_Internal_ClearData((GUIDSTRING)_Source, (STRING)_AuraID)
AND
DB_LeaderLib_Auras_Temp_LastTarget(_Source, _AuraID, (GUIDSTRING)_Target)
THEN
NOT DB_LeaderLib_Auras_Temp_LastTarget(_Source, _AuraID, (GUIDSTRING)_Target);
//END_REGION

//REGION PULSE
PROC
LeaderLib_Auras_OnPulse((GUIDSTRING)_Source, (STRING)_AuraID)
THEN
DB_NOOP(1);

IF
StoryEvent(_Source, "LeaderLib_Auras_OnPulseComplete")
AND
DB_LeaderLib_Auras_Temp_Pulsing(_Source, _AuraID)
THEN
NOT DB_LeaderLib_Auras_Temp_Pulsing(_Source, _AuraID);
LeaderLib_Auras_OnPulseComplete(_Source, _AuraID);

PROC
LeaderLib_Auras_Internal_CheckPulseTargets((GUIDSTRING)_Source, (STRING)_AuraID)
AND
DB_LeaderLib_Auras_Temp_LastTarget(_Source, _AuraID, (GUIDSTRING)_Target)
AND
NOT DB_LeaderLib_Auras_Temp_NewTarget(_Source, _AuraID, _Target)
THEN
NOT DB_LeaderLib_Auras_Temp_LastTarget(_Source, _AuraID, (GUIDSTRING)_Target);
LeaderLib_Auras_OnPulseTargetLost(_Source, _Target, _AuraID);

PROC
LeaderLib_Auras_Internal_CheckPulseTargets((GUIDSTRING)_Source, (STRING)_AuraID)
AND
DB_LeaderLib_Auras_Temp_NewTarget(_Source, _AuraID, (GUIDSTRING)_Target)
THEN
NOT DB_LeaderLib_Auras_Temp_NewTarget(_Source, _AuraID, (GUIDSTRING)_Target);
DB_LeaderLib_Auras_Temp_LastTarget(_Source, _AuraID, (GUIDSTRING)_Target);

PROC
LeaderLib_Auras_OnPulseTargetLost((GUIDSTRING)_Source, (GUIDSTRING)_Target, (STRING)_AuraID)
THEN
DB_NOOP(1);
//END_REGION

//REGION PULSE_HIT
IF
CharacterCharacterEvent(_Source, _Target, "LeaderLib_Auras_OnPulseHit")
AND
DB_LeaderLib_Auras_Temp_Pulsing(_Source, _AuraID)
THEN
LeaderLib_Auras_Internal_OnPulseHit(_Source, (GUIDSTRING)_Target, _AuraID);

IF
CharacterItemEvent(_Source, _Target, "LeaderLib_Auras_OnPulseHit")
AND
DB_LeaderLib_Auras_Temp_Pulsing(_Source, _AuraID)
THEN
LeaderLib_Auras_Internal_OnPulseHit(_Source, (GUIDSTRING)_Target, _AuraID);

PROC
LeaderLib_Auras_Internal_OnPulseHit((GUIDSTRING)_Source, (GUIDSTRING)_Target, (STRING)_AuraID)
AND
_Source != _Target
THEN
DB_LeaderLib_Auras_Temp_NewTarget(_Source, _AuraID, _Target);

PROC
LeaderLib_Auras_Internal_OnPulseHit((GUIDSTRING)_Source, (GUIDSTRING)_Target, (STRING)_AuraID)
AND
_Source != _Target
THEN
LeaderLib_Auras_OnPulseTargetFound(_Source, _Target, _AuraID);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
