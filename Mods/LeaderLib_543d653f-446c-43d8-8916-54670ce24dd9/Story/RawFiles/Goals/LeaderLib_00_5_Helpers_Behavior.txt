Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//REGION CREATE_SURFACE_OWNER
PROC
LeaderLib_Helper_CreateSurfaceWithOwner((CHARACTERGUID)_Owner, (STRING)_Surface)
THEN
LeaderLib_Helper_CreateSurfaceWithOwner(_Owner, _Owner, _Surface, 1.0, 2);

PROC
LeaderLib_Helper_CreateSurfaceWithOwner((CHARACTERGUID)_Owner, (GUIDSTRING)_Target, (STRING)_Surface)
THEN
LeaderLib_Helper_CreateSurfaceWithOwner(_Owner, _Target, _Surface, 1.0, 2);

PROC
LeaderLib_Helper_CreateSurfaceWithOwner((CHARACTERGUID)_Owner, (GUIDSTRING)_Target, (STRING)_Surface, (REAL)_Radius)
THEN
LeaderLib_Helper_CreateSurfaceWithOwner(_Owner, _Target, _Surface, _Radius, 2);

PROC
LeaderLib_Helper_CreateSurfaceWithOwner((CHARACTERGUID)_Owner, (GUIDSTRING)_Target, (STRING)_Surface, (REAL)_Radius, (INTEGER)_Turns)
AND
GetPosition(_Target, _x, _y, _z)
THEN
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition(_Owner, _x, _y, _z, _Surface, _Radius, _Turns);

PROC
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition((CHARACTERGUID)_Owner, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_Surface)
THEN
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition(_Owner, _x, _y, _z, _Surface, 1.0, 2);

PROC
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition((CHARACTERGUID)_Owner, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_Surface, (REAL)_Radius)
THEN
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition(_Owner, _x, _y, _z, _Surface, _Radius, 2);

PROC
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition((CHARACTERGUID)_Owner, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_Surface, (REAL)_Radius, (INTEGER)_Turns)
AND
NOT DB_LeaderLib_SurfaceToInt(_Surface, _)
THEN
SetVarFloat(_Owner, "LeaderLib_CreateSurface_Radius", _Radius);
SetVarInteger(_Owner, "LeaderLib_CreateSurface_Turns", _Turns);
SetVarFloat3(_Owner, "LeaderLib_CreateSurface_Target", _x, _y, _z);
SetVarObject(_Owner, "LeaderLib_CreateSurface_Owner", _Owner);
SetVarString(_Owner, "LeaderLib_StringToSurface_String", _Surface);
SetVarString(_Owner, "LeaderLib_StringToSurface_CompletionEvent", "LeaderLib_CreateSurface");
SetStoryEvent(_Owner, "LeaderLib_StringToSurface");

PROC
LeaderLib_Helper_CreateSurfaceWithOwnerAtPosition((CHARACTERGUID)_Owner, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_Surface, (REAL)_Radius, (INTEGER)_Turns)
AND
DB_LeaderLib_SurfaceToInt(_Surface, _Index)
THEN
SetVarFloat(_Owner, "LeaderLib_CreateSurface_Radius", _Radius);
SetVarInteger(_Owner, "LeaderLib_CreateSurface_Turns", _Turns);
SetVarFloat3(_Owner, "LeaderLib_CreateSurface_Target", _x, _y, _z);
SetVarObject(_Owner, "LeaderLib_CreateSurface_Owner", _Owner);
SetVarInteger(_Owner, "LeaderLib_StringToSurface_Surface", _Index); // INT => SURFACE
SetStoryEvent(_Owner, "LeaderLib_CreateSurface");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"