Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION ITEM_QUERIES
QRY
LeaderLib_Helper_QRY_ItemHasDeltaModifier((ITEMGUID)_Item, (STRING)_Deltamod)
AND
ItemHasDeltaModifier(_Item, _DeltaMod, _Count)
AND
_Count > 0
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_ItemIsPoisoned((ITEMGUID)_Item)
AND
IsTagged(_Item, "POISONED", 1)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_ItemIsPoisoned((ITEMGUID)_Item)
AND
IsTagged(_Item, "POISONED", 0)
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "Poison", 1)
AND
StringContains(_Template, "_Res_", 0)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_HasItemInSlot((CHARACTERGUID)_Character, (STRING)_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, _Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_HasItemTemplateInSlot((CHARACTERGUID)_Character, (STRING)_Slot, (STRING)_Template)
AND
CharacterGetEquippedItem(_Character, _Slot, _Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
GetTemplate(_Item, _Template)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_HasEmptyWeaponSlot((CHARACTERGUID)_Character)
AND
NOT LeaderLib_Helper_QRY_HasItemInSlot(_Character, "Weapon")
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_HasEmptyWeaponSlot((CHARACTERGUID)_Character)
AND
NOT LeaderLib_Helper_QRY_HasItemInSlot(_Character, "Shield")
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_HasEmptyHands((CHARACTERGUID)_Character)
AND
NOT LeaderLib_Helper_QRY_HasItemInSlot(_Character, "Weapon")
AND
NOT LeaderLib_Helper_QRY_HasItemInSlot(_Character, "Shield")
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_WeaponIsEquipped((CHARACTERGUID)_Character)
AND
CharacterGetEquippedItem(_Character, "Weapon", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_WeaponIsEquipped((CHARACTERGUID)_Character)
AND
CharacterGetEquippedItem(_Character, "Shield", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_WeaponIsEquipped((CHARACTERGUID)_Character, (ITEMGUID)_Item)
AND
CharacterGetEquippedItem(_Character, "Weapon", _Item)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_WeaponIsEquipped((CHARACTERGUID)_Character, (ITEMGUID)_Item)
AND
CharacterGetEquippedItem(_Character, "Shield", _Item)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_ItemIsEquipped((CHARACTERGUID)_Character, (ITEMGUID)_Item)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, _Item)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_ItemIsEquippedInSlot((CHARACTERGUID)_Character, (ITEMGUID)_Item, (STRING)_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, _Item)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_IsNonMagicalRangedWeapon((ITEMGUID)_Item)
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "_Bow_", 1)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_IsNonMagicalRangedWeapon((ITEMGUID)_Item)
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "_Crossbow_", 1)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_IsMagicalRangedWeapon((ITEMGUID)_Item)
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "_Wand_", 1)
THEN
DB_NOOP(1);

// [BEGIN_NO_OSITOOLS]
QRY
LeaderLib_Helper_QRY_IsRangedWeapon((ITEMGUID)_Item)
AND
LeaderLib_Helper_QRY_IsNonMagicalRangedWeapon(_Item)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_IsRangedWeapon((ITEMGUID)_Item)
AND
LeaderLib_Helper_QRY_IsMagicalRangedWeapon(_Item)
THEN
DB_NOOP(1);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
QRY
LeaderLib_Helper_QRY_IsRangedWeapon((ITEMGUID)_Item)
AND
NRD_ItemGetStatsId(_Item, _Stat)
AND
NRD_StatGetString(_Stat, "WeaponType", _WeaponType)
AND
LeaderLib_String_QRY_EqualsAny(_WeaponType, "Bow", "Crossbow", "Wand", "Rifle")
THEN
DB_NOOP(1);
*/

QRY
LeaderLib_Helper_QRY_ChargesAreMaxed((ITEMGUID)_Item)
AND
ItemGetCharges(_Item, _Current)
AND
ItemGetMaxCharges(_Item, _Max)
AND
_Current >= _Max
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_HasTaggedWeapon((CHARACTERGUID)_Character, (STRING)_Tag)
AND
CharacterGetEquippedItem(_Character, "Weapon", _Weapon)
AND
IsTagged(_Weapon, _Tag, 1)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_HasTaggedWeapon((CHARACTERGUID)_Character, (STRING)_Tag)
AND
CharacterGetEquippedItem(_Character, "Shield", _Weapon)
AND
IsTagged(_Weapon, _Tag, 1)
THEN
DB_NOOP(1);
//END_REGION

//REGION EQUIPMENT_REFRESH
IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Commands_RefreshEquipment_Delayed")
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Helper:RefreshEquipment_Delayed] Refreshing equipment for [",_Name,"]");
LeaderLib_Helper_UnequipAllAndSave(_Character);
LeaderLib_Timers_StartObjectTimer(_Character, 2500, "LeaderLib_Timers_EquipItemsTimer_", "LeaderLib_Helper_OnEquipTimerFinished");

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Helper_OnEquipTimerFinished")
AND
DB_LeaderLib_Helper_Temp_UnequippedItem(_Character, _Item)
THEN
NOT DB_LeaderLib_Helper_Temp_UnequippedItem(_Character, _Item);
CharacterEquipItem(_Character, _Item);

PROC
LeaderLib_Helper_UnequipAllAndSave((CHARACTERGUID)_Character)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);
DB_LeaderLib_Helper_Temp_UnequippedItem(_Character, _Item);

PROC
LeaderLib_Helper_UnequipAll((CHARACTERGUID)_Character)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Commands_RefreshEquipment")
THEN
LeaderLib_Helper_RefreshEquipment(_Character);

IF
CharacterItemEvent(_Character, _Item, "LeaderLib_Commands_EquipItem")
AND
ObjectExists(_Item, 1)
AND
CharacterIsDead(_Character, 0)
AND
ItemIsEquipable(_Item, 1)
AND
NOT LeaderLib_Helper_QRY_ItemIsEquipped(_Character, _Item)
THEN
LeaderLib_Helper_CanRefreshItem(_Character, _Item);
LeaderLib_Helper_Internal_EquipItem(_Character, _Item);

PROC
LeaderLib_Helper_CanRefreshItem((CHARACTERGUID)_Character, (ITEMGUID)_Item)
THEN
DB_NOOP(1);

PROC
LeaderLib_Helper_Internal_EquipItem((CHARACTERGUID)_Character, (ITEMGUID)_Item)
AND
NOT DB_CustomUseItemResponse(_Character, _Item, 0)
THEN
CharacterEquipItem(_Character, _Item);

PROC
LeaderLib_Helper_RefreshEquipment((CHARACTERGUID)_Character)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);
LeaderLib_Helper_CanRefreshItem(_Character, _Item);
LeaderLib_Helper_Internal_EquipItem(_Character, _Item);

PROC
LeaderLib_Helper_RefreshWeapons((CHARACTERGUID)_Character)
AND
CharacterGetEquippedItem(_Character, "Weapon", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);
LeaderLib_Helper_CanRefreshItem(_Character, _Item);
LeaderLib_Helper_Internal_EquipItem(_Character, _Item);

PROC
LeaderLib_Helper_RefreshWeapons((CHARACTERGUID)_Character)
AND
CharacterGetEquippedItem(_Character, "Shield", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);
LeaderLib_Helper_CanRefreshItem(_Character, _Item);
LeaderLib_Helper_Internal_EquipItem(_Character, _Item);

PROC
LeaderLib_Helper_RefreshWeapons((CHARACTERGUID)_Character, (INTEGER)_Delay, (INTEGER)_OffhandDelay)
AND
CharacterGetEquippedItem(_Character, "Shield", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);
LeaderLib_Timers_StartCharacterItemTimer(_Character, _Item, _OffhandDelay, "LeaderLib_Timers_RefreshWeapon_", "LeaderLib_Commands_EquipItem");

PROC
LeaderLib_Helper_RefreshWeapons((CHARACTERGUID)_Character, (INTEGER)_Delay, (INTEGER)_OffhandDelay)
AND
CharacterGetEquippedItem(_Character, "Weapon", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);
LeaderLib_Timers_StartCharacterItemTimer(_Character, _Item, _Delay, "LeaderLib_Timers_RefreshWeapon_", "LeaderLib_Commands_EquipItem");

PROC
LeaderLib_Helper_UnequipWeapons((CHARACTERGUID)_Character)
AND
CharacterGetEquippedItem(_Character, "Weapon", (ITEMGUID)_Item)
THEN
CharacterUnequipItem(_Character, _Item);

PROC
LeaderLib_Helper_UnequipWeapons((CHARACTERGUID)_Character)
AND
CharacterGetEquippedItem(_Character, "Shield", (ITEMGUID)_Item)
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "WPN", 1)
THEN
CharacterUnequipItem(_Character, _Item);
//END_REGION

//REGION UNEQUIP_HELPERS
QRY
LeaderLib_Helper_QRY_UnequipInSlot((CHARACTERGUID)_Character, (STRING)_SLot)
THEN
LeaderLib_Helper_UnequipInSlot(_Character, _Slot);

PROC
LeaderLib_Helper_UnequipInSlot((CHARACTERGUID)_Character, (STRING)_SLot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);
ItemToInventory(_Item, _Character);

PROC
LeaderLib_Helper_UnequipIfEquipped((CHARACTERGUID)_Character, (ITEMGUID)_Item)
AND
LeaderLib_Helper_QRY_ItemIsEquipped(_Character, _Item)
THEN
CharacterUnequipItem(_Character, _Item);
ItemToInventory(_Item, _Character);
//END_REGION

//REGION MISC_CHAR_ITEM_EVENTS
IF
CharacterItemEvent(_Character, _Item, "LeaderLib_Commands_ItemToInventory")
AND
ObjectExists(_Character, 1)
AND
ObjectExists(_Item, 1)
AND
ItemGetAmount(_Item, _Amount)
THEN
ItemToInventory(_Item, _Character, _Amount, 0, 0);

IF
CharacterItemEvent(_Character, _Item, "LeaderLib_Commands_ItemToInventory")
AND
ObjectExists(_Character, 1)
AND
ObjectExists(_Item, 1)
AND
NOT ItemGetAmount(_Item, _)
THEN
ItemToInventory(_Item, _Character, 1, 0, 0);

IF
CharacterItemEvent(_Character, _Item, "LeaderLib_Commands_UnequipItem")
AND
ItemIsEquipable(_Item, 1)
THEN
CharacterUnEquipItem(_Character, _Item);
//END_REGION

//REGION BEHAVIOR_SCRIPT_HELPERS
IF
CharacterItemEvent(_Character, _Item, "LeaderLib_Commands_EquipItem")
AND
ItemIsEquipable(_Item, 1)
THEN
CharacterEquipItem(_Character, _Item);
//CharacterUseItem(_Character, _Item, "");

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Commands_UnequipItemInSlot")
AND
GetVarString(_Character, "LeaderLib_ItemSlot", _Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
CharacterUnequipItem(_Character, _Item);

IF
StoryEvent(_Item, "LeaderLib_Commands_RemoveItem")
AND
ObjectIsItem((ITEMGUID)_Item, 1)
THEN
ItemRemove(_Item);

PROC
ProcObjectTimerFinished((ITEMGUID)_Item, "LeaderLib_Commands_RemoveItem")
AND
ObjectExists(_Item, 1)
THEN
ItemRemove(_Item);

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Commands_GetItemInSlot")
AND
GetVarString(_Character, "LeaderLib_ItemSlot", _Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
GetVarString(_Character, "LeaderLib_EquippedItemFoundEvent", _Event)
THEN
DB_LeaderLib_Helper_Temp_ItemFoundInSlot(_Character, _Slot, _Item);
CharacterItemSetEvent(_Character, _Item, _Event);

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Commands_GetItemInSlot")
AND
NOT DB_LeaderLib_Helper_Temp_ItemFoundInSlot(_Character, _, _)
AND
GetVarString(_Character, "LeaderLib_EquippedItemNotFoundEvent", _Event)
THEN
SetStoryEvent(_Character, _Event);

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Commands_GetItemInSlot")
AND
DB_LeaderLib_Helper_Temp_ItemFoundInSlot(_Character, _Slot, _Item)
THEN
NOT DB_LeaderLib_Helper_Temp_ItemFoundInSlot(_Character, _Slot, _Item);
//END_REGION

//REGION AUTO_IDENTIFY
IF
ItemOpened(_Item)
AND
IsTagged(_Item, "LeaderLib_AutoIdentify", 1)
THEN
ContainerIdentifyAll(_Item);
//END_REGION

//REGION COMBO_HELPERS
QRY
LeaderLib_Helper_QRY_Combo_TemplateMatch((STRING)_Template, (STRING)_T1, (STRING)_T2, (STRING)_T3, (STRING)_T4, (STRING)_T5)
AND
StringContains(_T1, _Template, _x1)
AND
StringContains(_T2, _Template, _x2)
AND
StringContains(_T3, _Template, _x3)
AND
StringContains(_T4, _Template, _x4)
AND
StringContains(_T5, _Template, _x5)
AND
IntegerSum(_x1, _x2, _r1)
AND
IntegerSum(_x3, _r1, _r2)
AND
IntegerSum(_x4, _r2, _r3)
AND
IntegerSum(_x5, _r3, _r4)
AND
_r4 > 0
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_Combo_TemplateMatch_FromItem((STRING)_Template, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
GetTemplate(_T1, _Template1)
AND
GetTemplate(_T2, _Template2)
AND
GetTemplate(_T3, _Template3)
AND
GetTemplate(_T4, _Template4)
AND
GetTemplate(_T5, _Template5)
AND
LeaderLib_Helper_QRY_Combo_TemplateMatch(_Template, _Template1, _Template2, _Template3, _Template4, _Template5)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_Combo_HasTag((STRING)_Tag, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
IsTagged(_T1, _Tag, _x1)
AND
IsTagged(_T2, _Tag, _x2)
AND
IsTagged(_T3, _Tag, _x3)
AND
IsTagged(_T4, _Tag, _x4)
AND
IsTagged(_T5, _Tag, _x5)
AND
IntegerSum(_x1, _x2, _r1)
AND
IntegerSum(_x3, _r1, _r2)
AND
IntegerSum(_x4, _r2, _r3)
AND
IntegerSum(_x5, _r3, _r4)
AND
_r4 > 0
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_Internal_QRY_Combo_ClearTagged((STRING)_Tag)
THEN
LeaderLib_Helper_Internal_Combo_ClearTagged(_Tag);

PROC
LeaderLib_Helper_Internal_Combo_ClearTagged((STRING)_Tag)
AND
DB_LeaderLib_Helper_Temp_Combo_FoundTaggedItem(_Tag, _Last)
THEN
NOT DB_LeaderLib_Helper_Temp_Combo_FoundTaggedItem(_Tag, _Last);

QRY
LeaderLib_Helper_QRY_Combo_FindTagged((STRING)_Tag, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
IsTagged(_T1, _Tag, 1)
THEN
DB_LeaderLib_Helper_Temp_Combo_FoundTaggedItem(_Tag, _T1);

QRY
LeaderLib_Helper_QRY_Combo_FindTagged((STRING)_Tag, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
IsTagged(_T2, _Tag, 1)
THEN
DB_LeaderLib_Helper_Temp_Combo_FoundTaggedItem(_Tag, _T2);

QRY
LeaderLib_Helper_QRY_Combo_FindTagged((STRING)_Tag, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
IsTagged(_T3, _Tag, 1)
THEN
DB_LeaderLib_Helper_Temp_Combo_FoundTaggedItem(_Tag, _T3);

QRY
LeaderLib_Helper_QRY_Combo_FindTagged((STRING)_Tag, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
IsTagged(_T4, _Tag, 1)
THEN
DB_LeaderLib_Helper_Temp_Combo_FoundTaggedItem(_Tag, _T4);

QRY
LeaderLib_Helper_QRY_Combo_FindTagged((STRING)_Tag, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
IsTagged(_T5, _Tag, 1)
THEN
DB_LeaderLib_Helper_Temp_Combo_FoundTaggedItem(_Tag, _T5);

QRY
LeaderLib_Helper_QRY_Combo_ItemMatch((ITEMGUID)_Item, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
_Item == _T1
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_Combo_ItemMatch((ITEMGUID)_Item, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
_Item == _T1
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_Combo_ItemMatch((ITEMGUID)_Item, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
_Item == _T2
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_Combo_ItemMatch((ITEMGUID)_Item, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
_Item == _T3
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_Combo_ItemMatch((ITEMGUID)_Item, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
_Item == _T4
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_Combo_ItemMatch((ITEMGUID)_Item, (ITEMGUID)_T1, (ITEMGUID)_T2, (ITEMGUID)_T3, (ITEMGUID)_T4, (ITEMGUID)_T5)
AND
_Item == _T5
THEN
DB_NOOP(1);
//END_REGION

//REGION RUNE_HELPERS
QRY
LeaderLib_Helper_QRY_RemoveRunes((CHARACTERGUID)_Player, (ITEMGUID)_Item)
THEN
LeaderLib_Helper_RemoveRunes(_Player, _Item);

PROC
LeaderLib_Helper_RemoveRunes((CHARACTERGUID)_Player, (ITEMGUID)_Item)
THEN
LeaderLib_Helper_RemoveRunes(_Player, _Item, 0);
LeaderLib_Helper_RemoveRunes(_Player, _Item, 1);
LeaderLib_Helper_RemoveRunes(_Player, _Item, 2);

QRY
LeaderLib_Helper_QRY_RemoveRunes((CHARACTERGUID)_Player, (ITEMGUID)_Item, (INTEGER)_SLot)
THEN
LeaderLib_Helper_RemoveRunes(_Player, _Item, _SLot);

PROC
LeaderLib_Helper_RemoveRunes((CHARACTERGUID)_Player, (ITEMGUID)_Item, (INTEGER)_SLot)
AND
ItemRemoveRune(_Player, _Item, _SLot, _Rune)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_InsertRuneInSlot((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Rune, (INTEGER)_Slot)
AND
NOT ItemGetRuneItemTemplate(_Item, _Slot, _)
THEN
ItemInsertRune(_Char, _Item, _Rune, _Slot);
//END_REGION

//REGION RUNE_SLOT_HELPERS
QRY
LeaderLib_Helper_QRY_RuneInSlot((ITEMGUID)_Item, (INTEGER)_Slot)
AND
ItemGetRuneItemTemplate(_Item, _Slot, _)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_RuneInAnySlot((ITEMGUID)_Item)
AND
LeaderLib_Helper_QRY_RuneInSlot(_Item, 0)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_RuneInAnySlot((ITEMGUID)_Item)
AND
LeaderLib_Helper_QRY_RuneInSlot(_Item, 1)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_RuneInAnySlot((ITEMGUID)_Item)
AND
LeaderLib_Helper_QRY_RuneInSlot(_Item, 2)
THEN
DB_NOOP(1);

QRY
LeaderLib_Helper_QRY_Internal_InitRuneSlotCount((ITEMGUID)_Item)
THEN
DB_LeaderLib_Helper_Temp_RuneSlotTotal(_Item, 0);

QRY
LeaderLib_Helper_QRY_Internal_UpdateRuneSlotCount((ITEMGUID)_Item, (INTEGER)_Count, (INTEGER)_LastCount)
THEN
NOT DB_LeaderLib_Helper_Temp_RuneSlotTotal(_Item, _LastCount);
DB_LeaderLib_Helper_Temp_RuneSlotTotal(_Item, _Count);

QRY
LeaderLib_Helper_QRY_Internal_ClearRuneSlotCount((ITEMGUID)_Item)
AND
DB_LeaderLib_Helper_Temp_RuneSlotTotal(_Item, _Count)
THEN
NOT DB_LeaderLib_Helper_Temp_RuneSlotTotal(_Item, _Count);

QRY
LeaderLib_Helper_QRY_ItemHasMaxRuneSlots((ITEMGUID)_Item)
AND
LeaderLib_Helper_QRY_Internal_InitRuneSlotCount(_Item)
AND
DB_LeaderLib_RuneSlotDeltaMod(_Group, _DeltaMod)
AND
ItemHasDeltaModifier(_Item, _DeltaMod, _Count)
AND
_Count > 0
AND
DB_LeaderLib_Helper_Temp_RuneSlotTotal(_Item, _LastTotal)
AND
IntegerSum(_LastTotal, _Count, _NextTotalSlots)
AND
LeaderLib_Helper_QRY_Internal_UpdateRuneSlotCount(_Item, _LastTotal, _NextTotalSlots)
AND
LeaderLog_QRY_LogInt("DEBUG", "[LeaderLib_00_5_Helpers_Items:QRY_ItemHasMaxRuneSlots] Rune slots in item: (", _NextTotalSlots, ").")
AND
_NextTotalSlots >= 3
THEN
DB_NOOP(1);

//No script-based deltamods added
QRY
LeaderLib_Helper_QRY_ItemHasMaxRuneSlots((ITEMGUID)_Item)
AND
DB_LeaderLib_Helper_Temp_RuneSlotTotal(_Item, 0)
AND
LeaderLib_Helper_QRY_RuneInSlot(_Item, 0)
AND
LeaderLib_Helper_QRY_RuneInSlot(_Item, 1)
AND
LeaderLib_Helper_QRY_RuneInSlot(_Item, 2)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib_00_5_Helpers_Items:QRY_ItemHasMaxRuneSlots] Item has 3 runeslots.");

//Clear without returning true if slots are less than 3
QRY
LeaderLib_Helper_QRY_ItemHasMaxRuneSlots((ITEMGUID)_Item)
AND
DB_LeaderLib_Helper_Temp_RuneSlotTotal(_Item, _Count)
AND
LeaderLib_Helper_QRY_Internal_ClearRuneSlotCount(_Item)
AND
_Count >= 3
THEN
DB_NOOP(1);
//END_REGION

//REGION COPY_ITEM_TRANSFORM
PROC
LeaderLib_Helper_CopyItemTransform((ITEMGUID)_Item, (ITEMGUID)_Target)
THEN
LeaderLib_Helper_CopyItemTransform(_Item, _Target, 0);

//Find a valid position, or use the given position if no valid one is found.
QRY
LeaderLib_Helper_QRY_FindValidPosition((GUIDSTRING)_Object, (REAL)_x, (REAL)_y, (REAL)_z, (REAL)_Radius)
AND
FindValidPosition(_x, _y, _z, _Radius, _Object, _tx, _ty, _tz)
THEN
DB_LeaderLib_Helper_Temp_ValidPosition(_Object, _x, _y, _z, _tx, _ty, _tz);

QRY
LeaderLib_Helper_QRY_FindValidPosition((GUIDSTRING)_Object, (REAL)_x, (REAL)_y, (REAL)_z, (REAL)_Radius)
AND
NOT DB_LeaderLib_Helper_Temp_ValidPosition(_Object, _x, _y, _z, _, _, _)
THEN
DB_LeaderLib_Helper_Temp_ValidPosition(_Object, _x, _y, _z, _x, _y, _z);

PROC
LeaderLib_Helper_CopyItemTransform((ITEMGUID)_Item, (ITEMGUID)_Target)
AND
ItemGetAmount(_Item, _Amount)
AND
ItemGetOwner(_Item, _Owner)
THEN
LeaderLib_Helper_CopyItemTransform(_Item, _Target, 0, _Amount, _Owner);

PROC
LeaderLib_Helper_CopyItemTransform((ITEMGUID)_Item, (ITEMGUID)_Target, (INTEGER)_UseValidPosition)
AND
ItemGetAmount(_Item, _Amount)
AND
ItemGetOwner(_Item, _Owner)
THEN
LeaderLib_Helper_CopyItemTransform(_Item, _Target, _UseValidPosition, _Amount, _Owner);

PROC
LeaderLib_Helper_CopyItemTransform((ITEMGUID)_Item, (ITEMGUID)_Target, (INTEGER)_UseValidPosition, (INTEGER)_Amount)
AND
ItemGetOwner(_Item, _Owner)
THEN
LeaderLib_Helper_CopyItemTransform(_Item, _Target, _UseValidPosition, _Amount, _Owner);

//Use valid position
PROC
LeaderLib_Helper_CopyItemTransform((ITEMGUID)_Item, (ITEMGUID)_Target, (INTEGER)_UseValidPosition, (INTEGER)_Amount, (CHARACTERGUID)_Owner)
AND
_UseValidPosition != 0
AND
GetPosition(_Target, _x, _y, _z)
AND
LeaderLib_Helper_QRY_FindValidPosition(_Item, _x, _y, _z, 1.0)
AND
DB_LeaderLib_Helper_Temp_ValidPosition(_Object, _x, _y, _z, _tx, _ty, _tz)
AND
GetRotation(_Target, _PitchDegree, _YawDegree, _RollDegree)
AND
RealProduct(_PitchDegree, 0.0174533, _Pitch)
AND
RealProduct(_YawDegree, 0.0174533, _Yaw)
AND
RealProduct(_RollDegree, 0.0174533, _Roll)
AND
ItemGetAmount(_Item, _Amount)
AND
ItemGetOwner(_Item, _Owner)
THEN
NOT DB_LeaderLib_Helper_Temp_ValidPosition(_Object, _x, _y, _z, _tx, _ty, _tz);
ItemToTransform(_Item, _x, _ty, _z, _Pitch, _Yaw, _Roll, _Amount, _Owner);

PROC
LeaderLib_Helper_CopyItemTransform((ITEMGUID)_Item, (ITEMGUID)_Target, 0, (INTEGER)_Amount, (CHARACTERGUID)_Owner)
AND
GetPosition(_Target, _x, _y, _z)
AND
GetRotation(_Target, _PitchDegree, _YawDegree, _RollDegree)
AND
RealProduct(_PitchDegree, 0.0174533, _Pitch)
AND
RealProduct(_YawDegree, 0.0174533, _Yaw)
AND
RealProduct(_RollDegree, 0.0174533, _Roll)
THEN
ItemToTransform(_Item, _x, _y, _z, _Pitch, _Yaw, _Roll, _Amount, _Owner);
//END_REGION

//REGION LEVEL_UP_ITEMS
//Sets the level of all equipped items to that of the character's.
//This will override the item's level (if it was higher, it will get reduced).
PROC
LeaderLib_Helper_LevelUpEquipment((CHARACTERGUID)_Character)
AND
CharacterGetLevel(_Character, _Level)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
THEN
ItemLevelUpTo(_Item, _Level);
SetVarInteger(_Item, "LeaderLib_Level", _Level);
//END_REGION

//REGION ADD_AND_EQUIP
QRY
LeaderLib_Helper_QRY_AddItemTemplateTo((CHARACTERGUID)_Char, (STRING)_Template)
THEN
ItemTemplateAddTo(_Template, _Char, 1, 1);

QRY
LeaderLib_Helper_QRY_AddItemTemplateTo((CHARACTERGUID)_Char, (STRING)_Template, (INTEGER)_Amount)
THEN
ItemTemplateAddTo(_Template, _Char, _Amount, 1);

QRY
LeaderLib_Helper_QRY_AddItemTemplateTo((CHARACTERGUID)_Char, (STRING)_Template, (INTEGER)_Amount, (INTEGER)_ShowNotification)
THEN
ItemTemplateAddTo(_Template, _Char, _Amount, _ShowNotification);

QRY
LeaderLib_Helper_QRY_AddAndEquipItemTemplate((CHARACTERGUID)_Char, (STRING)_Template)
THEN
LeaderLib_Helper_AddAndEquipItemTemplate(_Char, _Template, 1);

QRY
LeaderLib_Helper_QRY_AddAndEquipItemTemplate((CHARACTERGUID)_Char, (STRING)_Template, (INTEGER)_ShowNotification)
THEN
LeaderLib_Helper_AddAndEquipItemTemplate(_Char, _Template, _ShowNotification);

PROC
LeaderLib_Helper_AddAndEquipItemTemplate((CHARACTERGUID)_Char, (STRING)_Template)
THEN
LeaderLib_Helper_AddAndEquipItemTemplate(_Char, _Template, 1);

PROC
LeaderLib_Helper_AddAndEquipItemTemplate((CHARACTERGUID)_Char, (STRING)_Template, (INTEGER)_ShowNotification)
AND
LeaderLib_Helper_QRY_AddItemTemplateTo(_Char, _Template, 1, _ShowNotification)
AND
GetItemForItemTemplateInInventory(_Char, _Template, _Item)
THEN
CharacterEquipItem(_Char, _Item);

QRY
LeaderLib_Helper_QRY_EquipOrAddItemTemplate((CHARACTERGUID)_Char, (STRING)_Template)
THEN
LeaderLib_Helper_EquipOrAddItemTemplate(_Char, _Template, 1);

QRY
LeaderLib_Helper_QRY_EquipOrAddItemTemplate((CHARACTERGUID)_Char, (STRING)_Template, (INTEGER)_ShowNotification)
THEN
LeaderLib_Helper_EquipOrAddItemTemplate(_Char, _Template, _ShowNotification);

PROC
LeaderLib_Helper_EquipOrAddItemTemplate((CHARACTERGUID)_Char, (STRING)_Template)
THEN
LeaderLib_Helper_EquipOrAddItemTemplate(_Char, _Template, 1);

PROC
LeaderLib_Helper_EquipOrAddItemTemplate((CHARACTERGUID)_Char, (STRING)_Template, (INTEGER)_ShowNotification)
AND
LeaderLib_Helper_QRY_GetItemByTemplate(_Char, _Template)
AND
DB_LeaderLib_Helper_Temp_ItemFound(_Char, _Template, _Item)
THEN
NOT DB_LeaderLib_Helper_Temp_ItemFound(_Char, _Template, _Item);
DB_LeaderLib_Helper_Temp_ItemFoundAndEquipped(_Char, _Template, _Item);
CharacterEquipItem(_Char, _Item);

PROC
LeaderLib_Helper_EquipOrAddItemTemplate((CHARACTERGUID)_Char, (STRING)_Template, (INTEGER)_ShowNotification)
AND
NOT DB_LeaderLib_Helper_Temp_ItemFoundAndEquipped(_Char, _Template, _)
THEN
LeaderLib_Helper_EquipOrAddItemTemplate(_Char, _Template, _ShowNotification);

PROC
LeaderLib_Helper_EquipOrAddItemTemplate((CHARACTERGUID)_Char, (STRING)_Template, (INTEGER)_ShowNotification)
AND
DB_LeaderLib_Helper_Temp_ItemFoundAndEquipped(_Char, _Template, _Item)
THEN
NOT DB_LeaderLib_Helper_Temp_ItemFoundAndEquipped(_Char, _Template, _Item);
//END_REGION

//REGION GET_ITEM_BY_TEMPLATE
QRY
LeaderLib_Helper_QRY_GetItemByTemplate((CHARACTERGUID)_Char, (STRING)_Template)
AND
GetItemForItemTemplateInInventory(_Char, _Template, _Item)
THEN
DB_LeaderLib_Helper_Temp_ItemFound(_Char, _Template, _Item);

QRY
LeaderLib_Helper_QRY_GetItemByTemplate((CHARACTERGUID)_Char, (STRING)_Template)
AND
NOT DB_LeaderLib_Helper_Temp_ItemFound(_Char, _Template, _)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Char, _Slot, _Item)
AND
GetTemplate(_Item, _Template)
THEN
DB_LeaderLib_Helper_Temp_ItemFound(_Char, _Template, (ITEMGUID)_Item);
//END_REGION

//REGION BEHAVIOR_ITEM_SET_AMOUNT
// Uses behavior to modify an item's amount by a set value. If the next amount is <= 0, the item is destroyed.
PROC
LeaderLib_Helper_ModifyItemAmount((ITEMGUID)_Item, (INTEGER)_ModifyBy)
AND
ItemGetAmount(_Item, _Amount)
AND
IntegerSum(_Amount, _ModifyBy, _NextAmount)
THEN
SetVarInteger(_Item, "LeaderLib_ItemAmount", _Amount);
SetStoryEvent(_Item, "LeaderLib_Commands_Internal_ItemSetAmount");

IF
StoryEvent((ITEMGUID)_Item, "LeaderLib_Commands_ItemSetAmount")
AND
LeaderLib_Variables_QRY_IntegerVariableSet((GUIDSTRING)_Item, "LeaderLib_ItemAmount")
THEN
DB_LeaderLib_Helper_Temp_ItemSetAmount((ITEMGUID)_Item);
SetStoryEvent(_Item, "LeaderLib_Commands_Internal_ItemSetAmount");

IF
StoryEvent((ITEMGUID)_Item, "LeaderLib_Commands_ItemSetAmount")
AND
NOT DB_LeaderLib_Helper_Temp_ItemSetAmount(_Item)
AND
ItemGetAmount(_Item, _Amount)
THEN
DB_LeaderLib_Helper_Temp_ItemSetAmount(_Item);
SetVarInteger(_Item, "LeaderLib_ItemAmount", _Amount);
SetStoryEvent(_Item, "LeaderLib_Commands_Internal_ItemSetAmount");

IF
StoryEvent((ITEMGUID)_Item, "LeaderLib_Commands_ItemSetAmount")
AND
DB_LeaderLib_Helper_Temp_ItemSetAmount(_Item)
THEN
NOT DB_LeaderLib_Helper_Temp_ItemSetAmount(_Item);
//END_REGION

//REGION BEHAVIOR_CREATE_BY_STAT
PROC
LeaderLib_Items_CreateByStat((CHARACTERGUID)_Target, (STRING)_Stat)
THEN
LeaderLib_Items_CreateByStat(_Target, _Stat, "", 1, 1);

PROC
LeaderLib_Items_CreateByStat((CHARACTERGUID)_Target, (STRING)_Stat, (STRING)_AddedEvent)
THEN
LeaderLib_Items_CreateByStat(_Target, _Stat, _AddedEvent, 1, 1);

PROC
LeaderLib_Items_CreateByStat((CHARACTERGUID)_Target, (STRING)_Stat, (STRING)_AddedEvent, (INTEGER)_Amount)
THEN
LeaderLib_Items_CreateByStat(_Target, _Stat, _AddedEvent, _Amount, 1);

PROC
LeaderLib_Items_CreateByStat((CHARACTERGUID)_Target, (STRING)_Stat, (STRING)_AddedEvent, (INTEGER)_Amount, (INTEGER)_ShowInTrade)
THEN
LeaderLib_Items_CreateByStat(_Target, _Stat, _AddedEvent, _Amount, _ShowInTrade, CHARACTERGUID_S_LeaderLib_Dummy_TargetHelper_A_36069245-0e2d-44b1-9044-6797bd29bb15);

PROC
LeaderLib_Items_CreateByStat((CHARACTERGUID)_Target, (STRING)_Stat, (STRING)_AddedEvent, (INTEGER)_Amount, (INTEGER)_ShowInTrade, (CHARACTERGUID)_Proxy)
THEN
SetVarFixedString(_Proxy, "LeaderLib_CBS_Generate_Stat", _Stat);
SetVarInteger(_Proxy, "LeaderLib_CBS_Generate_Amount", _Amount);
SetVarInteger(_Proxy, "LeaderLib_CBS_Generate_ShowInTrade", _ShowInTrade);
SetVarObject(_Proxy, "LeaderLib_CBS_ItemAdded_Target", _Target);
SetVarString(_Proxy, "LeaderLib_CBS_ItemAdded_AddedEvent", _AddedEvent);
SetStoryEvent(_Proxy, "LeaderLib_CreateByStat_Generate");
//END_REGION

//REGION EQUIP_IN_SLOT
// [BEGIN_NO_OSITOOLS]
PROC
LeaderLib_Helper_EquipInSlot((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Slot)
THEN
CharacterEquipItem(_Char, _Item);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LeaderLib_Helper_EquipInSlot((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Slot)
AND
String(_Char, _CharStr)
AND
String(_Item, _ItemStr)
THEN
NRD_ModCall("LeaderLib", "EquipInSlot", _CharStr, _ItemStr, _Slot);
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
