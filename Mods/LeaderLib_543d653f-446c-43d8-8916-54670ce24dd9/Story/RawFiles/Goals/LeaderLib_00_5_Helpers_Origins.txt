Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION ORIGIN_SETUP

PROC
LeaderLib_Origins_Register_Main((CHARACTERGUID)_Char, (STRING)_NpcAlignment, (STRING)_DefaultFaction)
THEN
DB_Origins(_Char);
DB_OriginNPCAlignment(_Char, _NpcAlignment);
DB_GLO_PartyMembers_OriginalAlignment(_Char, _NpcAlignment);
DB_GLO_PartyMembers_DefaultFaction(_Char, _DefaultFaction);

PROC
LeaderLib_Origins_Register_Tags((CHARACTERGUID)_Char, (STRING)_ProfessionTag1, (STRING)_ProfessionTag2, (STRING)_PolymorphPreservedTag)
AND
_ProfessionTag1 != ""
THEN
DB_OriginProfessionTags(_Char, _ProfessionTag1);
SetTag(_Char, _ProfessionTag1);

PROC
LeaderLib_Origins_Register_Tags((CHARACTERGUID)_Char, (STRING)_ProfessionTag1, (STRING)_ProfessionTag2, (STRING)_PolymorphPreservedTag)
AND
_ProfessionTag2 != ""
THEN
DB_OriginProfessionTags(_Char, _ProfessionTag2);
SetTag(_Char, _ProfessionTag2);

PROC
LeaderLib_Origins_Register_Tags((CHARACTERGUID)_Char, (STRING)_ProfessionTag1, (STRING)_ProfessionTag2, (STRING)_PolymorphPreservedTag)
AND
_PolymorphPreservedTag != ""
THEN
DB_PolymorphTags(_Char, _PolymorphPreservedTag);

PROC
LeaderLib_Origins_Register_Tags((CHARACTERGUID)_Char, (STRING)_ProfessionTag1, (STRING)_ProfessionTag2, (STRING)_PolymorphPreservedTag)
AND
NOT DB_CurrentLevel(_)
THEN
DB_LeaderLib_Origins_Temp_ApplyProfessionTags(_Char);

PROC
LeaderLib_Origins_Register_Dialog_Default((CHARACTERGUID)_Char, (STRING)_DefaultRecruitmentDialog, (STRING)_DefaultInPartyDialog)
THEN
DB_OriginRecruitmentDialog(_Char, _DefaultRecruitmentDialog);
DB_OriginInPartyDialog(_Char, _DefaultInPartyDialog);

PROC
LeaderLib_Origins_Register_All((CHARACTERGUID)_Char, (STRING)_NpcAlignment, (STRING)_DefaultFaction, (STRING)_DefaultRecruitmentDialog, (STRING)_DefaultInPartyDialog, (STRING)_ProfessionTag1, (STRING)_ProfessionTag2, (STRING)_PolymorphPreservedTag)
THEN
LeaderLib_Origins_Register_Main(_Char, _NpcAlignment, _DefaultFaction);
LeaderLib_Origins_Register_Tags(_Char, _ProfessionTag1, _ProfessionTag2, _PolymorphPreservedTag);
LeaderLib_Origins_Register_Dialog_Default(_Char, _DefaultRecruitmentDialog, _DefaultInPartyDialog);

PROC
LeaderLib_Origins_Internal_ApplyProfessionTags()
AND
DB_LeaderLib_Origins_Temp_ApplyProfessionTags(_Char)
AND
DB_OriginProfessionTags(_Char, _ProfessionTag)
THEN
SetTag(_Char, _ProfessionTag);
//END_REGION

//REGION STARTING_ITEMS
PROC
LeaderLib_Origins_Register_StartingItems_Global((CHARACTERGUID)_Char, (ITEMGUID)_Item)
THEN
LeaderLib_Origins_Register_StartingItems_Global(_Char, _Item, 0);

PROC
LeaderLib_Origins_Register_StartingItems_Global((CHARACTERGUID)_Char, (ITEMGUID)_Item, (INTEGER)_AutoEquip)
THEN
DB_LeaderLib_Origins_GlobalStartingItems(_Char, _Item, _AutoEquip);

PROC
LeaderLib_Origins_Internal_MoveGlobalItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, (INTEGER)_AutoEquip)
AND
ItemGetAmount(_Item, _Amount)
THEN
ItemToInventory(_Item, _Char, _Amount, 0, 1);
ItemSetOwner(_Item, _Char);

PROC
LeaderLib_Origins_Internal_MoveGlobalItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, 1)
AND
ItemIsEquipable(_Item, 1)
AND
ObjectExists(_Item, 1)
AND
ObjectExists(_Char, 1)
THEN
CharacterEquipItem((CHARACTERGUID)_Char, (ITEMGUID)_Item);
//END_REGION

//REGION GAME_LEVEL_INIT
PROC
LeaderLib_Origins_Internal_GameLevelInit((STRING)_Region)
AND
DB_LeaderLib_Origins_GlobalStartingItems(_Char, _Item, _AutoEquip)
AND
ObjectExists(_Char, 1)
AND
ObjectExists(_Item, 1)
THEN
LeaderLib_Origins_Internal_MoveGlobalItem(_Char, _Item, _AutoEquip);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"