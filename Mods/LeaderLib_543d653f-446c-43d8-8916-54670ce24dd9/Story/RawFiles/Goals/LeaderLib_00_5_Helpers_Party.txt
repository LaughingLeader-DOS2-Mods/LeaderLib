Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
/*
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
DB_CurrentLevel(_Level)
AND
NOT LeaderLib_Helper_QRY_IsCharacterCreationOrLobby(_Level)
AND
NOT GlobalGetFlag("LeaderLib_CountedParties", 1)
THEN
TimerCancel("LeaderLib_Timers_StartCountingParty");
TimerLaunch("LeaderLib_Timers_StartCountingParty", 2500);
DB_LeaderLib_PartyCounter_TimerIsActive(1);
*/

IF
GameStarted(_,1)
THEN
LeaderLib_Party_CountParties();

IF
RegionEnded(_Region)
AND
IsCharacterCreationLevel(_Region, 1)
THEN
LeaderLib_Party_CountParties();

IF
SavegameLoaded(_,_,_,_)
AND
CharacterGetHostCharacter(_Host)
AND
PartyGetFlag(_Host, "LeaderLib_PartyCounted", 0)
THEN
LeaderLib_Party_CountParties();

IF
ObjectFlagSet("LeaderLib_PartyCounted", _, _)
AND
DB_LeaderLib_PartyCounter_TimerIsActive(1)
THEN
NOT DB_LeaderLib_PartyCounter_TimerIsActive(1);
TimerCancel("LeaderLib_Timers_StartCountingParty");
GlobalSetFlag("LeaderLib_CountedParties");

IF
TimerFinished("LeaderLib_Timers_StartCountingParty")
AND
DB_LeaderLib_PartyCounter_TimerIsActive(1)
THEN
NOT DB_LeaderLib_PartyCounter_TimerIsActive(1);
LeaderLib_Party_CountParties();

QRY
LeaderLib_Party_QRY_CountParties()
THEN
LeaderLib_Party_CountParties();

PROC
LeaderLib_Party_CountParties()
THEN
GlobalSetFlag("LeaderLib_CountedParties");
SysClear("DB_LeaderLib_Temp_PartyCheck", 2);
SysClear("DB_LeaderLib_PartyMembers", 2);

PROC
LeaderLib_Party_CountParties()
AND
DB_IsPlayer(_Char)
AND
DB_IsPlayer(_Player)
AND
_Char != _Player
AND
CharacterIsInPartyWith(_Char, _Player, 1)
THEN
DB_LeaderLib_PartyMembers(_Player, _Char);
DB_LeaderLib_PartyMembers(_Char, _Player);
LeaderLib_Party_Internal_BuildPartyCount(_Char);
LeaderLib_Party_Internal_BuildPartyCount(_Player);

PROC
LeaderLib_Party_Internal_Old_CountParties()
THEN
GlobalSetFlag("LeaderLib_CountedParties");
SysClear("DB_LeaderLib_Temp_PartyCheck", 2);
IterateParties("LeaderLib_Events_ResetPartyCount");
IterateParties("LeaderLib_Events_AddToPartyCheck");

IF
StoryEvent((CHARACTERGUID)_Char, "LeaderLib_Events_ResetPartyCount")
AND
NOT QRY_IsSummonOrPartyFollower(_Char)
THEN
SetVarInteger(_Char, "LeaderLib_PartyCount", 1);

IF
StoryEvent((CHARACTERGUID)_Char, "LeaderLib_Events_ResetPartyCount")
AND
NOT QRY_IsSummonOrPartyFollower(_Char)
AND
PartyGetFlag(_Char, "LeaderLib_PartyCounted", 1)
THEN
PartyClearFlag(_Char, "LeaderLib_PartyCounted", 0);

IF
StoryEvent((CHARACTERGUID)_Char, "LeaderLib_Events_AddToPartyCheck")
AND
NOT QRY_IsSummonOrPartyFollower(_Char)
AND
PartyGetFlag(_Char, "LeaderLib_PartyCounted", 0)
AND
GetUUID(_Char, _ID)
AND
StringConcatenate("LeaderLib_Events_CheckParty_", _ID, _EventName)
THEN
PartySetFlag(_Char, "LeaderLib_PartyCounted", 0);
DB_LeaderLib_Temp_PartyCheck(_Char, _EventName);
LeaderLib_Timers_StartObjectTimer(_Char, 16, "Timers_LeaderLib_StartPartyCountIterator", "LeaderLib_StartPartyCountIterator");

IF
StoryEvent((CHARACTERGUID)_Char, "LeaderLib_StartPartyCountIterator")
AND
DB_LeaderLib_Temp_PartyCheck(_Char, _EventName)
THEN
SetVarInteger(_Char, "LeaderLib_PartyCount", 1);
LeaderLog_Log("DEBUG", "[LeaderLib:Party:LeaderLib_StartPartyCountIterator] Iterating party and setting count.");
SysActivateGoal("LeaderLib_19_TS_PartyCountIterator");
IterateParty(_Char, _EventName);
LeaderLib_Timers_StartObjectTimer(_Char, 250, "LeaderLib_Timers_ResetPartyDB_", "LeaderLib_PartyCountFinished");

IF
StoryEvent((CHARACTERGUID)_Char, "LeaderLib_PartyCountFinished")
AND
DB_LeaderLib_Temp_PartyCheck(_Char, _EventName)
THEN
NOT DB_LeaderLib_Temp_PartyCheck(_Char, _EventName);
LeaderLog_Log("DEBUG", "[LeaderLib:Party:LeaderLib_PartyCountFinished] Iterator finished.");
TimerCancel("LeaderLib_Timers_PartyCountingFinished");
TimerLaunch("LeaderLib_Timers_PartyCountingFinished", 500);

IF
TimerFinished("LeaderLib_Timers_PartyCountingFinished")
THEN
SysCompleteGoal("LeaderLib_19_TS_PartyCountIterator");

/* Moved to LeaderLib_19_TS_PartyCountIterator
IF
StoryEvent((CHARACTERGUID)_Char, _EventName)
AND
NOT QRY_IsSummonOrPartyFollower(_Char)
AND
DB_LeaderLib_Temp_PartyCheck(_Leader, _EventName)
AND
_Char != _Leader
THEN
LeaderLib_Party_IncreaseCount(_Leader);
LeaderLib_Party_IncreaseCount(_Char);
*/
//REGION INCREASE_DECREASE
PROC
LeaderLib_Party_IncreaseCount((CHARACTERGUID)_Char)
THEN
LeaderLib_Variables_ModifyVariableInt(_Char, "LeaderLib_PartyCount", 1, 999, 1);

PROC
LeaderLib_Party_DecreaseCount((CHARACTERGUID)_Char)
THEN
LeaderLib_Variables_ModifyVariableInt(_Char, "LeaderLib_PartyCount", -1, 999, 1);
//END_REGION

//REGION EVENTS
IF
CharacterJoinedParty(_Char)
AND
DB_LeaderLib_PartyCounter_TimerIsActive(1)
THEN
TimerCancel("LeaderLib_Timers_StartCountingParty");
TimerLaunch("LeaderLib_Timers_StartCountingParty", 999);

IF
CharacterJoinedParty(_Char)
AND
NOT QRY_IsSummonOrPartyFollower(_Char)
AND
NOT DB_LeaderLib_Temp_PartyCheck(_, _)
AND
DB_IsPlayer(_Player)
AND
_Char != _Player
AND
CharacterIsInPartyWith(_Char, _Player, 1)
THEN
DB_LeaderLib_PartyMembers(_Player, _Char);
DB_LeaderLib_PartyMembers(_Char, _Player);
LeaderLib_Party_Internal_BuildPartyCount(_Char);
LeaderLib_Party_Internal_BuildPartyCount(_Player);
LeaderLib_Party_OnCharacterJoinedParty_Start(_Char, _Player);

PROC
LeaderLib_Party_Internal_BuildPartyCount((CHARACTERGUID)_Char)
THEN
LeaderLib_Variables_SetVariableInt(_Char, "LeaderLib_PartyCount", 1);

PROC
LeaderLib_Party_Internal_BuildPartyCount((CHARACTERGUID)_Char)
AND
DB_LeaderLib_PartyMembers(_Char, _PartyMember)
THEN
LeaderLib_Variables_ModifyVariableInt(_Char, "LeaderLib_PartyCount", 1, 999, 1);

PROC
LeaderLib_Party_OnCharacterJoinedParty_Start((CHARACTERGUID)_JoiningCharacter, (CHARACTERGUID)_PartyMember)
AND
GetVarInteger(_PartyMember, "LeaderLib_PartyCount", _Count)
THEN
LeaderLib_Party_OnCharacterJoinedParty(_JoiningCharacter, _PartyMember, _Count);

PROC
LeaderLib_Party_OnCharacterJoinedParty_Start((CHARACTERGUID)_JoiningCharacter, (CHARACTERGUID)_PartyMember)
AND
PartyGetFlag(_PartyMember, "LeaderLib_PartyCounted", 0)
THEN
PartySetFlag(_PartyMember, "LeaderLib_PartyCounted", 0);

PROC
LeaderLib_Party_OnCharacterJoinedParty((CHARACTERGUID)_JoiningCharacter, (CHARACTERGUID)_PartyMember, (INTEGER)_PartyCount)
THEN
DB_NOOP(1);

IF
CharacterLeftParty(_Char)
AND
DB_LeaderLib_PartyMembers(_Char, _Player)
THEN
NOT DB_LeaderLib_PartyMembers(_Player, _Char);
NOT DB_LeaderLib_PartyMembers(_Char, _Player);
LeaderLib_Party_Internal_BuildPartyCount(_Char);
LeaderLib_Party_Internal_BuildPartyCount(_Player);
LeaderLib_Party_Internal_OnCharacterLeftParty_Start(_Char, _Player);

PROC
LeaderLib_Party_Internal_OnCharacterLeftParty_Start((CHARACTERGUID)_LeavingCharacter, (CHARACTERGUID)_FormerPartyMember)
AND
GetVarInteger(_FormerPartyMember, "LeaderLib_PartyCount", _Count)
THEN
LeaderLib_Party_OnCharacterLeftParty(_LeavingCharacter, _FormerPartyMember, _Count);

PROC
LeaderLib_Party_OnCharacterLeftParty((CHARACTERGUID)_LeavingCharacter, (CHARACTERGUID)_FormerPartyMember, (INTEGER)_PartyCount)
THEN
DB_NOOP(1);
//END_REGION

//REGION TRACING
PROC
LeaderLib_Party_OnCharacterJoinedParty((CHARACTERGUID)_JoiningCharacter, (CHARACTERGUID)_PartyMember, (INTEGER)_PartyCount)
AND
CharacterGetDisplayName(_JoiningCharacter, _, _JoiningCharacterName)
AND
CharacterGetDisplayName(_PartyMember, _, _PartyMemberName)
AND
IntegertoString(_PartyCount, _CountStr)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Party:OnCharacterJoinedParty] [",_JoiningCharacterName,"] is now in a party with [",_PartyMemberName,"]. Party Count [",_CountStr,"].");

PROC
LeaderLib_Party_OnCharacterLeftParty((CHARACTERGUID)_LeavingCharacter, (CHARACTERGUID)_FormerPartyMember, (INTEGER)_PartyCount)
AND
CharacterGetDisplayName(_LeavingCharacter, _, _LeavingCharacterName)
AND
CharacterGetDisplayName(_FormerPartyMember, _, _FormerPartyMemberName)
AND
IntegertoString(_PartyCount, _CountStr)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Party:OnCharacterLeftParty] [",_LeavingCharacterName,"] is no longer in a party with [",_FormerPartyMemberName,"]. Party Count [",_CountStr,"].");
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1,5,4,0)
THEN
LeaderLib_Party_CountParties();
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
