Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
DB_CurrentLevel(_Level)
AND
NOT LeaderLib_Helper_QRY_IsCharacterCreationOrLobby(_Level)
AND
NOT GlobalGetFlag("LeaderLib_CountedParties", 1)
THEN
TimerCancel("LeaderLib_Timers_StartCountingParty");
TimerLaunch("LeaderLib_Timers_StartCountingParty", 500);

IF
TimerFinished("LeaderLib_Timers_StartCountingParty")
THEN
LeaderLib_Party_CountParties();

QRY
LeaderLib_Party_QRY_CountParties()
THEN
LeaderLib_Party_CountParties();

PROC
LeaderLib_Party_CountParties()
THEN
SysActivateGoal("LeaderLib_19_TS_PartyCountIterator");
GlobalSetFlag("LeaderLib_CountedParties");
SysClear("DB_LeaderLib_Temp_PartyCheck", 2);
IterateParties("LeaderLib_Events_ResetPartyCount");
IterateParties("LeaderLib_Events_AddToPartyCheck");
FireOsirisEvents();

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Events_ResetPartyCount")
AND
NOT QRY_IsSummonOrPartyFollower(_Character)
THEN
SetVarInteger(_Character, "LeaderLib_PartyCount", 1);

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Events_ResetPartyCount")
AND
NOT QRY_IsSummonOrPartyFollower(_Character)
AND
PartyGetFlag(_Character, "LeaderLib_PartyCounted", 1)
THEN
PartyClearFlag(_Character, "LeaderLib_PartyCounted", 0);

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Events_AddToPartyCheck")
AND
NOT QRY_IsSummonOrPartyFollower(_Character)
AND
PartyGetFlag(_Character, "LeaderLib_PartyCounted", 0)
AND
GetUUID(_Character, _ID)
AND
StringConcatenate("LeaderLib_Events_CheckParty_", _ID, _EventName)
THEN
PartySetFlag(_Character, "LeaderLib_PartyCounted");
DB_LeaderLib_Temp_PartyCheck(_Character, _EventName);
IterateParty(_Character, _EventName);
LeaderLib_Timers_StartObjectTimer(_Character, 250, "LeaderLib_Timers_ResetPartyDB_", "LeaderLib_PartyCountFinished");

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_PartyCountFinished")
AND
DB_LeaderLib_Temp_PartyCheck(_Character, _EventName)
THEN
NOT DB_LeaderLib_Temp_PartyCheck(_Character, _EventName);
TimerCancel("LeaderLib_Timers_PartyCountingFinished");
TimerLaunch("LeaderLib_Timers_PartyCountingFinished", 500);

IF
TimerFinished("LeaderLib_Timers_PartyCountingFinished")
THEN
SysCompleteGoal("LeaderLib_19_TS_PartyCountIterator");

/* Moved to LeaderLib_19_TS_PartyCountIterator
IF
StoryEvent((CHARACTERGUID)_Character, _EventName)
AND
NOT QRY_IsSummonOrPartyFollower(_Character)
AND
DB_LeaderLib_Temp_PartyCheck(_Leader, _EventName)
AND
_Character != _Leader
THEN
LeaderLib_Party_IncreaseCount(_Leader);
LeaderLib_Party_IncreaseCount(_Character);
*/
//REGION INCREASE_DECREASE
PROC
LeaderLib_Party_IncreaseCount((CHARACTERGUID)_Character)
THEN
LeaderLib_Helper_ModifyVariableInt(_Character, "LeaderLib_PartyCount", 1, 999, 1);

PROC
LeaderLib_Party_DecreaseCount((CHARACTERGUID)_Character)
THEN
LeaderLib_Helper_ModifyVariableInt(_Character, "LeaderLib_PartyCount", -1, 999, 1);
//END_REGION

//REGION EVENTS
IF
CharacterJoinedParty(_Character)
AND
NOT QRY_IsSummonOrPartyFollower(_Character)
AND
NOT DB_LeaderLib_Temp_PartyCheck(_, _)
AND
DB_IsPlayer(_Player)
AND
_Character != _Player
AND
CharacterIsInPartyWith(_Character, _Player, 1)
THEN
LeaderLib_Party_IncreaseCount(_Player);
DB_LeaderLib_PartyMembers(_Player, _Character);
DB_LeaderLib_PartyMembers(_Character, _Player);
LeaderLib_Party_Internal_CopyPartyCount(_Character, _Player);

PROC
LeaderLib_Party_Internal_CopyPartyCount((CHARACTERGUID)_Character, (CHARACTERGUID)_Player)
AND
GetVarInteger(_Player, "LeaderLib_PartyCount", _Count)
THEN
SetVarInteger(_Character, "LeaderLib_PartyCount", _Count);
LeaderLib_Party_OnCharacterJoinedParty(_Character, _Player, _Count);

/*
IF
CharacterJoinedParty(_Character)
AND
LeaderLib_Party_QRY_CountNotSet(_Character)
AND
DB_IsPlayer(_Player)
AND
_Player != _Character
AND
CharacterIsInPartyWith(_Character, _Player, 1)
AND
LeaderLib_Party_QRY_CountNotSet(_Character)
AND
GetVarInteger(_Player, "LeaderLib_PartyCount", _Count)
THEN
SetVarInteger(_Player, "LeaderLib_PartyCount", _Count);
*/

PROC
LeaderLib_Party_OnCharacterJoinedParty((CHARACTERGUID)_JoiningCharacter, (CHARACTERGUID)_PartyMember, (INTEGER)_PartyCount)
THEN
DB_NOOP(1);

IF
CharacterLeftParty(_Character)
AND
DB_IsPlayer(_Player)
AND
_Player != _Character
AND
DB_LeaderLib_PartyMembers(_Player, _Character)
THEN
LeaderLib_Party_DecreaseCount(_Player);
NOT DB_LeaderLib_PartyMembers(_Player, _Character);

IF
CharacterLeftParty(_Character)
AND
NOT QRY_IsSummonOrPartyFollower(_Character)
THEN
SetVarInteger(_Character, "LeaderLib_PartyCount", 1);

IF
CharacterLeftParty(_Character)
AND
DB_LeaderLib_PartyMembers(_Character, _Player)
AND
GetVarInteger(_Player, "LeaderLib_PartyCount", _Count)
THEN
NOT DB_LeaderLib_PartyMembers(_Character, _Player);
LeaderLib_Party_OnCharacterLeftParty(_Character, _Player, _Count);

PROC
LeaderLib_Party_OnCharacterLeftParty((CHARACTERGUID)_LeavingCharacter, (CHARACTERGUID)_FormerPartyMember, (INTEGER)_PartyCount)
THEN
DB_NOOP(1);
//END_REGION

//REGION TRACING
PROC
LeaderLib_Party_OnCharacterJoinedParty((CHARACTERGUID)_JoiningCharacter, (CHARACTERGUID)_PartyMember, (INTEGER)_PartyCount)
AND
CharacterGetDisplayName(_JoiningCharacter, _, _JoiningCharacterName)
AND
CharacterGetDisplayName(_PartyMember, _, _PartyMemberName)
AND
IntegertoString(_PartyCount, _CountStr)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Party:OnCharacterJoinedParty] [",_JoiningCharacterName,"] is now in a party with [",_PartyMemberName,"]. Party Count [",_CountStr,"].");

PROC
LeaderLib_Party_OnCharacterLeftParty((CHARACTERGUID)_LeavingCharacter, (CHARACTERGUID)_FormerPartyMember, (INTEGER)_PartyCount)
AND
CharacterGetDisplayName(_LeavingCharacter, _, _LeavingCharacterName)
AND
CharacterGetDisplayName(_FormerPartyMember, _, _FormerPartyMemberName)
AND
IntegertoString(_PartyCount, _CountStr)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Party:OnCharacterLeftParty] [",_LeavingCharacterName,"] is no longer in a party with [",_FormerPartyMemberName,"]. Party Count [",_CountStr,"].");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
