Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//Mimics Treasure Table logic where each entry (subtable)'s dropcount drops by 1 each time it is rolled, until all entries hit 0 and the table resets.

//REGION TABLE_REGISTER
PROC
LeaderLib_Randomization_Register_Table((STRING)_TableID, (INTEGER)_Max)
THEN
DB_LeaderLib_Randomization_Tables(_TableID, _Max);

PROC
LeaderLib_Randomization_Register_Entry((STRING)_TableID, (STRING)_EntryID, (INTEGER)_Frequency, (INTEGER)_StartingDropCount)
AND
NOT DB_LeaderLib_Randomization_Tables(_TableID, _)
THEN
LeaderLib_Randomization_Register_Table(_TableID, 999);

PROC
LeaderLib_Randomization_Register_Entry((STRING)_TableID, (STRING)_EntryID, (INTEGER)_Frequency, (INTEGER)_StartingDropCount)
THEN
DB_LeaderLib_Randomization_Entries(_TableID, _EntryID, _Frequency, _StartingDropCount);
//END_REGION

//REGION TABLE_BUILD
QRY
LeaderLib_Randomization_Tables_QRY_TableInitialized((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _Max)
THEN
DB_NOOP(1);

PROC
LeaderLib_Randomization_Tables_Internal_CheckForCompletion((GUIDSTRING)_Object, (STRING)_TableID)
AND
NOT DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _, _)
THEN
LeaderLib_Randomization_Tables_OnTableReset(_Object, _TableID);

PROC
LeaderLib_Randomization_Tables_OnTableReset((GUIDSTRING)_Object, (STRING)_TableID)
AND
String(_Object, _ObjectStr)
THEN
LeaderLog_Log("TRACE", "[LeaderLib_00_5_Helpers_Randomization:OnTableReset] Table (",_TableID,") finished rolling all entries for object (",_ObjectStr,").");

PROC
LeaderLib_Randomization_Tables_BuildTable((GUIDSTRING)_Object, (STRING)_TableID)
AND
NOT DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _, _)
AND
DB_LeaderLib_Randomization_Entries(_TableID, _EntryID, _Frequency, _StartingDropCount)
THEN
DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _EntryID, _StartingDropCount);

PROC
LeaderLib_Randomization_Tables_BuildTable((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _LastMax)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _LastMax);

PROC
LeaderLib_Randomization_Tables_BuildTable((GUIDSTRING)_Object, (STRING)_TableID)
THEN
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, 0, 0);
LeaderLib_Randomization_Tables_Internal_BuildTableEntries(_Object, _TableID);
LeaderLib_Randomization_Tables_Internal_BuildIndexes_Start(_Object, _TableID);
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Start(_Object, _TableID);

PROC
LeaderLib_Randomization_Tables_Internal_BuildTableEntries((GUIDSTRING)_Object, (STRING)_TableID)
AND
NOT DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _, _, _, _, _)
AND
DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _EntryID, _DropCount)
THEN
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, -1, _EntryID, _DropCount, 0, 0);

PROC
LeaderLib_Randomization_Tables_Internal_BuildIndexes_Start((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _DropCount, _Start, _End)
THEN
LeaderLib_Randomization_Tables_Internal_SetIndex(_Object, _TableID, _EntryID);

PROC
LeaderLib_Randomization_Tables_Internal_SetIndex((GUIDSTRING)_Object, (STRING)_TableID, (STRING)_EntryID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Index, _MaxRange)
AND
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _LastIndex, _EntryID, _DropCount, _Start, _End)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _LastIndex, _EntryID, _DropCount, _Start, _End);
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _DropCount, _Start, _End);
LeaderLib_Randomization_Tables_Internal_IncreaseTotal(_Object, _TableID);
LeaderLog_Log("TRACE", "[LeaderLib_00_5_Helpers_Randomization:SetIndex] Table (",_TableID,") Entry (",_EntryID,")");
LeaderLog_LogInt("TRACE", "[LeaderLib_00_5_Helpers_Randomization:SetIndex] --- Index ", _Index, "");

PROC
LeaderLib_Randomization_Tables_Internal_IncreaseTotal((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange)
AND
IntegerSum(_Total, 1, _Next)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange);
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Next, _MaxRange);

PROC
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Start((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Tables(_TableID, _Max)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange)
AND
_Total > 0
AND
IntegerDivide(_Max, _Total, _DefaultAmountPerEntry)
THEN
LeaderLog_Log("TRACE", "[LeaderLib:Randomization:Tables:BuildTable] Generating entries for table (",_TableID,")");
LeaderLog_LogInt("TRACE", "[LeaderLib:Randomization:Tables:BuildTable] --- DefaultAmountPerEntry (",_DefaultAmountPerEntry,")");
LeaderLib_Randomization_Tables_Internal_SetDropRanges(_Object, _TableID, _DefaultAmountPerEntry, 0, -1, _Total);

PROC
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Start((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Tables(_TableID, _Max)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange)
AND
_Total <= 0
THEN
LeaderLog_Log("ERROR", "[LeaderLib:Randomization:Tables:BuildTable] *ERROR* Table (",_TableID,") total is 0! No entries!");

PROC
LeaderLib_Randomization_Tables_Internal_SetDropRanges((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_DefaultAmountPerEntry, (INTEGER)_Index, (INTEGER)_LastStart, (INTEGER)_Total)
AND
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _CurrentDropCount, _PrevStart, _PrevEnd)
AND
DB_LeaderLib_Randomization_Entries(_TableID, _EntryID, _Frequency, _DropCount)
AND
IntegerProduct(_DefaultAmountPerEntry, _Frequency, _Range)
AND
IntegerSum(_LastStart, 1, _Start)
AND
IntegerSum(_LastStart, _Range, _End)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _CurrentDropCount, _PrevStart, _PrevEnd);
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _CurrentDropCount, _Start, _End);
LeaderLog_Log("TRACE", "[LeaderLib:Randomization:Tables:SetDropRanges] Added entry. Table (",_TableID,") Entry (",_EntryID,")");
LeaderLog_LogInt("TRACE", "[LeaderLib:Randomization:Tables:SetDropRanges] --- Start (",_Start,") End (",_End,")");
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Next(_Object, _TableID, _DefaultAmountPerEntry, _Index, _End, _Total);

PROC
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Next((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_DefaultAmountPerEntry, (INTEGER)_Index, (INTEGER)_LastEnd, (INTEGER)_Total)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _MaxRange)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _MaxRange);
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _LastEnd);

PROC
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Next((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_DefaultAmountPerEntry, (INTEGER)_Index, (INTEGER)_LastEnd, (INTEGER)_Total)
AND
IntegerSum(_Index, 1, _NextIndex)
AND
_NextIndex < _Total
THEN
LeaderLib_Randomization_Tables_Internal_SetDropRanges(_Object, _TableID, _DefaultAmountPerEntry, _NextIndex, _LastEnd, _Total);
//END_REGION

//REGION TABLE_ROLL
QRY
LeaderLib_Randomization_QRY_Tables_Roll((GUIDSTRING)_Object, (STRING)_TableID)
THEN
LeaderLib_Randomization_Tables_Roll(_Object, _TableID);

PROC
LeaderLib_Randomization_Tables_Roll((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_RollResult(_Object, _TableID, _LastEntryID)
THEN
NOT DB_LeaderLib_Randomization_Temp_RollResult(_Object, _TableID, _LastEntryID);

PROC
LeaderLib_Randomization_Tables_Roll((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange)
AND
LeaderLib_Random_QRY(0, _MaxRange)
AND
DB_LeaderLib_Random(_Ran)
AND
LeaderLog_QRY_Log("TRACE", "[LeaderLib:Randomization:Tables:Roll] Rolled for table (", _TableID, ")")
AND
LeaderLog_QRY_LogInt("TRACE", "[LeaderLib:Randomization:Tables:Roll] -- MaxRange (", _MaxRange, ") Total (",_Total,")")
AND
LeaderLog_QRY_LogInt("TRACE", "[LeaderLib:Randomization:Tables:Roll] -- Roll (", _Ran, ")")
AND
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _DropCount, _Start, _End)
AND
NOT DB_LeaderLib_Randomization_Temp_RollResult(_Object, _TableID, _)
AND
_DropCount > 0
AND
LeaderLog_QRY_Log("TRACE", "[LeaderLib:Randomization:Tables:Roll] -- Entry (", _EntryID, ")")
AND
LeaderLog_QRY_LogInt("TRACE", "[LeaderLib:Randomization:Tables:Roll] --- Index (",_Index,") Dropcount (", _DropCount, ")")
AND
LeaderLog_QRY_LogInt("TRACE", "[LeaderLib:Randomization:Tables:Roll] --- _Start (", _Start, ") End (",_End,")")
AND
_Ran >= _Start
AND
_Ran <= _End
AND
IntegerSubtract(_DropCount, 1, _NextDropCount)
THEN
LeaderLog_Log("TRACE", "[LeaderLib:Randomization:Tables:Roll] === Success. Lowering dropcount. Table (",_TableID,") Entry (",_EntryID,") ===");
DB_LeaderLib_Randomization_Temp_RollResult(_Object, _TableID, _EntryID);
NOT DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _DropCount, _Start, _End);
LeaderLib_Randomization_Tables_Internal_UpdateEntry(_Object, _TableID, _Index, _EntryID, _NextDropCount, _Start, _End);

PROC
LeaderLib_Randomization_Tables_Internal_UpdateEntry((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_Index, (STRING)_EntryID, (INTEGER)_NextDropCount, (INTEGER)_Start, (INTEGER)_End)
AND
_NextDropCount > 0
THEN
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _NextDropCount, _Start, _End);

//Rebuild table
PROC
LeaderLib_Randomization_Tables_Internal_UpdateEntry((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_Index, (STRING)_EntryID, (INTEGER)_NextDropCount, (INTEGER)_Start, (INTEGER)_End)
AND
_NextDropCount <= 0
AND
DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _EntryID, _LastDropCount)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _EntryID, _LastDropCount);
LeaderLog_Log("TRACE", "[LeaderLib:Randomization:Tables:UpdateEntry] Table (",_TableID,") Entry (",_EntryID,") hit a drop count of 0 and was removed from the active entries.");
LeaderLib_Randomization_Tables_Internal_CheckForCompletion(_Object, _TableID);
LeaderLib_Randomization_Tables_BuildTable(_Object, _TableID);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"