Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//Mimics Treasure Table logic where each entry (subtable)'s dropcount drops by 1 each time it is rolled, until all entries hit 0 and the table resets.

//REGION TABLE_REGISTER
PROC
LeaderLib_Randomization_Register_Table((STRING)_TableID, (INTEGER)_Max)
THEN
DB_LeaderLib_Randomization_Tables(_TableID, _Max);

PROC
LeaderLib_Randomization_Register_Entry((STRING)_TableID, (STRING)_EntryID, (INTEGER)_Frequency, (INTEGER)_StartingDropCount)
AND
NOT DB_LeaderLib_Randomization_Tables(_TableID, _)
THEN
LeaderLib_Randomization_Register_Table(_TableID, 999);

PROC
LeaderLib_Randomization_Register_Entry((STRING)_TableID, (STRING)_EntryID, (INTEGER)_Frequency, (INTEGER)_StartingDropCount)
THEN
DB_LeaderLib_Randomization_Entries(_TableID, _EntryID, _Frequency, _StartingDropCount);
//END_REGION

//REGION TABLE_BUILD
QRY
LeaderLib_Randomization_Tables_QRY_TableInitialized((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _Max)
THEN
DB_NOOP(1);

PROC
LeaderLib_Randomization_Tables_Internal_CheckForCompletion((GUIDSTRING)_Object, (STRING)_TableID)
AND
NOT DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _, _)
THEN
LeaderLib_Randomization_Tables_OnTableReset(_Object, _TableID);

PROC
LeaderLib_Randomization_Tables_OnTableReset((GUIDSTRING)_Object, (STRING)_TableID)
AND
String(_Object, _ObjectStr)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib_00_5_Helpers_Randomization:OnTableReset] Table (",_TableID,") finished rolling all entries for object (",_ObjectStr,").");

PROC
LeaderLib_Randomization_Tables_BuildTable((GUIDSTRING)_Object, (STRING)_TableID)
AND
NOT DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _, _)
AND
DB_LeaderLib_Randomization_Entries(_TableID, _EntryID, _Frequency, _StartingDropCount)
THEN
DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _EntryID, _StartingDropCount);

PROC
LeaderLib_Randomization_Tables_BuildTable((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _LastMax)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _LastMax);

PROC
LeaderLib_Randomization_Tables_BuildTable((GUIDSTRING)_Object, (STRING)_TableID)
THEN
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, 0, 0);

PROC
LeaderLib_Randomization_Tables_BuildTable((GUIDSTRING)_Object, (STRING)_TableID)
AND
NOT DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _, _, _, _, _)
AND
DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _EntryID, _DropCount)
THEN
LeaderLib_Randomization_Tables_Internal_RegisterEntry(_Object, _TableID, _EntryID, _DropCount);
LeaderLib_Randomization_Tables_Internal_IncreaseTotal(_Object, _TableID);

PROC
LeaderLib_Randomization_Tables_Internal_RegisterEntry((GUIDSTRING)_Object, (STRING)_TableID, (STRING)_EntryID, (INTEGER)_DropCount)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange)
THEN
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Total, _EntryID, _DropCount, 0, 0);

PROC
LeaderLib_Randomization_Tables_Internal_IncreaseTotal((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange)
AND
IntegerSum(_Total, 1, _Next)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange);
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Next, _MaxRange);

PROC
LeaderLib_Randomization_Tables_BuildTable((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Tables(_TableID, _Max)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange)
AND
IntegerDivide(_Max, _Total, _DefaultAmountPerEntry)
THEN
LeaderLib_Randomization_Tables_Internal_SetDropRanges(_Object, _TableID, _DefaultAmountPerEntry, 0, 0, _Max, _Total);

PROC
LeaderLib_Randomization_Tables_Internal_SetDropRanges((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_DefaultAmountPerEntry, (INTEGER)_Index, (INTEGER)_Start, (INTEGER)_Max, (INTEGER)_Total)
AND
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _CurrentDropCount, 0, 0)
AND
DB_LeaderLib_Randomization_Entries(_TableID, _EntryID, _Frequency, _DropCount)
AND
IntegerProduct(_DefaultAmountPerEntry, _Frequency, _Range)
AND
IntegerSum(_Start, _Range, _NextStart)
AND
IntegerSum(_Max, _Range, _NextMax)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _CurrentDropCount, 0, 0);
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _CurrentDropCount, _Start, _NextMax);
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Next(_Object, _TableID, _DefaultAmountPerEntry, _Index, _NextStart, _NextMax, _Total);

PROC
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Next((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_DefaultAmountPerEntry, (INTEGER)_Index, (INTEGER)_Min, (INTEGER)_Max, (INTEGER)_Total)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _MaxRange)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _MaxRange);
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _LastTotal, _Max);

PROC
LeaderLib_Randomization_Tables_Internal_SetDropRanges_Next((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_DefaultAmountPerEntry, (INTEGER)_Index, (INTEGER)_Min, (INTEGER)_Max, (INTEGER)_Total)
AND
IntegerSum(_Index, 1, _Next)
AND
_Next < _Total
THEN
LeaderLib_Randomization_Tables_Internal_SetDropRanges(_Object, _TableID, _DefaultAmountPerEntry, _Next, _Min, _Max, _Total);
//END_REGION

QRY
LeaderLib_Randomization_QRY_Tables_Roll((GUIDSTRING)_Object, (STRING)_TableID)
THEN
LeaderLib_Randomization_Tables_Roll(_Object, _TableID);

PROC
LeaderLib_Randomization_Tables_Roll((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_RollResult(_Object, _TableID, _LastEntryID)
THEN
NOT DB_LeaderLib_Randomization_Temp_RollResult(_Object, _TableID, _LastEntryID);

PROC
LeaderLib_Randomization_Tables_Roll((GUIDSTRING)_Object, (STRING)_TableID)
AND
DB_LeaderLib_Randomization_Temp_ActiveTableTotal(_Object, _TableID, _Total, _MaxRange)
AND
LeaderLib_Random_QRY(0, _MaxRange)
AND
DB_LeaderLib_Random(_Ran)
AND
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _DropCount, _Start, _End)
AND
NOT DB_LeaderLib_Randomization_Temp_RollResult(_Object, _TableID, _)
AND
_DropCount > 0
AND
_Start >= _Ran
AND
_End <= _Ran
AND
IntegerSubtract(_DropCount, 1, _NextDropCount)
THEN
DB_LeaderLib_Randomization_Temp_RollResult(_Object, _TableID, _EntryID);
NOT DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _DropCount, _Start, _End);
LeaderLib_Randomization_Tables_Internal_UpdateEntry(_Object, _TableID, _Index, _EntryID, _NextDropCount, _Start, _End);

PROC
LeaderLib_Randomization_Tables_Internal_UpdateEntry((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_Index, (STRING)_EntryID, (INTEGER)_NextDropCount, (INTEGER)_Start, (INTEGER)_End)
AND
_NextDropCount > 0
THEN
DB_LeaderLib_Randomization_Temp_ActiveTables(_Object, _TableID, _Index, _EntryID, _NextDropCount, _Start, _End);

//Rebuild table
PROC
LeaderLib_Randomization_Tables_Internal_UpdateEntry((GUIDSTRING)_Object, (STRING)_TableID, (INTEGER)_Index, (STRING)_EntryID, (INTEGER)_NextDropCount, (INTEGER)_Start, (INTEGER)_End)
AND
_NextDropCount <= 0
AND
DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _EntryID, _LastDropCount)
THEN
NOT DB_LeaderLib_Randomization_Temp_ActiveEntries(_Object, _TableID, _EntryID, _LastDropCount);
LeaderLib_Randomization_Tables_Internal_CheckForCompletion(_Object, _TableID);
LeaderLib_Randomization_Tables_BuildTable(_Object, _TableID);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"