Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION AUTO_PRESERVE
PROC
LeaderLib_Tags_Register_AutoPreserveTag((STRING)_Group, (STRING)_Tag)
AND
NOT DB_LeaderLib_PreserveTag(_, _Tag)
THEN
DB_LeaderLib_PreserveTag(_Group, _Tag);

PROC
LeaderLib_Tags_Register_AutoPreserveTag((STRING)_Group, (STRING)_Tag)
AND
NOT LeaderLib_ToggleScripts_QRY_ScriptIsEnabled("LeaderLib_TS_AutoTagPreserverEnabled")
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_AutoTagPreserverEnabled", "LeaderLib");

PROC
LeaderLib_Tags_Remove_AutoPreservedTag((STRING)_Tag)
AND
DB_LeaderLib_PreserveTag(_Group, _Tag)
THEN
NOT DB_LeaderLib_PreserveTag(_Group, _Tag);

PROC
LeaderLib_Tags_Remove_AutoPreservedTag((STRING)_Group, (STRING)_Tag)
AND
DB_LeaderLib_PreserveTag(_Group, _Tag)
THEN
NOT DB_LeaderLib_PreserveTag(_Group, _Tag);

PROC
LeaderLib_Tags_Remove_AutoPreservedTagForGroup((STRING)_Group)
AND
DB_LeaderLib_PreserveTag(_Group, _Tag)
THEN
NOT DB_LeaderLib_PreserveTag(_Group, _Tag);

PROC
LeaderLib_Tags_Internal_TagRemoved()
AND
NOT DB_LeaderLib_PreserveTag(_,_)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_AutoTagPreserverEnabled", "LeaderLib")
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LeaderLib_TS_AutoTagPreserverEnabled", "LeaderLib", 250);
//END_REGION

//REGION MANUAL_PRESERVE
PROC
LeaderLib_Tags_PreserveTag((GUIDSTRING)_Object, (STRING)_Tag)
THEN
DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag);
SetTag(_Object, _Tag);

PROC
LeaderLib_Tags_PreserveTag((GUIDSTRING)_Object, (STRING)_Tag)
AND
NOT LeaderLib_ToggleScripts_QRY_ScriptIsEnabled("LeaderLib_TS_TagReapplyEventsEnabled")
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_TagReapplyEventsEnabled", "LeaderLib");

PROC
LeaderLib_Tags_ClearPreservedTag((GUIDSTRING)_Object, (STRING)_Tag)
THEN
NOT DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag);
ClearTag(_Object, _Tag);

PROC
LeaderLib_Tags_ClearPreservedTag((GUIDSTRING)_Object, (STRING)_Tag)
AND
NOT DB_LeaderLib_TagPreserver_AssignedTags(_,_)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_TagReapplyEventsEnabled", "LeaderLib")
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LeaderLib_TS_TagReapplyEventsEnabled", "LeaderLib", 1000);

PROC
LeaderLib_Tags_ClearAllPreservedTags((GUIDSTRING)_Object)
AND
DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag)
THEN
LeaderLib_Tags_ClearPreservedTag(_Object, _Tag);

IF
RegionEnded(_)
AND
DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag)
AND
ObjectIsGlobal(_Object, 0)
THEN
NOT DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag);
//END_REGION

//REGION REAPPLY_TAGS
QRY
LeaderLib_Tags_QRY_HasPreservedTags((GUIDSTRING)_Object)
AND
DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag)
THEN
DB_NOOP(1);

IF
StoryEvent(_Object, "LeaderLib_Commands_ReapplyTags")
THEN
LeaderLib_Tags_ReapplyTags(_Object);

PROC
LeaderLib_Tags_ReapplyTags((GUIDSTRING)_Object)
AND
ObjectExists(_Object, _Exists)
AND
DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag)
THEN
LeaderLib_Tags_Internal_ReapplyTag(_Object, _Tag, _Exists);

PROC
LeaderLib_Tags_Internal_ReapplyTag((GUIDSTRING)_Object, (STRING)_Tag, 1)
THEN
SetTag(_Object, _Tag);

PROC
LeaderLib_Tags_Internal_ReapplyTag((GUIDSTRING)_Object, (STRING)_Tag, 0)
THEN
NOT DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag);

PROC
LeaderLib_Tags_ReapplyAllTags()
AND
DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag)
AND
ObjectExists(_Object, _Exists)
AND
DB_LeaderLib_TagPreserver_AssignedTags(_Object, _Tag)
THEN
LeaderLib_Tags_Internal_ReapplyTag(_Object, _Tag, _Exists);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"