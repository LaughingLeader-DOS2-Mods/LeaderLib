Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//REGION OBJECT_TIMER
QRY
LeaderLib_Timers_QRY_ObjectTimerStarted((GUIDSTRING)_Target)
AND
DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix)
THEN
DB_NOOP(1);

QRY
LeaderLib_Timers_QRY_ObjectTimerStarted((GUIDSTRING)_Target, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix)
THEN
DB_NOOP(1);

QRY
LeaderLib_Timers_QRY_ObjectTimerStarted((GUIDSTRING)_Target, (STRING)_TimerPrefix, (STRING)_CompleteEvent)
AND
DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix)
THEN
DB_NOOP(1);

PROC
LeaderLib_Timers_StartObjectTimer((GUIDSTRING)_Target, (INTEGER)_Time, (STRING)_TimerPrefix, (STRING)_CompleteEvent)
AND
GetUUID(_Target, _UUID)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_StartObjectTimerName", _TimerPrefix, _UUID)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_StartObjectTimerName", _TimerName)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_StartObjectTimerName", _TimerName);
DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix);
SetStoryEvent(_Target, _CompleteEvent);

PROC
LeaderLib_Timers_RestartObjectTimer((GUIDSTRING)_Target, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

PROC
LeaderLib_Timers_CancelObjectTimer((GUIDSTRING)_Target, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix)
THEN
TimerCancel(_TimerName);
NOT DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix);

PROC
LeaderLib_Timers_CancelObjectTimerByEvent((GUIDSTRING)_Target, (STRING)_CompleteEvent)
AND
DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix)
THEN
TimerCancel(_TimerName);
NOT DB_LeaderLib_Helper_Temp_ObjectTimer(_TimerName, _Target, _CompleteEvent, _TimerPrefix);
//END_REGION

//REGION CHARACTER_ITEM_TIMER
PROC
LeaderLib_Timers_StartCharacterItemTimer((CHARACTERGUID)_Char, (ITEMGUID)_Item, (INTEGER)_Time, (STRING)_TimerPrefix, (STRING)_CompleteEvent)
AND
GetUUID(_Char, _CharID)
AND
GetUUID(_Item, _ItemID)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_StartCharacterItemTimerName", _TimerPrefix, _CharID, _ItemID)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_StartCharacterItemTimerName", _TimerName)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_StartCharacterItemTimerName", _TimerName);
DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix);
CharacterItemSetEvent(_Char, _Item, _CompleteEvent);

PROC
LeaderLib_Timers_RestartCharacterItemTimer((CHARACTERGUID)_Char, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

PROC
LeaderLib_Timers_RestartCharacterItemTimerByItem((ITEMGUID)_Item, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

PROC
LeaderLib_Timers_CancelCharacterItemTimer((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);

PROC
LeaderLib_Timers_CancelCharacterItemTimerForCharacter((CHARACTERGUID)_Char, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_CharItemTimer(_TimerName, _Char, _Item, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);
//END_REGION

//REGION CHARACTER_CHARACTER_TIMER
PROC
LeaderLib_Timers_StartCharacterCharacterTimer((CHARACTERGUID)_Char1, (CHARACTERGUID)_Char2, (INTEGER)_Time, (STRING)_TimerPrefix, (STRING)_CompleteEvent)
AND
GetUUID(_Char1, _Char1ID)
AND
GetUUID(_Char2, _Char2ID)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_StartCharacterCharacterTimerName", _TimerPrefix, _Char1ID, _Char2ID)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_StartCharacterCharacterTimerName", _TimerName)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_StartCharacterCharacterTimerName", _TimerName);
DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

PROC
LeaderLib_Timers_RestartCharacterCharacterTimer((CHARACTERGUID)_Char1, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
NOT DB_LeaderLib_Timers_Temp_RestartedCharCharTimer(_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);
DB_LeaderLib_Timers_Temp_RestartedCharCharTimer(_TimerPrefix);

PROC
LeaderLib_Timers_RestartCharacterCharacterTimer((CHARACTERGUID)_Char2, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
NOT DB_LeaderLib_Timers_Temp_RestartedCharCharTimer(_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);
DB_LeaderLib_Timers_Temp_RestartedCharCharTimer(_TimerPrefix);

//This DB ensures we're only restarting once
PROC
LeaderLib_Timers_RestartCharacterCharacterTimer((CHARACTERGUID)_Char1, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
DB_LeaderLib_Timers_Temp_RestartedCharCharTimer(_TimerPrefix)
THEN
NOT DB_LeaderLib_Timers_Temp_RestartedCharCharTimer(_TimerPrefix);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix);
CharacterCharacterSetEvent(_Char1, _Char2, _CompleteEvent);

PROC
LeaderLib_Timers_CancelCharacterCharacterTimer((CHARACTERGUID)_Char1, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);

PROC
LeaderLib_Timers_CancelCharacterCharacterTimer((CHARACTERGUID)_Char2, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_CharCharTimer(_TimerName, _Char1, _Char2, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);
//END_REGION

//REGION OBJECT_OBJECT_TIMER
QRY
LeaderLib_Timers_QRY_ObjectObjectTimerStarted((GUIDSTRING)_Obj1, (GUIDSTRING)_Obj2, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
DB_NOOP(1);

QRY
LeaderLib_Timers_QRY_ObjectObjectTimerStartedWithEvent((GUIDSTRING)_Obj1, (GUIDSTRING)_Obj2, (STRING)_CompleteEvent)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
DB_NOOP(1);

PROC
LeaderLib_Timers_StartObjectObjectTimer((GUIDSTRING)_Obj1, (GUIDSTRING)_Obj2, (INTEGER)_Time, (STRING)_TimerPrefix, (STRING)_CompleteEvent)
AND
GetUUID(_Obj1, _UUID1)
AND
GetUUID(_Obj1, _UUID2)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_StartObjectObjectTimerName", _TimerPrefix, _UUID1, _UUID2)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_StartObjectObjectTimerName", _TimerName)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_StartObjectObjectTimerName", _TimerName);
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix);
LeaderLib_Timers_ObjectObjectTimerFinished(_Obj1, _Obj2, _CompleteEvent);

PROC
LeaderLib_Timers_ObjectObjectTimerFinished((GUIDSTRING)_Obj1, (GUIDSTRING)_Obj2, (STRING)_CompleteEvent)
THEN
DB_NOOP(1);

PROC
LeaderLib_Timers_RestartObjectObjectTimer((GUIDSTRING)_Obj1, (GUIDSTRING)_Obj2, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

PROC
LeaderLib_Timers_RestartObjectObjectTimer((GUIDSTRING)_Obj1, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
NOT DB_LeaderLib_Timers_Temp_RestartedObjObjTimer(_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
DB_LeaderLib_Timers_Temp_RestartedObjObjTimer(_TimerPrefix);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

PROC
LeaderLib_Timers_RestartObjectObjectTimer((GUIDSTRING)_Obj2, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
NOT DB_LeaderLib_Timers_Temp_RestartedObjObjTimer(_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
DB_LeaderLib_Timers_Temp_RestartedObjObjTimer(_TimerPrefix);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Time);

PROC
LeaderLib_Timers_RestartObjectObjectTimer((GUIDSTRING)_Obj, (STRING)_TimerPrefix, (INTEGER)_Time)
AND
DB_LeaderLib_Timers_Temp_RestartedObjObjTimer(_TimerPrefix)
THEN
NOT DB_LeaderLib_Timers_Temp_RestartedObjObjTimer(_TimerPrefix);

PROC
LeaderLib_Timers_CancelObjectObjectTimer((GUIDSTRING)_Obj1, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);

PROC
LeaderLib_Timers_CancelObjectObjectTimer((GUIDSTRING)_Obj2, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);

PROC
LeaderLib_Timers_CancelObjectObjectTimer((GUIDSTRING)_Obj1, (GUIDSTRING)_Obj2, (STRING)_TimerPrefix)
AND
DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix)
THEN
NOT DB_LeaderLib_Helper_Temp_ObjectObjectTimer(_TimerName, _Obj1, _Obj2, _CompleteEvent, _TimerPrefix);
TimerCancel(_TimerName);
//END_REGION

//REGION LUA_TIMERS
// [BEGIN_NO_OSITOOLS]
PROC
LeaderLib_Timers_StartLuaTimer((STRING)_Event, (INTEGER)_Delay)
THEN
DB_NOOP(1);

PROC
LeaderLib_Timers_StartLuaTimer((STRING)_Event, (INTEGER)_Delay, (GUIDSTRING)_Obj)
THEN
DB_NOOP(1);

PROC
LeaderLib_Timers_StartLuaTimer((STRING)_Event, (INTEGER)_Delay, (GUIDSTRING)_Obj, (GUIDSTRING)_Obj2)
THEN
DB_NOOP(1);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LeaderLib_Timers_StartLuaTimer((STRING)_Event, (INTEGER)_d)
AND
IntegertoString(_d, _Delay)
THEN
NRD_LuaCall("LeaderLib_Ext_StartTimer", _Event, _Delay);

PROC
LeaderLib_Timers_StartLuaTimer((STRING)_Event, (INTEGER)_d, (GUIDSTRING)_Obj)
AND
IntegertoString(_d, _Delay)
AND
GetUUID(_Obj, _UUID)
THEN
NRD_LuaCall("LeaderLib_Ext_StartTimer", _Event, _Delay, _UUID);

PROC
LeaderLib_Timers_StartLuaTimer((STRING)_Event, (INTEGER)_d, (GUIDSTRING)_Obj, (GUIDSTRING)_Obj2)
AND
IntegertoString(_d, _Delay)
AND
GetUUID(_Obj, _UUID)
AND
GetUUID(_Obj, _UUID2)
THEN
NRD_LuaCall("LeaderLib_Ext_StartTimer", _Event, _Delay, _UUID, _UUID2);

PROC
LeaderLib_Timers_Internal_StoreLuaData((STRING)_TimerName, (STRING)_Event)
THEN
DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event);

PROC
LeaderLib_Timers_Internal_StoreLuaData((STRING)_TimerName, (STRING)_Event, (STRING)_UUID)
THEN
DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event, _UUID);

PROC
LeaderLib_Timers_Internal_StoreLuaData((STRING)_TimerName, (STRING)_Event, (STRING)_UUID, (STRING)_UUID2)
THEN
DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event, _UUID, _UUID2);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event)
THEN
NOT DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event);
NRD_LuaCall("LeaderLib_Ext_TimerFinished", _Event);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event, _UUID)
THEN
NOT DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event, _UUID);
NRD_LuaCall("LeaderLib_Ext_TimerFinished", _Event, _UUID);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event, _UUID, _UUID2)
THEN
NOT DB_LeaderLib_Helper_Temp_LuaTimer(_TimerName, _Event, _UUID, _UUID2);
NRD_LuaCall("LeaderLib_Ext_TimerFinished", _Event, _UUID, _UUID2);
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"