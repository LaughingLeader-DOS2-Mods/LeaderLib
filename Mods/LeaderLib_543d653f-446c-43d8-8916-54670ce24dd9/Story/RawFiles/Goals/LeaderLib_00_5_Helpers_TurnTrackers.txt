Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION QUERIES
QRY
LeaderLib_Turns_QRY_TrackerHasPosition("All")
THEN
DB_NOOP(1);

QRY
LeaderLib_Turns_QRY_TrackerHasPosition("Position")
THEN
DB_NOOP(1);

QRY
LeaderLib_Turns_QRY_TrackerHasObject("All")
THEN
DB_NOOP(1);

QRY
LeaderLib_Turns_QRY_TrackerHasObject("Object")
THEN
DB_NOOP(1);
//END_REGION

//REGION ID_CREATION
QRY
LeaderLib_Turns_Internal_QRY_CreateIdentifierByObject((GUIDSTRING)_Object, (STRING)_CompletionEvent)
THEN
LeaderLib_Turns_Internal_CreateIdentifierByObject(_Object, _CompletionEvent);

PROC
LeaderLib_Turns_Internal_CreateIdentifierByObject((GUIDSTRING)_Object, (STRING)_CompletionEvent)
AND
GetUUID(_Object, _UUID)
AND
Random(99999, _Ran)
AND
IntegerSum(_Ran, 12931, _RanInt)
AND
IntegertoString(_RanInt, _RanStr)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_CreateIdentifierByObject", "LeaderLib_TurnTracker_", _CompletionEvent, _UUID, _RanStr)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_CreateIdentifierByObject", _ID)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_CreateIdentifierByObject", _ID);
DB_LeaderLib_Turns_Temp_NextObjectID(_Object, _CompletionEvent, _ID);

QRY
LeaderLib_Turns_Internal_QRY_CreateIdentifierByPosition((REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent)
THEN
LeaderLib_Turns_Internal_CreateIdentifierByPosition(_x, _y, _z, _CompletionEvent);

PROC
LeaderLib_Turns_Internal_CreateIdentifierByPosition((REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent)
AND
RealSum(_x, _y, _a)
AND
RealSum(_z, _a, _b)
AND
Integer(_b, _IntB)
AND
IntegertoString(_IntB, _PosStr)
AND
Random(99999, _Ran)
AND
IntegerSum(_Ran, 12931, _RanInt)
AND
IntegertoString(_RanInt, _RanStr)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_CreateIdentifierByPosition", "LeaderLib_TurnTracker_", _CompletionEvent, _PosStr, _RanStr)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_CreateIdentifierByPosition", _ID)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_CreateIdentifierByPosition", _ID);
DB_LeaderLib_Turns_Temp_NextPositionID(_x, _y, _z, _CompletionEvent, _ID);

QRY
LeaderLib_Turns_Internal_QRY_CreateIdentifierByPositionWithObject((GUIDSTRING)_Object, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent)
THEN
LeaderLib_Turns_Internal_CreateIdentifierByPositionWithObject(_Object, _x, _y, _z, _CompletionEvent);

PROC
LeaderLib_Turns_Internal_CreateIdentifierByPositionWithObject((GUIDSTRING)_Object, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent)
AND
String(_Object, _ObjectStr)
AND
RealSum(_x, _y, _a)
AND
RealSum(_z, _a, _b)
AND
Integer(_b, _IntB)
AND
IntegertoString(_IntB, _PosStr)
AND
Random(99999, _Ran)
AND
IntegerSum(_Ran, 12931, _RanInt)
AND
IntegertoString(_RanInt, _RanStr)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_CreateIdentifierByPositionWithObject", "LeaderLib_TurnTracker_", _CompletionEvent, _ObjectStr, _PosStr, _RanStr)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_CreateIdentifierByPositionWithObject", _ID)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_CreateIdentifierByPositionWithObject", _ID);
DB_LeaderLib_Turns_Temp_NextObjectPositionID(_Object, _x, _y, _z, _CompletionEvent, _ID);

QRY
LeaderLib_Turns_Internal_QRY_CreateIdentifierByCombat((INTEGER)_CombatID, (STRING)_CompletionEvent)
THEN
LeaderLib_Turns_Internal_CreateIdentifierByCombat(_CombatID, _CompletionEvent);

PROC
LeaderLib_Turns_Internal_CreateIdentifierByCombat((INTEGER)_CombatID, (STRING)_CompletionEvent)
AND
IntegertoString(_CombatID, _CombatIDStr)
AND
Random(99999, _Ran)
AND
IntegerSum(_Ran, 12931, _RanInt)
AND
IntegertoString(_RanInt, _RanStr)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_CreateIdentifierByCombatID", "LeaderLib_TurnTracker_", _CompletionEvent, _CombatIDStr, _RanStr)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_CreateIdentifierByCombatID", _ID)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_CreateIdentifierByCombatID", _ID);
DB_LeaderLib_Turns_Temp_NextCombatID(_CombatID, _CompletionEvent, _ID);
//END_REGION

//REGION MAIN_DB
QRY
LeaderLib_Turns_QRY_HasRemainingTurns((STRING)_ID)
AND
DB_LeaderLib_Turns_ActiveTracker(_ID, _CompletionEvent, _Turns)
AND
_Turns > 0
THEN
DB_NOOP(1);

PROC
LeaderLib_Turns_Internal_CreateTracker((STRING)_ID, (STRING)_CompletionEvent, (INTEGER)_StartingTurns)
THEN
DB_LeaderLib_Turns_ActiveTracker(_ID, _CompletionEvent, _StartingTurns);

PROC
LeaderLib_Turns_Internal_ClearTracker((STRING)_ID)
AND
DB_LeaderLib_Turns_ActiveTracker(_ID, _CompletionEvent, _Turns)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker(_ID, _CompletionEvent, _Turns);

PROC
LeaderLib_Turns_Internal_ClearTracker((STRING)_ID)
AND
DB_LeaderLib_Turns_ActiveTracker_Timer(_ID)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_Timer(_ID);
TimerCancel(_ID);

PROC
LeaderLib_Turns_Internal_ClearTracker((STRING)_ID)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, _Type, _AnyTurns)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, _Type, _AnyTurns);

PROC
LeaderLib_Turns_Internal_ClearTracker((STRING)_ID)
AND
DB_LeaderLib_Turns_Temp_ListenForCombat(_ID)
THEN
NOT DB_LeaderLib_Turns_Temp_ListenForCombat(_ID);

PROC
LeaderLib_Turns_Internal_ClearTracker((STRING)_ID)
AND
DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID);
//END_REGION

//REGION MAIN_PROCS
PROC
LeaderLib_Turns_DecreaseCounter((STRING)_ID)
AND
DB_LeaderLib_Turns_ActiveTracker(_ID, _CompletionEvent, _Turns)
AND
IntegerSubtract(_Turns, 1, _NextTurns)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker(_ID, _CompletionEvent, _Turns);
DB_LeaderLib_Turns_ActiveTracker(_ID, _CompletionEvent, _NextTurns);
LeaderLib_Turns_OnTurnChanged(_ID, _CompletionEvent, _Turns, _NextTurns);

PROC
LeaderLib_Turns_OnTurnChanged((STRING)_ID, (STRING)_CompletionEvent, (INTEGER)_LastTurns, (INTEGER)_NextTurns)
AND
_NextTurns <= 0
THEN
LeaderLib_Turns_OnTurnCounterComplete(_ID, _CompletionEvent);
LeaderLib_Turns_Internal_ClearTracker(_ID);

PROC
LeaderLib_Turns_OnTurnChanged((STRING)_ID, (STRING)_CompletionEvent, (INTEGER)_LastTurns, (INTEGER)_NextTurns)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "Object", _AnyTurns)
THEN
LeaderLib_Turns_OnObjectTurnChanged(_ID, _Object, _CompletionEvent, _LastTurns, _NextTurns);

PROC
LeaderLib_Turns_OnObjectTurnChanged((STRING)_ID, (GUIDSTRING)_Object, (STRING)_CompletionEvent, (INTEGER)_LastTurns, (INTEGER)_NextTurns)
THEN
DB_NOOP(1);

PROC
LeaderLib_Turns_OnTurnChanged((STRING)_ID, (STRING)_CompletionEvent, (INTEGER)_LastTurns, (INTEGER)_NextTurns)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "Position", _AnyTurns)
THEN
LeaderLib_Turns_OnPositionTurnChanged(_ID, _x, _y, _z, _CompletionEvent, _LastTurns, _NextTurns);

PROC
LeaderLib_Turns_OnPositionTurnChanged((STRING)_ID, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent, (INTEGER)_LastTurns, (INTEGER)_NextTurns)
THEN
DB_NOOP(1);

PROC
LeaderLib_Turns_OnTurnChanged((STRING)_ID, (STRING)_CompletionEvent, (INTEGER)_LastTurns, (INTEGER)_NextTurns)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "All", _AnyTurns)
THEN
LeaderLib_Turns_OnPositionObjectTurnChanged(_ID, _Object, _x, _y, _z, _CompletionEvent, _LastTurns, _NextTurns);

PROC
LeaderLib_Turns_OnPositionObjectTurnChanged((STRING)_ID, (GUIDSTRING)_Object, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent, (INTEGER)_LastTurns, (INTEGER)_NextTurns)
THEN
DB_NOOP(1);
//END_REGION

//REGION COMPLETION_EVENTS
PROC
LeaderLib_Turns_OnTurnCounterComplete((STRING)_ID, (STRING)_CompletionEvent)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "Object", _AnyTurns)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "Object", _AnyTurns);
LeaderLib_Turns_OnTurnCounterComplete_Object(_ID, _Object, _CompletionEvent);

PROC
LeaderLib_Turns_OnTurnCounterComplete_Object((STRING)_ID, (GUIDSTRING)_Object, (STRING)_CompletionEvent)
AND
NOT DB_LeaderLib_Turns_ActiveTracker_Data(_, _Object, _, _, _, _, _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Object, "LeaderLib_Turns_TrackCombatObject", "LeaderLib");

PROC
LeaderLib_Turns_OnTurnCounterComplete((STRING)_ID, (STRING)_CompletionEvent)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "Position", _AnyTurns)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "Position", _AnyTurns);
LeaderLib_Turns_OnTurnCounterComplete_Position(_ID, _x, _y, _z, _CompletionEvent);

PROC
LeaderLib_Turns_OnTurnCounterComplete_Position((STRING)_ID, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent)
THEN
DB_NOOP(1);

PROC
LeaderLib_Turns_OnTurnCounterComplete((STRING)_ID, (STRING)_CompletionEvent)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "All", _AnyTurns)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "All", _AnyTurns);
LeaderLib_Turns_OnTurnCounterComplete_PositionWithObject(_ID, _Object, _x, _y, _z, _CompletionEvent);

PROC
LeaderLib_Turns_OnTurnCounterComplete_PositionWithObject((STRING)_ID, (GUIDSTRING)_Object, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent)
AND
NOT DB_LeaderLib_Turns_ActiveTracker_Data(_, _Object, _, _, _, _, _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Object, "LeaderLib_Turns_TrackCombatObject", "LeaderLib");

PROC
LeaderLib_Turns_OnTurnCounterComplete((STRING)_ID, (STRING)_CompletionEvent)
AND
DB_LeaderLib_Turns_ActiveTracker_CombatData(_ID, _CombatID)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_CombatData(_ID, _CombatID);
LeaderLib_Turns_OnTurnCounterComplete_Combat(_ID, _CombatID, _CompletionEvent);

PROC
LeaderLib_Turns_OnTurnCounterComplete_Combat((STRING)_ID, (INTEGER)_CombatID, (STRING)_CompletionEvent)
AND
NOT DB_LeaderLib_Turns_ActiveTracker_CombatData(_, _)
THEN
LeaderLib_ToggleScripts_DisableScript("LeaderLib_Turns_TrackCombatObject", "LeaderLib");
//END_REGION

//REGION TIMERS
PROC
LeaderLib_Turns_Internal_CreateTimerWatcher((STRING)_ID)
AND
NOT DB_LeaderLib_Turns_ActiveTracker_Timer(_ID)
THEN
DB_LeaderLib_Turns_ActiveTracker_Timer(_ID);
TimerLaunch(_ID, 6000);

IF
TimerFinished(_ID)
AND
DB_LeaderLib_Turns_ActiveTracker_Timer(_ID)
THEN
LeaderLib_Turns_DecreaseCounter(_ID);
TimerLaunch(_ID, 6000);

PROC
LeaderLib_Turns_Internal_ClearTrackerTimer((STRING)_ID)
AND
DB_LeaderLib_Turns_ActiveTracker_Timer(_ID)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_Timer(_ID);
TimerCancel(_ID);
//END_REGION

//REGION TRACK_COMBAT_BASED
PROC
LeaderLib_Turns_TrackCombat((INTEGER)_CombatID, (STRING)_CompletionEvent, (INTEGER)_StartingTurns)
AND
LeaderLib_Turns_Internal_QRY_CreateIdentifierByCombat(_CombatID, _CompletionEvent)
AND
DB_LeaderLib_Turns_Temp_NextCombatID(_CombatID, _CompletionEvent, _ID)
THEN
NOT DB_LeaderLib_Turns_Temp_NextCombatID(_CombatID, _CompletionEvent, _ID);
LeaderLib_Turns_Internal_CreateTracker(_ID, _CompletionEvent, _StartingTurns);
DB_LeaderLib_Turns_ActiveTracker_CombatData(_ID, _CombatID);
DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID);
LeaderLib_ToggleScripts_EnableScript("LeaderLib_Turns_TrackCombat", "LeaderLib");
//END_REGION

//REGION TRACK_OBJECT_BASED
PROC
LeaderLib_Turns_TrackForObject((GUIDSTRING)_Object, (STRING)_CompletionEvent, (INTEGER)_StartingTurns, (INTEGER)_AnyTurns)
AND
LeaderLib_Turns_Internal_QRY_CreateIdentifierByObject(_Object, _CompletionEvent)
AND
DB_LeaderLib_Turns_Temp_NextObjectID(_Object, _CompletionEvent, _ID)
THEN
NOT DB_LeaderLib_Turns_Temp_NextObjectID(_Object, _CompletionEvent, _ID);
LeaderLib_Turns_Internal_CreateTracker(_ID, _CompletionEvent, _StartingTurns);
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, 0.0, 0.0, 0.0, "Object", _AnyTurns);
LeaderLib_ToggleScripts_EnableScriptForObject(_Object, "LeaderLib_Turns_TrackCombatObject", "LeaderLib");
LeaderLib_Turns_Internal_CreateWatcherForObject(_ID, _Object, _CompletionEvent, _AnyTurns);

PROC
LeaderLib_Turns_Internal_CreateWatcherForObject((STRING)_ID, (GUIDSTRING)_Object, (STRING)_CompletionEvent, (INTEGER)_AnyTurns)
AND
DB_CombatObjects(_Object, _CombatID)
THEN
DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID);

PROC
LeaderLib_Turns_Internal_CreateWatcherForObject((STRING)_ID, (GUIDSTRING)_Object, (STRING)_CompletionEvent, (INTEGER)_AnyTurns)
AND
NOT DB_CombatObjects(_Object, _)
THEN
LeaderLib_Turns_Internal_CreateTimerWatcher(_ID);
//END_REGION

//REGION TRACK_POSITION_BASED_NONCOMBAT
PROC
LeaderLib_Turns_TrackForPosition((REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent, (INTEGER)_StartingTurns, (INTEGER)_UseTimer)
AND
LeaderLib_Turns_Internal_QRY_CreateIdentifierByPosition(_x, _y, _z, _CompletionEvent)
AND
DB_LeaderLib_Turns_Temp_NextPositionID(_x, _y, _z, _CompletionEvent, _ID)
THEN
NOT DB_LeaderLib_Turns_Temp_NextPositionID(_x, _y, _z, _CompletionEvent, _ID);
LeaderLib_Turns_Internal_CreateTracker(_ID, _CompletionEvent, _StartingTurns);
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, NULL_00000000-0000-0000-0000-000000000000, _x, _y, _z, "Position", 1);
LeaderLib_Turns_Internal_CreateWatcherForPosition(_ID, _x, _y, _z, _CompletionEvent, _UseTimer);

PROC
LeaderLib_Turns_Internal_CreateWatcherForPosition((STRING)_ID, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent, 1)
THEN
LeaderLib_Turns_Internal_CreateTimerWatcher(_ID);
//END_REGION

//REGION TRACK_POSITION_BASED_COMBAT
PROC
LeaderLib_Turns_TrackForPositionInCombat((REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent, (INTEGER)_StartingTurns, (INTEGER)_CombatID)
AND
LeaderLib_Turns_Internal_QRY_CreateIdentifierByPosition(_x, _y, _z, _CompletionEvent)
AND
DB_LeaderLib_Turns_Temp_NextPositionID(_x, _y, _z, _CompletionEvent, _ID)
THEN
NOT DB_LeaderLib_Turns_Temp_NextPositionID(_x, _y, _z, _CompletionEvent, _ID);
LeaderLib_Turns_Internal_CreateTracker(_ID, _CompletionEvent, _StartingTurns);
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, NULL_00000000-0000-0000-0000-000000000000, _x, _y, _z, "Position", 1);
LeaderLib_Turns_Internal_CreateWatcherForPositionInCombat(_ID, _x, _y, _z, _CompletionEvent, _CombatID);

PROC
LeaderLib_Turns_Internal_CreateWatcherForPositionInCombat((STRING)_ID, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent, (INTEGER)_CombatID)
AND
DB_LeaderLib_Combat_ActiveCombat(_CombatID)
THEN
DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID);

PROC
LeaderLib_Turns_Internal_CreateWatcherForPositionInCombat((STRING)_ID, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent, (INTEGER)_CombatID)
AND
NOT DB_LeaderLib_Combat_ActiveCombat(_)
THEN
DB_LeaderLib_Turns_Temp_ListenForCombat(_ID);
LeaderLib_Turns_Internal_CreateTimerWatcher(_ID);
//END_REGION

//REGION TRACK_POSITION_COMBAT_WITH_SOURCE
PROC
LeaderLib_Turns_TrackPositionWithObject((GUIDSTRING)_Object, (REAL)_x, (REAL)_y, (REAL)_z, (STRING)_CompletionEvent, (INTEGER)_StartingTurns)
AND
LeaderLib_Turns_Internal_QRY_CreateIdentifierByPositionWithObject(_Object, _x, _y, _z, _CompletionEvent)
AND
DB_LeaderLib_Turns_Temp_NextObjectPositionID(_Object, _x, _y, _z, _CompletionEvent, _ID)
THEN
NOT DB_LeaderLib_Turns_Temp_NextObjectPositionID(_Object, _x, _y, _z, _CompletionEvent, _ID);
LeaderLib_Turns_Internal_CreateTracker(_ID, _CompletionEvent, _StartingTurns);
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, "All", 1);
LeaderLib_Turns_Internal_CreateWatcherForObject(_ID, _Object, _CompletionEvent, 1);
//END_REGION

//REGION TURN_ENDED
// Track any turns ending
PROC
LeaderLib_Combat_Clear_ActiveTurn((INTEGER)_CombatID, (GUIDSTRING)_Object)
AND
DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID)
AND
NOT DB_LeaderLib_Turns_Temp_CounterDecremented(_ID)
AND
NOT DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _, _, _, _, "Object", 0) // Isn't tracking a specific object
THEN
DB_LeaderLib_Turns_Temp_CounterDecremented(_ID);
LeaderLib_Turns_DecreaseCounter(_ID);

PROC
LeaderLib_Combat_Clear_ActiveTurn((INTEGER)_CombatID, (GUIDSTRING)_Object)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _, _, _, "Object", 0)
AND
NOT DB_LeaderLib_Turns_Temp_CounterDecremented(_ID)
THEN
DB_LeaderLib_Turns_Temp_CounterDecremented(_ID);
LeaderLib_Turns_DecreaseCounter(_ID);

PROC
LeaderLib_Combat_Clear_ActiveTurn((INTEGER)_CombatID, (GUIDSTRING)_Object)
AND
DB_LeaderLib_Turns_ActiveTracker_CombatData(_ID, _CombatID)
AND
NOT DB_LeaderLib_Turns_Temp_CounterDecremented(_ID)
THEN
DB_LeaderLib_Turns_Temp_CounterDecremented(_ID);
LeaderLib_Turns_DecreaseCounter(_ID);

PROC
LeaderLib_Combat_Clear_ActiveTurn((INTEGER)_CombatID, (GUIDSTRING)_Object)
AND
DB_LeaderLib_Turns_Temp_CounterDecremented(_ID)
THEN
NOT DB_LeaderLib_Turns_Temp_CounterDecremented(_ID);
//END_REGION

//REGION COMBAT_STARTED
IF
CombatStarted(_CombatID)
AND
NOT SysCount("DB_LeaderLib_Turns_Temp_ListenForCombat", 1, 0)
THEN
TimerCancel("LeaderLib_Timers_Turns_CombatStarted");
TimerLaunch("LeaderLib_Timers_Turns_CombatStarted", 50);

IF
TimerFinished("LeaderLib_Timers_Turns_CombatStarted")
AND
DB_LeaderLib_Turns_Temp_ListenForCombat(_ID)
AND
DB_LeaderLib_Combat_ActiveCombat(_CombatID)
AND
DB_LeaderLib_Turns_ActiveTracker_Data(_ID, _Object, _x, _y, _z, _Type, _AnyTurns)
AND
LeaderLib_Turns_QRY_TrackerHasPosition(_Type)
AND
LeaderLib_Combat_QRY_DistanceToCombatIsLessThan(_CombatID, _x, _y, _z, 6.0)
AND
NOT DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _)
THEN
NOT DB_LeaderLib_Turns_Temp_ListenForCombat(_ID);
LeaderLib_Turns_Internal_ClearTrackerTimer(_ID);
DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID);
//END_REGION

//REGION COMBAT_ENDED
PROC
LeaderLib_Combat_OnCombatEnded((INTEGER)_CombatID, (INTEGER)_TotalRounds)
AND
DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID)
AND
LeaderLib_Turns_QRY_HasRemainingTurns(_ID)
THEN
NOT DB_LeaderLib_Turns_ActiveTracker_Combat(_ID, _CombatID);
LeaderLib_Turns_Internal_CreateTimerWatcher(_ID);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"