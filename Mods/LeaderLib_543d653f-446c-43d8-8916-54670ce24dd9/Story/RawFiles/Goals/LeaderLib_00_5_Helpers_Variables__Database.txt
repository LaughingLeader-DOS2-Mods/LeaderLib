Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
/* Database versions of variable helpers. For keeping character data clear of variables. */

//REGION VARIABLE_QUERIES
QRY
LeaderLib_Variables_DB_QRY_IntegerVariableSet((GUIDSTRING)_Object, (STRING)_Variable)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source)
THEN
DB_NOOP(1);

QRY
LeaderLib_Variables_DB_QRY_IntegerVariableIsLessThanOrEqualTo((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_MinValue)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source)
AND
_Value <= _MinValue
THEN
DB_NOOP(1);

QRY
LeaderLib_Variables_DB_QRY_IntegerVariableIsLessThanOrEqualTo((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_MinValue)
AND
NOT LeaderLib_Variables_DB_QRY_IntegerVariableSet(_Object, _Variable)
THEN
DB_NOOP(1);

QRY
LeaderLib_Variables_DB_QRY_IntegerVariableIsGreaterThanOrEqualTo((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_MinValue)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source)
AND
_Value >= _MinValue
THEN
DB_NOOP(1);
//END_REGION

//REGION INTEGER_VARIABLES
PROC
LeaderLib_Variables_DB_ClearVariableInt((GUIDSTRING)_Object, (STRING)_Variable)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source)
THEN
NOT DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source);

PROC
LeaderLib_Variables_DB_ClearVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (GUIDSTRING)_Source)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source)
THEN
NOT DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source);

PROC
LeaderLib_Variables_DB_Internal_SetVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_Value, (GUIDSTRING)_Source)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _LastValue, _Source)
THEN
NOT DB_LeaderLib_Variables_Integer(_Object, _Variable, _LastValue, _Source);
DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source);

PROC
LeaderLib_Variables_DB_Internal_SetVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_Value, (GUIDSTRING)_Source)
AND
NOT DB_LeaderLib_Variables_Integer(_Object, _Variable, _, _Source)
THEN
DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source);

PROC
LeaderLib_Variables_DB_SetVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_Value)
THEN
LeaderLib_Variables_DB_SetVariableInt(_Object, _Variable, _Value, NULL_00000000-0000-0000-0000-000000000000);

PROC
LeaderLib_Variables_DB_SetVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_Value, (GUIDSTRING)_Source)
AND
NOT LeaderLib_Variables_DB_QRY_IntegerVariableSet(_Object, _Variable)
THEN
LeaderLib_Variables_DB_Internal_SetVariableInt(_Object, _Variable, 0, _Source);

PROC
LeaderLib_Variables_DB_SetVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_Value, (GUIDSTRING)_Source)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _LastValue, _Source)
THEN
NOT DB_LeaderLib_Variables_Integer(_Object, _Variable, _LastValue, _Source);
LeaderLib_Variables_DB_Internal_SetVariableInt(_Object, _Variable, _Value, _Source);
LeaderLib_Variables_DB_Internal_FireOnVariableIntChanged(_Object, _Variable, _LastValue, _Value, _Source);

PROC
LeaderLib_Variables_DB_ModifyVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_ModifyBy)
THEN
LeaderLib_Variables_DB_ModifyVariableInt(_Object, _Variable, _ModifyBy, 999, 0);

PROC
LeaderLib_Variables_DB_ModifyVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_ModifyBy, (INTEGER)_Max)
THEN
LeaderLib_Variables_DB_ModifyVariableInt(_Object, _Variable, _ModifyBy, _Max, 0);

PROC
LeaderLib_Variables_DB_ModifyVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_ModifyBy, (INTEGER)_Max, (INTEGER)_Min)
THEN
LeaderLib_Variables_DB_ModifyVariableInt(_Object, _Variable, _ModifyBy, _Max, _Min, NULL_00000000-0000-0000-0000-000000000000);

PROC
LeaderLib_Variables_DB_ModifyVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_ModifyBy, (INTEGER)_Max, (INTEGER)_Min, (GUIDSTRING)_Source)
AND
LeaderLib_Variables_DB_QRY_IntegerVariableIsLessThanOrEqualTo(_Object, _Variable, _Min)
THEN
LeaderLib_Variables_DB_Internal_SetVariableInt(_Object, _Variable, _Min, _Source);

PROC
LeaderLib_Variables_DB_ModifyVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_ModifyBy, (INTEGER)_Max, (INTEGER)_Min, (GUIDSTRING)_Source)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _Value, _Source)
AND
IntegerSum(_Value, _ModifyBy, _NextValue)
THEN
LeaderLib_Variables_DB_ClampVariableInt(_Object, _Variable, _NextValue, _Max, _Min, _Source);
LeaderLib_Variables_DB_Internal_FireOnVariableIntChanged(_Object, _Variable, _Value, _Source);

PROC
LeaderLib_Variables_DB_ClampVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_NextValue, (INTEGER)_Max, (INTEGER)_Min, (GUIDSTRING)_Source)
AND
_NextValue > _Max
THEN
LeaderLib_Variables_DB_Internal_SetVariableInt(_Object, _Variable, _Max, _Source);

PROC
LeaderLib_Variables_DB_ClampVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_NextValue, (INTEGER)_Max, (INTEGER)_Min, (GUIDSTRING)_Source)
AND
_NextValue <= _Max
AND
_NextValue >= _Min
THEN
LeaderLib_Variables_DB_Internal_SetVariableInt(_Object, _Variable, _NextValue, _Source);

PROC
LeaderLib_Variables_DB_ClampVariableInt((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_NextValue, (INTEGER)_Max, (INTEGER)_Min, (GUIDSTRING)_Source)
AND
_NextValue < _Min
THEN
LeaderLib_Variables_DB_Internal_SetVariableInt(_Object, _Variable, _Min, _Source);

PROC
LeaderLib_Variables_DB_Internal_FireOnVariableIntChanged((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_LastValue, (GUIDSTRING)_Source)
AND
DB_LeaderLib_Variables_Integer(_Object, _Variable, _CurrentValue, _Source)
THEN
LeaderLib_Variables_DB_Internal_FireOnVariableIntChanged(_Object, _Variable, _LastValue, _CurrentValue, _Source);

PROC
LeaderLib_Variables_DB_Internal_FireOnVariableIntChanged((GUIDSTRING)_Object, (STRING)_Variable, (INTEGER)_LastValue, (INTEGER)_CurrentValue, (GUIDSTRING)_Source)
AND
_LastValue != _CurrentValue
THEN
LeaderLib_Variables_OnVariableIntChanged(_Object, _Variable, _LastValue, _CurrentValue);
LeaderLib_Variables_OnVariableIntChanged(_Object, _Variable, _LastValue, _CurrentValue, _Source);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"