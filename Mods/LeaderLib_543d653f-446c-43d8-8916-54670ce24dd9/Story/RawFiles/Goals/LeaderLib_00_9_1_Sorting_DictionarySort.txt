Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LeaderLib_Codex(_Index, _Char)
//DB_LeaderLib_Codex_Iterator(_Index, _IndexStr)
/*Input*/
//DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _ID, _StringEntry)
/*Sorting Vars*/
//DB_LeaderLib_DictionarySort_SortingStarted(_DictionaryID, _Val)
//DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _Val)
//DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName)
//DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID)
/*TIMERS*/
//DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate)
//DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex)
/*DEBUG*/
//DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, _TimeTaken)
KBSECTION
//REGION STRING_ADDING
//Run this first to add strings to sort.
PROC
LeaderLib_DictionarySort_AddString((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry)
AND
NOT DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _ID, _StringEntry)
THEN
DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _ID, _StringEntry);

PROC
LeaderLib_DictionarySort_AddTarget((STRING)_DictionaryID, (GUIDSTRING)_Object)
THEN
DB_LeaderLib_DictionarySort_Temp_Target(_DictionaryID, _Object);

QRY
LeaderLib_DictionarySort_QRY_QueueEmpty((STRING)_DictionaryID)
AND
NOT DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _, _)
THEN
DB_NOOP(1);
//END_REGION

//REGION START
PROC
LeaderLib_DictionarySort_StartSorting((STRING)_DictionaryID, (INTEGER)_TickRate, (STRING)_CompleteEvent)
THEN
LeaderLib_DictionarySort_StartSorting(_DictionaryID, _TickRate, _CompleteEvent, 0);

PROC
LeaderLib_DictionarySort_StartSorting((STRING)_DictionaryID, (INTEGER)_TickRate, (STRING)_CompleteEvent, (INTEGER)_EntryTickRate)
AND
DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID)
THEN
LeaderLib_DictionarySort_CleanupLastTrackedTime(_DictionaryID);
NOT DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID);


PROC
LeaderLib_DictionarySort_StartSorting((STRING)_DictionaryID, (INTEGER)_TickRate, (STRING)_CompleteEvent, (INTEGER)_EntryTickRate)
AND
NOT LeaderLib_QRY_ExtenderIsActive()
THEN
LeaderLib_DictionarySort_StartSorting_Old(_DictionaryID, _TickRate, _CompleteEvent, _EntryTickRate);

PROC
LeaderLib_DictionarySort_StartSorting_Old((STRING)_DictionaryID, (INTEGER)_TickRate, (STRING)_CompleteEvent, (INTEGER)_EntryTickRate)
AND
IntegertoString(_TickRate, _TickRateStr)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort:StartSorting] Started sorting dictionary '",_DictionaryID,"' at a tick rate of [",_TickRateStr,"].");

PROC
LeaderLib_DictionarySort_StartSorting_Old((STRING)_DictionaryID, (INTEGER)_TickRate, (STRING)_CompleteEvent, (INTEGER)_EntryTickRate)
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_DictionarySort_StartSorting", "LeaderLib_Timers_DictSorter_", _DictionaryID)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_DictionarySort_StartSorting", _TimerName)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_DictionarySort_StartSorting", _TimerName);
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate);
DB_LeaderLib_DictionarySort_SortingStarted(_DictionaryID, 1);
LeaderLib_DictionarySort_Internal_OnTick(_DictionaryID);

/* [OSITOOLS_ONLY]
PROC
LeaderLib_DictionarySort_StartSorting((STRING)_DictionaryID, (INTEGER)_TickRate, (STRING)_CompleteEvent, (INTEGER)_EntryTickRate)
THEN
//DB_LeaderLib_Dictionary_Data(_DictionaryID, _Index, _CheckID, _CheckDisplayName)
//LeaderLib_Ext_SortDatabase("DB_LeaderLib_Dictionary_Data", 4, 4);
NRD_ModCall("LeaderLib", "SortDictionary", _DictionaryID);
LeaderLib_SendEvent(_CompleteEvent);
*/
//END_REGION

//REGION STOP
PROC
LeaderLib_DictionarySort_StopSorting((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName)
THEN
TimerCancel(_TimerName);

PROC
LeaderLib_DictionarySort_StopSorting((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex)
THEN
TimerCancel(_TimerName);

PROC
LeaderLib_DictionarySort_StopSorting((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_SortingStarted(_DictionaryID, _)
THEN
LeaderLib_DictionarySort_CleanupJob(_DictionaryID);
LeaderLib_DictionarySort_CleanupLastTrackedTime(_DictionaryID);
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort:StopSorting] Sorting stopped for '",_DictionaryID,"'. Cleaned up data.");
//END_REGION

//REGION COMPLETE
PROC
LeaderLib_DictionarySort_SortingComplete((STRING)_DictionaryID)
AND
NOT DB_LeaderLib_DictionarySort_Temp_Target(_DictionaryID, _)
AND
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate)
AND
NOT DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID)
THEN
DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID);
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort:SortingComplete] Sorting complete for '",_DictionaryID,"'. Sending completion event [",_CompleteEvent,"].");
LeaderLib_SendEvent(_CompleteEvent);

PROC
LeaderLib_DictionarySort_SortingComplete((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_Temp_Target(_DictionaryID, _Object)
AND
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate)
AND
NOT DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID)
THEN
DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID);
NOT DB_LeaderLib_DictionarySort_Temp_Target(_DictionaryID, _Object);
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort:SortingComplete] Sorting complete for '",_DictionaryID,"'. Sending completion event [",_CompleteEvent,"] to object.");
SetStoryEvent(_Object, _CompleteEvent);
//END_REGION

//REGION TIME_TRACKING
PROC
LeaderLib_DictionarySort_Debug_AddTickToTotalTime((STRING)_DictionaryID, (INTEGER)_TickRate)
AND
NOT DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, _)
THEN
DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, 0);

PROC
LeaderLib_DictionarySort_Debug_AddTickToTotalTime((STRING)_DictionaryID, (INTEGER)_TickRate)
AND
_TickRate > 0
AND
DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, _TimeTaken)
AND
IntegerSum(_TimeTaken, _TickRate, _NewTime)
THEN
NOT DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, _TimeTaken);
DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, _NewTime);

// [BEGIN_NO_OSITOOLS]
PROC
LeaderLib_DictionarySort_SortingComplete((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, _TimeTaken)
AND
DB_LeaderLib_Array_Length(_DictionaryID, _Length)
AND
IntegertoString(_TimeTaken, _TimeStr)
AND
IntegertoString(_Length, _LengthStr)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort:SortingComplete] Took ",_TimeStr,"ms to sort [",_LengthStr,"] entries in [",_DictionaryID,"].");
// [END_NO_OSITOOLS]

PROC
LeaderLib_DictionarySort_SortingComplete((STRING)_DictionaryID)
THEN
LeaderLib_DictionarySort_CleanupJob(_DictionaryID);
//END_REGION

//REGION TICK
PROC
LeaderLib_DictionarySort_Internal_NextTick((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate)
AND
NOT DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName)
AND
_TickRate > 0
THEN
DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName);
//LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort] Starting next tick for '",_DictionaryID,"'.");
LeaderLib_DictionarySort_Debug_AddTickToTotalTime(_DictionaryID, _TickRate);
TimerLaunch(_TimerName, _TickRate);

PROC
LeaderLib_DictionarySort_Internal_NextTick((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate)
AND
NOT DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName)
AND
_TickRate <= 0
THEN
//Needs to be delayed by a frame to avoid recursive crashing
DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName);
//LeaderLib_DictionarySort_Debug_AddTickToTotalTime(_DictionaryID, 1);
//TimerLaunch(_TimerName, 1);
LeaderLib_SendEvent(_TimerName);

IF
StoryEvent(_, _TimerName)
AND
DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName)
THEN
NOT DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName);
LeaderLib_DictionarySort_Internal_OnTick(_DictionaryID);

PROC
LeaderLib_DictionarySort_Internal_NextEntryTick((STRING)_DictionaryID, (STRING)_ID)
AND
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _MainTimerName, _TickRate, _CompleteEvent, _EntryTickRate)
AND
_EntryTickRate > 0
AND
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex)
AND
NOT DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName)
THEN
DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName);
//LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort:NextEntryTick] Starting next entry tick for [",_DictionaryID,"][",_ID,"].");
LeaderLib_DictionarySort_Debug_AddTickToTotalTime(_DictionaryID, _EntryTickRate);
TimerLaunch(_TimerName, _EntryTickRate);

PROC
LeaderLib_DictionarySort_Internal_NextEntryTick((STRING)_DictionaryID, (STRING)_ID)
AND
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _MainTimerName, _TickRate, _CompleteEvent, _EntryTickRate)
AND
_EntryTickRate <= 0
AND
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex)
THEN
//LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort:SortJob(TimerFinished)] Ticking entry [",_DictionaryID,"][",_ID,"].");
LeaderLib_DictionarySort_Internal_OnEntryTick(_DictionaryID, _ID);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName)
THEN
NOT DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate)
THEN
//LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort:SortJob(TimerFinished)] Ticking '",_DictionaryID,"'.");
LeaderLib_DictionarySort_Internal_OnTick(_DictionaryID);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex)
THEN
//LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort:SortJob(TimerFinished)] Ticking entry [",_DictionaryID,"][",_ID,"].");
LeaderLib_DictionarySort_Internal_OnEntryTick(_DictionaryID, _ID);

PROC
LeaderLib_DictionarySort_Internal_OnTick((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _Val)
THEN
NOT DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _Val);

PROC
LeaderLib_DictionarySort_Internal_OnTick((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _ID, _StringEntry)
AND
NOT DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
THEN
DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, 1);
LeaderLib_DictionarySort_SortEntry(_DictionaryID, _ID, _StringEntry);

PROC
LeaderLib_DictionarySort_Internal_OnTick((STRING)_DictionaryID)
AND
NOT DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID)
AND
NOT DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _, _)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort:OnTick] [ERROR] String queue for [",_DictionaryID,"] is empty!");

PROC
LeaderLib_DictionarySort_Internal_OnTick((STRING)_DictionaryID)
AND
NOT DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID)
AND
DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _ID, _StringEntry)
AND
NOT DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _)
AND
DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort:OnTick] [ERROR] Queue entry [",_ID,"][",_StringEntry,"] is already in dictionary [",_DictionaryID,"].");

PROC
LeaderLib_DictionarySort_Internal_OnTick((STRING)_DictionaryID)
AND
NOT DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID)
AND
NOT DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort:OnTick] [ERROR] No entry was added for in this tick. Check the logs for an error. Completing sort for [",_DictionaryID,"].");
LeaderLib_DictionarySort_SortingComplete(_DictionaryID);

PROC
LeaderLib_DictionarySort_OnStringSorted((STRING)_DictionaryID, (STRING)_ID)
AND
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex)
THEN
NOT DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex);
TimerCancel(_TimerName);
//LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort:OnMenuSorted] Menu '",_StringEntry,"' was sorted successfully.");

PROC
LeaderLib_DictionarySort_OnStringSorted((STRING)_DictionaryID, (STRING)_ID)
AND
LeaderLib_DictionarySort_QRY_SortingNeeded()
THEN
//LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort:OnStringSorted] Starting sort timer. ");
LeaderLib_DictionarySort_Internal_NextTick(_DictionaryID);

PROC
LeaderLib_DictionarySort_OnStringSorted((STRING)_DictionaryID, (STRING)_ID)
AND
DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _Val)
THEN
NOT DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _Val);

PROC
LeaderLib_DictionarySort_OnStringSorted((STRING)_DictionaryID, (STRING)_ID)
AND
NOT LeaderLib_DictionarySort_QRY_SortingNeeded()
THEN
LeaderLib_DictionarySort_SortingComplete(_DictionaryID);

QRY
LeaderLib_DictionarySort_QRY_SortingNeeded()
AND
DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _ID, _StringEntry)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
THEN
DB_NOOP(1);

PROC
LeaderLib_DictionarySort_SortEntry((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _, _)
THEN
LeaderLib_Array_AddToDictionary(_DictionaryID, _ID, _StringEntry);
LeaderLib_DictionarySort_OnStringSorted(_DictionaryID, _ID);

PROC
LeaderLib_DictionarySort_SortEntry((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry)
AND
NOT DB_LeaderLib_Array_Length(_DictionaryID, _)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:DictionarySort] [ERROR] Dictionary '",_DictionaryID,"' has no length!");

PROC
LeaderLib_DictionarySort_SortEntry((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
AND
DB_LeaderLib_Array_Length(_DictionaryID, _Length)
AND
_Length > 0
AND
LeaderLib_StringExt_QRY_SafeCombine("LeaderLib_DictionarySort_SortEntry", "LeaderLib_Timers_DictEntrySorter_", _DictionaryID, _ID, _StringEntry)
AND
DB_LeaderLib_StringExt_CombineResult("LeaderLib_DictionarySort_SortEntry", _TimerName)
THEN
NOT DB_LeaderLib_StringExt_CombineResult("LeaderLib_DictionarySort_SortEntry", _TimerName);
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, 0);
//LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort] [",_DictionaryID,"] Finding sorted position for '",_StringEntry,"'.");
LeaderLib_Array_BuildArrayIterator(_DictionaryID);
LeaderLib_DictionarySort_Internal_FindNextEntryIndex(_DictionaryID, _ID, _StringEntry);

PROC
LeaderLib_DictionarySort_Internal_OnEntryTick((STRING)_DictionaryID, (STRING)_ID)
AND
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex)
AND
IntegerSum(_CurrentIndex, 1, _NextIndex)
THEN
NOT DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex);
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _NextIndex);
LeaderLib_DictionarySort_Internal_FindNextEntryIndex(_DictionaryID, _ID, _StringEntry);

PROC
LeaderLib_DictionarySort_Internal_FindNextEntryIndex((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry)
AND
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _Index)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
THEN
LeaderLib_DictionarySort_Internal_OnIterator(_DictionaryID, _ID, _StringEntry, _Index);
LeaderLib_DictionarySort_Internal_NextEntryTick(_DictionaryID, _ID);

PROC
LeaderLib_DictionarySort_Internal_OnIterator((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry, (INTEGER)_Index)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
AND
DB_LeaderLib_Dictionary_Data(_DictionaryID, _Index, _CheckID, _CheckDisplayName)
AND
LeaderLib_StringExt_QRY_CompareStrings(_DictionaryID, _StringEntry, _CheckDisplayName)
AND
DB_LeaderLib_StringCompare_Results(_StringEntry, _CheckDisplayName, _ComparisonVal)
THEN
LeaderLib_DictionarySort_Internal_ProcessCompareResults(_DictionaryID, _ID, _StringEntry, _Index, _ComparisonVal);

PROC
LeaderLib_DictionarySort_Internal_OnIterator((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry, (INTEGER)_Index)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
AND
DB_LeaderLib_Array_Length(_DictionaryID, _Length)
AND
_Index >= _Length
THEN
LeaderLib_Array_AddToDictionary(_DictionaryID, _ID, _StringEntry);
LeaderLib_DictionarySort_OnStringSorted(_DictionaryID, _ID);

PROC
LeaderLib_DictionarySort_Internal_ProcessCompareResults((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry, (INTEGER)_CurrentIndex, (INTEGER)_ComparisonVal)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
AND
_ComparisonVal <= 0
THEN
LeaderLib_Array_AddToDictionaryAtIndex(_DictionaryID, _ID, _StringEntry, _CurrentIndex);
LeaderLib_DictionarySort_OnStringSorted(_DictionaryID, _ID);

PROC
LeaderLib_DictionarySort_Internal_ProcessCompareResults((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry, (INTEGER)_CurrentIndex, (INTEGER)_ComparisonVal)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
AND
_ComparisonVal > 0
AND
DB_LeaderLib_Array_Length(_DictionaryID, _Length)
AND
_CurrentIndex < _Length
THEN
//Keep iterating
DB_NOOP(1);

PROC
LeaderLib_DictionarySort_Internal_ProcessCompareResults((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry, (INTEGER)_CurrentIndex, (INTEGER)_ComparisonVal)
AND
NOT DB_LeaderLib_Dictionary_Data(_DictionaryID, _, _ID, _StringEntry)
AND
_ComparisonVal > 0
AND
DB_LeaderLib_Array_Length(_DictionaryID, _Length)
AND
_CurrentIndex >= _Length
THEN
LeaderLib_Array_AddToDictionary(_DictionaryID, _ID, _StringEntry);
LeaderLib_DictionarySort_OnStringSorted(_DictionaryID, _ID);

//END_REGION

//REGION CLEANUP
PROC
LeaderLib_DictionarySort_CleanupJob((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_SortingStarted(_DictionaryID, _Val)
THEN
NOT DB_LeaderLib_DictionarySort_SortingStarted(_DictionaryID, _Val);

PROC
LeaderLib_DictionarySort_CleanupJob((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _Val)
THEN
NOT DB_LeaderLib_DictionarySort_CurrentlySorting(_DictionaryID, _Val);

PROC
LeaderLib_DictionarySort_CleanupJob((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName)
THEN
NOT DB_LeaderLib_DictionarySort_TimerRunning(_DictionaryID, _TimerName);

/*
PROC
LeaderLib_DictionarySort_CleanupJob((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID)
THEN
NOT DB_LeaderLib_DictionarySort_SortingComplete(_DictionaryID);
*/

PROC
LeaderLib_DictionarySort_CleanupJob((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _ID, _StringEntry)
THEN
NOT DB_LeaderLib_DictionarySort_Temp_StringQueue(_DictionaryID, _ID, _StringEntry);

PROC
LeaderLib_DictionarySort_CleanupJob((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate)
THEN
NOT DB_LeaderLib_DictionarySort_Temp_SortJob(_DictionaryID, _TimerName, _TickRate, _CompleteEvent, _EntryTickRate);

PROC
LeaderLib_DictionarySort_CleanupJob((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex)
THEN
NOT DB_LeaderLib_DictionarySort_EntrySortTimer(_TimerName, _DictionaryID, _ID, _StringEntry, _CurrentIndex);

PROC
LeaderLib_DictionarySort_CleanupLastTrackedTime((STRING)_DictionaryID)
AND
DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, _TimeTaken)
THEN
NOT DB_LeaderLib_DictionarySort_TotalTime(_DictionaryID, _TimeTaken);
//END_REGION

//REGION DEBUG
/*
PROC
LeaderLib_DictionarySort_Internal_OnIterator((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry, (INTEGER)_Index)
AND
IntegertoString(_Index, _CurrentIndexStr)
AND
DB_LeaderLib_Array_Length(_DictionaryID, _Length)
AND
IntegertoString(_Length, _LengthStr)
THEN
LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort:OnIterator] Iterating [",_DictionaryID,"][",_CurrentIndexStr,"/",_LengthStr,"] [",_ID,"] = ",_StringEntry);

PROC
LeaderLib_DictionarySort_Internal_OnIterator((STRING)_DictionaryID, (STRING)_ID, (STRING)_StringEntry, (INTEGER)_Index)
AND
DB_LeaderLib_Dictionary_Data(_DictionaryID, _Index, _CheckID, _CheckDisplayName)
AND
DB_LeaderLib_StringCompare_Results(_StringEntry, _CheckDisplayName, _ComparisonVal)
AND
IntegertoString(_ComparisonVal, _CompValStr)
THEN
LeaderLog_Log("TRACE", "[LeaderLib:DictionarySort:ProcessCompareResults] Comparison value: '",_StringEntry,"' => '",_CheckDisplayName,"' = ",_CompValStr);
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
