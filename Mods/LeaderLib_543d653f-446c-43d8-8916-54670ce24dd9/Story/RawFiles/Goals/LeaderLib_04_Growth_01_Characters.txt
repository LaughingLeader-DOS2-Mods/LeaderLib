Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue)
KBSECTION
//REGION REGISTERING
/* _MatchType Values:
"Tag"
"Template"
"UUID"
*/
PROC
LeaderLib_Growth_Characters_Register_Skill((STRING)_ID, (INTEGER)_MinLevel, (STRING)_Skill, (STRING)_MatchType, (STRING)_MatchValue)
THEN
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue);

IF
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue)
THEN
LeaderLib_GC_Internal_SkillRegistered(_ID, _MinLevel, _Skill, _MatchType, _MatchValue);

PROC
LeaderLib_Growth_Characters_Register_Skill((STRING)_ID, (INTEGER)_MinLevel, (STRING)_Skill, (STRING)_MatchType, (STRING)_MatchValue)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_GrowthEvents_Character_Enabled", "LeaderLib")
AND
DB_CurrentLevel(_Level)
AND
IsGameLevel(_Level, 1)
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_GrowthEvents_Character_Enabled", "LeaderLib");
//END_REGION

//REGION CLEARING
PROC
LeaderLib_Growth_Characters_Clear_ByEntriesID((STRING)_ID)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue)
THEN
NOT DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue);

PROC
LeaderLib_Growth_Characters_Clear_ByEntriesID((STRING)_ID)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_GrowthEvents_Character_Enabled", "LeaderLib")
AND
SysCount("DB_LeaderLib_Growth_Characters_Skills", 5, 0)
THEN
LeaderLib_ToggleScripts_DisableScript("LeaderLib_TS_GrowthEvents_Character_Enabled", "LeaderLib");
//END_REGION

//REGION ITERATOR_START
PROC
LeaderLib_GC_Internal_SkillRegistered((STRING)_ID, (INTEGER)_MinLevel, (STRING)_Skill, "Tag", (STRING)_Tag)
AND
NOT DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag)
THEN
DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag);
LeaderLib_GC_Internal_NewTagAdded(_Tag);

PROC
LeaderLib_GC_Internal_NewTagAdded((STRING)_Tag)
//DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag, _TimerName, _TimerDelay)
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Characters:NewTagAdded(Already Initialized)] Starting character tag iterator after 250ms.");
TimerCancel("Timers_LeaderLib_GC_StartTagIterator");
TimerLaunch("Timers_LeaderLib_GC_StartTagIterator", 250);

IF
GlobalFlagSet("LeaderLib_Initialized")
AND
DB_CurrentLevel(_Level)
AND
IsGameLevel(_Level, 1)
AND
SysCount("DB_LeaderLib_Growth_Characters_Temp_NewTags", 1, _Count)
AND
_Count > 0
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Characters:NewTagAdded(JustInitialzed)] Starting character tag iterator after 250ms.");
TimerCancel("Timers_LeaderLib_GC_StartTagIterator");
TimerLaunch("Timers_LeaderLib_GC_StartTagIterator", 250);

IF
TimerFinished("Timers_LeaderLib_GC_StartTagIterator")
THEN
CharacterLaunchOsirisOnlyIterator("Iterator_LeaderLib_GC_CheckForNewTags");
TimerCancel("Timers_LeaderLib_GC_ClearNewTags");
TimerLaunch("Timers_LeaderLib_GC_ClearNewTags", 25);

IF
StoryEvent((CHARACTERGUID)_Char, "Iterator_LeaderLib_GC_CheckForNewTags")
AND
DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag)
AND
NOT DB_LeaderLib_Growth_Characters_Temp_AddedNewSkills(_Char)
AND
IsTagged(_Char, _Tag, 1)
THEN
DB_LeaderLib_Growth_Characters_Temp_AddedNewSkills(_Char);
LeaderLib_Growth_Characters_AddSkills(_Char);

IF
TimerFinished("Timers_LeaderLib_GC_ClearNewTags")
THEN
SysClear("DB_LeaderLib_Growth_Characters_Temp_NewTags", 1);
SysClear("DB_LeaderLib_Growth_Characters_Temp_AddedNewSkills", 1);
//END_REGION

//REGION SKILLS
QRY
LeaderLib_Growth_Characters_QRY_CharacterHasSkills((CHARACTERGUID)_Character)
AND
GetTemplate(_Character, _Template)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Template", _Template)
THEN
DB_NOOP(1);

QRY
LeaderLib_Growth_Characters_QRY_CharacterHasSkills((CHARACTERGUID)_Character)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Tag", _Tag)
AND
IsTagged(_Character, _Tag, 1)
THEN
DB_NOOP(1);

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Commands_AddGrowthSkills")
AND
IsTagged(_Character, "LeaderLib_Growth_AddedSkillsThisLevel", 0)
AND
LeaderLib_Growth_Characters_QRY_CharacterHasSkills(_Character)
THEN
LeaderLib_Growth_Characters_AddSkills(_Character);

PROC
LeaderLib_Growth_Characters_AddSkills((CHARACTERGUID)_Character)
AND
CharacterGetLevel(_Character, _CharacterLevel)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Tag", _Tag)
AND
IsTagged(_Character, _Tag, 1)
AND
_CharacterLevel >= _MinLevel
AND
NOT CharacterHasSkill(_Character, _Skill, 1)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Characters:AddSkills(Tag)] Added skill [",_Skill,"] to character [",_Name,"].");
CharacterAddSkill(_Character, _Skill, 1);

PROC
LeaderLib_Growth_Characters_AddSkills((CHARACTERGUID)_Character)
AND
CharacterGetLevel(_Character, _CharacterLevel)
AND
GetTemplate(_Character, _Template)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Template", _Template)
AND
_CharacterLevel >= _MinLevel
AND
NOT CharacterHasSkill(_Character, _Skill, 1)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Characters:AddSkills(Template)] Added skill [",_Skill,"] to character [",_Name,"].");
CharacterAddSkill(_Character, _Skill, 1);

PROC
LeaderLib_Growth_Characters_AddSkills((CHARACTERGUID)_Character)
AND
CharacterGetLevel(_Character, _CharacterLevel)
AND
GetUUID(_Character, _UUID)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "UUID", _UUID)
AND
_CharacterLevel >= _MinLevel
AND
NOT CharacterHasSkill(_Character, _Skill, 1)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Characters:AddSkills(UUID)] Added skill [",_Skill,"] to character [",_Name,"].");
CharacterAddSkill(_Character, _Skill, 1);

PROC
LeaderLib_Growth_Characters_Internal_ApplySkillTag((CHARACTERGUID)_Character)
AND
NOT IsTagged(_Character, "LeaderLib_Growth_AddedSkillsThisLevel", 1)
THEN
SetTag(_Character, "LeaderLib_Growth_AddedSkillsThisLevel");
SetStoryEvent(_Character, "LeaderLib_Events_GrowthSkillsAdded");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"