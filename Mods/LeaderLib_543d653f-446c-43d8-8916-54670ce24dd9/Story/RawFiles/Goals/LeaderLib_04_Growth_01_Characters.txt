Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue)
KBSECTION
//REGION REGISTERING
/* _MatchType Values:
"Tag"
"Template"
"UUID"
*/
PROC
LeaderLib_Growth_Characters_Register_Skill((STRING)_ID, (INTEGER)_MinLevel, (STRING)_Skill, (STRING)_MatchType, (STRING)_MatchValue)
THEN
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue);
//END_REGION

//REGION CLEARING
PROC
LeaderLib_Growth_Characters_Clear_ByEntriesID((STRING)_ID)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue)
THEN
NOT DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, _MatchType, _MatchValue);
//END_REGION

//REGION ITERATOR_START
QRY
LeaderLib_Growth_Characters_QRY_NewTagAlreadyExists((STRING)_Tag)
AND
DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag, _TimerName, _TimerDelay)
THEN
DB_NOOP(1);

IF
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Tag", _Tag)
AND
NOT LeaderLib_Growth_Characters_QRY_NewTagAlreadyExists(_Tag)
AND
SysCount("DB_LeaderLib_Growth_Characters_Temp_NewTags", 3, _Count)
AND
IntegerSum(_Count, 1, _MultBy)
AND
IntegerProduct(_MultBy, 250, _TimerDelay)
AND
StringConcatenate("LeaderLib_Timers_Growth_Characters_StartTagIterator_", _Tag, _TimerName)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Growth:Characters:NewTagAdded] Adding tag [",_Tag,"][",_TimerName,"] to [DB_LeaderLib_Growth_Characters_Temp_NewTags].");
DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag, _TimerName, _TimerDelay);

IF
DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag, _TimerName, _TimerDelay)
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
AND
IntegertoString(_TimerDelay, _TimerDelayStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Growth:Characters:NewTagAdded(Already Initialized)] Starting character tag iterator for tag [",_Tag,"] after [",_TimerDelayStr,"] ms.");
TimerLaunch(_TimerName, _TimerDelay);

IF
GlobalFlagSet("LeaderLib_Initialized")
AND
DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag, _TimerName, _TimerDelay)
AND
IntegertoString(_TimerDelay, _TimerDelayStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Growth:Characters:NewTagAdded(JustInitialzed)] Starting character tag iterator for tag [",_Tag,"] after [",_TimerDelayStr,"] ms.");
TimerLaunch(_TimerName, _TimerDelay);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag, _TimerName, _TimerDelay)
THEN
NOT DB_LeaderLib_Growth_Characters_Temp_NewTags(_Tag, _TimerName, _TimerDelay);
LeaderLib_EventParser_LaunchCharacterIterator("LeaderLib_Commands_AddGrowthSkills", _Tag);
//END_REGION

//REGION SKILLS
QRY
LeaderLib_Growth_Characters_QRY_CharacterHasSkills((CHARACTERGUID)_Character)
AND
GetTemplate(_Character, _Template)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Template", _Template)
THEN
DB_NOOP(1);

QRY
LeaderLib_Growth_Characters_QRY_CharacterHasSkills((CHARACTERGUID)_Character)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Tag", _Tag)
AND
IsTagged(_Character, _Tag, 1)
THEN
DB_NOOP(1);

IF
CharacterLeveledUp(_Character)
AND
IsTagged(_Character, "LeaderLib_Growth_AddedSkillsThisLevel", 1)
THEN
ClearTag(_Character, "LeaderLib_Growth_AddedSkillsThisLevel");

IF
CharacterLeveledUp(_Character)
AND
LeaderLib_Growth_Characters_QRY_CharacterHasSkills(_Character)
THEN
LeaderLib_Growth_Characters_AddSkills(_Character);

IF
CharacterJoinedParty(_Character)
AND
NOT IsTagged(_Character, "LeaderLib_Growth_AddedSkillsThisLevel", 1)
AND
LeaderLib_Growth_Characters_QRY_CharacterHasSkills(_Character)
THEN
LeaderLib_Growth_Characters_AddSkills(_Character);

IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Commands_AddGrowthSkills")
AND
NOT IsTagged(_Character, "LeaderLib_Growth_AddedSkillsThisLevel", 1)
AND
LeaderLib_Growth_Characters_QRY_CharacterHasSkills(_Character)
THEN
LeaderLib_Growth_Characters_AddSkills(_Character);

PROC
LeaderLib_Growth_Characters_AddSkills((CHARACTERGUID)_Character)
AND
CharacterGetLevel(_Character, _CharacterLevel)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Tag", _Tag)
AND
IsTagged(_Character, _Tag, 1)
AND
_CharacterLevel >= _MinLevel
AND
NOT CharacterHasSkill(_Character, _Skill, 1)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Characters:AddSkills(Tag)] Added skill [",_Skill,"] to character [",_Name,"].");
CharacterAddSkill(_Character, _Skill, 1);

PROC
LeaderLib_Growth_Characters_AddSkills((CHARACTERGUID)_Character)
AND
CharacterGetLevel(_Character, _CharacterLevel)
AND
GetTemplate(_Character, _Template)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "Template", _Template)
AND
_CharacterLevel >= _MinLevel
AND
NOT CharacterHasSkill(_Character, _Skill, 1)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Characters:AddSkills(Template)] Added skill [",_Skill,"] to character [",_Name,"].");
CharacterAddSkill(_Character, _Skill, 1);

PROC
LeaderLib_Growth_Characters_AddSkills((CHARACTERGUID)_Character)
AND
CharacterGetLevel(_Character, _CharacterLevel)
AND
GetUUID(_Character, _UUID)
AND
DB_LeaderLib_Growth_Characters_Skills(_ID, _MinLevel, _Skill, "UUID", _UUID)
AND
_CharacterLevel >= _MinLevel
AND
NOT CharacterHasSkill(_Character, _Skill, 1)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Characters:AddSkills(UUID)] Added skill [",_Skill,"] to character [",_Name,"].");
CharacterAddSkill(_Character, _Skill, 1);

PROC
LeaderLib_Growth_Characters_Internal_ApplySkillTag((CHARACTERGUID)_Character)
AND
NOT IsTagged(_Character, "LeaderLib_Growth_AddedSkillsThisLevel", 1)
THEN
SetTag(_Character, "LeaderLib_Growth_AddedSkillsThisLevel");
SetStoryEvent(_Character, "LeaderLib_Events_GrowthSkillsAdded");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
