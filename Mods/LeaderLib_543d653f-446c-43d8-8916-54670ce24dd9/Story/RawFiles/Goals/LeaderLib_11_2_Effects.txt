Version 1
SubGoalCombiner SGC_AND
INITSECTION
LeaderLib_EffectFlags_InitSettings();

//DB_LeaderLib_Effects_EffectFlags(_ID, _Group, _Flag, _Effect, _Loop, _BoneName)
//DB_LeaderLib_Effects_StopGroupFlags(_ID, _Group, _StopFlag)
KBSECTION
//REGION SETTINGS
PROC
LeaderLib_EffectFlags_InitSettings()
THEN
LeaderLib_Effects_Register_StopGroupFlag("LeaderLib.Default", "CameraShake", "LeaderLib_Effects_StopCameraShake");
LeaderLib_Effects_Register_EffectFlag("LeaderLib.Default", "CameraShake", "LeaderLib_Effects_CameraShake_Loop_Normal", "RS3_FX_GP_ScriptedEvent_CameraShake_Loop_00-50", 1, "");
//END_REGION

//REGION REGISTERING
PROC
LeaderLib_Effects_Register_EffectFlag((STRING)_ID, (STRING)_Group, (STRING)_Flag, (STRING)_Effect, (INTEGER)_Loop, (STRING)_BoneName)
AND
DB_LeaderLib_Effects_EffectFlags(_OtherID, _OtherGroup, _Flag, _OtherEffect, _OtherLoop, _OtherBoneName)
THEN
NOT DB_LeaderLib_Effects_EffectFlags(_OtherID, _OtherGroup, _Flag, _OtherEffect, _OtherLoop, _OtherBoneName);

PROC
LeaderLib_Effects_Register_EffectFlag((STRING)_ID, (STRING)_Group, (STRING)_Flag, (STRING)_Effect, (INTEGER)_Loop, (STRING)_BoneName)
THEN
DB_LeaderLib_Effects_EffectFlags(_ID, _Group, _Flag, _Effect, _Loop, _BoneName);

PROC
LeaderLib_Effects_Register_StopGroupFlag((STRING)_ID, (STRING)_Group, (STRING)_Flag)
THEN
DB_LeaderLib_Effects_StopGroupFlags(_ID, _Group, _Flag);
//END_REGION

//REGION EFFECT_PROCS
PROC
LeaderLib_Effects_Internal_ParseEffect((GUIDSTRING)_Target, (STRING)_Effect, (INTEGER)_Loop, (STRING)_BoneName)
AND
_Loop > 0
AND
DB_CurrentLevel(_Region)
AND
GetUUID(_Target, _UUID)
AND
StringConcatenate(_UUID, "_", _Msg1)
AND
StringConcatenate(_Msg1, _Effect, _Msg2)
AND
StringConcatenate(_Msg2, "_", _Msg3)
AND
StringConcatenate(_Msg3, _BoneName, _ID)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:EffectFlags:ParseEffect] Playing loop effect [",_Effect,"] on target [",_UUID,"].");
PROC_LoopEffect(_Effect, _Target, _ID, _Region, _BoneName);

PROC
LeaderLib_Effects_Internal_ParseEffect((GUIDSTRING)_Target, (STRING)_Effect, (INTEGER)_Loop, (STRING)_BoneName)
AND
_Loop <= 0
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:EffectFlags:ParseEffect] Playing effect [",_Effect,"] on target.");
PlayEffect(_Target, _Effect, _BoneName);

PROC
LeaderLib_Effects_StopEffect((GUIDSTRING)_Target, (STRING)_Effect)
AND
DB_LoopEffect(_Target, _fxHandle, _ID, _Region, _Effect, _BoneName)
THEN
PROC_StopLoopEffect(_Target, _ID);

PROC
LeaderLib_Effects_StopEffect((GUIDSTRING)_Target, (STRING)_Effect, (STRING)_BoneName)
AND
DB_LoopEffect(_Target, _fxHandle, _ID, _Region, _Effect, _BoneName)
THEN
PROC_StopLoopEffect(_Target, _ID);

PROC
LeaderLib_Effects_StopGroupEffects((GUIDSTRING)_Target, (STRING)_Group)
AND
DB_LoopEffect(_Target, _fxHandle, _ID, _Region, _Effect, _BoneName)
THEN
PROC_StopLoopEffect(_Target, _ID);
//END_REGION

//REGION EFFECT_FLAGS
IF
ObjectFlagSet(_Flag, _Target, _)
AND
DB_LeaderLib_Effects_EffectFlags(_ID, _Group, _Flag, _Effect, _Loop, _BoneName)
THEN
LeaderLib_Effects_Internal_ParseEffect(_Target, _Effect, _Loop, _BoneName);

IF
ObjectFlagCleared(_Flag, _Target, _)
AND
DB_LeaderLib_Effects_EffectFlags(_ID, _Group, _Flag, _Effect, _Loop, _BoneName)
THEN
LeaderLib_Effects_StopEffect(_Target, _Effect, _BoneName);

IF
ObjectFlagSet(_StopFlag, _Target, _)
AND
DB_LeaderLib_Effects_StopGroupFlags(_ID, _Group, _StopFlag)
THEN
ObjectClearFlag(_Target, _StopFlag);
LeaderLib_Effects_StopGroupEffects(_Target, _Group);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LeaderLib_11__Start"
