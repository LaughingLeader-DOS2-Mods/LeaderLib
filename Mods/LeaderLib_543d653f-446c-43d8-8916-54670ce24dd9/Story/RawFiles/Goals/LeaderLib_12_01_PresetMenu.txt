Version 1
SubGoalCombiner SGC_AND
INITSECTION
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets();

KBSECTION
PROC
//REGION SETTINGS
LeaderLib_PresetMenu_InitSettings()
THEN
LeaderLib_DynamicMenu_Register_DefaultMenu("LeaderLib.Settings.PresetMenu");
LeaderLib_DynamicMenu_Register_Dialog("LeaderLib.Settings.PresetMenu", "LeaderLib_PresetMenu");

//Old
PROC
LeaderLib_PresetMenu_RegisterPreset((STRING)_Preset, (STRING)_DisplayName, (STRING)_EnabledGlobalFlag)
THEN
LeaderLib_PresetMenu_RegisterPreset(_EnabledGlobalFlag, _Preset, _DisplayName, _EnabledGlobalFlag);

PROC
LeaderLib_PresetMenu_RegisterPreset((STRING)_Group, (STRING)_Preset, (STRING)_DisplayName, (STRING)_EnabledGlobalFlag)
AND
NOT DB_LeaderLib_PresetMenu_RegisteredPreset(_, _Preset, _)
THEN
LeaderLib_DynamicMenu_Register_Entry("LeaderLib.Settings.PresetMenu", _Preset, _DisplayName);
DB_LeaderLib_PresetMenu_RegisteredPreset(_Group, _Preset, _EnabledGlobalFlag);

PROC
LeaderLib_PresetMenu_RegisterPreset((STRING)_Group, (STRING)_Preset, (STRING)_DisplayName, (STRING)_EnabledGlobalFlag)
AND
_Group != "DOS2_Act1"
AND
_Group != "DOS2_Act2"
THEN
LeaderLib_PresetMenu_StartSorting();

PROC
LeaderLib_PresetMenu_ClearEntriesWithGroup((STRING)_Group)
AND
DB_LeaderLib_PresetMenu_RegisteredPreset(_Group, _Preset, _EnabledGlobalFlag)
THEN
NOT DB_LeaderLib_PresetMenu_RegisteredPreset(_Group, _Preset, _EnabledGlobalFlag);
LeaderLib_DynamicMenu_Remove_Entry("LeaderLib.Settings.PresetMenu", _Preset);

PROC
LeaderLib_PresetMenu_ClearEntriesWithFlag((STRING)_EnabledGlobalFlag)
AND
DB_LeaderLib_PresetMenu_RegisteredPreset(_Group, _Preset, _EnabledGlobalFlag)
THEN
LeaderLib_DynamicMenu_Remove_Entry("LeaderLib.Settings.PresetMenu", _Preset);
NOT DB_LeaderLib_PresetMenu_RegisteredPreset(_Group, _Preset, _EnabledGlobalFlag);
//END_REGION

//REGION SETTINGS_PROCS
PROC
LeaderLib_PresetMenu_StartSorting()
THEN
LeaderLib_Array_ClearDictionary("LeaderLib.Settings.PresetMenu.Sorted");
TimerCancel("LeaderLib_Timers_SortPresetMenu");
TimerLaunch("LeaderLib_Timers_SortPresetMenu", 50);
GlobalSetFlag("LeaderLib_PresetMenu_LockOpening");

IF
TimerFinished("LeaderLib_Timers_SortPresetMenu")
AND
DB_LeaderLib_Dictionary_Data("LeaderLib.Settings.PresetMenu", _Index, _Preset, _DisplayName)
THEN
LeaderLib_DictionarySort_AddString("LeaderLib.Settings.PresetMenu.Sorted", _Preset, _DisplayName);

IF
TimerFinished("LeaderLib_Timers_SortPresetMenu")
THEN
LeaderLib_DictionarySort_StartSorting("LeaderLib.Settings.PresetMenu.Sorted", 50, "LeaderLib_Events_OnPresetMenuSortComplete", 0);

IF
StoryEvent(_, "LeaderLib_Events_OnPresetMenuSortComplete")
AND
DB_LeaderLib_Dictionary_Data("LeaderLib.Settings.PresetMenu", _Index, _Key, _Value)
THEN
NOT DB_LeaderLib_Dictionary_Data("LeaderLib.Settings.PresetMenu", _Index, _Key, _Value);
//Failsafe
DB_LeaderLib_PresetMenu_Temp_PreSortedValues(_Key, _Value);

IF
StoryEvent(_, "LeaderLib_Events_OnPresetMenuSortComplete")
AND
DB_LeaderLib_Dictionary_Data("LeaderLib.Settings.PresetMenu.Sorted", _Index, _Preset, _DisplayName)
THEN
DB_LeaderLib_Dictionary_Data("LeaderLib.Settings.PresetMenu", _Index, _Preset, _DisplayName);

IF
StoryEvent(_, "LeaderLib_Events_OnPresetMenuSortComplete")
AND
DB_LeaderLib_PresetMenu_Temp_PreSortedValues(_Preset, _DisplayName)
AND
NOT DB_LeaderLib_Dictionary_Data("LeaderLib.Settings.PresetMenu", _, _Preset, _DisplayName)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_PreSortedValues(_Preset, _DisplayName);
LeaderLib_Array_AddToDictionary("LeaderLib.Settings.PresetMenu", _Preset, _DisplayName);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:OnPresetMenuSortComplete] [ERROR] Preset [",_Preset,"] with DisplayName [",_DisplayName,"] was never added to the post-sorted dictionary! Adding manually.");
DB_LeaderLib_PresetMenu_Temp_RebuildLength(1);

IF
StoryEvent(_, "LeaderLib_Events_OnPresetMenuSortComplete")
AND
DB_LeaderLib_PresetMenu_Temp_RebuildLength(1)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_RebuildLength(1);
LeaderLib_Array_RebuildArrayLength("LeaderLib.Settings.PresetMenu", "Dictionary");
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:OnPresetMenuSortComplete] Rebuild dictionary length.");

IF
StoryEvent(_, "LeaderLib_Events_OnPresetMenuSortComplete")
THEN
GlobalClearFlag("LeaderLib_PresetMenu_LockOpening");
SysClear("DB_LeaderLib_PresetMenu_Temp_PreSortedValues", 2);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:OnPresetMenuSortComplete] The Preset Menu was sorted successfully.");
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1,1,19,0)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib_12_01_PresetMenu:LeaderUpdater_ModUpdated] Updating settings from version [",_PastVersion,"]. Migrating registered presets.");
LeaderLib_PresetMenu_Internal_Migrate();

PROC
LeaderLib_PresetMenu_Internal_Migrate()
AND
DB_LeaderLib_PresetMenu_RegisteredPreset((STRING)_Preset, "LeaderLib_PresetMenu_IsAct1")
THEN
LeaderLib_DynamicMenu_Remove_Entry("LeaderLib.Settings.PresetMenu", _Preset);
NOT DB_LeaderLib_PresetMenu_RegisteredPreset(_Preset, "LeaderLib_PresetMenu_IsAct1");

PROC
LeaderLib_PresetMenu_Internal_Migrate()
AND
DB_LeaderLib_PresetMenu_RegisteredPreset((STRING)_Preset, "LeaderLib_PresetMenu_IsNotAct1")
THEN
LeaderLib_DynamicMenu_Remove_Entry("LeaderLib.Settings.PresetMenu", _Preset);
NOT DB_LeaderLib_PresetMenu_RegisteredPreset(_Preset, "LeaderLib_PresetMenu_IsNotAct1");

PROC
LeaderLib_PresetMenu_Internal_Migrate()
THEN
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets();

PROC
LeaderLib_PresetMenu_Internal_Migrate()
AND
DB_LeaderLib_PresetMenu_RegisteredPreset((STRING)_Preset, (STRING)_EnabledGlobalFlag)
AND
DB_LeaderLib_Dictionary_Data("LeaderLib.Settings.PresetMenu", _Index, _Preset, _DisplayName)
AND
DB_Mods_Registered(_ModID, _Author, _CurrentVersion)
AND
StringConcatenate(_ModID, "_IsActive", _EnabledGlobalFlag)
THEN
NOT DB_LeaderLib_PresetMenu_RegisteredPreset(_Preset, _EnabledGlobalFlag);
LeaderLib_DynamicMenu_Remove_Entry("LeaderLib.Settings.PresetMenu", _Preset);
LeaderLib_PresetMenu_RegisterPreset(_ModID, _Preset, _DisplayName, _EnabledGlobalFlag);

PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1,5,10,1)
THEN
LeaderLib_PresetMenu_Internal_UpdateDefaultPresets();

PROC
LeaderLib_PresetMenu_Internal_UpdateDefaultPresets()
AND
LeaderLib_Helper_QRY_IsPastFortJoy()
THEN
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(1);

PROC
LeaderLib_PresetMenu_Internal_UpdateDefaultPresets()
AND
NOT LeaderLib_Helper_QRY_IsPastFortJoy()
THEN
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(0);

IF
TimerFinished("Timers_LeaderLib_PresetMenu_StartSorting")
THEN
LeaderLib_PresetMenu_StartSorting();

PROC
LeaderLib_PresetMenu_Internal_UpdateDefaultPresets()
AND
DB_LeaderLib_DynamicMenu_Flags("LeaderLib.Settings.PresetMenu", _HasMultiplePagesFlag, _NextPageFlag, _PreviousPageFlag, _FirstPageFlag, _LastPageFlag)
AND
DB_IsPlayer(_Char)
THEN
ObjectClearFlag(_Char, _HasMultiplePagesFlag, 0);
ObjectClearFlag(_Char, _NextPageFlag, 0);
ObjectClearFlag(_Char, _PreviousPageFlag, 0);
ObjectClearFlag(_Char, _FirstPageFlag, 0);
ObjectClearFlag(_Char, _LastPageFlag, 0);
//END_REGION

//REGION DEFAULT_PRESETS
IF
DB_GlobalFlag("LeaderLib_IsEditorMode")
THEN
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(1);

IF
RegionStarted(_Level)
AND
GlobalGetFlag("LeaderLib_PresetMenu_IsNotAct1", 0)
AND
LeaderLib_Helper_QRY_IsPastFortJoy(_Level)
THEN
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(1);

IF
RegionStarted(_Level)
AND
GlobalGetFlag("LeaderLib_PresetMenu_IsAct1", 0)
AND
NOT LeaderLib_Helper_QRY_IsPastFortJoy(_Level)
THEN
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(0);

PROC
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets()
AND
NOT LeaderLib_Helper_QRY_IsPastFortJoy()
THEN
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(0);

PROC
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets()
AND
LeaderLib_Helper_QRY_IsPastFortJoy()
THEN
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(1);

PROC
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(0)
THEN
LeaderLib_PresetMenu_ClearEntriesWithGroup("DOS2_Act2");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Battlemage", "Battlemage [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Cleric", "Cleric [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Conjurer", "Conjurer [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Enchanter", "Enchanter [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Fighter", "Fighter [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Inquisitor", "Inquisitor [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Knight", "Knight [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Metamorph", "Metamorph [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Ranger", "Ranger [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Rogue", "Rogue [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Shadowblade", "Shadowblade [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Wayfarer", "Wayfarer [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Witch", "Witch [DOS2]", "LeaderLib_PresetMenu_IsAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act1", "Wizard", "Wizard [DOS2]", "LeaderLib_PresetMenu_IsAct1");
GlobalSetFlag("LeaderLib_PresetMenu_IsAct1");
GlobalClearFlag("LeaderLib_PresetMenu_IsNotAct1");

PROC
LeaderLib_PresetMenu_Internal_RegisterDefaultPresets(1)
THEN
LeaderLib_PresetMenu_ClearEntriesWithGroup("DOS2_Act1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Battlemage_Act2", "Battlemage [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Cleric_Act2", "Cleric [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Conjurer_Act2", "Conjurer [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Enchanter_Act2", "Enchanter [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Fighter_Act2", "Fighter [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Inquisitor_Act2", "Inquisitor [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Knight_Act2", "Knight [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Metamorph_Act2", "Metamorph [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Ranger_Act2", "Ranger [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Rogue_Act2", "Rogue [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Shadowblade_Act2", "Shadowblade [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Wayfarer_Act2", "Wayfarer [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Witch_Act2", "Witch [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
LeaderLib_PresetMenu_RegisterPreset("DOS2_Act2", "Wizard_Act2", "Wizard [DOS2]", "LeaderLib_PresetMenu_IsNotAct1");
GlobalClearFlag("LeaderLib_PresetMenu_IsAct1");
GlobalSetFlag("LeaderLib_PresetMenu_IsNotAct1");
//END_REGION

//REGION PRESET_MENU
IF
DialogStarted("LeaderLib_PresetMenu", _Instance)
THEN
DialogSetVariableStringForInstance(_Instance, "LeaderLib_PresetMenu_CurrentPreset_2e904bf9-1a93-447a-8f07-5383aea20068", "None");

IF
DialogEnded("LeaderLib_PresetMenu", _Instance)
AND
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _LastPreset, _Instance, _IsRace)
THEN
ObjectClearFlag(_Player, "LeaderLib_PresetMenu_PresetSelected", _Instance);
NOT DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _LastPreset, _Instance, _IsRace);

//(_Player, _ID, _DialogVar, _AvailableFlag, _Instance, _EntryKey, _DisplayText)
PROC
LeaderLib_DynamicMenu_BlockEntry((GUIDSTRING)_Player, "LeaderLib.Settings.PresetMenu", (STRING)_DialogVar, (STRING)_AvailableFlag, (INTEGER)_Instance, (STRING)_Preset, (STRING)_DisplayText)
AND
DB_LeaderLib_PresetMenu_RegisteredPreset(_ModID, _Preset, _GlobalFlag)
AND
GlobalGetFlag(_GlobalFlag, 0)
THEN
DB_LeaderLib_DynamicMenu_EntryBlocked(_Player, "LeaderLib.Settings.PresetMenu", _Preset, _Instance);

PROC
LeaderLib_DynamicMenu_OnEntrySelected("LeaderLib.Settings.PresetMenu", (GUIDSTRING)_Player, (INTEGER)_Instance, (STRING)_Preset)
AND
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _LastPreset, _Instance, _IsRace)
THEN
NOT DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _LastPreset, _Instance, _IsRace);

PROC
LeaderLib_DynamicMenu_OnEntrySelected("LeaderLib.Settings.PresetMenu", (GUIDSTRING)_Player, (INTEGER)_Instance, (STRING)_Preset)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:OnEntrySelected] Player selected preset [",_Preset,"].");
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, 0);
ObjectSetFlag(_Player, "LeaderLib_PresetMenu_PresetSelected", _Instance);
LeaderLib_DynamicMenu_FetchDialogVariableByKey("LeaderLib.Settings.PresetMenu", _Instance, _Preset);

PROC
LeaderLib_DynamicMenu_OnFetchDialogVariable("LeaderLib.Settings.PresetMenu", (STRING)_DialogVar, (STRING)_AvailableFlag, (INTEGER)_Instance, (STRING)_EntryKey, (STRING)_DisplayText)
THEN
DialogSetVariableStringForInstance(_Instance, "LeaderLib_PresetMenu_CurrentPreset_2e904bf9-1a93-447a-8f07-5383aea20068", _DisplayText);
//END_REGION

//REGION PRESET_APPLYING
IF
ObjectFlagSet("LeaderLib_PresetMenu_ConfirmSelection", (CHARACTERGUID)_Player, _Instance)
AND
LeaderLib_Helper_QRY_ClearObjectFlag(_Player, "LeaderLib_PresetMenu_ConfirmSelection")
AND
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, _IsRace)
THEN
ObjectClearFlag(_Player, "LeaderLib_PresetMenu_PresetSelected", _Instance);
LeaderLib_PresetMenu_StartApplyingPreset(_Player, _Preset, _IsRace);

PROC
LeaderLib_PresetMenu_StartApplyingPreset((CHARACTERGUID)_Player, (STRING)_Preset, (INTEGER)_IsRace)
AND
NOT DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _, _, _)
THEN
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, -1, _IsRace);

PROC
LeaderLib_PresetMenu_StartApplyingPreset((CHARACTERGUID)_Player, (STRING)_Preset, 1)
AND
DB_Origins_OriginTag(_, _Tag)
AND
IsTagged(_Player, _Tag, 1)
THEN
DB_LeaderLib_PresetMenu_Temp_OriginTags(_Player, _Tag);

PROC
LeaderLib_PresetMenu_StartApplyingPreset((CHARACTERGUID)_Player, (STRING)_Preset, (INTEGER)_IsRace)
AND
GetPosition(_Player, _x, _y, _z)
AND
CreateItemTemplateAtPosition("LOOT_LeaderLib_BackPack_Invisible_98fa7688-0810-4113-ba94-9a8c8463f830", _x, _y, _z, _Backpack)
AND
GetUUID(_Player, _UUID)
AND
StringConcatenate("LeaderLib_Array_PrePresetTalents_", _UUID, _ArrayID)
THEN
DB_LeaderLib_PresetMenu_Temp_ItemBackpack(_Player, _Backpack);
DB_LeaderLib_PresetMenu_Temp_TalentArray(_Player, _ArrayID);
LeaderLib_PresetMenu_RecordEquipment(_Player, _Backpack);
MoveAllItemsTo(_Player, _Backpack, 0, 0, 0);
LeaderLib_PresetMenu_RecordSourceMax(_Player, _IsRace);
LeaderLib_PresetMenu_RecordTalents(_Player, _ArrayID);
LeaderLib_PresetMenu_RecordSkills(_Player, _Preset, _IsRace);
ProcObjectTimerCancel(_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset");
ProcObjectTimer(_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset", 500);
//END_REGION

//REGION SAVING_PREAPPLYING_DATA
PROC
LeaderLib_PresetMenu_RecordSourceMax((CHARACTERGUID)_Player, 1)
AND
CharacterGetMaxSourcePoints(_Player, _MaxSP)
THEN
DB_LeaderLib_PresetMenu_Temp_MaxSourcePoints(_Player, _MaxSP);

PROC
LeaderLib_PresetMenu_RecordEquipment((CHARACTERGUID)_Player, (ITEMGUID)_Backpack)
AND
GetItemForItemTemplateInInventory(_Player, "RC_FTJ_Amulet_SourceCollar_8bf8b97c-556e-4e69-8fc7-39241783dcc1", _Item)
THEN
ItemLockUnEquip(_Item, 0);
DB_LeaderLib_PresetMenu_Temp_HadSourceCollar(_Player, _Item);
ItemToInventory(_Item, _Backpack);

PROC
LeaderLib_PresetMenu_RecordEquipment((CHARACTERGUID)_Player, (ITEMGUID)_Backpack)
AND
CharacterGetEquippedItem(_Player, "Amulet", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
GetTemplate(_Item, "RC_FTJ_Amulet_SourceCollar_8bf8b97c-556e-4e69-8fc7-39241783dcc1")
THEN
ItemLockUnEquip(_Item, 0);
DB_LeaderLib_PresetMenu_Temp_HadSourceCollar(_Player, _Item);
ItemToInventory(_Item, _Backpack);

// [BEGIN_NO_OSITOOLS]
PROC
LeaderLib_PresetMenu_RecordEquipment((CHARACTERGUID)_Player, (ITEMGUID)_Backpack)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT GetTemplate(_Item, "RC_FTJ_Amulet_SourceCollar_8bf8b97c-556e-4e69-8fc7-39241783dcc1")
THEN
ItemLockUnEquip(_Item, 0);
DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, _Slot, _Item);
ItemToInventory(_Item, _Backpack);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LeaderLib_PresetMenu_RecordEquipment((CHARACTERGUID)_Player, (ITEMGUID)_Backpack)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT GetTemplate(_Item, "RC_FTJ_Amulet_SourceCollar_8bf8b97c-556e-4e69-8fc7-39241783dcc1")
AND
LeaderLib_Ext_QRY_ItemIsLocked(_Item, _IsLocked)
THEN
ItemLockUnEquip(_Item, 0);
DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, _Slot, _Item);
DB_LeaderLib_PresetMenu_Temp_LockedEquipment(_Player, _Item, _IsLocked);
ItemToInventory(_Item, _Backpack);
*/

PROC
LeaderLib_PresetMenu_RecordTalents((CHARACTERGUID)_Player, (STRING)_ArrayID)
AND
DB_LeaderLib_Talents(_Talent)
AND
CharacterHasTalent(_Player, _Talent, 1)
THEN
LeaderLib_Array_AddToArray(_ArrayID, _Talent);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:RecordTalents] Adding talent [",_Talent,"] to pre-talent array.");

PROC
LeaderLib_PresetMenu_RecordTalents((CHARACTERGUID)_Player, (STRING)_ArrayID)
AND
DB_LeaderLib_QuestTalents(_Talent)
AND
CharacterHasTalent(_Player, _Talent, 1)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:RecordTalents] Adding talent [",_Talent,"] to quest talents to add.");
DB_LeaderLib_PresetMenu_Temp_QuestTalents(_Player, _Talent);

PROC
LeaderLib_PresetMenu_RecordTalents((CHARACTERGUID)_Player, (STRING)_ArrayID)
AND
CharacterGetTalentPoints(_Player, _TalentPoints)
AND
_TalentPoints > 0
THEN
LeaderLog_LogInt("DEBUG", "[LeaderLib:PresetMenu:RecordTalents] Character has [",_TalentPoints,"] talent points.");
DB_LeaderLib_PresetMenu_Temp_TalentPoints(_Player, _TalentPoints);

PROC
LeaderLib_PresetMenu_RecordSkills((CHARACTERGUID)_Player, (STRING)_Preset, (INTEGER)_IsRace)
AND
GetUUID(_Player, _ID)
AND
StringConcatenate("LeaderLib_PresetMenu_", _ID, _DummyID)
AND
LeaderLib_Skills_QRY_CreateTargetDummyAtObject(_DummyID, _Player, "", 2000)
AND
DB_LeaderLib_Skills_TargetDummies(_DummyID, _Dummy)
THEN
DB_LeaderLib_PresetMenu_Temp_CopySkills(_DummyID, _Dummy, _Player, _Preset, _IsRace);
CharacterCloneSkillsTo(_Player, _Dummy, 0);

IF
SkillAdded(_Dummy, _Skill, 1)
AND
DB_LeaderLib_PresetMenu_Temp_CopySkills(_DummyID, _Dummy, _Player, _Preset, _IsRace)
THEN
LeaderLib_PresetMenu_BlockSkillCopying(_Player, _Preset, _Skill, _IsRace);
LeaderLib_PresetMenu_Internal_AddSkill(_Player, _Preset, _Skill, _IsRace);

//Set DB_LeaderLib_PresetMenu_Temp_BlockSkill(_Player, _Skill) to block
PROC
LeaderLib_PresetMenu_BlockSkillCopying((CHARACTERGUID)_Player, (STRING)_Preset, (STRING)_Skill, (INTEGER)_ChangingRace)
THEN
DB_NOOP(1);

PROC
LeaderLib_PresetMenu_Internal_AddSkill((CHARACTERGUID)_Player, (STRING)_Preset, (STRING)_Skill, (INTEGER)_ChangingRace)
AND
NOT DB_LeaderLib_BlockedSkill(_Skill) // Globally blocked
AND
NOT DB_LeaderLib_PresetMenu_Temp_BlockSkill(_Player, _Skill)
AND
NOT LeaderLib_Helper_QRY_IsRaceOrOriginSkill(_Skill)
THEN
DB_LeaderLib_PresetMenu_Temp_Skills(_Player, _Skill);

PROC
LeaderLib_PresetMenu_Internal_AddSkill((CHARACTERGUID)_Player, (STRING)_Preset, (STRING)_Skill, (INTEGER)_ChangingRace)
AND
DB_LeaderLib_PresetMenu_Temp_BlockSkill(_Player, _Skill)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_BlockSkill(_Player, _Skill);

//Only copy racial skills if the race matches
PROC
LeaderLib_PresetMenu_BlockSkillCopying((CHARACTERGUID)_Player, (STRING)_Preset, (STRING)_Skill, 1)
THEN
LeaderLib_PresetMenu_Internal_CheckRaceSkill(_Player, _Preset, _Skill);

PROC
LeaderLib_PresetMenu_Internal_CheckRaceSkill((CHARACTERGUID)_Player, (STRING)_Race, (STRING)_Skill)
AND
DB_LeaderLib_RacialSkills(_RaceTag, _Skill)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag) // The target race matches, so save this skill
THEN
DB_LeaderLib_PresetMenu_Temp_Skills(_Player, _Skill);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset")
AND
NOT DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _, _, _)
THEN
LeaderLog_Log("ERROR", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_ApplyPreset] [ERROR] No preset selected?");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset")
AND
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, _IsRace)
AND
NOT GetVarString(_Player, "LeaderLib_CurrentPreset", _)
AND
GetVarFixedString(_Player, "LeaderLib_CharacterCreationPreset", _CurrentPreset)
THEN
SetVarString(_Player, "LeaderLib_LastAppliedPreset", _CurrentPreset);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset")
AND
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, 0)
AND
GetVarString(_Player, "LeaderLib_CurrentPreset", _CurrentPreset)
THEN
SetVarString(_Player, "LeaderLib_LastAppliedPreset", _CurrentPreset);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset")
AND
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, 0)
THEN
NOT DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, 0);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_ApplyPreset] Applying class preset [",_Preset,"] to player.");
LeaderLib_ApplyCharacterPreset(_Player, _Preset);
SetVarString(_Player, "LeaderLib_CurrentPreset", _Preset);
DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, 0);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset")
AND
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, 1)
THEN
NOT DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, 1);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_ApplyPreset] Applying race preset [",_Preset,"] to player.");
LeaderLib_ApplyRacePreset(_Player, _Preset);
DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, 1);

//When _RaceOrPreset is not 1 or 0 (skip applying)
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset")
AND
DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, _RaceOrPreset)
THEN
DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, _RaceOrPreset);
NOT DB_LeaderLib_PresetMenu_SelectedPreset(_Player, _Preset, _Instance, _RaceOrPreset);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset")
THEN
ProcObjectTimer(_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_ApplyPreset")
AND
DB_LeaderLib_PresetMenu_Temp_CopySkills(_DummyID, _Dummy, _Player, _Preset, _IsRace)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_CopySkills(_DummyID, _Dummy, _Player, _Preset, _IsRace);
LeaderLib_Skills_ResetDummy(_Dummy);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_ApplyPreset] Reset skill dummy (used to copy pre-preset skills).");

//Equipment/Items
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
DB_LeaderLib_PresetMenu_Temp_ItemBackpack(_Player, _Backpack)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_OnPresetApplied] Moving pre-preset items back to player.");
MoveAllItemsTo(_Backpack, _Player, 1, 1, 0);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
THEN
ProcObjectTimer(_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems")
AND
DB_LeaderLib_PresetMenu_Temp_ItemBackpack(_Player, _Backpack)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_ItemBackpack(_Player, _Backpack);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems] Deleting backpack.");
ItemRemove(_Backpack);

QRY
LeaderLib_PresetMenu_Internal_QRY_WeaponsSaved((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, "Weapon", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_NOOP(1);

QRY
LeaderLib_PresetMenu_Internal_QRY_WeaponsSaved((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, "Shield", (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_NOOP(1);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems")
AND
DB_LeaderLib_PresetMenu_Temp_HadSourceCollar(_Player, _Item)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_HadSourceCollar(_Player, _Item);
NOT DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, "Amulet", _Item);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems] Character had a source collar on. Re-equipping and locking.");
CharacterEquipItem(_Player, _Item);
ItemLockUnEquip(_Item, 1);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems")
AND
DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, _Slot, (ITEMGUID)_Item)
AND
_Slot != "Weapon"
AND
_Slot != "Shield"
THEN
NOT DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, _Slot, (ITEMGUID)_Item);
// [BEGIN_NO_OSITOOLS]
CharacterEquipItem(_Player, _Item);
// [END_NO_OSITOOLS]
/* [OSITOOLS_ONLY]
NRD_CharacterEquipItem(_Player, _Item, _Slot, 0, 0, 1, 1);
*/

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems")
AND
GetVarString(_Player, "LeaderLib_LastAppliedPreset", _Preset)
AND
GetVarString(_Player, "LeaderLib_CurrentPreset", _Preset)
AND
LeaderLib_PresetMenu_Internal_QRY_WeaponsSaved(_Player)
THEN
LeaderLib_Helper_UnequipWeapons(_Player);
LeaderLib_PresetMenu_Internal_EquipWeapons(_Player);

PROC
LeaderLib_PresetMenu_Internal_EquipWeapons((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, "Weapon", (ITEMGUID)_Item)
THEN
// [BEGIN_NO_OSITOOLS]
CharacterEquipItem(_Player, _Item);
// [END_NO_OSITOOLS]
/* [OSITOOLS_ONLY]
NRD_CharacterEquipItem(_Player, _Item, "Weapon", 0, 0, 1, 1);
*/

PROC
LeaderLib_PresetMenu_Internal_EquipWeapons((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, "Shield", (ITEMGUID)_Item)
THEN
// [BEGIN_NO_OSITOOLS]
LeaderLib_Timers_StartCharacterItemTimer(_Player, _Item, 250, "LeaderLib_Timers_PresetMenu_EquipShield", "LeaderLib_Commands_EquipItem");
// [END_NO_OSITOOLS]
/* [OSITOOLS_ONLY]
NRD_CharacterEquipItem(_Player, _Item, "Shield", 0, 0, 1, 1);

IF
ItemEquipped(_Item, _Player)
AND
DB_LeaderLib_PresetMenu_Temp_LockedEquipment(_Player, _Item, _IsLocked)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_LockedEquipment(_Player, _Item, _IsLocked);
ItemLockUnEquip(_Item, _IsLocked);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
AND
DB_LeaderLib_PresetMenu_Temp_LockedEquipment(_Player, _Item, _IsLocked)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_LockedEquipment(_Player, _Item, _IsLocked);
*/

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems")
AND
DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, _Slot, (ITEMGUID)_Item)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_Equipment(_Player, _Slot, (ITEMGUID)_Item);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_OnPresetApplied_EquipItems] Equipped previous armor.");

//Skills
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
DB_LeaderLib_PresetMenu_Temp_Skills(_Player, _Skill)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:OnPresetApplied] Adding skill [",_Skill,"] to player.");
CharacterAddSkill(_Player, _Skill);
NOT DB_LeaderLib_PresetMenu_Temp_Skills(_Player, _Skill);

//Talents
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
GetUUID(_Player, _UUID)
AND
StringConcatenate("LeaderLib_Array_PostPresetTalents_", _UUID, _PostArrayID)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_OnPresetApplied] Recording new post-preset talents.");
LeaderLib_PresetMenu_Internal_RecordNewTalents(_Player, _PostArrayID);
DB_LeaderLib_PresetMenu_Temp_PostTalentArray(_Player, _PostArrayID);
ProcObjectTimer(_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents", 250);

PROC
LeaderLib_PresetMenu_Internal_RecordNewTalents((CHARACTERGUID)_Player, (STRING)_PostArrayID)
AND
DB_LeaderLib_Talents(_Talent)
AND
CharacterHasTalent(_Player, _Talent, 1)
THEN
LeaderLib_Array_AddToArray(_PostArrayID, _Talent);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents] Restoring pre-preset talent points.");
LeaderLib_PresetMenu_Internal_RestoreTalents(_Player);

//Race presets/transformations may reset the max SP
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
AND
DB_LeaderLib_PresetMenu_Temp_MaxSourcePoints(_Player, _MaxSP)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_MaxSourcePoints(_Player, _MaxSP);
CharacterOverrideMaxSourcePoints(_Player, _MaxSP);
LeaderLog_LogInt("DEBUG", "[LeaderLib:PresetMenu:LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents] Reset max source points to [",_MaxSP,"].");
//Proc_UnlockSourcePointsAndPowers(_Player, _Host);

/*
PROC
Proc_UnlockSourcePointsAndPowers((CHARACTERGUID)_Companion, (CHARACTERGUID)_Avatar)
THEN
DB_NOOP(1);
*/

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
AND
DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, 1)
AND
DB_Origins_OriginTag(_, _Tag)
AND
IsTagged(_Player, _Tag, 1)
THEN
ClearTag(_Player, _Tag);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
AND
DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, 1)
AND
DB_LeaderLib_PresetMenu_Temp_OriginTags(_Player, _Tag)
THEN
SetTag(_Player, _Tag);

//Fallback
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
AND
DB_Origins_OriginTag(_Player, _Tag)
AND
IsTagged(_Player, _Tag, 0)
THEN
SetTag(_Player, _Tag);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
AND
DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, 1)
AND
DB_LeaderLib_PresetMenu_Temp_OriginTags(_Player, _Tag)
AND
DB_LeaderLib_OriginSkills(_Tag, _Skill)
THEN
CharacterAddSkill(_Player, _Skill, 0);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
AND
DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, 1)
AND
DB_LeaderLib_PresetMenu_Temp_OriginTags(_Player, _Tag)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_OriginTags(_Player, _Tag);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_PresetMenu_OnPresetApplied_RestoreTalents")
AND
DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, _IsRace)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_AppliedPreset(_Player, _Preset, _IsRace);
LeaderLib_PresetApplied(_Player, _Preset, _IsRace);

PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Player, (STRING)_Preset, (INTEGER)_IsRace)
THEN
DB_NOOP(1);

PROC
LeaderLib_PresetMenu_Internal_RestoreTalents((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_TalentPoints(_Player, _TalentPoints)
AND
CharacterGetTalentPoints(_Player, _CurrentTalentPoints)
AND
_CurrentTalentPoints < _TalentPoints
AND
IntegerSubtract(_TalentPoints, _CurrentTalentPoints, _AddPoints)
AND
_AddPoints > 0
THEN
LeaderLog_LogInt("DEBUG", "[LeaderLib:PresetMenu:Internal:RestoreTalents] Added [",_AddPoints,"] talent points.");
CharacterAddTalentPoint(_Player, _AddPoints);
NOT DB_LeaderLib_PresetMenu_Temp_TalentPoints(_Player, _TalentPoints);

PROC
LeaderLib_PresetMenu_Internal_RestoreTalents((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_TalentPoints(_Player, _TalentPoints)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_TalentPoints(_Player, _TalentPoints);

PROC
LeaderLib_PresetMenu_Internal_RestoreTalents((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_QuestTalents(_Player, _Talent)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_QuestTalents(_Player, _Talent);
CharacterAddTalent(_Player, _Talent);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:Internal:RestoreTalents] Added quest talent [",_Talent,"].");

PROC
LeaderLib_PresetMenu_Internal_RestoreTalents((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_PostTalentArray(_Player, _PostArrayID)
AND
DB_LeaderLib_PresetMenu_Temp_TalentArray(_Player, _PreArrayID)
AND
DB_LeaderLib_Array_Length(_PreArrayID, _Total)
AND
DB_LeaderLib_Array_Length(_PostArrayID, _NewTotal)
AND
IntegerSubtract(_Total, _NewTotal, _PointsToAdd)
AND
_PointsToAdd > 0
THEN
LeaderLog_LogInt("DEBUG", "[LeaderLib:PresetMenu:RestoreTalents] Adding [",_PointsToAdd,"] missing talent points to player.");
CharacterAddTalentPoint(_Player, _PointsToAdd);

PROC
LeaderLib_PresetMenu_Internal_RestoreTalents((CHARACTERGUID)_Player)
AND
DB_LeaderLib_PresetMenu_Temp_PostTalentArray(_Player, _PostArrayID)
AND
DB_LeaderLib_PresetMenu_Temp_TalentArray(_Player, _PreArrayID)
THEN
NOT DB_LeaderLib_PresetMenu_Temp_TalentArray(_Player, _PreArrayID);
LeaderLib_Array_ClearArray(_PreArrayID);
NOT DB_LeaderLib_PresetMenu_Temp_PostTalentArray(_Player, _PostArrayID);
LeaderLib_Array_ClearArray(_PostArrayID);
LeaderLog_Log("DEBUG", "[LeaderLib:PresetMenu:RestoreTalents] Cleared arrays [",_PreArrayID,"] and [",_PostArrayID,"].");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
