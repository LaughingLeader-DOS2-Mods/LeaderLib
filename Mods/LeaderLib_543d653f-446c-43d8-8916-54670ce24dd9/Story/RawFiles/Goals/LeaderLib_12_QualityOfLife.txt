Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION COMMANDS
PROC
LeaderLib_Settings_AddStarterSkills((CHARACTERGUID)_Player)
AND
CharacterHasSkill(_Player, "Shout_LeaderLib_ChainAll", 0)
THEN
CharacterAddSkill(_Player, "Shout_LeaderLib_ChainAll", 0);
LeaderLib_Skills_SetSlot(_Player, "Shout_LeaderLib_ChainAll", 27);

PROC
LeaderLib_Settings_AddStarterSkills((CHARACTERGUID)_Player)
AND
CharacterHasSkill(_Player, "Shout_LeaderLib_UnchainAll", 0)
THEN
CharacterAddSkill(_Player, "Shout_LeaderLib_UnchainAll", 0);
LeaderLib_Skills_SetSlot(_Player, "Shout_LeaderLib_UnchainAll", 28);

IF
ObjectFlagSet("LeaderLib_Commands_AddChainSkills", (CHARACTERGUID)_Char, _)
THEN
ObjectClearFlag(_Char, "LeaderLib_Commands_AddChainSkills", 0);
CharacterAddSkill(_Char, "Shout_LeaderLib_ChainAll", 0);
CharacterAddSkill(_Char, "Shout_LeaderLib_UnchainAll", 0);

IF
ObjectFlagSet("LeaderLib_Commands_RemoveChainSkills", (CHARACTERGUID)_Char, _)
THEN
ObjectClearFlag(_Char, "LeaderLib_Commands_RemoveChainSkills", 0);
CharacterRemoveSkill(_Char, "Shout_LeaderLib_ChainAll");
CharacterRemoveSkill(_Char, "Shout_LeaderLib_UnchainAll");
//END_REGION

//REGION CHAIN_UNCHAIN_ALL
PROC
LeaderLib_LifeHacks_ChainToggle((CHARACTERGUID)_Player, (INTEGER)_UserID, 0)
AND
DB_IsPlayer(_OtherPlayer)
AND
CharacterGetReservedUserID(_OtherPlayer, _UserID)
AND
CharactersAreGrouped(_Player, _OtherPlayer, 0)
THEN
CharacterAttachToGroup(_OtherPlayer, _Player);

PROC
LeaderLib_LifeHacks_ChainToggle((CHARACTERGUID)_Player, (INTEGER)_UserID, 1)
AND
DB_IsPlayer(_OtherPlayer)
AND
CharacterGetReservedUserID(_OtherPlayer, _UserID)
AND
CharactersAreGrouped(_Player, _OtherPlayer, 1)
THEN
CharacterDetachFromGroup(_OtherPlayer);

IF
CharacterStatusApplied(_OtherPlayer, "LEADERLIB_CHAIN_ALL", (CHARACTERGUID)_Player)
AND
CharactersAreGrouped(_Player, _OtherPlayer, 0)
AND
CharacterGetReservedUserID(_Player, _UserID)
AND
CharacterGetReservedUserID(_OtherPlayer, _UserID)
THEN
CharacterAttachToGroup(_OtherPlayer, _Player);

IF
CharacterStatusApplied(_OtherPlayer, "LEADERLIB_UNCHAIN_ALL", (CHARACTERGUID)_Player)
AND
CharactersAreGrouped(_Player, _OtherPlayer, 1)
THEN
CharacterDetachFromGroup(_OtherPlayer);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LeaderLib_11__Start"