Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

IF
SkillAdded(_Player, _, _)
AND
_Player != NULL_00000000-0000-0000-0000-000000000000
THEN
ProcObjectTimerCancel(_Player, "LeaderLib_Timers_UpdateSelectedPreset");
ProcObjectTimer(_Player, "LeaderLib_Timers_UpdateSelectedPreset", 50);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LeaderLib_Timers_UpdateSelectedPreset")
//AND
//DB_SelectedCC(_Player, _UserProfile)
THEN
SetStoryEvent(_Player, "LeaderLib_CC_SetPreset");

IF
StoryEvent((CHARACTERGUID)_Player, "LeaderLib_Events_OnPresetSet")
THEN
LeaderLib_CC_SetSelectedPreset(_Player);

PROC
LeaderLib_CC_ClearLastSelectedPreset((CHARACTERGUID)_Player, (INTEGER)_UserID, (STRING)_NextPreset)
AND
NOT DB_LeaderLib_CC_Temp_SelectedPreset(_UserID, _)
THEN
LeaderLib_CC_OnPresetChanged(_Player, "", _NextPreset);

PROC
LeaderLib_CC_ClearLastSelectedPreset((CHARACTERGUID)_Player, (INTEGER)_UserID, (STRING)_NextPreset)
AND
DB_LeaderLib_CC_Temp_SelectedPreset(_UserID, _Preset)
THEN
NOT DB_LeaderLib_CC_Temp_SelectedPreset(_UserID, _Preset);
LeaderLib_CC_OnPresetChanged(_Player, _Preset, _NextPreset);

PROC
LeaderLib_CC_SetSelectedPreset((CHARACTERGUID)_Player)
AND
GetVarFixedString(_Player, "LeaderLib_CharacterCreationPreset", _Preset)
AND
CharacterGetReservedUserID(_Player, _UserID)
THEN
LeaderLib_CC_ClearLastSelectedPreset(_Player, _UserID, _Preset);
DB_LeaderLib_CC_Temp_SelectedPreset(_UserID, _Preset);

PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, (STRING)_LastPreset, (STRING)_Preset)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:CC:LeaderLib_CC_OnPresetChanged] Player's preset changed from [",_LastPreset,"] to [",_Preset,"] in Character Creation.");

PROC
LeaderLib_CC_SetSelectedPreset((CHARACTERGUID)_Player)
AND
NOT GetVarFixedString(_Player, "LeaderLib_CharacterCreationPreset", _)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:CC:LeaderLib_Events_OnPresetSet] [ERROR] Variable [LeaderLib_CharacterCreationPreset] is null!");

IF
RegionStarted(_Level)
AND
IsGameLevel(_Level, 1)
THEN
GoalCompleted;

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LeaderLib_13_00_CharacterCreation"