Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION WAYPOINT_SHRINE_TIMER
QRY
LeaderLib_Waypoints_QRY_PlayerMissingShrine((STRING)_WaypointID)
AND
DB_IsPlayer(_Player)
AND
NOT DB_WaypointUnlocked(_WaypointID,_Player)
THEN
DB_NOOP(1);

IF
StoryEvent((ITEMGUID)_Shrine, "CanActivateWaypointTimer")
AND
NOT DB_LeaderLib_ScriptOverrides_Temp_WaypointShrines(_, _Shrine, _)
AND
DB_WaypointInfo(_Shrine,(TRIGGERGUID)_TeleportTrigger,(STRING)_WaypointID)
AND
GetRegion(_Shrine, _Region)
THEN
DB_LeaderLib_ScriptOverrides_Temp_WaypointShrines(_Region, _Shrine, _WaypointID);

IF
StoryEvent((ITEMGUID)_Shrine, "CanActivateWaypointTimer")
AND
DB_LeaderLib_ScriptOverrides_Temp_WaypointShrines(_Region, _Shrine, _WaypointID)
AND
LeaderLib_Waypoints_QRY_PlayerMissingShrine(_WaypointID)
THEN
DB_LeaderLib_ScriptOverrides_Temp_ActiveWaypointTimers(_Shrine);
SetStoryEvent(_Shrine, "WaypointActivateTimer");

IF
StoryEvent((ITEMGUID)_Shrine, "CanActivateWaypointTimer")
AND
NOT DB_LeaderLib_ScriptOverrides_Temp_ActiveWaypointTimers(_Shrine)
THEN
SetStoryEvent(_Shrine, "WaypointDeactivateTimer");

IF
StoryEvent((ITEMGUID)_Shrine, "OnWaypointDeactivated")
THEN
DB_LeaderLib_ScriptOverrides_Temp_ActiveWaypointTimers(_Shrine);

// Deactivating shrine timers when all party members have unlocked it
PROC
PROC_UnlockWaypoint_RecordDB((STRING)_WaypointID, (CHARACTERGUID)_Player)
AND
NOT LeaderLib_Waypoints_QRY_PlayerMissingShrine(_WaypointID)
AND
DB_WaypointInfo(_Shrine,(TRIGGERGUID)_TeleportTrigger,(STRING)_WaypointID)
THEN
NOT DB_LeaderLib_ScriptOverrides_Temp_ActiveWaypointTimers(_Shrine);
SetStoryEvent(_Shrine, "WaypointDeactivateTimer");

// Re-activating "completed" shrines when a new party member joins
IF
CharacterJoinedParty(_Player)
AND
CharacterIsSummon(_Player, 0)
AND
CharacterIsPartyFollower(_Player, 0)
AND
DB_LeaderLib_ScriptOverrides_Temp_WaypointShrines(_Region, _Shrine, _WaypointID)
AND
NOT DB_LeaderLib_ScriptOverrides_Temp_ActiveWaypointTimers(_Shrine)
AND
NOT DB_WaypointUnlocked(_WaypointID, _Player)
THEN
DB_LeaderLib_ScriptOverrides_Temp_ActiveWaypointTimers(_Shrine);
SetStoryEvent(_Shrine, "WaypointActivateTimer");

// Cleanup
IF
RegionEnded(_Region)
THEN
SysClear("DB_LeaderLib_ScriptOverrides_Temp_ActiveWaypointTimers", 1);
SysClear("DB_LeaderLib_ScriptOverrides_Temp_WaypointShrines", 3);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LeaderLib_11__Start"