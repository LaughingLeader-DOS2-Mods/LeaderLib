Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION LEVEL_UP_EVENTS
IF
CharacterLeveledUp(_Character)
AND
LeaderLib_Helper_QRY_IsGameLevel()
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Character)
AND
LeaderLib_Helper_QRY_CharacterIsPlayerOrGM(_Character)
AND
LeaderLib_Growth_Items_QRY_HasAutoLevelItem(_Character)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Items:CharacterLeveledUp] Leveling up character equipment.");
LeaderLib_Growth_Items_LevelUpItems(_Character);

IF
ItemEquipped(_Item, _Character)
AND
LeaderLib_Helper_QRY_IsGameLevel()
AND
IsTagged(_Item, "LeaderLib_AutoLevel", 1)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Character)
THEN
LeaderLib_Growth_Items_LevelUpItem(_Character, _Item);

IF
ObjectWasTagged(_Item, "LeaderLib_AutoLevel")
AND
ObjectIsItem((ITEMGUID)_Item, 1)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
GetInventoryOwner(_Item, _Character)
AND
ObjectIsCharacter((CHARACTERGUID)_Character, 1)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Character)
THEN
LeaderLib_Growth_Items_LevelUpItem(_Character, _Item);

/*
IF
ItemAddedToCharacter(_Item, _Character)
AND
IsTagged(_Item, "LeaderLib_AutoLevel", 1)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Character)
THEN
LeaderLib_Growth_Items_LevelUpItem(_Character, _Item);
*/

//Transformed items
IF
ItemTemplateCombinedWithItemTemplate(_,_,_,_,_,_Player, _Item)
AND
IsTagged(_Item, "LeaderLib_AutoLevel", 1)
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth:Items:ItemTemplateCombinedWithItemTemplate] Combination item is set to auto-level. Checking level.");
CharacterItemSetEvent(_Player, _Item, "LeaderLib_Commands_CheckItemLevel");
//END_REGION

//REGION TRANSFORMATION
IF
ObjectTransformed(_Item, _Template)
AND
LeaderLib_Helper_QRY_IsGameLevel()
AND
ObjectIsItem((ITEMGUID)_Item, 1)
AND
IsTagged(_Item, "LeaderLib_AutoLevel", 1)
AND
String(_Template, _TemplateStr)
THEN
LeaderLib_Growth_Items_OnItemTransformed(_Item, _TemplateStr);
//END_REGION

//REGION DESTRUCTION_EVENTS
IF
ItemUnEquipped(_Item, _Character)
AND
IsTagged(_Item, "LeaderLib_RemoveAfterUnequip", 1)
AND
ItemIsDestroyed(_Item, 0)
AND
ItemIsStoryItem(_Item, 0)
THEN
ItemRemove(_Item);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LeaderLib_19_ToggledScripts"