Version 1
SubGoalCombiner SGC_AND
INITSECTION
LeaderLib_ToggleScripts_Internal_InitRegistration();

KBSECTION

//REGION INIT
PROC
LeaderLib_ToggleScripts_Internal_InitRegistration()
THEN
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_AutoPartyExperience", "LeaderLib_AutoBalancePartyExperience");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_StatusEvents__Main", "LeaderLib_StatusEvents_MainScriptEnabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_StatusEvents_Disables", "LeaderLib_StatusEvents_DisableScriptEnabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_StatusEvents_ToggleSkill", "LeaderLib_StatusEvents_ToggleSkillsEnabled");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Combat_DeadCharacters", "LeaderLib_Combat_TrackDeadCharacters");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Combat_DeadItems", "LeaderLib_Combat_TrackDeadItems");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Listeners_RemoveHarmfulStatuses", "LeaderLib_TS_RemoveHarmfulStatusesEnabled");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Tags_ReapplyEvents", "LeaderLib_TS_TagReapplyEventsEnabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Tags_AutoPreserve", "LeaderLib_TS_AutoTagPreserverEnabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_RuneBlocker", "LeaderLib_TS_RuneBlockingEnabled");

//Treasure/trader events
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_TreasureEvents_Dialog", "LeaderLib_TS_TreasureEvents_Dialog_Enabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_TreasureEvents_Flags", "LeaderLib_TS_TreasureEvents_Flags_Enabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_TreasureEvents_PartyLeveledUp", "LeaderLib_TS_TreasureEvents_PartyLeveledUp_Enabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_TreasureEvents_UsedItem", "LeaderLib_TS_TreasureEvents_UsedItem_Enabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_TreasureEvents_TradeGeneration", "LeaderLib_TS_TreasureEvents_TradeGeneration_Enabled");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_GrowthEvents_Character", "LeaderLib_TS_GrowthEvents_Character_Enabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_GrowthEvents_Item", "LeaderLib_TS_GrowthEvents_Item_Enabled");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Follower_TeleportEvents", "LeaderLib_Follower_Active");

LeaderLib_ToggleScripts_Register_Script("__AAA_Z_LeaderLib_TS_Top_DummyCrimes", "LeaderLib_DummyCrimesEnabled");
LeaderLib_ToggleScripts_Register_Script("__AAA_Z_LeaderLib_TS_Top_DialogOverriding", "LeaderLib_RegularDialogOverridingEnabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_DialogOverriding_AutomatedDialog", "LeaderLib_AutomatedDialogOverridingEnabled");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_ExplodeFix", "LeaderLib_ExplodeFix");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_ForceEvents", "LeaderLib_Force_EnableEvents");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Debug", "LeaderLib_IsEditorMode");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Autosaving", "LeaderLib_AutosavingEnabled");

LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_SummonEvents_Character", "LeaderLib_SummonEvents_Character_Enabled");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_SummonEvents_Item", "LeaderLib_SummonEvents_Item_Enabled");
//LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_GrowthEvents_Item_Enabled", "LeaderLib");
//END_REGION

//REGION UPDATE_SCRIPT_DISABLE
PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
SysCount("DB_LeaderLib_Growth_Characters_Skills", 5, 0)
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LeaderLib_TS_GrowthEvents_Character_Enabled", "LeaderLib", 1000);

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_GrowthEvents_Character_Enabled", "LeaderLib")
AND
SysCount("DB_LeaderLib_Growth_Items_TemplateID", 2, 0)
AND
SysCount("DB_LeaderLib_Growth_Items_Transformations", 4, 0)
AND
SysCount("DB_LeaderLib_Growth_Items_DeltaMods", 4, 0)
AND
CharacterGetHostCharacter(_Player)
AND
NOT PartyFindTaggedItem(_Player, "LeaderLib_AutoLevel", 0, _)
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LeaderLib_TS_GrowthEvents_Item_Enabled", "LeaderLib", 1000);
//END_REGION

//REGION UPDATE_SCRIPT_ENABLE
PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_Follower_Active", "LeaderLib")
AND
SysCount("DB_LeaderLib_PartyFollower", 2, _Count)
AND
_Count > 0
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_Follower_Active", "LeaderLib");

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_StatusEvents_ToggleSkillsEnabled", "LeaderLib")
AND
SysCount("DB_LeaderLib_Skills_StatusToggleSkills", 5, _Count)
AND
_Count > 0
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_StatusEvents_ToggleSkillsEnabled", "LeaderLib");

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_TreasureEvents_Dialog_Enabled", "LeaderLib")
AND
DB_LeaderLib_Treasure_RegisteredGenerationTypes("DIALOG_STARTED")
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_TreasureEvents_Dialog_Enabled", "LeaderLib");

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_TreasureEvents_PartyLeveledUp_Enabled", "LeaderLib")
AND
DB_LeaderLib_Treasure_RegisteredGenerationTypes("PARTY_LEVELED_UP")
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_TreasureEvents_PartyLeveledUp_Enabled", "LeaderLib");

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_TreasureEvents_Flags_Enabled", "LeaderLib")
AND
DB_LeaderLib_Treasure_RegisteredGenerationTypes("FLAG")
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_TreasureEvents_Flags_Enabled", "LeaderLib");

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_TreasureEvents_UsedItem_Enabled", "LeaderLib")
AND
DB_LeaderLib_Treasure_RegisteredGenerationTypes("OPENED")
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_TreasureEvents_UsedItem_Enabled", "LeaderLib");

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_TreasureEvents_TradeGeneration_Enabled", "LeaderLib")
AND
DB_LeaderLib_Treasure_RegisteredGenerationTypes("TRADE_GENERATION_START")
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_TreasureEvents_TradeGeneration_Enabled", "LeaderLib");

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_GrowthEvents_Character_Enabled", "LeaderLib")
AND
SysCount("DB_LeaderLib_Growth_Characters_Skills", 5, _Count)
AND
_Count > 0
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_GrowthEvents_Character_Enabled", "LeaderLib");

PROC
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags()
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_TS_GrowthEvents_Item_Enabled", "LeaderLib")
AND
CharacterGetHostCharacter(_Player)
AND
PartyFindTaggedItem(_Player, "LeaderLib_AutoLevel", 0, _Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_TS_GrowthEvents_Item_Enabled", "LeaderLib");
//END_REGION

//REGION UPDATING_SCRIPT_ENABLE_AUTO
PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
THEN
LeaderLib_ToggleScripts_Internal_InitRegistration();
LeaderLib_ToggleScripts_Internal_UpdateScriptFlags();

PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag)
AND
GlobalGetFlag(_GlobalFlag, 1)
AND
NOT SysIsActive(_GoalTitle)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:LeaderUpdater_ModUpdated] Retroactively activating toggle script [",_GoalTitle,"] (",_GlobalFlag,").");
SysActivateGoal(_GoalTitle);
//END_REGION

//REGION UPDATING
PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1, 1, 11, 2)
THEN
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_Listeners_RemoveHarmfulStatuses", "LeaderLib_TS_RemoveHarmfulStatusesEnabled");

PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1, 1, 13, 0)
THEN
LeaderLib_Queue_ClearQueue("LeaderLib_ItemGenerationQueue");

PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1, 2, 5, 0)
THEN
LeaderLib_ToggleScripts_Register_Script("__AAA_Z_LeaderLib_TS_Top_DummyCrimes", "LeaderLib_DummyCrimesEnabled");

PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1,2,9,1)
THEN
LeaderLog_Log("SYSTEM", "[LeaderLib:ToggledScripts] Adding [LeaderLib_19_TS_ForceEvents] toggle script from version [",_PastVersion,"].");
LeaderLib_ToggleScripts_Register_Script("LeaderLib_19_TS_ForceEvents", "LeaderLib_Force_EnableEvents");

PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1,2,9,1)
AND
DB_LeaderLib_ToggleScripts_Registered("__AAA_Z_LeaderLib_TS_Top_DummyCrimes", _GlobalFlag)
AND
GlobalGetFlag(_GlobalFlag, 1)
THEN
SysActivateGoal("__AAA_Z_LeaderLib_TS_Top_DummyCrimes");

PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1,4,0,0)
AND
DB_LeaderLib_ToggleScripts_Registered("LeaderLib_19_TS_Top_DummyCrimes", _GlobalFlag)
THEN
NOT DB_LeaderLib_ToggleScripts_Registered("LeaderLib_19_TS_Top_DummyCrimes", _GlobalFlag);
DB_LeaderLib_ToggleScripts_Registered("__AAA_Z_LeaderLib_TS_Top_DummyCrimes", _GlobalFlag);
//END_REGION

//REGION REGISTERING
PROC
LeaderLib_ToggleScripts_Register_Script((STRING)_GoalTitle, (STRING)_GlobalFlag)
AND
NOT DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag)
THEN
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag);
LeaderLib_ToggleScripts_OnScriptRegistered(_GoalTitle, _GlobalFlag, -1);

PROC
LeaderLib_ToggleScripts_Register_ObjectScript((STRING)_GoalTitle, (STRING)_TagOrFlag, (INTEGER)_IsTag)
THEN
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _TagOrFlag, _IsTag);
LeaderLib_ToggleScripts_OnScriptRegistered(_GoalTitle, _TagOrFlag, _IsTag);

//Support for mods that may have an older definition
PROC
LeaderLib_ToggleScripts_OnScriptRegistered((STRING)_GoalTitle, (STRING)_GlobalFlag)
THEN
LeaderLib_ToggleScripts_OnScriptRegistered(_GoalTitle, _GlobalFlag, -1);

PROC
LeaderLib_ToggleScripts_OnScriptRegistered((STRING)_GoalTitle, (STRING)_GlobalFlag, -1)
AND
DB_GlobalFlag(_GlobalFlag)
AND
NOT SysIsActive(_GoalTitle)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:OnScriptRegistered] Activating goal [",_GoalTitle,"] (",_GlobalFlag,").");
SysActivateGoal(_GoalTitle);

PROC
//LeaderLib_ToggleScripts_OnScriptRegistered((STRING)_GoalTitle, (STRING)_TagOrFlag, (INTEGER)_IsTag)
LeaderLib_ToggleScripts_OnScriptRegistered((STRING)_GoalTitle, (STRING)_TagOrFlag, 0)
AND
DB_IsPlayer(_Char)
AND
NOT SysIsActive(_GoalTitle)
AND
PartyGetFlag(_Char, _TagOrFlag, 1)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:OnScriptRegistered] Activating goal [",_GoalTitle,"] (",_TagOrFlag,").");
SysActivateGoal(_GoalTitle);

PROC
LeaderLib_ToggleScripts_OnScriptRegistered((STRING)_GoalTitle, (STRING)_TagOrFlag, 1)
AND
DB_IsPlayer(_Char)
AND
IsTagged(_Char, _TagOrFlag, 1)
AND
NOT SysIsActive(_GoalTitle)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:OnScriptRegistered] Activating goal [",_GoalTitle,"] (",_TagOrFlag,").");
SysActivateGoal(_GoalTitle);
//END_REGION

//REGION CLEARING
PROC
LeaderLib_ToggleScripts_Clear_ByGoal((STRING)_GoalTitle)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag)
THEN
NOT DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag);
LeaderLib_ToggleScripts_OnGoalCleared(_GoalTitle, _GlobalFlag);

PROC
LeaderLib_ToggleScripts_Clear_ByFlag((STRING)_GlobalFlag)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag)
THEN
NOT DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag);
LeaderLib_ToggleScripts_OnGoalCleared(_GoalTitle, _GlobalFlag);

PROC
LeaderLib_ToggleScripts_Clear_ByFlagOrTag((STRING)_TagOrFlag)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _TagOrFlag, _IsTag)
THEN
NOT DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _TagOrFlag, _IsTag);
LeaderLib_ToggleScripts_OnGoalCleared(_GoalTitle, _TagOrFlag);

PROC
LeaderLib_ToggleScripts_OnGoalCleared((STRING)_GoalTitle, (STRING)_TagOrFlag)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:OnGoalCleared] Completing goal [",_GoalTitle,"] (",_TagOrFlag,").");
SysCompleteGoal(_GoalTitle);
//END_REGION

QRY
LeaderLib_ToggleScripts_QRY_ScriptIsEnabled((STRING)_GlobalFlag)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_GlobalFlag, _EnablingModID)
THEN
DB_NOOP(1);

QRY
LeaderLib_ToggleScripts_QRY_ScriptIsEnabled((STRING)_TagOrFlag)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_Object, _TagOrFlag, _EnablingModID)
THEN
DB_NOOP(1);

//REGION ENABLING_DISABLING
PROC
LeaderLib_ToggleScripts_EnableScript((STRING)_GlobalFlag, (STRING)_EnablingModID)
AND
NOT DB_GlobalFlag(_GlobalFlag)
THEN
GlobalSetFlag(_GlobalFlag);

PROC
LeaderLib_ToggleScripts_EnableScript((STRING)_GlobalFlag, (STRING)_EnablingModID)
THEN
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_GlobalFlag, _EnablingModID);

PROC
LeaderLib_ToggleScripts_EnableScript((STRING)_GlobalFlag, (STRING)_ModID)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_GlobalFlag, _ModID)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_GlobalFlag, _ModID);
LeaderLib_ToggleScripts_Internal_OnCanceledDeactivation(_GlobalFlag, _ModID);
LeaderLog_Log("DEBUG", "[LeaderLib_19_ToggledScripts:EnableScript] Canceled deactivation of script [",_GlobalFlag,"] for mod [",_ModID,"].");

PROC
LeaderLib_ToggleScripts_DisableScript((STRING)_GlobalFlag, (STRING)_EnablingModID)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_GlobalFlag, _EnablingModID)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_GlobalFlag, _EnablingModID);

//Only clear the flag if no other mods are using this script
PROC
LeaderLib_ToggleScripts_DisableScript((STRING)_GlobalFlag, (STRING)_EnablingModID)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_GlobalFlag, _)
THEN
GlobalClearFlag(_GlobalFlag);

//Add to the "Mod is disabling a script" DB.
PROC
LeaderLib_ToggleScripts_DisableScriptAfterDelay((STRING)_GlobalFlag, (STRING)_ModID, (INTEGER)_Delay)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_GlobalFlag, _ModID)
THEN
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_GlobalFlag, _ModID);
LeaderLog_Log("DEBUG", "[LeaderLib_19_ToggledScripts:DisableScriptAfterDelay] Mod [",_ModID,"] is disabling script [",_GlobalFlag,"] after a delay.");

//Create the timer.
PROC
LeaderLib_ToggleScripts_DisableScriptAfterDelay((STRING)_GlobalFlag, (STRING)_ModID, (INTEGER)_Delay)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_, _GlobalFlag)
AND
StringConcatenate("LeaderLib_Timers_ToggleScripts_DeactivateScript_", _GlobalFlag, _TimerName)
THEN
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_TimerName, _GlobalFlag);

//Start/restart the timer
PROC
LeaderLib_ToggleScripts_DisableScriptAfterDelay((STRING)_GlobalFlag, (STRING)_ModID, (INTEGER)_Delay)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_TimerName, _GlobalFlag)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Delay);

//No mods left trying to deactivate this script, so cancel the timer.
PROC
LeaderLib_ToggleScripts_Internal_OnCanceledDeactivation((STRING)_GlobalFlag, (STRING)_ModID)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_GlobalFlag, _)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_TimerName, _GlobalFlag)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_TimerName, _GlobalFlag);
TimerCancel(_TimerName);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_TimerName, _GlobalFlag)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_GlobalFlag, _ModID)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_GlobalFlag, _ModID);
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_TimerName, _GlobalFlag);
LeaderLib_ToggleScripts_DisableScript(_GlobalFlag, _ModID);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_TimerName, _GlobalFlag)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptTimer(_TimerName, _GlobalFlag);
//END_REGION

//REGION LEADERLIB_ENABLE_SCRIPT
IF
GlobalFlagSet("LeaderLib_AutoBalancePartyExperience")
AND
DB_DialogName("LeaderLib_SettingsMenu", _)
THEN
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_AutoBalancePartyExperience", "LeaderLib");

IF
GlobalFlagCleared("LeaderLib_AutoBalancePartyExperience")
AND
DB_DialogName("LeaderLib_SettingsMenu", _)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_AutoBalancePartyExperience", "LeaderLib");
//END_REGION

//REGION STATUS_EVENTS_MAIN
PROC
LeaderLib_ToggleScripts_DisableAllStatusEvents()
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_StatusEvents_DisableScriptEnabled", _EnablingModID)
THEN
LeaderLib_ToggleScripts_DisableScript("LeaderLib_StatusEvents_DisableScriptEnabled", _EnablingModID);

PROC
LeaderLib_ToggleScripts_DisableAllStatusEvents()
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_StatusEvents_MainScriptEnabled", _EnablingModID)
THEN
LeaderLib_ToggleScripts_DisableScript("LeaderLib_StatusEvents_MainScriptEnabled", _EnablingModID);

PROC
LeaderLib_ToggleScripts_DisableAllStatusEvents((STRING)_EnablingModID)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_StatusEvents_DisableScriptEnabled", _EnablingModID)
THEN
LeaderLib_ToggleScripts_DisableScript("LeaderLib_StatusEvents_DisableScriptEnabled", _EnablingModID);

PROC
LeaderLib_ToggleScripts_DisableAllStatusEvents((STRING)_EnablingModID)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_StatusEvents_MainScriptEnabled", _EnablingModID)
THEN
LeaderLib_ToggleScripts_DisableScript("LeaderLib_StatusEvents_MainScriptEnabled", _EnablingModID);

PROC
LeaderLib_ToggleScripts_EnableScript("LeaderLib_StatusEvents_DisableScriptEnabled", (STRING)_EnablingModID)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts("LeaderLib_StatusEvents_MainScriptEnabled", _EnablingModID)
THEN
LeaderLib_ToggleScripts_EnableScript("LeaderLib_StatusEvents_MainScriptEnabled", _EnablingModID);
//END_REGION

//REGION TOGGLING_ON_INTERNAL
IF
GlobalFlagSet(_GlobalFlag)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag)
AND
NOT SysIsActive(_GoalTitle)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:GlobalFlagSet] Global flag [",_GlobalFlag,"] was set. Activating goal [",_GoalTitle,"].");
SysActivateGoal(_GoalTitle);

PROC
LeaderLib_ToggleScripts_Internal_ToggleOn((STRING)_TagOrFlag)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _TagOrFlag, _IsTag)
AND
NOT SysIsActive(_GoalTitle)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:ToggleOn] Activating goal [",_GoalTitle,"] (",_TagOrFlag,").");
SysActivateGoal(_GoalTitle);
//END_REGION

//REGION TOGGLING_OFF_INTERNAL
IF
GlobalFlagCleared(_GlobalFlag)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag)
AND
SysIsActive(_GoalTitle)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:GlobalFlagCleared] Global flag [",_GlobalFlag,"] was cleared. Deactivating goal [",_GoalTitle,"].");
SysCompleteGoal(_GoalTitle);

PROC
LeaderLib_ToggleScripts_Internal_ToggleOff((STRING)_TagOrFlag)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _TagOrFlag, _IsTag)
AND
SysIsActive(_GoalTitle)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:ToggleOff] Deactivating goal [",_GoalTitle,"] (",_TagOrFlag,").");
SysCompleteGoal(_GoalTitle);
//END_REGION

//REGION POST_TOGGLE
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _GlobalFlag)
AND
GlobalGetFlag(_GlobalFlag, _FlagIsSet)
THEN
LeaderLib_ToggleScripts_Internal_PostToggle(_GlobalFlag, _GoalTitle, _FlagIsSet);

PROC
LeaderLib_ToggleScripts_Internal_PostToggle((STRING)_GlobalFlag, (STRING)_GoalTitle, 1)
AND
NOT SysIsActive(_GoalTitle)
THEN
SysActivateGoal(_GoalTitle);
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:LeaderLib_Initialized] Global flag [",_GlobalFlag,"] is set. Activating goal [",_GoalTitle,"].");

PROC
LeaderLib_ToggleScripts_Internal_PostToggle((STRING)_GlobalFlag, (STRING)_GoalTitle, 0)
AND
SysIsActive(_GoalTitle)
THEN
SysCompleteGoal(_GoalTitle);
LeaderLog_Log("DEBUG", "[LeaderLib:ToggleScripts:LeaderLib_Initialized] Global flag [",_GlobalFlag,"] is not set. Deactivating goal [",_GoalTitle,"].");
//END_REGION

//REGION TREASURE_SCRIPTS_AUTO_ENABLE
IF
DB_LeaderLib_Treasure_RegisteredGenerationTypes("DIALOG_STARTED")
THEN
GlobalSetFlag("LeaderLib_TS_TreasureEvents_Dialog_Enabled");

IF
DB_LeaderLib_Treasure_RegisteredGenerationTypes("PARTY_LEVELED_UP")
THEN
GlobalSetFlag("LeaderLib_TS_TreasureEvents_PartyLeveledUp_Enabled");

IF
DB_LeaderLib_Treasure_RegisteredGenerationTypes("FLAG")
THEN
GlobalSetFlag("LeaderLib_TS_TreasureEvents_Flags_Enabled");

IF
DB_LeaderLib_Treasure_RegisteredGenerationTypes("OPENED")
THEN
GlobalSetFlag("LeaderLib_TS_TreasureEvents_UsedItem_Enabled");

IF
DB_LeaderLib_Treasure_RegisteredGenerationTypes("TRADE_GENERATION_START")
THEN
GlobalSetFlag("LeaderLib_TS_TreasureEvents_TradeGeneration_Enabled");
//END_REGION

//REGION ENABLING_DISABLING_WITH_OBJECT
PROC
LeaderLib_ToggleScripts_EnableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Object, _TagOrFlag, _EnablingModID, 1);

PROC
LeaderLib_ToggleScripts_EnableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, (INTEGER)_AddTagOrFlag)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _TagOrFlag, _IsTag)
THEN
LeaderLib_ToggleScripts_Internal_EnableScriptForObject(_Object, _TagOrFlag, _EnablingModID, _AddTagOrFlag, _IsTag);

PROC
LeaderLib_ToggleScripts_Internal_EnableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, 1, 1)
THEN
LeaderLib_Tags_PreserveTag(_Object, _TagOrFlag);
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_Object, _TagOrFlag, _EnablingModID);
LeaderLib_ToggleScripts_EnableScriptForObject_OnActivated(_Object, _TagOrFlag, _EnablingModID);

PROC
LeaderLib_ToggleScripts_Internal_EnableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, 0, 1)
AND
IsTagged(_Object, _TagOrFlag, 1)
THEN
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_Object, _TagOrFlag, _EnablingModID);
LeaderLib_ToggleScripts_EnableScriptForObject_OnActivated(_Object, _TagOrFlag, _EnablingModID);

PROC
LeaderLib_ToggleScripts_Internal_EnableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, 1, 0)
THEN
ObjectSetFlag(_Object, _TagOrFlag, 0);
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_Object, _TagOrFlag, _EnablingModID);
LeaderLib_ToggleScripts_EnableScriptForObject_OnActivated(_Object, _TagOrFlag, _EnablingModID);

PROC
LeaderLib_ToggleScripts_Internal_EnableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, 0, 0)
AND
ObjectGetFlag(_Object, _TagOrFlag, 1)
THEN
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_Object, _TagOrFlag, _EnablingModID);
LeaderLib_ToggleScripts_EnableScriptForObject_OnActivated(_Object, _TagOrFlag, _EnablingModID);

PROC
LeaderLib_ToggleScripts_EnableScriptForObject_OnActivated((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib_19_ToggledScriptsEnableScriptForObject:OnActivated] Toggling script [",_TagOrFlag,"] on for object/mod [",_EnablingModID,"].");
LeaderLib_ToggleScripts_Internal_ToggleOn(_TagOrFlag);

PROC
LeaderLib_ToggleScripts_EnableScriptForObject_OnActivated((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_TagOrFlag, _ModID)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByMod(_TagOrFlag, _ModID);
LeaderLib_ToggleScripts_Internal_OnCanceledDeactivation(_TagOrFlag, _ModID);
LeaderLog_Log("DEBUG", "[LeaderLib_19_ToggledScripts:OnActivated] Canceled deactivation of script [",_TagOrFlag,"] for mod [",_ModID,"].");

PROC
LeaderLib_ToggleScripts_DisableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Object, _TagOrFlag, _EnablingModID, 1);

PROC
LeaderLib_ToggleScripts_DisableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, (INTEGER)_RemoveTagOrFlag)
AND
DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_Object, _TagOrFlag, _EnablingModID)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_Object, _TagOrFlag, _EnablingModID);

PROC
LeaderLib_ToggleScripts_DisableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, 1)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _TagOrFlag, 0)
THEN
ObjectClearFlag(_Object, _TagOrFlag, 0);

PROC
LeaderLib_ToggleScripts_DisableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, 1)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _TagOrFlag, 1)
THEN
LeaderLib_Tags_ClearPreservedTag(_Object, _TagOrFlag);

PROC
LeaderLib_ToggleScripts_DisableScriptForObject((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_EnablingModID, (INTEGER)_RemoveTagOrFlag)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_ActiveScripts(_, _TagOrFlag, _)
THEN
LeaderLib_ToggleScripts_Internal_ToggleOff(_TagOrFlag);

//Add to the "Mod is disabling a script" DB.
PROC
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_ModID, (INTEGER)_Delay)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObject(_Object, _TagOrFlag, _)
THEN
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObject(_Object, _TagOrFlag, _ModID);
LeaderLog_Log("DEBUG", "[LeaderLib_19_ToggledScripts:DisableScriptAfterDelay] Mod [",_ModID,"] is disabling script [",_TagOrFlag,"] after a delay for object.");

//Create the timer.
PROC
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_ModID, (INTEGER)_Delay)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_, _, _TagOrFlag)
AND
StringConcatenate("LeaderLib_Timers_ToggleScripts_DeactivateScript_", _TagOrFlag, _TimerName)
THEN
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_Object, _TimerName, _TagOrFlag);

//Start/restart the timer
PROC
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay((GUIDSTRING)_Object, (STRING)_TagOrFlag, (STRING)_ModID, (INTEGER)_Delay)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_Object, _TimerName, _TagOrFlag)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _Delay);

//No mods left trying to deactivate this script, so cancel the timer.
PROC
LeaderLib_ToggleScripts_Internal_OnCanceledDeactivation((STRING)_TagOrFlag, (STRING)_ModID)
AND
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObject(_, _TagOrFlag, _ModID)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_Object, _TimerName, _TagOrFlag)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_Object, _TimerName, _TagOrFlag);
TimerCancel(_TimerName);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_Object, _TimerName, _TagOrFlag)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObject(_Object, _TagOrFlag, _ModID)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_Object, _TimerName, _TagOrFlag);
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObject(_Object, _TagOrFlag, _ModID);
LeaderLib_ToggleScripts_DisableScriptForObject(_Object, _TagOrFlag, _ModID);

IF
TimerFinished(_TimerName)
AND
DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_Object, _TimerName, _TagOrFlag)
THEN
NOT DB_LeaderLib_ToggleScripts_Temp_DeactivateScriptByObjectTimer(_Object, _TimerName, _TagOrFlag);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"