Version 1
SubGoalCombiner SGC_AND
INITSECTION
LeaderLib_GameScripts_InitSettings();
KBSECTION
PROC
LeaderLib_GameScripts_InitSettings()
THEN
LeaderLib_GameScripts_RegisterScript("LeaderLib.GameScripts", "LeaderLib_21_GS_RequirementEvents");
LeaderLib_GameScripts_RegisterScript("LeaderLib.GameScripts", "LeaderLib_21_GS_Statuses");

//REGION REGISTERING
PROC
LeaderLib_GameScripts_RegisterScript((STRING)_Group, (STRING)_GoalName)
THEN
DB_LeaderLib_GameScripts_Registered(_Group, _GoalName);

// Makes a toggle script only active if the game is started.
PROC
LeaderLib_GameScripts_RegisterToggleScript((STRING)_GoalName, (STRING)_ScriptFlag)
THEN
DB_LeaderLib_GameScripts_RegisteredToggleScripts(_GoalName, _ScriptFlag);

PROC
LeaderLib_GameScripts_ClearScriptsForGroup((STRING)_Group)
AND
DB_LeaderLib_GameScripts_Registered(_Group, _GoalName)
THEN
NOT DB_LeaderLib_GameScripts_Registered(_Group, _GoalName);
//END_REGION

//REGION SCRIPT_TOGGLING
IF
RegionEnded(_Region)
AND
DB_LeaderLib_GameScripts_Registered(_Group, _GoalName)
AND
SysIsActive(_GoalName)
THEN
SysCompleteGoal(_GoalName);

IF
RegionEnded(_Region)
AND
DB_LeaderLib_GameScripts_RegisteredToggleScripts(_Group, _GoalName)
AND
SysIsActive(_GoalName)
THEN
SysCompleteGoal(_GoalName);

IF
SavegameLoading(_,_,_,_)
AND
DB_LeaderLib_GameScripts_Registered(_Group, _GoalName)
AND
SysIsActive(_GoalName)
THEN
SysCompleteGoal(_GoalName);

IF
SavegameLoading(_,_,_,_)
AND
DB_LeaderLib_GameScripts_RegisteredToggleScripts(_Group, _GoalName)
AND
SysIsActive(_GoalName)
THEN
SysCompleteGoal(_GoalName);

IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
DB_LeaderLib_GameScripts_Registered(_Group, _GoalName)
AND
SysStatus(_GoalName, _Status) // Hopefully fails for scripts that don't exist?
AND
_Status != 2 // 2 == ACTIVE
THEN
SysActivateGoal(_GoalName);

IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
DB_LeaderLib_GameScripts_RegisteredToggleScripts(_GoalName, _ScriptFlag)
AND
LeaderLib_ToggleScripts_QRY_ScriptIsEnabled(_ScriptFlag)
AND
SysStatus(_GoalName, _Status) // Hopefully fails for scripts that don't exist?
AND
_Status != 2 // 2 == ACTIVE
THEN
SysActivateGoal(_GoalName);

QRY
LeaderLib_GameScripts_QRY_CanEnableScript((STRING)_GoalName, (STRING)_ScriptFlag)
AND
NOT DB_LeaderLib_GameScripts_RegisteredToggleScripts(_GoalName, _ScriptFlag)
AND
SysStatus(_GoalName, _Status)
AND
_Status != 2 // 2 == ACTIVE
THEN
DB_NOOP(1);

QRY
LeaderLib_GameScripts_QRY_CanEnableScript((STRING)_GoalName, (STRING)_ScriptFlag)
AND
DB_LeaderLib_GameScripts_RegisteredToggleScripts(_GoalName, _ScriptFlag)
AND
DB_LeaderLib_GameStarted(1)
AND
SysStatus(_GoalName, _Status)
AND
_Status != 2 // 2 == ACTIVE
THEN
DB_NOOP(1);
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_CurrentVersion)
THEN
LeaderLib_GameScripts_InitSettings();
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"