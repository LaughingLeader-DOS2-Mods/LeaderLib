Version 1
SubGoalCombiner SGC_AND
INITSECTION
LeaderLib_Debug_RunTests();
KBSECTION

IF
GameModeStarted(_, 1)
THEN
DebugBreak("[LeaderLib_90__Debug:GameModeStarted] Starting [DB_Origins] registration.");
LeaderLib_Debug_RegisterDebugPlayer();
LeaderLib_Debug_Internal_RegisterDebugPlayer_Fallback();

//For other mods to register an Origin character before LeaderLib does.
PROC
LeaderLib_Debug_RegisterDebugPlayer()
THEN
DB_NOOP(1);

PROC
LeaderLib_Debug_Internal_RegisterDebugPlayer_Fallback()
AND
NOT DB_Origins(_)
THEN
DebugBreak("[LeaderLib_90__Debug:RegisterDebugPlayer_Fallback] No character found in [DB_Origins]. Adding default dummy.");
DB_Origins((CHARACTERGUID)S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406);

// Add racial skills outside of race presets
IF
GameStarted(_,1)
AND
DB_Origins(_Char)
AND
DB_LeaderLib_RacialSkills(_RaceTag, _Skill)
AND
IsTagged(_Char, _RaceTag, 1)
THEN
CharacterAddSkill(_Char, _Skill, 0);

IF
GameModeStarted(_, 1)
AND
NOT DB_UndeadRacePresets(_,_)
THEN
DB_UndeadRacePresets(0,"Undead_Dwarf");
DB_UndeadRacePresets(1,"Undead_Elf");
DB_UndeadRacePresets(2,"Undead_Human");
DB_UndeadRacePresets(3,"Undead_Lizard");

IF
GameStarted(_,1)
THEN
GlobalSetFlag("LeaderLib_IsEditorMode");

IF
GameModeStarted(_, 1)
THEN
GlobalSetFlag("LeaderLib_IsEditorMode");

IF
RegionStarted("TestLevel_LL_LeaderLib")
THEN
GlobalSetFlag("LeaderLib_IsEditorMode");
GlobalSetFlag("LeaderLog_Debug_Enabled");
//GlobalSetFlag("LeaderLog_Trace_Enabled");
SysActivateGoal("LeaderLib_90_DebugLevel");

IF
RegionStarted("_TMPL_Sandbox")
THEN
GlobalSetFlag("LeaderLib_IsEditorMode");
GlobalSetFlag("LeaderLog_Debug_Enabled");
//GlobalSetFlag("LeaderLog_Trace_Enabled");
SysActivateGoal("LeaderLib_90_DebugLevel");

IF
RegionEnded("TestLevel_LL_LeaderLib")
THEN
SysCompleteGoal("LeaderLib_90_DebugLevel");

IF
RegionEnded("_TMPL_Sandbox")
THEN
SysCompleteGoal("LeaderLib_90_DebugLevel");

PROC
LeaderLib_Debug_RunTests()
THEN
//LeaderLib_Debug_MathTests();
DB_NOOP(1);

IF
TextEventSet("leaderlib_mathtests")
THEN
LeaderLib_Debug_MathTests();

PROC
LeaderLib_Debug_MathTests()
AND
LeaderLib_Math_QRY_Sine(90.0)
AND
DB_LeaderLib_Math_Sine(90.0, _SineResult)
AND
LeaderLog_QRY_RealToIntToString("LeaderLib.Debug.Sine", _SineResult)
AND
DB_LeaderLog_Temp_RealString("LeaderLib.Debug.Sine", _SineResultStr)
THEN
NOT DB_LeaderLog_Temp_RealString("LeaderLib.Debug.Sine", _SineResultStr);
LeaderLog_Log("DEBUG", "[LeaderLog:Debug:Math] Sine | Angle [90] = [",_SineResultStr,"]");

PROC
LeaderLib_Debug_MathTests()
AND
LeaderLib_Math_QRY_Cosine(90.0)
AND
DB_LeaderLib_Math_Cosine(90.0, _CosineResult)
AND
LeaderLog_QRY_RealToIntToString("LeaderLib.Debug.Cosine", _CosineResult)
AND
DB_LeaderLog_Temp_RealString("LeaderLib.Debug.Cosine", _CosineResultStr)
THEN
NOT DB_LeaderLog_Temp_RealString("LeaderLib.Debug.Cosine", _CosineResultStr);
LeaderLog_Log("DEBUG", "[LeaderLog:Debug:Math] Cosine | Angle [90] = [",_CosineResultStr,"]");

PROC
LeaderLib_Debug_MathTests()
AND
LeaderLib_Math_QRY_Factorial(5)
AND
DB_LeaderLib_Math_Factorial(5, _Result1)
AND
IntegertoString(_Result1, _Result1Str)
AND
LeaderLib_Math_QRY_Factorial(10)
AND
DB_LeaderLib_Math_Factorial(10, _Result2)
AND
IntegertoString(_Result2, _Result2Str)
THEN
LeaderLog_Log("DEBUG", "[LeaderLog:Debug:Math] Factorial | [5!] = [",_Result1Str,"] [10!] = [",_Result2Str,"]");

IF
TextEventSet("leaderlib_debug_versionstring")
AND
LeaderLib_Recon_QRY_GetString("leaderlib_debug_versionstring", "version", 1, "1.2.3.4")
AND
DB_LeaderLib_Recon_Temp_CommandParameters_String("leaderlib_debug_versionstring", "version", _VersionString, _NextParamInt)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_VersionString, 2,0,0,0)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib_90__Debug:] Version (",_VersionString,") is less than 2.0.0.0");

IF
TextEventSet("leaderlib_debug_versionstring")
AND
DB_LeaderLib_Recon_Temp_CommandParameters_String("leaderlib_debug_versionstring", "version", _VersionString, _NextParamInt)
AND
NOT LeaderLib_StringExt_QRY_VersionIsLessThan(_VersionString, 2,0,0,0)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib_90__Debug:] Version (",_VersionString,") is not less than 2.0.0.0?");

IF
TextEventSet("leaderlib_debug_versionstring")
THEN
LeaderLib_Recon_ClearParams("leaderlib_debug_versionstring");

IF
TextEventSet("leaderlib_debug_arraytest")
THEN
LeaderLib_Array_AddToArray("LeaderLib_Debug_ArrayTest", "HASTED");
LeaderLib_Array_AddToArray("LeaderLib_Debug_ArrayTest", "BURNING");
LeaderLib_Array_AddToArray("LeaderLib_Debug_ArrayTest", "SLOWED");

IF
TextEventSet("leaderlib_debug_arraytest")
AND
LeaderLib_Array_QRY_GetLastEntry("LeaderLib_Debug_ArrayTest")
AND
DB_LeaderLib_Array_Temp_LastEntry("LeaderLib_Debug_ArrayTest", _Index, _Value)
THEN
NOT DB_LeaderLib_Array_Temp_LastEntry("LeaderLib_Debug_ArrayTest", _Index, _Value);
LeaderLog_Log("DEBUG", "[LeaderLib_90__Debug:TextEventSet(leaderlib_debug_arraytest)] Removed '(",_Value,")'");
LeaderLib_Array_ClearArray("LeaderLib_Debug_ArrayTest");

//REGION LUA_DEBUG_COMMANDS
/* [OSITOOLS_ONLY]
IF
StoryEvent(_Obj, "Iterator_LeaderLib_Debug_Ext_PrintCharacter")
THEN
NRD_ModCall("LeaderLib", "Debug_Iterator_PrintCharacter", (STRING)_Obj);

IF
NRD_ItemDeltaModIteratorEvent("Iterator_LeaderLib_Debug_PrintDeltamods", _Item, _DeltaMod, _IsGenerated)
THEN
NRD_ModCall("LeaderLib", "Debug_Iterator_PrintDeltamod", (STRING)_Item, _DeltaMod, (STRING)_IsGenerated);

IF
TimerFinished("Timers_LeaderLib_Debug_LuaReset")
THEN
NRD_ModCall("LeaderLib", "OnLuaReset");
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
