Version 1
SubGoalCombiner SGC_AND
INITSECTION
//The version is registered in LeaderLib_00_1_1_System, so other mods will be able to see if LeaderLib is registered.
KBSECTION
//REGION UPDATE_EVENT
// [BEGIN_NO_OSITOOLS]
IF
GameStarted(_,_)
AND
LeaderUpdater_QRY_ModUpdateNeeded("LeaderLib", "LaughingLeader", 1,6,0,0)
THEN
LeaderLog_Log("SYSTEM", "[LeaderLib:Updater] Updating LeaderLib to latest version (1.6.0.0).");
LeaderUpdater_Register_Mod("LeaderLib", "LaughingLeader", 1,6,0,0);
// [END_NO_OSITOOLS]
//END_REGION

//REGION ACTIVE_FLAG
IF
GameStarted(_,_)
AND
GlobalGetFlag("LeaderLib_Active", 0)
THEN
GlobalSetFlag("LeaderLib_Active");
//END_REGION

//REGION GENERAL_UPDATES
PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1,5,9,1)
THEN
LeaderUpdater_Register_UUID("LeaderLib", "LaughingLeader", 7e737d2f-31d2-4751-963f-be6ccc59cd0c);

PROC
LeaderUpdater_ModUpdated("LeaderLib", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1,6,0,0)
AND
DB_LeaderLib_ModMenu_RegisteredMenuData("LaughingLeader.LeaderLib", "[LeaderLib] Settings", "LeaderLib_SettingsMenu", "LeaderLib", "LaughingLeader")
THEN
NOT DB_LeaderLib_ModMenu_RegisteredMenuData("LaughingLeader.LeaderLib", "[LeaderLib] Settings", "LeaderLib_SettingsMenu", "LeaderLib", "LaughingLeader");
//END_REGION

//REGION MIGRATING
PROC
LeaderLib_Mods_OnModRegistered((GUIDSTRING)_UUID, (INTEGER)_Version, (INTEGER)_Major, (INTEGER)_Minor, (INTEGER)_Revision, (INTEGER)_Build)
AND
DB_LeaderUpdater_RegisteredUUID(_ModID, _Author, _UUID)
THEN
DB_LeaderLib_Updater_Temp_MigrateGlobalSettings(_UUID, _ModID, _Author);
TimerCancel("Timers_LeaderLib_Updater_MigrateGlobalSettings");
TimerLaunch("Timers_LeaderLib_Updater_MigrateGlobalSettings", 500);

IF
TimerFinished("Timers_LeaderLib_Updater_MigrateGlobalSettings")
AND
DB_LeaderLib_Updater_Temp_MigrateGlobalSettings(_UUID, _ModID, _Author)
THEN
NOT DB_LeaderLib_Updater_Temp_MigrateGlobalSettings(_UUID, _ModID, _Author);
LeaderLib_GlobalSettings_Internal_MigrateMod(_ModID, _Author, _UUID);

PROC
LeaderLib_GlobalSettings_Internal_MigrateMod((STRING)_ModID, (STRING)_Author, (GUIDSTRING)_UUID)
AND
DB_LeaderLib_GlobalSettings_Flags(_Flag, _ModID, _Author, _SaveWhenFalse)
THEN
NOT DB_LeaderLib_GlobalSettings_Flags(_Flag, _ModID, _Author, _SaveWhenFalse);
DB_LeaderLib_GlobalSettings_GlobalFlags(_UUID, _Flag, _SaveWhenFalse);

PROC
LeaderLib_GlobalSettings_Internal_MigrateMod((STRING)_ModID, (STRING)_Author, (GUIDSTRING)_UUID)
AND
DB_LeaderLib_GlobalSettings_Integers(_ModID, _Author, _VarName, _DefaultValue)
THEN
NOT DB_LeaderLib_GlobalSettings_Integers(_ModID, _Author, _VarName, _DefaultValue);
DB_LeaderLib_GlobalSettings_GlobalIntegers(_UUID, _VarName, _DefaultValue);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
