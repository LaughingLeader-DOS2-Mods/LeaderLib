Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION DUMMY_DEFINITIONS
//These are missing in the editor's Shared scripts for some reason

//Dummy definition
QRY
QRY_GLOBAL_CrimeDialogIntercept_Wrapper((INTEGER)_Automated, (GUIDSTRING)_Speaker1, (GUIDSTRING)_Speaker2, (GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4, (GUIDSTRING)_Speaker5, (GUIDSTRING)_Speaker6)
AND
0 == 1
THEN
DB_NOOP(1);

//Dummy definition
QRY
QRY_PlayOriginMoment((STRING)_Dialog, (GUIDSTRING)_MainNPC, (GUIDSTRING)_Player)
AND
0 == 1
THEN
DB_NOOP(1);
//END_REGION

//REGION SKIPPING_REDIRECTION
/*If the player iniates a dialog directly, don't redirect it.*/
/*
IF    
DialogStartRequested(_Target, _Player)
THEN
UserSetFlag((CHARACTERGUID)_Player, "LeaderLib_DialogOverride_PlayerStarted", 0);
*/
//END_REGION

//REGION Proc_StartDialog_Override
PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1)
AND
GlobalGetFlag("LeaderLib_DialogRedirectionEnabled", _RedirectionEnabled)
THEN
LeaderLib_DialogOverride_Internal_StartDialog(_Automated,_RedirectionEnabled,_Dialog,_Speaker1);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2)
AND
NOT QRY_GLOBAL_CrimeDialogIntercept_Wrapper(_Automated, _Speaker1,_Speaker2, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000,  NULL_00000000-0000-0000-0000-000000000000)
AND
NOT QRY_PlayOriginMoment(_Dialog,_Speaker1,_Speaker2)
AND
NOT DB_OriginRecruitmentDialog((CHARACTERGUID)_,_Dialog)
AND
GlobalGetFlag("LeaderLib_DialogRedirectionEnabled", _RedirectionEnabled)
THEN
LeaderLib_DialogOverride_Internal_StartDialog(_Automated,_RedirectionEnabled,_Dialog,_Speaker1,_Speaker2);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3)
AND
NOT QRY_GLOBAL_CrimeDialogIntercept_Wrapper(_Automated, _Speaker1,_Speaker2, _Speaker3, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000,  NULL_00000000-0000-0000-0000-000000000000)
AND
NOT QRY_PlayOriginMoment(_Dialog,_Speaker1,_Speaker3)
AND
GlobalGetFlag("LeaderLib_DialogRedirectionEnabled", _RedirectionEnabled)
THEN
LeaderLib_DialogOverride_Internal_StartDialog(_Automated,_RedirectionEnabled,_Dialog,_Speaker1,_Speaker2,_Speaker3);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4)
AND
NOT QRY_GLOBAL_CrimeDialogIntercept_Wrapper(_Automated, _Speaker1,_Speaker2, _Speaker3, _Speaker4, NULL_00000000-0000-0000-0000-000000000000,  NULL_00000000-0000-0000-0000-000000000000)
AND
NOT QRY_PlayOriginMoment(_Dialog,_Speaker1,_Speaker4)
AND
GlobalGetFlag("LeaderLib_DialogRedirectionEnabled", _RedirectionEnabled)
THEN
LeaderLib_DialogOverride_Internal_StartDialog(_Automated,_RedirectionEnabled,_Dialog,_Speaker1,_Speaker2,_Speaker3,_Speaker4);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4,(GUIDSTRING)_Speaker5)
AND
NOT QRY_GLOBAL_CrimeDialogIntercept_Wrapper(_Automated, _Speaker1,_Speaker2, _Speaker3, _Speaker4, _Speaker5, NULL_00000000-0000-0000-0000-000000000000)
AND
NOT QRY_PlayOriginMoment(_Dialog,_Speaker1,_Speaker5)
AND
GlobalGetFlag("LeaderLib_DialogRedirectionEnabled", _RedirectionEnabled)
THEN
LeaderLib_DialogOverride_Internal_StartDialog(_Automated,_RedirectionEnabled,_Dialog,_Speaker1,_Speaker2,_Speaker3,_Speaker4,_Speaker5);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4,(GUIDSTRING)_Speaker5,(GUIDSTRING)_Speaker6)
AND
NOT QRY_GLOBAL_CrimeDialogIntercept_Wrapper(_Automated, _Speaker1,_Speaker2, _Speaker3, _Speaker4, _Speaker5, _Speaker6)
AND
NOT QRY_PlayOriginMoment(_Dialog,_Speaker1,_Speaker6)
AND
GlobalGetFlag("LeaderLib_DialogRedirectionEnabled", _RedirectionEnabled)
THEN
LeaderLib_DialogOverride_Internal_StartDialog(_Automated,_RedirectionEnabled,_Dialog,_Speaker1,_Speaker2,_Speaker3,_Speaker4,_Speaker5,_Speaker6);
//END_REGION

//REGION Proc_StartDialog_CompanionRecruitment
// To avoid type definition conflicts with __Global_Dialogs.
// This query is defined as (CHARACTERGUID),(CHARACTERGUID), but in the Dialogs script it's used with GUIDSTRING, leading to typedef errors
QRY
LeaderLib_DialogOverride_QRY_SpeakerIsAvailableAndInDialogRange_Workaround((CHARACTERGUID)_Char,(CHARACTERGUID)_SourceCharacter)
AND
DB_CustomDialogRange(_SourceCharacter,_MaxDist)
AND
CharacterIsDeadOrFeign(_Char,0)
AND
IsSpeakerReserved(_Char,0)
AND
CharacterIsInCombat(_Char,0)
AND
GetDistanceTo(_Char,_SourceCharacter,_Distance)
AND
_Distance < _MaxDist
THEN
DB_NOOP(1);

QRY
LeaderLib_DialogOverride_QRY_SpeakerIsAvailableAndInDialogRange_Workaround((CHARACTERGUID)_Char,(CHARACTERGUID)_SourceCharacter)
AND
NOT DB_CustomDialogRange(_SourceCharacter,_)
AND
CharacterIsDeadOrFeign(_Char,0)
AND
IsSpeakerReserved(_Char,0)
AND
CharacterIsInCombat(_Char,0)
AND
GetDistanceTo(_Char,_SourceCharacter,_Distance)
AND
_Distance < 10.0
THEN
DB_NOOP(1);

//Companion in party with initiates conversation with unhired companion.
PROC
Proc_StartDialog(0,(STRING)_RecruitementDialog,(GUIDSTRING)_UnhiredCompanion,(GUIDSTRING)_NonAvatar)
AND
DB_OriginRecruitmentDialog((CHARACTERGUID)_UnhiredCompanion,_RecruitementDialog)
AND
NOT DB_Avatars((CHARACTERGUID)_NonAvatar)
AND
DB_CompanionAvatarBond((CHARACTERGUID)_NonAvatar,(CHARACTERGUID)_Avatar)
AND
LeaderLib_DialogOverride_QRY_SpeakerIsAvailableAndInDialogRange_Workaround((CHARACTERGUID)_Avatar, (CHARACTERGUID)_NonAvatar)
AND
CharacterIsControlled(_Avatar,0)
AND
DB_LeaderLib_DialogOverride_Registered_RegularDialog((GUIDSTRING)_SpeakerMatch, _RecruitementDialog, _UseDialog)
AND
LeaderLib_DialogOverride_QRY_SpeakerMatch(_RecruitementDialog, (GUIDSTRING)_SpeakerMatch, (GUIDSTRING)_UnhiredCompanion, (GUIDSTRING)_NonAvatar)
AND
QRY_StartDialog(0,_UseDialog,_UnhiredCompanion,_Avatar)
THEN
PROC_CheckPlayTut(_Avatar,"TUT_Comp_Talking_To_Comp");
MakePlayerActive(_Avatar);

PROC
Proc_StartDialog(0,(STRING)_RecruitementDialog,(GUIDSTRING)_UnhiredCompanion,(GUIDSTRING)_NonAvatar)
AND
DB_OriginRecruitmentDialog((CHARACTERGUID)_UnhiredCompanion,_RecruitementDialog)
AND
DB_LeaderLib_DialogOverride_Registered_RegularDialog((GUIDSTRING)_SpeakerMatch, _RecruitementDialog, _UseDialog)
AND
LeaderLib_DialogOverride_QRY_SpeakerMatch(_RecruitementDialog, (GUIDSTRING)_SpeakerMatch, (GUIDSTRING)_UnhiredCompanion, (GUIDSTRING)_NonAvatar)
AND
QRY_StartDialog(0,_UseDialog,_UnhiredCompanion,_NonAvatar)
THEN
DB_NOOP(1);
//END_REGION

//REGION Proc_StartDialog_CLEANUP
PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1)
THEN
LeaderLib_DialogOverride_Internal_ClearTempData(_Dialog);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2)
THEN
LeaderLib_DialogOverride_Internal_ClearTempData(_Dialog);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3)
THEN
LeaderLib_DialogOverride_Internal_ClearTempData(_Dialog);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4)
THEN
LeaderLib_DialogOverride_Internal_ClearTempData(_Dialog);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4,(GUIDSTRING)_Speaker5)
THEN
LeaderLib_DialogOverride_Internal_ClearTempData(_Dialog);

PROC
Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4,(GUIDSTRING)_Speaker5,(GUIDSTRING)_Speaker6)
THEN
LeaderLib_DialogOverride_Internal_ClearTempData(_Dialog);
//END_REGION

//REGION POST_DIALOG_ACTIVATION
IF
DialogEnded(_Dialog, _)
AND
DB_LeaderLib_DialogOverride_Temp_PostDialogActivation(_Dialog, (CHARACTERGUID)_Target)
AND
CharacterIsPartyMember(_Target, 1)
THEN
NOT DB_LeaderLib_DialogOverride_Temp_PostDialogActivation(_Dialog, (CHARACTERGUID)_Target);
MakePlayerActive(_Target);

IF
DialogEnded(_Dialog, _)
AND
DB_LeaderLib_DialogOverride_Temp_PostDialogActivation(_Dialog, (CHARACTERGUID)_Target)
THEN
NOT DB_LeaderLib_DialogOverride_Temp_PostDialogActivation(_Dialog, (CHARACTERGUID)_Target);

//No dialogs active, cleanup
IF
DialogEnded(_,_)
AND
NOT SysCount("DB_LeaderLib_DialogOverride_Temp_PostDialogActivation", 2, 0)
THEN
TimerCancel("Timers_LeaderLib_DialogOverride_ClearPostActivationDB");
TimerLaunch("Timers_LeaderLib_DialogOverride_ClearPostActivationDB", 25);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__AAA_Z_LeaderLib_TopToggledScripts"
