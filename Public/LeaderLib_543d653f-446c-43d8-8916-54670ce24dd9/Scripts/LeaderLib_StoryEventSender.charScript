INIT
	CHARACTER:__Me
	INT:%LeaderLib_StoryEvents_Initialized = 0
	FIXEDSTRING:%LeaderLib_CharacterPlayAnimation_Animation = ""
	INT:%LeaderLib_CharacterPlayAnimation_ExitOnFinish = 0
	INT:%LeaderLib_CharacterPlayAnimation_WaitForCompletion = 0
	INT:%LeaderLib_CharacterPlayAnimation_NoBlend = 1
EVENTS

EVENT LeaderLib_StoryEvents_OnInit
ON
	OnInit()
ACTIONS
IF "c1"
	HasGlobalFlag("LeaderLib_LevelIsLoaded")
THEN
	CharacterEvent(__Me, "LeaderLib_StoryEvents_OnInit")
ELSE
	SetPriority("LeaderLib_StoryEvents_PostLoadInit", 999)
	DelayReaction("LeaderLib_StoryEvents_PostLoadInit", 1.0)
ENDIF

EVENT LeaderLib_StoryEvents_OnActivate
ON
	OnActivate()
ACTIONS
IF "c1"
	HasGlobalFlag("LeaderLib_LevelIsLoaded")
THEN
	CharacterEvent(__Me, "LeaderLib_StoryEvents_OnActivate")
	IF "!c1"
		IsEqual(%LeaderLib_StoryEvents_Initialized, 1)
	THEN
		CharacterEvent(__Me, "LeaderLib_StoryEvents_Initialize")
		Set(%LeaderLib_StoryEvents_Initialized, 1)
	ENDIF
ELSE
	SetPriority("LeaderLib_StoryEvents_PostLoadInit", 999)
	DelayReaction("LeaderLib_StoryEvents_PostLoadInit", 1.0)
ENDIF

EVENT LeaderLib_StoryEvents_OnDeActivate
ON
	OnDeactivate()
ACTIONS
	CharacterEvent(__Me, "LeaderLib_StoryEvents_OnDeactivate")
	
EVENT LeaderLib_StoryEvents_OnShutdown
ON
	OnShutdown()
ACTIONS
	CharacterEvent(__Me, "LeaderLib_StoryEvents_OnShutdown")
	Set(%LeaderLib_StoryEvents_Initialized, 0)
	
EVENT LeaderLib_StoryEvents_CharacterPlayAnimation
ON
	OnCharacterEvent(__Me, "LeaderLib_StoryEvents_CharacterPlayAnimation")
ACTIONS
	//StatusText(__Me, %LeaderLib_CharacterPlayAnimation_Animation)
	CharacterSetAnimationOverride(__Me, "")
	SetPriority("LeaderLib_StoryEvents_PlayAnimation", 999)
	
BEHAVIOUR

REACTION LeaderLib_StoryEvents_PlayAnimation,0
USAGE ALL
CHECK "!c1"
	IsEqual(%LeaderLib_CharacterPlayAnimation_Animation, "")
ACTIONS
	CharacterPlayAnimation(%LeaderLib_CharacterPlayAnimation_Animation, %LeaderLib_CharacterPlayAnimation_ExitOnFinish, %LeaderLib_CharacterPlayAnimation_WaitForCompletion, %LeaderLib_CharacterPlayAnimation_NoBlend)
	SetPriority("LeaderLib_StoryEvents_PlayAnimation", 0)
INTERRUPT
ACTIONS
	Reset()

REACTION LeaderLib_StoryEvents_PostLoadInit,0
USAGE ALL
CHECK "c1"
	HasGlobalFlag("LeaderLib_LevelIsLoaded")
ACTIONS
	CharacterEvent(__Me, "LeaderLib_StoryEvents_OnInit")
IF "c1"
	IsActive(__Me)
THEN
	CharacterEvent(__Me, "LeaderLib_StoryEvents_OnActivate")
ENDIF
IF "!c1"
	IsEqual(%LeaderLib_StoryEvents_Initialized, 1)
THEN
	CharacterEvent(__Me, "LeaderLib_StoryEvents_Initialize")
	Set(%LeaderLib_StoryEvents_Initialized, 1)
ENDIF
	SetPriority("LeaderLib_StoryEvents_PostLoadInit", 0)
